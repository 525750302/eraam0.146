@COM80
;貝あわせ
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL SETFLAG, "下半身を脱がす", PLAYER

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM80, ARG
IF CHECK_COM("双頭バイブプレイ", 80)
	IF TALENT:PLAYER:処女
		CALL SETFLAG, "処女喪失フラグ", PLAYER
		CALL SETFLAG, "処女簒奪フラグ", TARGET
	ENDIF
ENDIF
;対象のＶ⇔調教者のＶの汚れが移動
CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "Ｖ"


@SOURCE_COM80, ARG
SIF CHECK_COM("双頭バイブプレイ", 80)
	CALL SOURCE_INSERTION, "双頭バイブプレイ", ARG, 100

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 80

;SOURCE:0:恐れ = 500
SOURCE:0:露出 = 250
SOURCE:0:屈従 = 400
SOURCE:0:逸脱 = 300
SOURCE:0:性行動 = 500 + ABL:奉仕精神*100
SOURCE:0:達成感 = 300

SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 2, 120)


SIF CHECK_COM("双頭バイブプレイ", 80)
	TIMES SOURCE:0:Ｃ快楽, 0.30
;調教者の腰使いをみる
SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "腰使い", PLAYER)

;陰毛の生え具合補正
SIF COND("陰毛") <= 5 && COND("陰毛", PLAYER) >= 5
	TIMES SOURCE:0:Ｃ快楽, 1.20

IF COND("好きな体位中")
	TIMES SOURCE:0:Ｃ快楽, 1.20
	TIMES SOURCE:0:Ｖ快楽, 1.20
	TIMES SOURCE:0:性行動, 1.20
	TIMES SOURCE:0:達成感, 1.20
	TIMES SOURCE:0:恐れ, 0.80
	TIMES SOURCE:0:逸脱, 0.80
ENDIF

SIF TALENT:Ｃ性向
	CALL CALC, "好みのプレイ", SOURCE:0:Ｃ快楽

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM80
IF PREVCOM == SELECTCOM
	PRINTFORM %CALLNAME:PLAYER%と%CALLNAME:TARGET%は
	IF RAND:3
		IF RAND:3
			PRINTFORML 互いの肌を%TEXTR("桜色に/朱く")%染め%TEXTR("合い/あげ/")%ながらも、
		ELSEIF RAND:2
			PRINTFORML 互いの愛液を%TEXTR("唾液のように絡ませ/絡め合い")%ながら、
		ENDIF
		PRINTFORMW %TEXTR("何度も/幾度となくその")%%TEXTR("はしたない/淫猥な/陰/")%唇%TEXTR("を重ね/で口づけ")%合った…
	ELSE
		IF RAND:3
			PRINTFORML 部屋中に%TEXTR("嬌声/水音")%を響かせながら、
		ELSEIF RAND:2
			PRINTFORML %TEXTR("体中/全身")%から%TEXTR("雌の香り/淫臭/いやらしい香り/エッチな香り")%を%TEXTR("発し/たたえ/発散させ/立ち上らせ/漂わせ")%つつ、
		ELSE
			PRINTFORML %TEXTR("体中/全身")%%TEXTR("に玉のような汗を浮かべ/を火照らせ/に汗を浮かべ")%ながら、
		ENDIF
		SIF COND("性別嫌悪") == 0 && COND("性別嫌悪", PLAYER) == 0
			PRINT 夢中で
		IF RAND:3
			PRINTFORMW 互いの腰を押し付け合っている…
		ELSEIF RAND:2
			PRINTFORMW %TEXTR("舐るように/")%互いのアソコを%TEXTR("擦り合っ/擦り合わせ")%ている…
		ELSE
			PRINTFORMW 股間を擦り合わせて快感を%TEXTR("貪りあ/分かち合/与え合")%っている…
		ENDIF
	ENDIF
ELSEIF TEQUIP:押し倒し
	PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の体を押さえ%TEXTR("込む/つける")%と、
	IF TALENT:PLAYER:処女 && CHECK_COM("双頭バイブプレイ", 80)
		PRINTFORMW 股間に咥えた双頭バイブの反対側を、%CALLNAME:PLAYER%の膣口へと押し当ててきた。
		PRINTFORML 亀頭を模したその先端が何度か入り口をつついたと思うと、
		PRINTFORMW バイブはたやすく%CALLNAME:PLAYER%の膣肉をこじ開けて、そのまま処女を散らしてしまった
		PRINTFORMW 体重のかけられたバイブは瞬く間に%CALLNAME:PLAYER%の膣奥まで達し、鮮やかな痛みを伝えてくる…
	ELSE
		IF CHECK_COM("双頭バイブプレイ", 80)
			PRINTFORMW %TEXTR("そそり立つ/股間の")%双頭バイブで膣を犯して%TEXTR("/激しく")%腰を振ってきた…
		ELSE
			PRINTFORMW 貪るように互いの%TEXTR("割れ目/性器")%を%TEXTR("重ね/擦り")%あわせてきた…
		ENDIF
	ENDIF
ELSEIF CHECK_COM("双頭バイブプレイ", 80)
	PRINTFORM %CALLNAME:PLAYER%
	IF TALENT:PLAYER:処女
		PRINTFORMW は%CALLNAME:TARGET%の膣から頭を出しているバイブに跨り、自ら処女を散らした…
		PRINTFORM %CALLNAME:TARGET%は
		IF TALENT:恋慕 || TALENT:両刀
			PRINTFORML %CALLNAME:PLAYER%の初めてを貰えた事に%TEXTR("ゾクゾクし/頬を上気させ/喜色を浮かべ")%ながらも、
			PRINTFORMW %CALLNAME:PLAYER%を%TEXTR("気遣う/労わる")%ように、%TEXTR("優しく/ゆっくり")%%TEXTR("腰を動かし始めた/行為を始めた/腰を揺すっている/腰を揺らし始めた")%…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%の事をなんともいえない表情で眺めつつ、上の空で行為に付き合っている…
		ENDIF
	ELSEIF RAND:2 && COND("性別嫌悪") == 0 && COND("性別嫌悪", PLAYER) == 0
		PRINTFORM と%CALLNAME:TARGET%は後ろ手で身体を支えるＭ字開脚の姿勢で向き合い、双頭バイブで互いを連結させて
		IF RAND:3 == 0
			PRINTFORMW 互いの膣を責めあった
		ELSEIF RAND:2
			PRINTFORMW 腰を突き%TEXTR("/出し")%あった
		ELSE
			PRINTFORMW 腰を大きく揺すりあった
		ENDIF

		;攻め手の判定
		SELECTCASE ABL:Ｖ使い - ABL:Ｖ拡張 - (ABL:PLAYER:Ｖ使い - ABL:PLAYER:Ｖ拡張)
		;TARGET有利
		CASE IS > 0
			LOCAL = TARGET
			LOCAL:1 = PLAYER
		CASE 0
			LOCAL = TARGET
			LOCAL:1 = PLAYER
			SIF RAND:2
				SWAP LOCAL, LOCAL:1
		CASE IS < 0
			LOCAL = PLAYER
			LOCAL:1 = TARGET
		ENDSELECT

		PRINTFORM %CALLNAME:LOCAL%の
		IF ABL:LOCAL:Ｖ拡張 >= 4 && RAND:3
			PRINTFORM 拡張を繰り返された
		ELSEIF ABL:LOCAL:Ｖ名器度 >= 70 && RAND:3
			PRINTFORM 包み込むような
		ELSEIF TALENT:LOCAL:淫壷
			PRINTFORM 蕩ける様に
		ENDIF
		PRINTFORM 柔らかな膣は、%CALLNAME:(LOCAL:1)%の
		IF ABL:(LOCAL:1):Ｖ使い >= 5 && RAND:3
			PRINTFORM 握力のように強い
		ELSEIF ABL:(LOCAL:1):Ｖ拡張 <= 2 && RAND:3
			PRINTFORM キツい
		ELSEIF ABL:(LOCAL:1):Ｖ名器度 >= 70 && RAND:3
			PRINTFORM 吸い付くような
		ELSEIF TALENT:(LOCAL:1):淫壷
			PRINTFORM 貪欲とも言える
		ENDIF
		PRINTFORM 締め付けで支えられたバイブ
		IF RAND:3 == 0
			PRINTFORMW の容赦ない責めに翻弄されている…
		ELSEIF RAND:2
			PRINTFORMW の侵入を\@ RAND:2 ? 送り返す # 為す \@術もなく受け入れている…
		ELSE
			PRINTFORMW に奥まで余すことなく蹂躙されている…
		ENDIF

	ELSE
		PRINTFORML は%CALLNAME:TARGET%の膣から頭を出しているバイブを%TEXTR("/貝あわせの体勢で")%咥え込み、
		IF RAND:2
			PRINTFORMW お互いの膣内を掻き回し合いながらアソコを擦りあわせた
		ELSE
			PRINTFORMW 膣肉を締めながら腰を振りはじめた
		ENDIF
		IF RAND:2
			PRINTFORMW バイブがガイドの役目を果たしているおかげで激しく擦り付けられる結合部は淫液が泡立ち、太腿に至るまでべっとりと濡れ光っている…
		ELSE
			PRINTFORMW 常とは異なり膣内をも刺激しあえるプレイに、否応もなく二人の興奮は高まっていく…
		ENDIF
	ENDIF
ELSE
	PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:TARGET%は互いのアソコを擦りあわせた
	IF RAND:5 == 0
		IF COND("陰毛") <= 3 && COND("陰毛", PLAYER) >= 5
			PRINTFORMW 無毛のソコを陰毛で刺激され、%CALLNAME:TARGET%はくすぐったそうにしている…
		ELSE
			PRINTFORMW %TEXTR("互いのクリトリス/陰核同士")%が擦れあう刺激に、%CALLNAME:TARGET%はくすぐったそうにしている…
		ENDIF
	ELSEIF RAND:4 == 0
		IF COND("陰毛") >= 5 && COND("陰毛", PLAYER) <= 3
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の無防備なソコを自らのヘアで刺激し、楽し%TEXTR("げな笑み/そうな表情")%を浮かべている…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の無防備な陰核を自らの陰核で刺激し、楽し%TEXTR("げな笑み/そうな表情")%を浮かべている…
		ENDIF
	ELSEIF RAND:3 == 0
		PRINTFORMW %TEXTR("重い水音/淫靡な粘着質の音")%と共にもたらされる快感に、腰の動きは徐々に激しさを増していく…
	ELSEIF RAND:2
		PRINTFORMW 押し付けあわされている陰唇は%TEXTR("何度も/ぐにゅぐにゅと")%いやらしく形を変え、混ざりあった愛液が淫らに香り立っている…
	ELSE
		PRINTFORMW 陰唇を押し付けあうたびに、互いの香りが混ざり合ってより淫らな%TEXTS("匂い")%%TEXTR("へと変わ/にな")%っていく…
	ENDIF
ENDIF
