@COM140
;イラマチオ
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 140

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
SIF TEQUIP:拘束
	TCVAR:やる気 -= 30
SIF TEQUIP:口枷 == 2
	TCVAR:やる気 -= 30
TCVAR:やる気 = MAX(TCVAR:やる気, 0)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1


@FLAG_COM140, ARG
CALL SETFLAG, "フェラチオフラグ", TARGET
;対象の口⇔調教者のＰの汚れが移動
CALL EQ_STAIN, TARGET, "Ｍ", PLAYER, "Ｐ"

@SOURCE_COM140, ARG
CALL SOURCE_SMPLAY, "イラマチオ", ARG

CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "イラマチオ"

CALL SOURCE_DOWNBASE_HP, 140

SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｍ")*200 + 100

SOURCE:0:屈従 = 3000
SOURCE:0:逸脱 = 3000

;快Mのソース・M感覚を見る
SOURCE:0:Ｍ快楽 = CALCF("Ｍ刺激", 2, 120)

;喉性感がないならば激減
IF TALENT:喉性感 == 0
	TIMES SOURCE:0:Ｍ快楽, 0.05
	SIF SOURCE:0:Ｍ快楽 < 100
		SOURCE:0:Ｍ快楽 = 0
ENDIF

;喉ＬＶを見る
SELECTCASE ABL:喉拡張 - SIZE("ペニス", PLAYER)
CASE IS <= -2
	SOURCE:0:痛み = 3000
	SOURCE:0:恐れ = 8000
	TIMES SOURCE:0:Ｍ快楽, 0.00
CASE -1
	SOURCE:0:痛み = 1000
	SOURCE:0:恐れ = 3000
	TIMES SOURCE:0:Ｍ快楽, 0.00
CASE 0
	SOURCE:0:痛み = 500
	SOURCE:0:恐れ = 1000
CASE 1
	SOURCE:0:痛み = 100
	SOURCE:0:恐れ = 300
ENDSELECT

;堅さを見る
SELECTCASE HARD("ペニス", PLAYER)
CASE 3
	TIMES SOURCE:0:痛み, 1.20
	TIMES SOURCE:0:恐れ, 1.20
CASE IS >= 4
	TIMES SOURCE:0:痛み, 1.50
	TIMES SOURCE:0:恐れ, 1.50
ENDSELECT

;ABL:マゾっ気をみる
SELECTCASE ABL:マゾっ気
CASE 1
	TIMES SOURCE:0:屈従, 1.20
	TIMES SOURCE:0:恐れ, 0.80
CASE 2
	TIMES SOURCE:0:屈従, 1.50
	TIMES SOURCE:0:恐れ, 0.50
CASE 3
	TIMES SOURCE:0:屈従, 2.00
	TIMES SOURCE:0:恐れ, 0.20
CASE 4
	TIMES SOURCE:0:屈従, 2.50
	TIMES SOURCE:0:恐れ, 0.10
CASE IS >= 5
	TIMES SOURCE:0:屈従, 3.00
	TIMES SOURCE:0:恐れ, 0.05
ENDSELECT

;ディープスロート中？
IF TEQUIP:ディープスロート
	TIMES SOURCE:0:性行動, 1.50
	TIMES SOURCE:0:達成感, 1.50
	TIMES SOURCE:0:痛み, 1.50
	TIMES SOURCE:0:不潔, 1.50
	TIMES SOURCE:0:恐れ, 1.50
	TIMES SOURCE:0:屈従, 1.50
	TIMES SOURCE:0:逸脱, 1.50
	TIMES SOURCE:0:Ｍ快楽, 1.50
ENDIF

SIF TALENT:Ｍ性向
	CALL CALC, "好みのプレイ", SOURCE:0:Ｍ快楽

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM140
IF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
;前回もイラマチオ
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の
	;角がありますか？
	PRINTFORM \@ COND("ツノハンドル") ? 角を掴んで # 頭を押さえて \@、
	IF SIZE("ペニス", PLAYER) >= 2 && RANDIF(2, ABL:PLAYER:サドっ気 >= 3)
		PRINTFORM 長いペニスで
	ELSEIF ABL:PLAYER:サドっ気 >= 3 && RAND:2
		PRINTFORM 息継ぎもさせない勢いで
	ELSEIF ABL:マゾっ気 >= 3 && CHECK("喉姦")
		PRINTFORM 望まれるがままに
	ENDIF
	IF (ABL:技巧 >= 4 || ABL:PLAYER:サドっ気 >= 3 || TEQUIP:ディープスロート) && SIZE("ペニス", PLAYER) && RAND:2
		IF SIZE("ペニス", PLAYER) >= 3 && TEQUIP:ディープスロート && RAND:2
			PRINT 食道
		ELSE
			PRINT 喉
		ENDIF
	ELSE
		PRINT 口
	ENDIF
	PRINTFORMW を容赦なく犯し続けている…
ELSE
	;特に[恋慕＋奉仕精神Lv5]、[奉仕精神Lv3]、[PLAYERがサド]、[直前の調教]、[角があるか]、[アイマスク]等でテキストが変わります。
	PRINTFORM %CALLNAME:PLAYER%は
	;前回はフェラチオ
	IF PREVCOM == 91 && COND("前Ｔ調教者") == PLAYER
		;汚れてますか？汚臭鈍感で無いならば、実行値の10分の一の値とRAND:5を比べて判定
		IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ") && TALENT:汚臭鈍感 == 0 && RAND:6 > TCVAR:やる気/10
			PRINTFORM 臭いに眉根を寄せつつ
		;恋慕or献身的or奉仕精神3以上or汚臭鈍感
		ELSEIF (TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3 || TALENT:汚臭鈍感) && RAND:2 == 0
			PRINT 美味しそうに
		;恥じらい
		ELSEIF TALENT:恥じらい && RAND:3 == 0
			PRINT 恥ずかしそうに
		;欲情4以上
		ELSEIF PALAM:欲情 >= PALAMLV:4 && RAND:3 == 0
			SIF TALENT:倒錯的 && RAND:3 == 0
				PRINTFORM %TEXTS("匂い")%に
			PRINT 興奮しつつ
		;恋慕かつ奉仕精神5or献身的かつ奉仕精神3以上
		ELSEIF ( (TALENT:恋慕 && ABL:奉仕精神 >= 5) || (TALENT:献身 && ABL:奉仕精神 >= 3) ) && RAND:3 == 0
			PRINT 嬉しそうに
		;恋慕or献身的or奉仕精神3以上
		ELSEIF TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3
			PRINT 美味しそうに
		ELSEIF RAND:2 == 0 && ABL:技巧 < 2
			PRINT えづきながらも
		ENDIF
		;前回お掃除フェラ
		IF PREVPLAY("お掃除フェラ")
			PRINTFORM ペニスをお掃除している%CALLNAME:TARGET%の
		ELSE
			PRINTFORM 奉仕している%CALLNAME:TARGET%の
		ENDIF
	;ディープスロート中
	ELSEIF TEQUIP:ディープスロート
		PRINTFORM 根元までペニスを咥え込んだ%CALLNAME:TARGET%の
	ELSE
		IF BIND("ベッド", TEQUIP:拘束) && RAND:2
			PRINTFORM 仰向けにされて喉と一直線になっている%CALLNAME:TARGET%の口の中に
		;リングギャグ
		ELSEIF TEQUIP:口枷 == 2
			PRINTFORM 器具で開きっぱなしにされている%CALLNAME:TARGET%の口の中に
		;アイマスク+縄
		ELSEIF TEQUIP:アイマスク && TEQUIP:拘束
			PRINTFORM 完全に拘束されている%CALLNAME:TARGET%の口の中に
		;アイマスクあり？
		ELSEIF TEQUIP:アイマスク
			PRINTFORM 目隠しをした%CALLNAME:TARGET%の口の中に
		;縛ってますか？
		ELSEIF TEQUIP:拘束
			PRINTFORM 身動きの取れない%CALLNAME:TARGET%の口の中に
		;恋慕＋奉仕精神Lv5
		ELSEIF ABL:奉仕精神 >= 5 && TALENT:恋慕 && RAND:2 == 0
			PRINTFORM %NAMES("ペニス", PLAYER)%に頬擦りをしてきた%CALLNAME:TARGET%の口の中に
		ELSE
			PRINTFORM 跪かせた%CALLNAME:TARGET%の口の中に
		ENDIF
		;直前の調教と汚れで分岐
		IF PREVPLAY("挿入中", PLAYER)
			CALL ADD_TEXT, "性器から口へ"
		ELSEIF PENIS(PLAYER) == 0
		;セックス系を直前にしていないなら乱数分岐
		ELSE
			PRINTFORM %TEXTS("ペニス：視覚", PLAYER)%
		ENDIF
		IF PENIS(PLAYER)
			PRINTFORM ペニスを
		ELSE
			PRINTFORM ペニバンを
		ENDIF
		IF (ABL:PLAYER:サドっ気 >= 3 || ABL:奉仕精神 >= 5) && RAND:3 == 0
			PRINT 一気に
		ELSEIF ABL:PLAYER:サドっ気 >= 3 || (ABL:奉仕精神 >= 3 && RAND:2 == 0)
			PRINT 奥まで
		ELSE
			PRINT ゆっくりと
		ENDIF
		IF (ABL:PLAYER:サドっ気 >= 3 || ABL:奉仕精神 >= 5) && RAND:3 == 0
			PRINT 突き入れると、
		ELSEIF ABL:奉仕精神 >= 3 && RAND:2 == 0
			PRINT 挿入すると、
		ELSE
			PRINT 入れると、
		ENDIF
		;奉仕精神5かつ恋慕で追加
		SIF ABL:奉仕精神 >= 5 && TALENT:恋慕 && RAND:3 >= 1
			PRINT 舌を絡めさせながら、
	ENDIF
	;角がありますか？
	IF COND("ツノハンドル")
		PRINT 角を掴んで
	ELSE
		PRINT 頭を持って
	ENDIF
	SIF PREVCOM == 91 && COND("前Ｔ調教者") == PLAYER
		PRINT 、
	IF PREVCOM >= 60 && PREVCOM <= 79 && RAND:3 == 0
		PRINT 先ほど犯していた穴と比べながら
	ELSEIF (ABL:PLAYER:サドっ気 >= 3 || ABL:奉仕精神 >= 3) && RAND:3 == 0
		PRINT セックスする時のように
	ENDIF
	IF (ABL:PLAYER:サドっ気 >= 3 || ABL:奉仕精神 >= 5) && RAND:3 == 0
		PRINTFORMW 荒々しく口の中を陵辱した…
	ELSEIF ABL:PLAYER:サドっ気 >= 3 || (ABL:奉仕精神 >= 3 && RAND:2 == 0)
		PRINTFORMW 激しく出し入れした…
	ELSE
		PRINTFORMW 腰を前後に動かした…
	ENDIF
ENDIF

;いけるようなら喉奥まで
IF CHECK("喉姦")
	TEQUIP:ディープスロート = PLAYER
	;フラグとTEQUIPが分かれているのは、射精後などにTEQUIP:ディープスロートが０となっている時でも参照するため
	CALL SETFLAG, "ディープスロートフラグ", TARGET
ENDIF

@COM_ORDER_COM140, ARG
#DIM MEMO_STAIN

VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:マゾっ気
IF ABL:マゾっ気
	LOCAL += MIN(ABL:マゾっ気, 5) * 3
	LOCALS = %LOCALS% + マゾっ気LV{ABL:マゾっ気}({MIN(ABL:マゾっ気, 5) * 3})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF

;恥じらい
IF TALENT:恥じらい
	LOCAL -= 5
	LOCALS = %LOCALS% - 恥じらい(5)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF
;Ｍ性向
IF TALENT:Ｍ性向
	LOCAL += 10
	LOCALS = %LOCALS% + Ｍ性向(10)
ENDIF
;縄
IF TEQUIP:拘束
	LOCAL += 30
	LOCALS = %LOCALS% + 緊縛中(30)
ENDIF

;リングギャグ
IF TEQUIP:口枷 == 2
	LOCAL += 30
	LOCALS = %LOCALS% + リングギャグ(30)
ENDIF

MEMO_STAIN = COM_ORDER_STAIN("Ｐ", "Ｍ")
;サドなら、汚れていても、かまわずに突っ込みます
SIF ABL:PLAYER:サドっ気 >= 3
	MEMO_STAIN /= 3

;汚れあり
IF MEMO_STAIN
	LOCAL -= MEMO_STAIN
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({MEMO_STAIN})
ENDIF

;合計を表示(50以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 50

RETURN RESULT
