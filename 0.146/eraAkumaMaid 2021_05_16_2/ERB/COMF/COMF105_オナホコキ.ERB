@COM105
;オナホコキ
PRINTL オナホコキ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 105

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = オナホコキ

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM105, ARG


@SOURCE_COM105, ARG
CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "オナホコキ"

CALL SOURCE_DOWNBASE_HP, 105

SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "")*10
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:屈従 = 500
SOURCE:0:逸脱 = 500

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM105
;前回と同じ
IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:キス
		PRINTFORM %CALLNAME:PLAYER%%TEXTS("キス継続助詞付き")%、
		CALL SETFLAG, "追加キスフラグ", TARGET
	ELSEIF TEQUIP:顔面騎乗
		PRINTFORM %CALLNAME:PLAYER%の顔に股間をこすりつけつつ
	ENDIF
	PRINT 、
	IF TALENT:恋慕 && TEQUIP:顔面騎乗 == 0 && RAND:4 == 0
		PRINTFORMW 楽しそうに%CALLNAME:PLAYER%のペニスをオナホで扱き続けている…
	ELSEIF ABL:奉仕精神 >= 3 && TEQUIP:顔面騎乗 == 0 && RAND:3 == 0
		PRINTFORMW %CALLNAME:PLAYER%の顔を見つめながらペニスをオナホで扱き続けている…
	ELSEIF ABL:技巧 >= 3 && RAND:2 == 0
		PRINTFORMW 緩急をつけながら長いストロークでペニスをオナホで扱き続けている…
	ELSE
		PRINTFORMW 夢中になってペニスをオナホで扱き続けている…
	ENDIF
;押し倒されているとき
ELSEIF TEQUIP:押し倒し
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:顔面騎乗
		PRINT 顔に跨ったまま
	;プライド高いor淫乱orサドっ気Lv4以上
	ELSEIF (TALENT:高慢 || TALENT:淫乱 || ABL:サドっ気 >= 4) && RAND:2 == 0
		PRINT 愉快そうに微笑みながら
	;小悪魔
	ELSEIF TALENT:小悪魔
		PRINT ニヤニヤしながら
	;淫乱or欲情4LV以上
	ELSEIF (TALENT:淫乱 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
		PRINT 妖艶に微笑みながら
	ELSEIF (TALENT:淫乱 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
		PRINT 息を荒げながら
	ELSEIF RAND:2 == 0 || TEQUIP:アイマスク
		SIF RAND:2 == 0
			PRINT 楽しそうに
		PRINT 微笑みながら
	ENDIF
	PRINTFORM ペニスをオナホで弄び、
	IF PREVCOM == 1 || PREVCOM == 9
		PRINT 股間を押し付けたりしながら
	ELSE
		PRINT 快感に悶える
	ENDIF
	PRINTFORMW %CALLNAME:PLAYER%の反応を楽しんでいる…
;それ以外
ELSE
	PRINTFORM %CALLNAME:TARGET%は
	IF PALAM:PLAYER:Ｐ潤 < 3000
		PRINTFORM 付属のローションをたっぷりと注いだオナホールで、%CALLNAME:PLAYER%のペニスを
	ELSEIF (PREVCOM >= 60 && PREVCOM <= 79) || INSERT("インサート")
		PRINTFORM 自身の恥ずかしい液でドロドロの%CALLNAME:PLAYER%のペニスを、オナホールで
	ELSE
		PRINTFORM 粘液で濡れ光る%CALLNAME:PLAYER%のペニスを、オナホールで
	ENDIF
	IF ABL:サドっ気 >= 3 && RAND:3 == 0
		PRINTFORM 痛いぐらいに力を込めて
	ELSEIF TALENT:好奇心 && RAND:3 == 0
		PRINTFORM 興味深そうに
	ELSEIF MARK:屈服刻印 == 0 && TEQUIP:押し倒し == 0 && RAND:3 == 0
		PRINTFORM 複雑な顔をしながら
	ELSEIF RAND:3 == 0
		PRINTFORM 激しく
	ELSEIF RAND:2 == 0
		PRINTFORM グチュグチュと
	ELSE
		PRINTFORM ゆっくりと
	ENDIF
	PRINTFORMW 扱きはじめた…
ENDIF


@COM_ORDER_COM105, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF

;強気
IF TALENT:強気
	LOCAL += 10
	LOCALS = %LOCALS% + 強気(10)
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 5
	LOCALS = %LOCALS% - 恥じらい(5)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF

;汚れあり
IF COM_ORDER_STAIN("Ｐ", "")/5
	LOCAL -= COM_ORDER_STAIN("Ｐ", "")/5
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "")/5})
ENDIF

;合計を表示(25以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 25

RETURN RESULT
