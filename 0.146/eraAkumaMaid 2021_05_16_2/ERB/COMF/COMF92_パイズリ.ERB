@COM92
;パイズリ
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 92

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
IF CHECK_COM("パイズリフェラ")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 91
	CALL SAMEN_CHECK, 50
	SELECTCOM = 92
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM92, ARG
SIF CHECK_COM("パイズリフェラ") == 92
	CALL FLAG_COM91, 50
;対象のＢ⇔調教者のＰの汚れが移動
CALL EQ_STAIN, TARGET, "Ｂ", PLAYER, "Ｐ"
SIF ASSIPLAY == 0 && EXP:フェラ経験 >= EXPLV:3
	TFLAG:好感度ボーナス += 1

@SOURCE_COM92, ARG
;パイズリフェラ
SIF CHECK_COM("パイズリフェラ") == 92
	CALL SOURCE_COM91, 50

CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "パイズリ"

CALL SOURCE_DOWNBASE_HP, 92

SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｂ")*40
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:屈従 = 1800
SOURCE:0:逸脱 = 300

;巨乳
SIF TALENT:巨乳
	SOURCE:0:逸脱 -= 200
;貧乳
SIF TALENT:貧乳
	SOURCE:0:逸脱 += 400

;ABL:Ｂ感覚をみる
SOURCE:0:Ｂ快楽 = CALCF("Ｂ刺激", 2)

SIF TALENT:Ｂ性向
	CALL CALC, "好みのプレイ", SOURCE:0:Ｂ快楽

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM92
IF TALENT:巨乳 && RAND:3 == 0
	LOCALS = %TOFULL(CONDS("バストカップ") )%カップの%TALENT_NAME(GETNUM(TALENT, "巨乳"), TALENT:巨乳)%
ELSEIF TALENT:オトコ == 0 && RAND:2
	LOCALS = 乳房
ELSE
	LOCALS = 胸
ENDIF
IF CHECK_COM("Ｗ奉仕")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 二人は
		IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:3 == 0
			PRINT うっとりしながら
		ELSEIF RAND:2 == 0
			PRINT 夢中になって
		ENDIF
		PRINTFORMW %CALLNAME:MASTER%のペニスを胸で扱き続けている…
	ELSE
		PRINTFORM %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%は
		SIF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 + TALENT:淫乱 + TALENT:恋慕 && RAND:2 == 0
			PRINTFORM 上気した瞳で%CALLNAME:MASTER%を見つめながら、
		IF RAND:3 == 0
			PRINTFORMW 競うようにしてペニスに胸を擦りつけてきた…
		ELSEIF RAND:2 == 0
			PRINTFORMW 胸の谷間でペニスを挟み込んで刺激してきた…
		ELSE
			PRINTFORMW 協力してペニスを胸で扱いてきた…
		ENDIF
	ENDIF
ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
;同時押し倒し
ELSEIF TEQUIP:Ｗ押し倒し
	PRINTFORM %CALLNAME:TARGET%は
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM 、%TEXTS("Ｗ押し倒し中")%
	ENDIF
	IF SELECTCOM == PREVCOM
		PRINTFORMW %CALLNAME:MASTER%のペニスを胸で挟んで熱心に擦りあげ続けている…
	ELSE
		PRINTFORMW %CALLNAME:MASTER%のペニスを胸で挟んで擦りあげてきた…
	ENDIF
;パイズリフェラ
ELSEIF CHECK_COM("パイズリフェラ")
	PRINTFORM %CALLNAME:TARGET%は
	IF TALENT:恋慕 && RAND:4 == 0
		PRINTFORM 、うっとりしながら%CALLNAME:PLAYER%の
	ELSEIF ABL:奉仕精神 >= 3 && RAND:3 == 0
		PRINTFORM %CALLNAME:PLAYER%の顔を見つめながら
	ELSEIF ABL:技巧 >= 3 && RAND:2 == 0
		PRINTFORM 緩急をつけながら%CALLNAME:PLAYER%の
	ELSE
		PRINTFORM %CALLNAME:PLAYER%の
	ENDIF
	IF RAND:4 == 0
		PRINTFORM 亀頭を舌で擦り上げつつ、
	ELSEIF RAND:3 == 0
		PRINTFORM 尿道を舌先で刺激しつつ、
	ELSEIF RAND:2
		PRINTFORM ペニスに舌を這わせつつ、
	ELSE
		PRINTFORM ペニスをしゃぶりつつ、
	ENDIF
	;妊娠
	IF TALENT:妊娠 && TALENT:貧乳 == 0 && RAND:3 == 0
		PRINT パンパンに張った
	;巨乳
	ELSEIF TALENT:巨乳
		;爆乳
		IF TALENT:巨乳 >= 2 && RAND:3 == 0
			PRINT ずっしりと重量感のある
		ELSEIF RAND:2
			PRINT 弾力に富んだ
		ELSE
			PRINT 豊かな
		ENDIF
	;貧乳
	ELSEIF TALENT:貧乳
		PRINT 小さな
		LOCALS = ふくらみ
	ELSEIF TALENT:オトコ == 0 
		IF RAND:3 == 0
			PRINT ほどよい弾力の
		ELSEIF RAND:2
			PRINT 張りのある
		ELSE
			PRINT 形の良い
		ENDIF
	ENDIF

	PRINTFORM %LOCALS%でペニスを
	IF TALENT:巨乳 >= 2 && RAND:2
		PRINTFORM 包んで
	ELSEIF TALENT:貧乳 == 0 && TALENT:オトコ == 0 
		PRINTFORM 挟んで
	ENDIF
	PRINTFORMW 擦りあげはじめた…
;前回と同じ
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%は、
	IF TALENT:恋慕 && RAND:4 == 0
		PRINTFORM うっとりしながら%CALLNAME:PLAYER%のペニス
	ELSEIF ABL:奉仕精神 >= 3 && RAND:3 == 0
		PRINTFORM %CALLNAME:PLAYER%の顔を見つめながらペニス
	ELSEIF ABL:技巧 >= 3 && RAND:2
		PRINTFORM 緩急をつけながら%CALLNAME:PLAYER%のペニス
	ELSEIF ABL:精液中毒 >= 3 && RAND:2
		PRINTFORM 熱心に%CALLNAME:PLAYER%のペニス
	ELSE
		PRINTFORM %CALLNAME:PLAYER%のペニス
	ENDIF
	IF TALENT:貧乳 || TALENT:オトコ
		PRINTFORMW に%LOCALS%を擦り付け続けている…
	ELSE
		PRINTFORMW を%LOCALS%で扱き続けている…
	ENDIF
;前回は胸の谷間を犯す
ELSEIF PREVCOM == 81 && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:PLAYER%が腰の動きを止めて胸で奉仕してくれるように促すと、%CALLNAME:TARGET%は
	;恋慕かつ奉仕精神5or献身的かつ奉仕精神3以上
	IF ( (TALENT:恋慕 && ABL:奉仕精神 == 5) || (TALENT:献身 && ABL:奉仕精神 >= 3) ) && RAND:3 == 0
		IF RAND:2 == 0
			PRINT 嬉しそうに
		ELSE
			PRINT 上目遣いで見つめながら
		ENDIF
	;恋慕or献身的or奉仕精神3以上
	ELSEIF TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3
		PRINT 楽しそうな表情を見せつつ
	;強気
	ELSEIF TALENT:強気 && RAND:3
		PRINTFORM 力強い瞳で%CALLNAME:PLAYER%を見つめながら、
	ENDIF
	IF TALENT:貧乳 || TALENT:オトコ
		PRINTFORM 薄い胸で懸命にペニスを
	ELSEIF TALENT:巨乳
		PRINTFORM 豊満な胸でペニスを包み込んで
	ELSE
		PRINTFORM %LOCALS%でペニスをはさんで
	ENDIF
	PRINTW 擦りあげ始めた…

;においに弱い分岐
ELSEIF PLAYER == MASTER && TALENT:においに弱い && TALENT:恋慕 && (CHOSECOM(SELECTCOM) == 0 || RAND:3 == 0)
	PRINTFORM %CALLNAME:TARGET%は
	IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ恥垢") && RANDIF(2, CHECK_STAIN(STAIN:PLAYER:Ｐ, "精液") )
		IF RAND:2
			PRINTFORM %CALLNAME:PLAYER%のペニス
			IF TALENT:貧乳 || TALENT:オトコ
				PRINTFORMW の恥垢の%TEXTS("匂い")%に興奮しながら胸を擦り付けて刺激してきた…
			ELSE
				PRINTFORMW を胸ではさむと、恥垢の%TEXTS("匂い")%で興奮しながら胸で扱いた…
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:PLAYER%の蒸れた股間の%TEXTS("匂い")%を嗅ぎながら、
			IF TALENT:貧乳 || TALENT:オトコ
				PRINTFORMW ペニスに胸を擦り付けはじめた…
			ELSE
				PRINTFORMW ペニスを乳房で挟んで擦りはじめた…
			ENDIF
		ENDIF
	ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "精液") && RAND:2
		IF RAND:2
			PRINTFORM 精液で汚れた%CALLNAME:PLAYER%のペニス
			IF TALENT:貧乳 || TALENT:オトコ
				PRINTFORMW に身を寄せると、%TEXTS("匂い")%を嗅ぎながら興奮した様子で胸を擦り付けてきた…
			ELSE
				PRINTFORMW を胸で挟むと、%TEXTS("匂い")%を嗅ぎながら興奮した様子で扱いてきた…
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:PLAYER%の精液の%TEXTS("匂い")%がするペニス
			IF TALENT:貧乳 || TALENT:オトコ
				PRINTFORMW に身を寄せると、嬉しそうな表情で胸を擦り付けて奉仕してきた…
			ELSE
				PRINTFORMW を胸で挟むと、嬉しそうな表情で上下に胸を揺らした…
			ENDIF
		ENDIF
	ELSEIF TALENT:貧乳 || TALENT:オトコ
		PRINTFORMW 匂い立つペニスに胸で奉仕しながら、蒸れた%CALLNAME:PLAYER%の股間の%TEXTS("匂い")%を嗅いで興奮している…
	ELSE
		IF RAND:2
			PRINTFORMW 匂い立つペニスを胸に挟んで擦りながら、蒸れた%CALLNAME:PLAYER%の股間の%TEXTS("匂い")%を嗅いで興奮している…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%のペニスを乳房で扱きながら、時折先端に鼻を近づけ%TEXTS("匂い")%を嗅いでいる…
		ENDIF
	ENDIF
;押し倒されているとき
ELSEIF TEQUIP:押し倒し
	IF TEQUIP:顔面騎乗
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の顔に跨りながら、胸元を股間に寄せる…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の下半身を押さえ込み、胸元を股間に寄せる…
	ENDIF
	;淫乱orサドorサドっ気Lv.5以上で「恥じらい」を持たない
	SIF (TALENT:淫乱 || TALENT:小悪魔 || ABL:サドっ気 >= 5) && TALENT:恥じらい == 0
		PRINT 唾液を垂らしてローション代わりにしながら、
	PRINTFORM %CALLNAME:TARGET%は
	;胸元が露出しているなら、そこではさむ
	IF CHECK_CLO("谷間露出") && RAND:3
		IF TEQUIP:シックスナイン
			PRINTFORM %TEXTS("６９")%
		ELSEIF TALENT:貧乳
		ELSEIF TALENT:巨乳 && RAND:2 == 0
			PRINT 谷間を覗かせている
		ELSE
			PRINT 大胆に露出している
		ENDIF
	;はだけられるのに、胸元をはだけていないなら、ずらす
	ELSEIF CHECK_CLO("はだけ可")
		IF CHECK_SHIRT("はだけ可")
			PRINTFORM %SHIRT(TARGET, "略")%を捲って、
		ELSE
			PRINT 胸元をはだけて、
		ENDIF
		CALL SETFLAG, "胸をはだける", TARGET
	ENDIF
	IF TALENT:貧乳 || TALENT:オトコ
		PRINT 薄い胸でペニスを
	ELSEIF GETBITS(CFLAG:妊娠で巨乳化, 0, 1) && RAND:2 == 0
		PRINT 以前とは違う大きな胸でペニスをはさんで
	ELSEIF TALENT:巨乳 && RAND:2
		PRINT 豊満な胸でペニスを包み込んで
	ELSE
		PRINTFORM %LOCALS%でペニスをはさんで
	ENDIF
	PRINTW 擦りあげた…
;お風呂(胸は当然剥き出しと想定)
ELSEIF TEQUIP:お風呂プレイ
	PRINTFORM %CALLNAME:TARGET%は
	IF TALENT:貧乳 || TALENT:オトコ
		PRINTW 泡をまぶした薄い胸で、懸命にペニスを擦り洗ってきた…
	ELSEIF TALENT:巨乳
		PRINTW 谷間にたっぷりと泡を湛えた豊満な胸で、ペニスを根元まで包み込んで揉み洗ってきた…
	ELSE
		PRINTW 泡立てた胸をスポンジに見立てて、ペニスをはさんで洗ってきた…
	ENDIF
;それ以外
ELSE
	PRINTFORM %CALLNAME:TARGET%は
	;胸元が露出しているなら、そこではさむ
	IF CHECK_CLO("谷間露出")
		IF TEQUIP:シックスナイン
			PRINTFORM %TEXTS("６９")%
		ELSEIF TALENT:貧乳
		ELSEIF TALENT:巨乳 && RAND:2 == 0
			PRINT 谷間を覗かせている
		ELSE
			PRINT 大胆に露出している
		ENDIF
	;はだけられるのに、胸元をはだけていないなら、ずらす
	ELSEIF CHECK_CLO("はだけ可")
		IF CHECK_SHIRT("はだけ可")
			PRINTFORM %SHIRT(TARGET, "略")%を捲って、
		ELSE
			PRINT 胸元をはだけて、
		ENDIF
		CALL SETFLAG, "胸をはだける", TARGET
	ENDIF
	IF TALENT:貧乳 || TALENT:オトコ
		PRINTFORM 薄い胸で懸命にペニスを
	ELSEIF TALENT:巨乳
		PRINTFORM 豊満な胸でペニスを包み込んで
	ELSE
		PRINTFORM %LOCALS%でペニスをはさんで
	ENDIF
	PRINTW 擦りあげた…
ENDIF
;サドっ気LV3以上で尿道刺激、ボールギャグ使用中は不可
SIF ABL:サドっ気 >= 3 && TEQUIP:口枷 == 0 && RAND:2 == 0
	PRINTFORMW 同時に、尿道を尖らせた舌先で刺激してくる…


@COM_ORDER_COM92, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	LOCALS = %LOCALS% - 恥じらい(3)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF
;Ｂ性向
IF TALENT:Ｂ性向
	LOCAL += 10
	LOCALS = %LOCALS% + Ｂ性向(10)
ENDIF
;オトコ
IF TALENT:オトコ && TALENT:両刀 == 0
	LOCAL -= 15
	LOCALS = %LOCALS% - オトコ(15)
ENDIF

;汚れあり
IF COM_ORDER_STAIN("Ｐ", "Ｂ")
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｂ")
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｂ")})
ENDIF

;合計を表示(30以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
