@COM141
;フィストファック
PRINTL フィストファック

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = フィストファック

RETURN 1

@FLAG_COM141, ARG
CALL FLAG_INSERTION, "Ｖフィスト", ARG, 100
;アソコを露出させる
SIF CHECK_CLO("Ｖ露出") == 0
	CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET

@SOURCE_COM141, ARG
CALL SOURCE_INSERTION, "Ｖフィスト", ARG, 100
CALL SOURCE_SMPLAY, "フィスト", ARG

@TRAIN_MESSAGE_COM141
#DIM FLAG_MESSAGE
#DIM FLAG_MESSAGE2
#DIM SIZE_FIST

IF SELECTCOM == PREVCOM || SELECTCOM == PREVCOM:2
	PRINTFORM %CALLNAME:PLAYER%は
	IF COND("前Ｔ調教者") == PLAYER
		PRINTFORMW %CALLNAME:TARGET%の膣を執拗に\@ CHECK_COM("二本フィスト") ? 二本の腕 # 拳 \@で攪拌し続けた…
	ELSE
		IF PLAYER == MASTER
			PRINTFORM %CALLNAME:ASSI%
		ELSE
			PRINTFORM %CALLNAME:MASTER%
		ENDIF
		PRINTFORMW に続いて、%CALLNAME:TARGET%の膣を拳でズボズボと掻き回した…
	ENDIF
	RETURN 1
ENDIF
IF ABL:PLAYER:サドっ気 >= 3 && ABL:Ｖ拡張 >= SIZE("二本フィスト") - 1
	PRINTFORML %CALLNAME:TARGET%の\@ ABL:Ｖ名器度 <= 20 ? ユルい # 拡張された \@膣なら、両腕同時でもいけそうです。
	PRINTFORML どうしますか？
	PRINTFORML [ 0] 両腕を捻じ込む
	PRINTFORML [ 1] 片手で楽しむ
	CALL INPUT_SELECT, 2
	SIF RESULT == 0
		CALL SETFLAG, "二本フィスト", PLAYER
ENDIF
IF CHECK_COM("二本フィスト")
	SIZE_FIST = SIZE("二本フィスト", PLAYER)
ELSE
	SIZE_FIST = SIZE("片手フィスト", PLAYER)
ENDIF
;膣の拡張具合の余裕 FLAG_MESSAGE == 3 でぴったり　2以下はかなり厳しい
FLAG_MESSAGE = 3 + ABL:Ｖ拡張 - SIZE_FIST
FLAG_MESSAGE2 = RAND:3

PRINTFORM %CALLNAME:PLAYER%は
;前回アナルフィスト
IF PREVCOM == 142 && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%のアナルを犯していた腕を引き抜いて、今度は
	IF TALENT:処女
		PRINT 再生された膜で塞がれた
	ELSEIF FLAG_MESSAGE >= 4 && RAND:2 == 0
		PRINT 緩められた
	ELSEIF PALAM:Ｖ潤 < 500
		PRINT 濡れていない
	ELSEIF PALAM:Ｖ潤 >= 10000 && CHECK("Ｖフィスト")
		PRINT 涎を垂らしている
	ENDIF
	PRINT 膣穴に
ELSE
	;前回指挿入れ
	IF PREVCOM == 8 && COND("前Ｔ調教者") == PLAYER
		PRINTFORM %CALLNAME:TARGET%の膣から指を引き抜いて愛液を手に塗り広げると、
	ELSE
		;シックスナイン
		IF TEQUIP:シックスナイン
			PRINTFORM %TEXTS("６９")%
		ELSEIF TEQUIP:拘束
			PRINT 大きく股を開いた格好で縛られている
		ELSEIF (ABL:マゾっ気 >= 3 || (FLAG_MESSAGE >= 2 && ABL:Ｖ感覚 >= 4) ) && FLAG_MESSAGE2
			IF TALENT:好色 + TALENT:淫乱 && FLAG_MESSAGE >= 3 && ABL:Ｖ感覚 >= 3 && RAND:3 == 0
				PRINT アソコを指で拡げて
			ELSEIF ABL:信頼 + ABL:欲望 >= 6 && RAND:3 == 0
				PRINT 両腕で脚を抱えて
			ELSEIF RAND:2 == 0
				PRINT 股を広げて
			ELSE
				PRINT 腰を突き出して
			ENDIF
			IF TALENT:淫乱 || FLAG_MESSAGE >= 3
				PRINT 誘っている
			ELSEIF RAND:2 == 0
				PRINT 待ち構えている
			ELSE
				PRINT 準備している
			ENDIF
		ENDIF
		PRINTFORM %CALLNAME:TARGET%の
		SIF (ABL:マゾっ気 < 3 || FLAG_MESSAGE2 == 0) && TEQUIP:拘束 + TEQUIP:シックスナイン == 0 && ABL:信頼 + ABL:欲望 < 6
			PRINT 脚を大きく開かせると、
	ENDIF
	IF TALENT:処女
		PRINT 処女膜に
	ELSE
		IF FLAG_MESSAGE >= 4 && RAND:2 == 0
			PRINT 緩められた
		ELSEIF PALAM:Ｖ潤 < 500
			PRINT 濡れていない
		ELSEIF PALAM:Ｖ潤 >= 10000
			PRINT ビショビショの
		ENDIF
		PRINT 膣口に
	ENDIF

	;拡張ＬＶがかなりあるなら握り拳ルート
	IF FLAG_MESSAGE >= 4 && RAND:2
		PRINTFORM \@ CHECK_COM("二本フィスト") ? 両手の # \@握り拳
		;拳フラグ
		SETBIT TFLAG:地の文分岐, 0
		FLAG_MESSAGE -= 1
	ELSEIF CHECK_COM("二本フィスト")
		PRINTFORM 合掌させた指先
	ELSEIF FLAG_MESSAGE >= 2 && RAND:2 == 0
		PRINTFORM 揃えた指先
	ELSE
		PRINTFORM すぼめた指先
	ENDIF
	PRINT を押し当て、
ENDIF
IF TALENT:恋慕 && TEQUIP:シックスナイン == 0 && RAND:3 == 0
	PRINT 顔を見つめながら
ELSEIF ABL:PLAYER:サドっ気 >= 3 && RAND:3 == 0
	PRINT 力任せに
ELSEIF FLAG_MESSAGE >= 4 && RAND:3 == 0
	PRINT 抵抗を感じることなく
ELSEIF FLAG_MESSAGE >= 3 && RAND:3 == 0
	PRINT 一気に
ELSEIF FLAG_MESSAGE >= 2 && RAND:2 == 0
	PRINT 捻りながら
ELSEIF (FLAG_MESSAGE >= 2 && TFLAG:地の文分岐) || FLAG_MESSAGE <= 2
	PRINT 慎重に
ELSE
	PRINT ゆっくりと
ENDIF
IF FLAG_MESSAGE >= 4 && CHECK_COM("二本フィスト") == 0 && RAND:2
	IF FLAG_MESSAGE >= 7 && RAND:2 == 0
		PRINT 肘まで
	ELSEIF FLAG_MESSAGE >= 5 && RAND:2 == 0
		PRINT 腕の半ばまで
	ELSEIF TALENT:小柄 && RAND:2 == 0
		PRINT お腹がぽっこりするまで
	ELSE
		PRINT 下腹が微かに膨らむまで
	ENDIF
ELSEIF FLAG_MESSAGE >= 4 || (FLAG_MESSAGE >= 3 && RAND:2)
	IF RAND:2 == 0
		PRINT 子宮口に当たるまで
	ELSE
		PRINT 膣の最奥まで
	ENDIF
ELSEIF FLAG_MESSAGE <= 2
	PRINT 手の甲まで
ELSE
	PRINT 手首まで
ENDIF
IF RAND:3 == 0
	PRINT 埋め込
ELSEIF RAND:2 == 0
	PRINT 捻じ込
ELSE
	PRINT 押し込
ENDIF
IF TFLAG:地の文分岐 == 0 && FLAG_MESSAGE >= 3 && RAND:2
	PRINTFORMW むと、中で\@ CHECK_COM("二本フィスト") ? 両手を握り合わせた。 # 握りこぶしを作った。 \@
	SETBIT TFLAG:地の文分岐, 0
	FLAG_MESSAGE -= 1
ELSE
	PRINTFORMW んだ。
ENDIF

;1bit膣口、 2bit膣道、3bit膣奥を攻めるイメージ
SETBIT TFLAG:地の文分岐, 1 + RAND:3

IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 3) && GETBIT(TFLAG:地の文分岐, 3)
	IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 4) && RAND:3 == 0
		PRINT 容赦なく
	ELSEIF RAND:2
		PRINT 勢い良く
	ELSE
		PRINT ズボズボと
	ENDIF
	IF FLAG_MESSAGE >= 4 && RAND:2 == 0
		PRINT 最奥に
	ELSEIF FLAG_MESSAGE >= 3
		PRINT 子宮に
	ELSE
		PRINT 敏感な場所に
	ENDIF
	PRINT 叩き込まれる
ELSEIF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1)
	PRINT ズボズボと
	SIF RAND:2 == 0
		PRINT 激しく
	PRINT 出し入れされる
ELSE
	IF ABL:Ｖ感覚 >= 3 && GETBIT(TFLAG:地の文分岐, 2)
		PRINT Ｇスポットも刺激しながら
	ELSEIF FLAG_MESSAGE >= 3  && RAND:2 == 0
		PRINT 中を探るように
	ELSE
		PRINT 中を拡げるように
	ENDIF
	PRINT 動かされる
ENDIF
IF GETBIT(TFLAG:地の文分岐, 0)
	PRINTFORM \@ CHECK_COM("二本フィスト") ? 束ねられた # \@握り拳
ELSEIF CHECK_COM("二本フィスト")
	PRINTFORM 両腕
ELSEIF FLAG_MESSAGE <= 1
	PRINTFORM 手
ELSE
	SIF RAND:2 && TALENT:PLAYER:オトコ && COND("男の娘", PLAYER) == 0
		PRINTFORM \@ RAND:2 ? 男の # 太い \@
	PRINTFORM 腕
ENDIF
;膣口描写
IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1)
	PRINT に
;受け入れられる
ELSEIF FLAG_MESSAGE >= 4
	PRINT を
;まだまだ厳しい
ELSE
	PRINT に
ENDIF
;前回アナルフィストならＡとＶの大きさ比較
IF PREVCOM == 142 && ABL:Ｖ拡張 > ABL:Ａ拡張 && CHOSECOM(SELECTCOM) == 0 && FLAG_MESSAGE >= 4
	IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1)
		PRINT 、肛門
	ELSE
		IF RAND:2
			PRINT 、後ろの穴
		ELSE
			PRINT 、アナル
		ENDIF
	ENDIF
	IF ABL:Ｖ拡張 > ABL:Ａ拡張 + 1
		PRINTFORM とは比べものにならない程に拡張された
	ELSE
		PRINTFORM よりもずっと柔らかな
	ENDIF
ENDIF
SIF TALENT:小柄 && RAND:3 == 0
	PRINT 幼い
IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1)
	PRINT 膣口は
ELSE
	PRINT 膣は
ENDIF
IF FLAG_MESSAGE >= 4
	IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1)
		IF RAND:2 == 0
			PRINT ぽっかりと
		ELSE
			PRINT 驚くほど
		ENDIF
		PRINT 拡げられ、
	ELSE
		IF GETBIT(TFLAG:地の文分岐, 3) && RAND:3 == 0
			PRINT 深々と
		ELSEIF RAND:2 == 0
			PRINT 柔らかく
		ELSE
			PRINT 楽々
		ENDIF
		PRINT 受け止め、
	ENDIF
ELSEIF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1) && RAND:2 == 0
	PRINT 捲りあげられ、
ELSEIF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 3) && GETBIT(TFLAG:地の文分岐, 3) && RAND:2 == 0
	PRINT パンパンに拡げられ、
ELSEIF FLAG_MESSAGE >= 2 || ABL:Ｖ感覚 >= 5 || PALAM:Ｖ潤 >= PALAMLV:4 || RAND:2 == 0
	PRINT 裂けそうなほどに押し広げられ、
ELSE
	PRINT 無残に傷つけられ、
ENDIF
;初めて？
IF FIRSTCOMS("フィスト") == 0
	PRINT 初めて味わう
ELSE
	PRINT その
ENDIF
;乱数分岐
IF CHECK("Ｖフィスト") && FLAG_MESSAGE >= 4
	PRINT たまらない
ELSEIF RAND:2
	PRINT 圧倒的な
ELSE
	PRINT 暴力的な
ENDIF
;Ｖ快感で分岐
IF CHECK("Ｖフィスト") && ABL:Ｖ感覚 >= SIZE_FIST && RAND:3 == 0
	PRINT 快感に
ELSEIF FLAG_MESSAGE <= 2 && (RAND:2 || CUP:苦痛 >= 500)
	PRINT 拡張感に
ELSE
	PRINT 挿入感に
ENDIF
PRINTFORM %CALLNAME:TARGET%は
;倒錯的かマゾっ気が3以上
IF (ABL:マゾっ気  >= SIZE_FIST || TALENT:倒錯的) && SIZE_FIST >= 3 && RAND:2
	IF CHECK("Ｖフィスト")
		PRINTFORM 自分の大切な所が\@ ABL:Ｖ拡張 >= 6 && RAND:2 ? 取り返しがつかない程に # \@拡張されてしまったと興奮し、
	ELSE
		PRINTFORM 自分の大切な所を\@ ABL:Ｖ名器度 >= 50 && RAND:2 ? ガバガバに # 壊 \@されるかもしれないと興奮し、
	ENDIF
ELSEIF CHECK("Ｖフィスト") && ABL:Ｖ感覚 >= SIZE_FIST && RAND:2
	PRINT 悦楽に蕩けた艶やかな声をあげて
ELSEIF (ABL:マゾっ気 < 3 || FLAG_MESSAGE <= 2) && CHECK("Ｖフィスト") == 0 && RAND:2
	PRINT 目尻に涙を浮かばせて
;プライド高いか恥じらい
ELSEIF (TALENT:高慢 || TALENT:恥じらい) && RAND:3
	PRINT 顔を真っ赤に染めて
ELSE
	PRINT 獣のような声をあげて
ENDIF
PRINTW 激しく身をくねらせた…
