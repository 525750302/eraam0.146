@COM81
;強制パイズリ(胸の谷間を犯す)

PRINTL 胸の谷間を犯す

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = 胸の谷間を犯す

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM81, ARG
;対象のＢ⇔調教者のＰの汚れが移動
CALL EQ_STAIN, TARGET, "Ｂ", PLAYER, "Ｐ"
SIF ASSIPLAY == 0 && ABL:Ｂ感覚 >= 3
	TFLAG:好感度ボーナス += 1


@SOURCE_COM81, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 81

SOURCE:0:屈従 = 3000
SOURCE:0:逸脱 = 900
SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｂ")*40

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
CASE 0
	SOURCE:0:性行動 = 420
	SOURCE:0:達成感 = 150
CASE 1
	SOURCE:0:性行動 = 500
	SOURCE:0:達成感 = 300
CASE 2
	SOURCE:0:性行動 = 580
	SOURCE:0:達成感 = 600
CASE 3
	SOURCE:0:性行動 = 660
	SOURCE:0:達成感 = 900
CASE 4
	SOURCE:0:性行動 = 740
	SOURCE:0:達成感 = 1500
CASEELSE
	SOURCE:0:性行動 = 820
	SOURCE:0:達成感 = 2200
ENDSELECT

;ABL:Ｂ感覚をみる
SOURCE:0:Ｂ快楽 = CALCF("Ｂ刺激", 2, 70)
SOURCE:0:Ｂ快楽 = SOURCE_EXABL(SOURCE:0:Ｂ快楽, "指使い", PLAYER)

SIF TALENT:Ｂ性向
	CALL CALC, "好みのプレイ", SOURCE:0:Ｂ快楽

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM81
IF SELECTCOM == PREVCOM
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の胸を犯し続けた…
	RETURN 0
ENDIF

IF TALENT:巨乳 && RAND:3 == 0
	LOCALS = %TOFULL(CONDS("バストカップ") )%カップの%TALENT_NAME(GETNUM(TALENT, "巨乳"), TALENT:巨乳)%
ELSEIF RAND:2
	IF TALENT:巨乳
		IF TALENT:巨乳 >= 2 && RAND:2
			IF RAND:2
				LOCALS = 圧倒的な大きさの乳房
			ELSE
				LOCALS = たっぷりとした重そうな乳房
			ENDIF
		ELSE
			IF RAND:2
				LOCALS = 魅力的な大きさの乳房
			ELSE
				LOCALS = たわわに実った乳房
			ENDIF
		ENDIF
	ELSE
		LOCALS = 乳房
	ENDIF
ELSE
	LOCALS = 胸
ENDIF

;胸元が露出しているなら、そこではさむ
IF CHECK_CLO("谷間露出")
;はだけられるのに、胸元をはだけていないなら、ずらす
ELSEIF CHECK_CLO("はだけ可")
	CALL SETFLAG, "胸をはだける", TARGET
ENDIF

PRINTFORM %CALLNAME:PLAYER%は
;前回はパイズリ
IF PREVCOM == 92 && COND("前Ｔと同ペア")
	;汚れてますか？汚臭鈍感で無いならば、実行値の10分の一の値とRAND:5を比べて判定
	IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｖ", "Ａ") && TALENT:汚臭鈍感 == 0 && RAND:6 > TCVAR:やる気/10
		PRINTFORM 臭いに眉根を寄せつつ
	;恋慕or献身的or奉仕精神3以上or汚臭鈍感
	ELSEIF (TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3 || TALENT:汚臭鈍感) && RAND:2 == 0
		PRINT 嬉しそうに
	;恥じらい
	ELSEIF TALENT:恥じらい && RAND:3 == 0
		PRINT 恥ずかしそうに
	;欲情4以上
	ELSEIF PALAM:欲情 >= PALAMLV:4 && RAND:3 == 0
		SIF TALENT:倒錯的 && RAND:3 == 0
			PRINTFORM %TEXTS("匂い")%に
		PRINT 興奮しつつ
	;恋慕かつ奉仕精神5or献身的かつ奉仕精神3以上
	ELSEIF ( (TALENT:恋慕 && ABL:奉仕精神 >= 5) || (TALENT:献身 && ABL:奉仕精神 >= 3) ) && RAND:3 == 0
		PRINT 熱心に
	;恋慕or献身的or奉仕精神3以上
	ELSEIF TALENT:恋慕 || TALENT:献身 || ABL:奉仕精神 >= 3
		PRINT 楽しそうに
	ELSEIF RAND:2 == 0 && ABL:技巧 < 2
		PRINT ぎこちなさの残る手つきで
	ENDIF
	PRINTFORM 奉仕している%CALLNAME:TARGET%の%LOCALS%を\@ RAND:2 ? 左右から # 両手で \@\@ RAND:2 ? 掴んで # 押さえて \@、
;ベッドに拘束しているなら相手に跨る
ELSE
	IF BIND("ベッド", TEQUIP:拘束)
		PRINTFORM ベッドに拘束されている%CALLNAME:TARGET%の
		IF AGE(TARGET) >= 3
			SELECTCASE CONDS("体型")
			CASE "太め"
				PRINTFORM 油断気味の
			CASE "細め"
				IF TALENT:力持ち
					PRINTFORM 引き締まった
				ELSE
					PRINTFORM ほっそりとした
				ENDIF
			CASEELSE
				SIF TALENT:力持ち
					PRINTFORM 引き締まった
			ENDSELECT
		ENDIF
		PRINTFORM お腹に跨って
	ELSE
		PRINTFORM %CALLNAME:TARGET%の
	ENDIF
	IF RAND:4 == 0
		PRINTFORM %LOCALS%の谷間にペニスを滑り込ませると、
	ELSEIF RAND:3 == 0
		PRINTFORM %LOCALS%の谷間にペニスを突き刺すと、
	ELSEIF RAND:2
		PRINTFORM %LOCALS%の間に向かって腰を突き出すと、
	ELSE
		PRINTFORM %LOCALS%でペニスを挟み込むと、
	ENDIF
	PRINTFORM \@ RAND:2 ? 左右から # 両手で \@\@ RAND:2 ? 掴んで # 押さえて \@
ENDIF

;快楽を与える
IF RAND:2
	IF RAND:3 == 0 && CALCF("Ｂ発情")
		PRINTFORM 乳首を指で扱きながら
	ELSEIF RAND:2
		PRINTFORM 乳輪を親指でくすぐりながら
	ELSE
		PRINTFORM 揉みしだきながら
	ENDIF
ELSE
	IF RAND:4 == 0
		PRINTFORM 内側に寄せながら
	ELSEIF RAND:3 == 0
		PRINTFORM 密着させながら
	ELSEIF RAND:2
		PRINTFORM 締め付けながら
	ELSE
		PRINTFORM 圧迫しながら
	ENDIF
ENDIF

;SIF EXP:PLAYER:Ｖ挿入経験 + EXP:PLAYER:Ａ挿入経験
;	PRINT セックスする時のように

IF ABL:PLAYER:サドっ気 && RAND:3 == 0
	PRINTFORMW 荒々しく腰を動かした…
ELSEIF RAND:2
	PRINTFORMW 腰を激しく動かした…
ELSE
	PRINTFORMW 腰を前後に動かした…
ENDIF

