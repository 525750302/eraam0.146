@COM11
;パイズリする
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK


RETURN 1

@FLAG_COM11, ARG
;対象のＰ⇔調教者の胸の汚れが移動
CALL EQ_STAIN, TARGET, "Ｐ", PLAYER, "Ｂ"

@SOURCE_COM11, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 11

SOURCE:0:情愛 = 1000
SOURCE:0:達成感 = 1000
SOURCE:0:露出 = 220
SOURCE:0:逸脱 = 50

;オトコかふたなり
IF PENIS(TARGET)
	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 1)
	SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "魔乳", PLAYER)
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM11
IF PREVCOM == SELECTCOM
	IF TEQUIP:押し倒し
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の%TEXTS("おっぱい", PLAYER)%で存分に楽しんでいる…
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%にパイズリ奉仕を続けた…
	ENDIF
ELSE
	IF TEQUIP:押し倒し
		PRINTFORM %CALLNAME:TARGET%は馬乗りになって
		IF RAND:2
			PRINTFORM 自らのペニス
		ELSE
			PRINTFORM %TEXTS("ペニス：視覚", TARGET)%ペニス
		ENDIF
		PRINTFORM を%CALLNAME:PLAYER%の%TEXTS("おっぱい", PLAYER)%で挟むと、
		IF RAND:2
			PRINTFORMW 胸の谷間を犯し始めた…
		ELSE
			PRINTFORMW 腰を動かしてパイズリを楽しみ始めた…
		ENDIF
	;それ以外
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は%TEXTS("おっぱい", PLAYER)%で%CALLNAME:TARGET%の%TEXTS("ペニス：視覚", TARGET)%ペニスを
		IF TALENT:PLAYER:巨乳 && RAND:2
			PRINT 包み込んで
		ELSEIF TALENT:PLAYER:貧乳 == 0 && RAND:2
			PRINT 挟んで
		ENDIF
		PRINTFORMW 擦りあげた…
	ENDIF
ENDIF
