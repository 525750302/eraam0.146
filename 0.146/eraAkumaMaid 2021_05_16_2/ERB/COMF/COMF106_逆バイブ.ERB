@COM106
;逆バイブ
;PLAYER側がバイブで楽しむコマンド。通常のバイブコマンドとは違い、入れっぱなしにはしない。
PRINTL 逆バイブ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 106

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = 逆バイブ
;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@TRAIN_MESSAGE_COM106
;バイブ選択
CALL SELECT_VIB, PLAYER

SIF REFUSE_CHECK()
	RETURN 0

IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%は、%CALLNAME:PLAYER%のアソコを
	IF SIZE("バイブ", PLAYER) >= 3
		PRINT 極太の
	ELSEIF SIZE("バイブ", PLAYER) == 2
		PRINT 大きな
	ENDIF
	PRINTW バイブでかき回し続けている…
	RETURN 1
ENDIF

PRINTFORM %CALLNAME:TARGET%は
IF TEQUIP:バイブ && SIZE("バイブ", PLAYER) > SIZE("バイブ", TARGET)
	IF RAND:2
		PRINT 自らが咥え込んでいるものより
	ELSE
		PRINT 自分の膣を掻き回しているものより
	ENDIF
	SIF SIZE("バイブ", PLAYER) > SIZE("バイブ", TARGET) + 1
		PRINT 遥かに
	SIF SIZE("バイブ", PLAYER) >= 3
		PRINT 凶悪な
ELSEIF SIZE("バイブ", PLAYER) >= 2 && RAND:2
	IF RAND:2
		PRINT 大きな亀頭を備えた
	ELSE
		PRINT ずっしりとした
	ENDIF
ELSEIF RAND:2
	IF RAND:2
		PRINT イボに覆われた
	ELSE
		PRINT ローションでぬめる
	ENDIF
ELSE
	IF PLAYER == MASTER && ASSI && RAND:2
		PRINTFORM %CALLNAME:ASSI%に手渡された
	ELSE
		PRINT 用意された
	ENDIF
ENDIF
IF SIZE("バイブ", PLAYER) >= 3
	PRINT 極太の
ELSEIF SIZE("バイブ", PLAYER) == 2
	PRINT 大きな
ENDIF
PRINT バイブを、
IF (TALENT:恋慕 || TALENT:PLAYER:好色) && RAND:2
	PRINT 脚を開いて誘っている
ELSE
	PRINT 無防備に目の前に晒された
ENDIF
PRINTFORM %CALLNAME:PLAYER%の
IF COND("陰毛", PLAYER) && COND("陰毛", PLAYER) <= 3 && RAND:3 == 0
	PRINT ツルツルの
ELSEIF TALENT:PLAYER:小柄 && RAND:3 == 0
	PRINT 幼い
ELSEIF RAND:2
	PRINT 濡れた
ENDIF
PRINT アソコに、
IF ABL:サドっ気 >= 3 && RAND:3 == 0
	PRINT 捻りながら
ELSEIF ABL:PLAYER:Ｖ拡張 >= SIZE("バイブ", PLAYER) && RAND:2
	IF ABL:PLAYER:Ｖ拡張 > SIZE("バイブ", PLAYER) && RAND:2
		PRINT 抵抗を感じることなく
	ELSEIF RAND:2 == 0
		PRINT 一気に
	ELSE
		PRINT 深々と
	ENDIF
	SIF ABL:PLAYER:Ｖ拡張 > SIZE("バイブ", PLAYER) && RAND:2
		PRINT 根元まで
ELSEIF RAND:2
	PRINT 肉襞を擦り上げながら
ELSE
	PRINT ゆっくりと
ENDIF
IF ABL:PLAYER:Ｖ拡張 >= SIZE("バイブ", PLAYER) && RAND:3 == 0
	IF RAND:2
		PRINTW 押し込んだ。
	ELSE
		PRINTW 突っ込んだ。
	ENDIF
ELSEIF RAND:2
	PRINTW 埋め込んだ。
ELSE
	PRINTW 挿入した。
ENDIF

LOCAL = RAND:2

IF LOCAL == 0
ELSEIF ABL:サドっ気 >= 3 && RAND:3 == 0
	PRINT ガシガシと凄い勢いで
ELSEIF ABL:技巧 >= 3 && RAND:3 == 0
	PRINT 長いストロークで
ELSEIF RAND:2
	PRINT うねりながら
ELSE
	PRINT ゆっくりと
ENDIF
IF (ABL:技巧 >= 4 || ABL:サドっ気 >= 3) && RAND:3 == 0
	IF LOCAL == 0 && RAND:2 == 0
		PRINT 容赦なく
	ELSEIF SIZE("バイブ", PLAYER) >= 2
		PRINT 膣奥に
	ENDIF
	PRINT 叩き込まれる
ELSEIF RAND:2
	PRINT 出し入れされる
ELSE
	PRINT 膣壁を掻き回す
ENDIF
PRINT バイブ
IF LOCAL
	PRINT で
ELSEIF SIZE("バイブ", PLAYER) >= 2 && RAND:3 == 0
	IF RAND:2 && ABL:PLAYER:Ｖ拡張 <= SIZE("バイブ", PLAYER)
		PRINT の膣を壊しかねない太さで
	ELSE
		PRINT が膣を押し広げる感覚で
	ENDIF
ELSEIF (ABL:PLAYER:Ｖ拡張 < SIZE("バイブ", PLAYER) && ABL:サドっ気 >= 3) || (SIZE("バイブ", PLAYER) >= 3 && RAND:3 == 0)
	PRINT の凶暴さで
ELSEIF RAND:3 == 0
	PRINT の蠢きで
ELSEIF RAND:2
	PRINT の威力で
ELSE
	PRINT の気持ちよさで
ENDIF
PRINTFORM %CALLNAME:PLAYER%が
IF ABL:PLAYER:Ｖ拡張 >= SIZE("バイブ", PLAYER) && EXP:PLAYER:Ｖ経験 >= EXP_LV(SIZE("バイブ", PLAYER) + 1) && RAND:3 == 0
	IF (TALENT:恋慕 || TALENT:PLAYER:好色) && RAND:2
		PRINT 大股開いてよがる
	ELSEIF RAND:2
		PRINT 愛液を垂れ流す
	ELSE
		PRINT 恍惚としている
	ENDIF
ELSEIF ABL:PLAYER:Ｖ拡張 < SIZE("バイブ", PLAYER) && ABL:サドっ気 >= 3
	PRINT 泣きそうになっている
	LOCAL = 2
ELSEIF RAND:3 == 0
	PRINT 腰を突き出す
ELSEIF RAND:2
	PRINT 身体を跳ねさせる
ELSE
	PRINT 甘い吐息を漏らす
ENDIF
PRINTFORM 様子を
IF RAND:2
	PRINT 横目で見ながら、
ELSE
	PRINT 眺めながら、
ENDIF
PRINTFORM %CALLNAME:TARGET%は
IF LOCAL == 2
	PRINTW 嗜虐の楽しみに浸っている…
ELSEIF TALENT:賢い && RANDIF(3, TALENT:小悪魔 + TALENT:陽気, TALENT:意地悪)
	PRINTW 巧みに言葉責めをしている…
ELSEIF TALENT:意地悪 && RANDIF(3, TALENT:小悪魔 + TALENT:陽気)
	IF ABL:PLAYER:Ｖ拡張 >= 3
		PRINT 膣の緩さを
	ELSE
		PRINT 淫乱振りを
	ENDIF
	PRINTW 意地悪く詰っている…
;陽気
ELSEIF TALENT:陽気 + TALENT:小悪魔 && RANDIF(3, TALENT:小悪魔)
	SIF TALENT:小悪魔
		PRINT ニヤニヤと
	PRINTW 楽しそうに微笑んでいる…
ELSEIF ABL:サドっ気 >= 3 && RAND:3 == 0
	IF SIZE("バイブ", PLAYER) >= 2 && RAND:2
		PRINTW 野太いソレでいじめ続けている…
	ELSE
		PRINTW 心配しているように見せかけて言葉責めをしている…
	ENDIF
ELSE
	;IF RAND:2
	;	PRINT 愛撫も交えながら
	;ELSE
	;	PRINT 動きを変えたりしながら
	;ENDIF
	IF ABL:欲望 >= 3 && SIZE("バイブ", PLAYER) >= 2 && RANDIF(2, ABL:サドっ気 >= 3, ABL:技巧 >= 3, ABL:奉仕精神 >= 3)
		PRINTW お腹を撫でて胎内で暴れるバイブの感触を楽しんでいる…
	ELSEIF ABL:奉仕精神 >= 3 && RANDIF(2, ABL:サドっ気 >= 3, ABL:技巧 >= 3)
		PRINTW 更なる奉仕に没頭していった…
	ELSEIF ABL:技巧 >= 3 && RANDIF(2, ABL:サドっ気 >= 3)
		PRINTW 更に巧みに責めていった…
	ELSEIF ABL:サドっ気 >= 3 && RAND:2
		PRINTW 更に責めを激しくしていった…
	ELSE
		PRINTW 奉仕を続けた…
	ENDIF
ENDIF

@FLAG_COM106, ARG
IF TALENT:PLAYER:処女
	CALL SETFLAG, "処女喪失フラグ", PLAYER
	CALL SETFLAG, "処女簒奪フラグ", TARGET
ENDIF

@SOURCE_COM106, ARG
CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "逆バイブ"

CALL SOURCE_DOWNBASE_HP, 106

SOURCE:0:情愛 = 1000
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:逸脱 = 500

CALL CALC, "調教ソース", ARG


@COM_ORDER_COM106, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:サドっ気
IF ABL:サドっ気
	LOCAL += MIN(ABL:サドっ気, 5) * 5
	LOCALS = %LOCALS% + サドっ気LV{ABL:サドっ気}({MIN(ABL:サドっ気, 5) * 5})
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF
;オトコ
IF TALENT:オトコ
	LOCAL += 20
	LOCALS = %LOCALS% + オトコ(20)
ENDIF

;合計を表示(30以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 30

RETURN RESULT
