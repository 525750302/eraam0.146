@COM70
;正常位アナル
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM70, ARG
CALL FLAG_INSERTION, "Ａインサート", ARG, 100

@SOURCE_COM70, ARG
CALL SOURCE_INSERTION, "Ａインサート", ARG, 100

CALL VARSET_COMF

SOURCE:0:露出 = 1000
SOURCE:0:情愛 = 500

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:情愛, 0.30
CASE IS < PALAMLV:2
	TIMES SOURCE:0:情愛, 0.60
CASE IS < PALAMLV:3
	TIMES SOURCE:0:情愛, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:情愛, 1.50
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:情愛, 1.80
ENDSELECT

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM70
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
CALL TRAIN_MESSAGE_INSERTION
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

;前回は助手を犯していた
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
	CALL ADD_TEXT, "性器から性器へ"
;アナルが露出していないで、パンツをずらしていないなら自動的にずらす
ELSEIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
	SIF TEQUIP:拘束
		PRINTFORM 拘束されている
	PRINTFORM %CALLNAME:TARGET%の
	CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET
;アナルセックスに慣れている、かつ欲情LV4以上で縛られてないと寝転がって誘ってきます
ELSEIF CHECK("Ａインサート") && PALAM:欲情 >= PALAMLV:4 && TEQUIP:拘束 == 0 && RAND:2 == 0
	IF PREVCOM == 13
		PRINT アソコを指で広げて
	ELSEIF PREVCOM == 14
		PRINT お尻を指で押し広げて
	;淫乱
	ELSEIF (TALENT:淫乱 || ABL:Ａ感覚 >= 3) && RAND:2 == 0
		PRINT 大事な場所を見せ付けながら
	ELSE
		PRINTFORM %TEXTR("恥ずかしい所が丸見えの/股間をさらけ出した")%格好で
	ENDIF
	;小悪魔
	IF TALENT:小悪魔 && RAND:3 >= 1
		PRINT ニヤニヤしている
	;(反抗的でなくて淫乱) or(反抗的かつ恋慕)
	ELSEIF  ( (TALENT:淫乱 && TALENT:反抗的 == 0) || (TALENT:反抗的 && TALENT:恋慕) ) && RAND:3 >= 1
		SIF TALENT:小悪魔 && RAND:2 == 0
			PRINT 、生意気にも
		PRINT 誘っている
	;反抗的かつ淫乱
	ELSEIF TALENT:反抗的 && TALENT:淫乱
		PRINT そっぽを向きながら顔を赤くしている
	;恥じらい
	ELSEIF TALENT:恥じらい
		PRINT 恥ずかしそうにしている
	ELSEIF RAND:2 == 0
		PRINTFORM %TEXTR("微笑んでいる/横たわっている")%
	ENDIF
	PRINTFORM %CALLNAME:TARGET%の
	;小柄かつＡ経験100未満
	SIF TALENT:小柄 && EXP:Ａ経験 < 100
		PRINT 小さな
ELSE
	;縛ってますか？
	IF TEQUIP:拘束
		PRINTFORM 拘束されている%CALLNAME:TARGET%の
	;ラミアですか？
	ELSEIF COND("種族：ラミア")
		SIF CHECK("Ａインサート") && RAND:2 == 0
			PRINT 擦り寄ってきた
		PRINTFORM %CALLNAME:TARGET%の
	;アナルセックスに慣れているなら誘ってきます
	ELSEIF CHECK("Ａインサート") && RAND:2 == 0
		SIF TALENT:恥じらい
			PRINT 恥ずかしがりながら
		PRINT 寝転がって脚を緩く開いた体勢をとっ
		;淫乱かつ欲情4以上
		IF TALENT:淫乱 && PALAM:欲情 >= PALAMLV:4 && RAND:2 == 0
			PRINT て、お尻を振りながら誘っている
		ELSE
			PRINT た
		ENDIF
		PRINTFORM %CALLNAME:TARGET%の
	ELSE
		IF TALENT:反抗的 && ABL:Ａ感覚 <= 2 && TALENT:恋慕 == 0
			PRINT 嫌がる
		ELSEIF TALENT:恥じらい
			PRINT 恥ずかしがる
		ENDIF
		PRINTFORM %CALLNAME:TARGET%を押し倒して
		SIF TALENT:恥じらい || (TALENT:反抗的 && ABL:Ａ感覚 <= 2 && TALENT:恋慕 == 0)
			PRINT 、
	ENDIF
	PRINTFORM %TEXTS("アナル")%
ENDIF
PRINTFORM %TEXTR("お尻の穴/アナル")%に
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
ELSE
	PRINTFORM %NAMES("ペニス", PLAYER)%を
ENDIF
IF NUM("ターン数") == 0
	PRINTFORM %TEXTR("前戯無しで/準備もせずに")%
;PLAYERがサドかつA経験20以下orＡ潤1LV未満
ELSEIF (ABL:PLAYER:サドっ気 >= 3 && EXP:Ａ経験 <= 20) || PALAM:Ａ潤 < PALAMLV:1
	PRINT 力任せに
;PLAYERがサドorアナルセックスに慣れている
ELSEIF (CHECK("Ａインサート") || ABL:PLAYER:サドっ気 >= 3) && RAND:2 == 0
	PRINT 一気に
ELSEIF RAND:2 == 0 || EXP:Ａ経験 <= EXPLV:3
	PRINT 慎重に
ENDIF
;PLAYERがサドorA経験20以上or恋慕かつA感覚Lv3以上
SIF ABL:PLAYER:サドっ気 >= 3 || MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || (TALENT:恋慕 && ABL:Ａ感覚 >= 3)
	PRINT 根元まで
;PLAYERがサドorＡ潤1LV未満
IF ABL:PLAYER:サドっ気 >= 3 || PALAM:Ａ潤 < PALAMLV:1
	PRINT 押し込み、
ELSE
	PRINT 挿入し、
ENDIF

IF TEQUIP:キス
	PRINTFORM 唇を奪いながら
	CALL SETFLAG, "追加キスフラグ", TARGET
ELSEIF TALENT:妊娠 && ABL:PLAYER:サドっ気 >= 3
	PRINT 妊娠していることなど構わずに
ELSEIF TALENT:妊娠 && RAND:2 == 0
	PRINT 大きなお腹をさすりながら
ELSEIF TALENT:妊娠
	PRINT 負担がかからないよう注意しながら
ELSEIF RAND:2
	PRINT 顔を見つめながら
;PLAYERがサドorA経験20以上or恋慕かつA感覚Lv3以上
ELSEIF ABL:PLAYER:サドっ気 >= 3 || MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || (TALENT:恋慕 && ABL:Ａ感覚 >= 3)
	PRINTFORM \@ TALENT:小柄 ? おなか # くびれ\@を掴んで
ENDIF
;PLAYERがサドor妊娠していない、かつA経験3以上or恋慕かつA感覚Lv4以上
IF (ABL:PLAYER:サドっ気 >= 3 || TALENT:妊娠 == 0) && (MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || (TALENT:恋慕 && ABL:Ａ感覚 >= 3) ) && RAND:3 == 0
	PRINT 荒々しく
ELSEIF (ABL:PLAYER:サドっ気 >= 3 || MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || ABL:Ａ感覚 >= 3 || ABL:PLAYER:腰使い) && SIZE("ペニス", PLAYER) && RAND:2 == 0
	SIF ABL:PLAYER:腰使い && RAND:2
		PRINT 入り口から奥まで責める
	PRINT 大きなストロークで
ELSEIF MAX(EXP:Ａ経験, EXP:Ａ性交経験) < 50
	PRINTFORM %TEXTR("浅めに/ゆっくりと")%
ELSE
	IF RAND:2 == 0
		IF RAND:2 == 0 && TALENT:オトコ == 0
			PRINT 腸壁を子宮に向かって押し込みつつ
		ELSE
			PRINT 腸壁をゴシゴシ擦りあげつつ
		ENDIF
	ELSE
		PRINT 激しく
	ENDIF
ENDIF
;PLAYERがサドorA経験20以上orA感覚Lv3以上
IF (ABL:PLAYER:サドっ気 >= 3 || MIN(EXP:Ａ経験, EXP:Ａ性交経験) >= 20 || ABL:Ａ感覚 >= 3) && RAND:3 == 0
	PRINTFORMW 腰を打ちつけた。
ELSE
	PRINTFORMW %TEXTR("出し入れした/腰を前後させた/腰を振った")%。
ENDIF
