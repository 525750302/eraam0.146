@COM150
;ディープスロート
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 150

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1


@FLAG_COM150, ARG
CALL SETFLAG, "フェラチオフラグ", TARGET
;対象の口⇔調教者のＰの汚れが移動
CALL EQ_STAIN, TARGET, "Ｍ", PLAYER, "Ｐ"

@SOURCE_COM150, ARG
;フェラとイラマチオの合わせ技
IF TEQUIP:ディープスロート
	CALL SOURCE_COM91, 30
	CALL SOURCE_COM140, 50
ELSE
	CALL SOURCE_COM91, 20
	CALL SOURCE_COM140, 20
ENDIF


@TRAIN_MESSAGE_COM150
;ディープスロート解除(今のところは死に分岐)
IF TEQUIP:ディープスロート
	;前回はイラマチオ
	IF PREVCOM == 140 && COND("前Ｔ調教者") == PLAYER && TEQUIP:押し倒し == 0
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の
		IF RAND:2
			PRINTFORM 喉を犯すのを中断して腰を引き、
		ELSE
			;角がありますか？
			PRINTFORM \@ COND("ツノハンドル") ? 角を掴んだままで # 頭を押さえながら \@腰を引き、
		ENDIF
		;大きいと食道にまで行ってしまう
		IF SIZE("ペニス", PLAYER) >= 3
			PRINTFORMW 食道にまで侵入していたペニスをズルズルと引きずり出した…
		ELSE
			PRINTFORMW 口からペニスを引きずり出した…
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は頭を引いて、根元まで飲み込んでいた%CALLNAME:PLAYER%のペニスを解放した…
	ENDIF
	TEQUIP:ディープスロート = 0
	RETURN 0
ENDIF

PRINTFORM %CALLNAME:TARGET%は
;前回フェラチオorイラマチオ
IF (PREVCOM == 91 || PREVCOM == 140) && COND("前Ｔ調教者") == PLAYER
	IF RAND:2 && CHECK("喉姦") >= 2
		IF RAND:3 == 0 && SIZE("ペニス", PLAYER) >= 3
			PRINTFORM 食道まで使って
		ELSE
			PRINTFORM %TEXTR("躊躇うことなく/舌の上を滑らせるようにして")%
		ENDIF
	ELSEIF TEQUIP:押し倒し
		PRINTFORM 股間に顔を埋めて
	ELSEIF PREVCOM == 140 && RAND:2
		PRINTFORM 腰の動きに合わせて
	ELSE
		PRINTFORM 顔を突き出して
	ENDIF
	PRINTFORM %CALLNAME:PLAYER%の
;押し倒されているとき
ELSEIF TEQUIP:押し倒し
	;直前の調教と汚れで分岐
	IF COND("Ｐ汚れ", PLAYER)
		SIF (PREVCOM >= 70 && PREVCOM <= 79) && COND("前Ｔ調教者") == PLAYER && TEQUIP:アイマスク == 0
			PRINT 自分のアナルから抜いたばかりの
		;濡れやすいで追加
		SIF TALENT:濡れやすい && RAND:3 && TEQUIP:アイマスク == 0
			PRINT べっとりと
		SIF (PREVCOM >= 60 && PREVCOM <= 69) && COND("前Ｔ調教者") == PLAYER && TEQUIP:アイマスク == 0
			PRINT 自分の愛液で
		PRINT 汚れた
	;淫乱orサドorサドっ気Lv.5以上で「恥じらい」を持たない
	;ELSEIF (TALENT:淫乱 || TALENT:小悪魔 || ABL:サドっ気 >= 5) && TALENT:恥じらい == 0
	;	PRINT 唾液を垂らしながら、
	ELSEIF RAND:3 == 0
		PRINTFORM 匂いたつ
	ELSE
		PRINTFORM %TEXTS("ペニス：視覚", PLAYER)%
	ENDIF
ELSE
	;シックスナイン
	IF TEQUIP:シックスナイン
		PRINTFORM %TEXTS("６９")%
	;アイマスクで分岐。ところで、Ｍ字開脚座りってもっといい名称無いですかね？
	ELSEIF TEQUIP:アイマスク || TEQUIP:拘束
		PRINT 、唇に押し付けられた
	ENDIF

	;直前の調教と汚れで分岐
	IF PREVEX("放尿", PLAYER)
		IF RAND:2
			PRINTFORM ツンと鼻に来る臭いを発している
		ELSE
			PRINTFORM 小用を足したばかりで臭いのきつい
		ENDIF
	ELSEIF PREVPLAY("挿入中", PLAYER)
		CALL ADD_TEXT, "性器から口へ"
	ELSEIF RAND:3 == 0
		PRINTFORM 匂いたつ
	ELSE
		PRINTFORM %TEXTS("ペニス：視覚", PLAYER)%
	ENDIF
ENDIF

PRINTFORM ペニスを根元まで%TEXTR("一息に/吸い込むように/難なく")%飲み込むと、
IF TALENT:恋慕 && RAND:4 == 0
	IF RAND:2 && COND("陰毛", PLAYER) >= 5 && TEQUIP:シックスナイン == 0
		PRINTFORM 陰毛に鼻先を埋めて擦りつけながら
	ELSE
		PRINTFORM うっとりしながら
	ENDIF
ELSEIF ABL:奉仕精神 >= 3 && TEQUIP:押し倒し == 0 && TEQUIP:シックスナイン == 0 && RAND:3 == 0
	PRINTFORM %TEXTR("上目遣い/潤んだ瞳")%で見上げながら
ELSEIF ABL:技巧 >= 3 && TEQUIP:口枷 == 0 && RAND:2
	PRINTFORM %TEXTR("緩急をつけ/唇できつく締め付け")%ながら
ELSEIF TALENT:嘔吐反射耐性 == 0 && RAND:2
	PRINTFORM 擦られた反射で瞳を潤ませながら
ELSE
	PRINTFORM 口元から%TEXTR("泡だった唾液をダラダラと/粘度の高い大量の涎を")%垂らしながら
ENDIF
;技巧４かつ奉仕精神３
IF ABL:技巧 >= 4 && ABL:奉仕精神 >= 3
	IF RAND:2
		PRINTFORMW 喉全体を駆使して刺激してきた…
	ELSE
		PRINTFORMW 絞った喉でしゃぶりはじめた…
	ENDIF
ELSEIF RAND:2
	PRINTFORMW 咽喉を鳴らしつつ扱いてきた…
ELSE
	PRINTFORMW 喉を用いて\@ TEQUIP:押し倒し ? 責め立てて # 奉仕をして \@きた…
ENDIF

;TEQUIP:ディープスロートの値で誰のものを咥えているかを記録
TEQUIP:ディープスロート = PLAYER
;フラグとTEQUIPが分かれているのは、射精後などにTEQUIP:ディープスロートが０となっている時でも参照するため
CALL SETFLAG, "ディープスロートフラグ", TARGET

@COM_ORDER_COM150, ARG
#DIM MEMO_STAIN
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:マゾっ気
IF ABL:マゾっ気
	LOCAL += MIN(ABL:マゾっ気, 5) * 3
	LOCALS = %LOCALS% + マゾっ気LV{ABL:マゾっ気}({MIN(ABL:マゾっ気, 5) * 3})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 1
	LOCALS = %LOCALS% - 恥じらい(1)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF
;Ｍ性向
IF TALENT:Ｍ性向
	LOCAL += 10
	LOCALS = %LOCALS% + Ｍ性向(10)
ENDIF
;嘔吐反射耐性
IF TALENT:嘔吐反射耐性
	LOCAL += 20
	LOCALS = %LOCALS% + 嘔吐反射耐性(20)
ENDIF

MEMO_STAIN = COM_ORDER_STAIN("Ｐ", "Ｍ")
;サドなら、汚れていても、かまわずに突っ込みます
SIF ABL:PLAYER:サドっ気 >= 3
	MEMO_STAIN /= 3

;汚れあり
IF MEMO_STAIN
	LOCAL -= MEMO_STAIN
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({MEMO_STAIN})
ENDIF

;合計を表示(60以上で実行。イラマチオよりも高難易度)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 60

RETURN RESULT
