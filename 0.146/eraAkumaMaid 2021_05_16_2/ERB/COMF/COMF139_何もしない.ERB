@COM139
;何もしない
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

RETURN 1


@FLAG_COM139, ARG
;全員の絶頂ゲージが3000か最大値の半分ぐらいか下がる
BASE:絶頂 = LIMIT(BASE:絶頂 - MAX(MAXBASE:絶頂/2, 3000), 0, BASE:絶頂*3/4)
BASE:MASTER:絶頂 = LIMIT(BASE:MASTER:絶頂 - MAX(MAXBASE:MASTER:絶頂/2, 3000), 0, BASE:MASTER:絶頂*3/4)
SIF ASSI
	BASE:ASSI:絶頂 = LIMIT(BASE:ASSI:絶頂 - MAX(MAXBASE:ASSI:絶頂/2, 3000), 0, BASE:ASSI:絶頂*3/4)
IF INSERT("インサート")

ELSEIF INSERT("挿入中")

ELSEIF TEQUIP:拘束 + TEQUIP:アイマスク

ELSEIF TEQUIP:お風呂プレイ
	;汚れを初期化
	CALL RESET_STAIN_EX, "全部", MASTER
	CALL RESET_STAIN_EX, "全部", TARGET
	;助手がいる場合は、助手もきれいに
	SIF ASSI
		CALL RESET_STAIN_EX, "全部", ASSI
ELSEIF TALENT:彼氏持ち && COND("初期彼氏持ち") && CONFIG("モブＨ") && TEQUIP:電話中 == 0
	TEQUIP:電話中 = 1
ELSEIF TEQUIP:電話中
	TEQUIP:電話中 = 0
ENDIF

@SOURCE_COM139, ARG
CALL VARSET_COMF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM139
PRINTFORM %CALLNAME:PLAYER%は
IF INSERT("インサート")
	;騎乗位中
	IF INSERT("騎乗位")
		PRINTFORMW 自分に跨って腰を振っている%CALLNAME:TARGET%を眺めながら休憩した
		CALL TEXT, "追加地の文：騎乗位"
	ELSE
		PRINTFORMW 腰の動きを止めて、休憩する事にした
	ENDIF
ELSEIF INSERT("挿入中")
	IF INSERT("騎乗位", PLAYER)
		PRINTFORMW %CALLNAME:TARGET%を押し倒した体勢で腰を止めて、顔を見下ろしつつ休憩した
	ELSE
		PRINTFORMW 身体の力を抜いて、%CALLNAME:TARGET%に犯されながらではあるが休憩を提案してみた
	ENDIF
ELSEIF TEQUIP:拘束 + TEQUIP:アイマスク
	PRINTFORMW 自由に動けない%CALLNAME:TARGET%を%TEXTR("尻目に/眺めながら")%休憩した
ELSEIF TEQUIP:お風呂プレイ
	PRINTFORMW %CALLNAME:TARGET%と一緒に湯船に浸かって休憩した
ELSEIF TALENT:彼氏持ち && COND("初期彼氏持ち") && CONFIG("モブＨ") && TEQUIP:電話中 == 0
	PRINTFORM %CALLNAME:TARGET%に
	IF TALENT:彼氏持ち == 2
		PRINT 旦那
	ELSEIF TALENT:彼氏持ち
		PRINT 彼氏
	ENDIF
	PRINTFORMW に電話をかけさせつつ休憩した
ELSEIF TEQUIP:電話中
	PRINTFORM %CALLNAME:TARGET%は%TEXTR("適当な嘘をついて/用事が出来たと伝えて/出掛けなくてはならないと言って")%
	IF TALENT:彼氏持ち == 2
		PRINT 旦那
	ELSEIF TALENT:彼氏持ち
		PRINT 彼氏
	ENDIF
	PRINTFORMW との電話を終わらせて休憩した
ELSE
	SELECTCASE PREVCOM
	CASE TNUM("抱きしめる")
		PRINTFORMW %CALLNAME:TARGET%を抱きしめつつ、そのまま休憩した
	CASEELSE
		PRINTFORMW 何もせずに休憩した
	ENDSELECT
ENDIF
