@COM88
;尿道騎乗位
CALL PRINT_TRAIN_NAME(SELECTCOM)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 88

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM88, ARG
IF INSERT("ワイルドスウィング")
	CALL FLAG_INSERTION, "尿道インサート", ARG, 120
ELSE
	CALL FLAG_INSERTION, "尿道インサート", ARG, 70
ENDIF


@SOURCE_COM88, ARG
IF INSERT("ワイルドスウィング")
	CALL SOURCE_INSERTION, "尿道インサート", ARG, 120
ELSE
	CALL SOURCE_INSERTION, "尿道インサート", ARG, 70
ENDIF

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 88

SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:露出 = 900
SOURCE:0:情愛 = 500

;根元まで受け入れられる場合にはクリを擦られてしまう
IF ABL:尿道拡張 > SIZE("ペニス", PLAYER) && PENIS(TARGET) == 0
	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 2, 30)
	SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "腰使い", PLAYER)
ENDIF

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
CASE 0 TO 5
	SOURCE:0:性行動 = 300 + ABL:奉仕精神*200
	SOURCE:0:達成感 = 100 + ABL:奉仕精神*300
	SOURCE:0:逸脱 = MAX(300 - ABL:奉仕精神*100, 0)
CASE 6 TO 10
	SOURCE:0:性行動 = 1300 + (ABL:奉仕精神 - 5)*100
	SOURCE:0:達成感 = 1600 + (ABL:奉仕精神 - 5)*200
CASEELSE
	SOURCE:0:性行動 = 1800 + (ABL:奉仕精神 - 10)*50
	SOURCE:0:達成感 = 2600 + (ABL:奉仕精神 - 10)*100
ENDSELECT

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:情愛, 0.30
CASE IS < PALAMLV:2
	TIMES SOURCE:0:情愛, 0.60
CASE IS < PALAMLV:3
	TIMES SOURCE:0:情愛, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:情愛, 1.50
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:情愛, 1.80
ENDSELECT

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM88
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
CALL TRAIN_MESSAGE_INSERTION
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

PRINTFORM %CALLNAME:TARGET%は
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
	CALL ADD_TEXT, "性器から性器へ"
;穴開きでないパンツをはいていて、ずらされていないなら、必ずずらす
ELSEIF CHECK_CLO("Ｖ露出") == 0 && CHECK_CLO("Ｖ露出可")
	CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
ENDIF
IF PREVCOM == 1 || PREVCOM == 9 || (PREVCOM >= 90 && PREVCOM <= 119)
	PRINTFORM %CALLNAME:PLAYER%の上にまたがると、
ELSEIF TALENT:好色 && RAND:2 == 0
	PRINTFORM %CALLNAME:PLAYER%のペニスに%TEXTR("照準/尿道口")%を合わせると、
ELSE
	PRINTFORM %CALLNAME:PLAYER%のペニスの位置を確認すると、
ENDIF

;スカートなどをはいているなら
IF PENIS(TARGET) == 0 && EQUIP:スカート + TEQUIP:新妻プレイ && (TALENT:好色 + TALENT:淫乱 || ABL:露出癖 >= 3) && RAND:2
	;エプロン
	IF TEQUIP:新妻プレイ
		PRINTFORM エプロンを
	ELSE
		PRINTFORM %SKIRT(TARGET, "略")%を
	ENDIF
	IF COND("口の使用") && RANDIF(2, COND("指の使用") )
		PRINT 口で咥えて
	ELSEIF COND("指の使用") && RAND:2
		PRINT 指で摘んで
	ELSE
		PRINT めくって
	ENDIF
	IF RAND:2
		PRINTFORM %TEXTS("アソコ：視覚")%
	ELSEIF RAND:2 && PALAM:Ｖ潤 >= 10000 && PALAM:欲情 >= 10000
		PRINTFORM 涎を垂らす穴
	ELSE
		PRINTFORM %TEXTR("恥ずかしい所/股間")%
	ENDIF
	PRINTFORM を%TEXTR("見せ付け/見せびらかし")%ながら
ELSE
	IF TALENT:好色 + TALENT:淫乱 + TALENT:恋慕 && COND("視力") && RAND:3 == 0
		PRINT 顔を見つめながら
	ELSEIF TEQUIP:押し倒し && ABL:サドっ気 >= 2 && RAND:2
		IF TALENT:高慢 + TALENT:強気 && RANDIF(2, TALENT:生意気 + TALENT:小悪魔)
			PRINT 不敵な笑みを浮かべながら
		ELSEIF TALENT:生意気 + TALENT:小悪魔 && RAND:2
			PRINT ニヤニヤしながら
		ELSE
			PRINT 嗜虐的な悦びを滲ませながら
		ENDIF
	ELSEIF CHECK("尿道インサート") && RAND:2
		PRINTFORM %TEXTR("甘い吐息を漏らし/気持ち良さそうな顔を見せ")%ながら
	ELSE
		PRINT 熱っぽい息を吐きながら
	ENDIF
	IF ABL:尿道拡張 > SIZE("ペニス", PLAYER) && ABL:サドっ気 && RAND:2
		PRINT 一気に
	ELSE
		PRINT ゆっくりと
	ENDIF
ENDIF
IF PENIS(TARGET) == 0
	PRINTFORM %TEXTR(@"%TEXTS("お尻：視覚")%/腰")%を下ろして
ENDIF
IF ABL:尿道拡張 >= SIZE("ペニス", PLAYER) && CHECK("尿道インサート") && RAND:3
	IF ABL:尿道拡張 > SIZE("ペニス", PLAYER) && RAND:2
		PRINTW 、根元まで呑み込んできた…
	ELSE
		PRINTW 、すっかり性器と化した穴でペニスを呑み込んできた…
	ENDIF
ELSEIF PENIS(TARGET)
	PRINTFORMW 自らのペニスで咥えこんできた…
ELSE
	PRINTW きた…
ENDIF


@COM_ORDER_COM88, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;一定の尿道経験か尿道感覚が必要
IF EXP:尿道経験 < 100 && ABL:尿道感覚 < 3
	LOCAL -= 30
	IF EXP:尿道経験 < 100
		LOCALS = %LOCALS% - 尿道経験不足(30)
	ELSEIF ABL:尿道感覚 < 3
		LOCALS = %LOCALS% - 尿道感覚不足(30)
	ENDIF
ENDIF

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF

;強気
IF TALENT:強気
	LOCAL += 10
	LOCALS = %LOCALS% + 強気(10)
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 10
	LOCALS = %LOCALS% - 恥じらい(10)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF
;尿道性感
IF TALENT:尿道性感
	LOCAL += 10
	LOCALS = %LOCALS% + 尿道性感(10)
ENDIF
;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	LOCALS = %LOCALS% + 媚薬(6)
ENDIF

;合計を表示(40以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 40

RETURN RESULT
