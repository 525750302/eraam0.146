;-------------------------------------------------
;120番台,調教者セックス系コマンド
;-------------------------------------------------
@COM_ABLE120
;正常位奉仕
SIF COMABLE_DEF(120)
	RETURN 0
;調教者がオトコだとダメ
SIF TALENT:PLAYER:オトコ
	RETURN 0
;ペニスあり
IF PENIS(TARGET)
	;ペニスを使う時には股間が露出できないとダメ
	SIF !CHECK_CLO("Ｖ露出可")
		RETURN 0
;ペニスなし
ELSE
	;ペニスバンドがないとダメ
	SIF COND("ペニバン") == 0
		RETURN 0
ENDIF
IF TEQUIP:押し倒し
	;サドっ気が必要
	SIF ABL:サドっ気 == 0
		RETURN 0
	SIF TALENT:PLAYER:処女 && ABL:サドっ気 < 3
		RETURN 0
ENDIF
CALL COM_ORDER_COM120, 1
RETURN LIMIT(RESULT, 0, 1)

@COM_ABLE121
;後背位奉仕
SIF COMABLE_DEF(121)
	RETURN 0
SIF TEQUIP:押し倒し
	RETURN 0
;調教者がオトコだとダメ
SIF TALENT:PLAYER:オトコ
	RETURN 0
;ペニスあり
IF PENIS(TARGET)
	;ペニスを使う時には股間が露出できないとダメ
	SIF !CHECK_CLO("Ｖ露出可")
		RETURN 0
;ペニスなし
ELSE
	;ペニスバンドがないとダメ
	SIF COND("ペニバン") == 0
		RETURN 0
ENDIF

CALL COM_ORDER_COM121, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE122
;騎乗位で犯す
SIF COMABLE_DEF(122)
	RETURN 0
SIF TEQUIP:押し倒し
	RETURN 0
;調教者がオトコだとダメ
SIF TALENT:PLAYER:オトコ
	RETURN 0
;ペニスあり
IF PENIS(TARGET)
	;ペニスを使う時には股間が露出できないとダメ
	SIF !CHECK_CLO("Ｖ露出可")
		RETURN 0
;ペニスなし
ELSE
	;ペニスバンドがないとダメ
	SIF COND("ペニバン") == 0
		RETURN 0
ENDIF
SIF ABL:PLAYER:サドっ気 == 0 || ABL:PLAYER:Ｖ拡張 < SIZE("ペニス", TARGET) || EXP:PLAYER:Ｖ経験 < 20 || EXP:PLAYER:Ｖ性交経験 < 5
	RETURN 0
RETURN 1

@COM_ABLE123
RETURN 0

@COM_ABLE124
RETURN 0

@COM_ABLE125
;Ａ正常位奉仕実行判定
SIF COMABLE_DEF(125)
	RETURN 0
SIF TEQUIP:押し倒し
	RETURN 0
;ペニスあり
IF PENIS(TARGET)
	;ペニスを使う時には股間が露出できないとダメ
	SIF !CHECK_CLO("Ｖ露出可")
		RETURN 0
;ペニスなし
ELSE
	;ペニスバンドがないとダメ
	SIF COND("ペニバン") == 0
		RETURN 0
ENDIF

CALL COM_ORDER_COM125, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE126
;Ａ後背位奉仕実行判定
SIF COMABLE_DEF(126)
	RETURN 0
;ペニスあり
IF PENIS(TARGET)
	;ペニスを使う時には股間が露出できないとダメ
	SIF !CHECK_CLO("Ｖ露出可")
		RETURN 0
;ペニスなし
ELSE
	;ペニスバンドがないとダメ
	SIF COND("ペニバン") == 0
		RETURN 0
ENDIF

IF TEQUIP:押し倒し
	;サドっ気が必要
	SIF ABL:サドっ気 == 0
		RETURN 0
	SIF TALENT:PLAYER:Ａ処女 && ABL:サドっ気 < 3
		RETURN 0
ENDIF
CALL COM_ORDER_COM126, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE127
;Ａ騎乗位で犯す実行判定
SIF COMABLE_DEF(127)
	RETURN 0
SIF TEQUIP:押し倒し
	RETURN 0
;ペニスあり
IF PENIS(TARGET)
	;ペニスを使う時には股間が露出できないとダメ
	SIF !CHECK_CLO("Ｖ露出可")
		RETURN 0
;ペニスなし
ELSE
	;ペニスバンドがないとダメ
	SIF COND("ペニバン") == 0
		RETURN 0
ENDIF

SIF ABL:PLAYER:サドっ気 == 0 || ABL:PLAYER:Ａ拡張 < SIZE("ペニス", TARGET) || EXP:PLAYER:Ａ経験 < 50 || EXP:PLAYER:Ａ性交経験 < 20
	RETURN 0
RETURN 1

@COM_ABLE128
RETURN 0

@COM_ABLE129
RETURN 0


;-------------------------------------------------
;130～159番,SM系orハードなコマンド
;-------------------------------------------------
@COM_ABLE130
;スパンキング実行判定
SIF COMABLE_DEF(130)
	RETURN 0
RETURN 1

@COM_ABLE132
;鼻フック実行判定
SIF COMABLE_DEF(132)
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:鼻フック
	RETURN 1
;アイテムを持っていないとダメ
SIF ITEM:拘束具 == 0
	RETURN 0
RETURN 1

@COM_ABLE133
;アイマスク実行判定
SIF COMABLE_DEF(133)
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:アイマスク
	RETURN 1
;アイテムを持っていないとダメ
SIF ITEM:拘束具 == 0
	RETURN 0
RETURN 1

@COM_ABLE134
;縄実行判定
SIF COMABLE_DEF(134)
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:拘束
	RETURN 1
;アイテムを持ってい無いとダメ
SIF ITEM:拘束具 == 0
	RETURN 0
;助手は技巧5でないとダメ
IF ASSIPLAY
	SIF ABL:ASSI:技巧 <= 4
		RETURN 0
ENDIF
RETURN 1

@COM_ABLE135
;口枷実行判定
SIF COMABLE_DEF(135)
	RETURN 0
;解除はいつでも可能
SIF TEQUIP:口枷
	RETURN 1
;アイテムを持っていないとダメ
SIF ITEM:拘束具 == 0
	RETURN 0
;ASSIの場合、技巧3以上じゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3
		RETURN 0
ENDIF
RETURN 1

@COM_ABLE136
;浣腸実行判定
SIF COMABLE_DEF(136)
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:浣腸 == 0
	RETURN 0
;解除、追加はいつでも可能
IF TEQUIP:浣腸 && COND("アナル栓")
	SIF COND("精液浣腸")
		RETURN 0
	RETURN 1
ELSE
	SIF COND("空気浣腸") || COND("液体浣腸") >= 4
		RETURN 0
ENDIF
;アナル使用判定
SIF USE_A(TARGET, "浣腸") == 0
	RETURN 0
RETURN 1


@COM_ABLE138
;浴尿させる実行判定（とはいえ今のところは口か膣か腸内への放尿のみ）
SIF COMABLE_DEF(138)
	RETURN 0
;コンドームつけてるとできない
SIF TEQUIP:PLAYER:コンドーム
	RETURN 0
;プレイヤーのサドっ気が必須
SIF ABL:PLAYER:サドっ気 == 0
	RETURN 0
SIF ABL:PLAYER:サドっ気 < 3 && ABL:マゾっ気 < 3
	RETURN 0
;尿意が溜まってる？
SIF BASE:PLAYER:尿意 < 5000
	RETURN 0
;膣内放尿が可能かつ前回子宮挿入なら子宮内放尿になる
SIF CHECK_COM("口内放尿", 138) || CHECK_COM("膣内放尿", 138) || CHECK_COM("温泉浣腸", 138) || CHECK_COM("乳内放尿", 138) || CHECK_COM("膀胱内放尿", 138)
	RETURN 1
RETURN 0

@COM_ABLE139
;何もしない実行判定
SIF COMABLE_DEF(139)
	RETURN 0
;主人以外は不可にすべきかどうか
;SIF PLAYER != MASTER
;	RETURN 0
;連続実行は無理
SIF PREVCOM == 139
	RETURN 0
SIF COND("前Ｔと同ペア") == 0
	RETURN 0
;３Ｐ中はとりあえず封印
SIF COND("３Ｐ")
	RETURN 0
;ビデオ中も封印
SIF TEQUIP:ビデオカメラ
	RETURN 0
RETURN 1

@COM_ABLE140
;イラマチオ実行判定
SIF COMABLE_DEF(140)
	RETURN 0
;調教者がオトコかふたなりorペニバンがないとダメ
SIF PENIS(PLAYER) == 0 && COND("ペニバン", PLAYER) == 0
	RETURN 0
;ボールギャグ使用中は不可
SIF TEQUIP:口枷 == 1
	RETURN 0
;調教者がサドっ気あり、信頼+欲望が6以上、リングギャグ装着、喉LVが高いでないとダメ
SIF ABL:PLAYER:サドっ気 == 0 && ABL:信頼 + ABL:欲望 < 6 && TEQUIP:口枷 != 2 && CHECK("喉姦") < 2
	RETURN 0

CALL COM_ORDER_COM140, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE141
;フィストファック
SIF COMABLE_DEF(141)
	RETURN 0
;Ｖ拡張LVが拳のサイズに匹敵していないと実行できない
SIF SIZE("片手フィスト", PLAYER) - (ABL:Ｖ拡張 + TEQUIP:弛緩/2) >= 2
	RETURN 0
RETURN 1

@COM_ABLE142
;アナルフィスト
SIF COMABLE_DEF(142)
	RETURN 0
;Ａ拡張LVが拳のサイズに匹敵していないと実行できない
SIF SIZE("片手フィスト", PLAYER) - (ABL:Ａ拡張 + TEQUIP:弛緩/2) >= 2
	RETURN 0
RETURN 1

@COM_ABLE143
;両穴フィスト
SIF COMABLE_DEF(143)
	RETURN 0
;ＶＡフィストが出来ないとダメ
CALL COM_ABLE141
SIF RESULT == 0
	RETURN 0
CALL COM_ABLE142
SIF RESULT == 0
	RETURN 0
;Ｖ拡張LVが拳のサイズに匹敵していないと実行できない
SIF SIZE("Ｗフィスト", PLAYER) - (ABL:Ｖ拡張 + TEQUIP:弛緩/3) >= 2
	RETURN 0
;Ａ拡張LVが拳のサイズに匹敵していないと実行できない
SIF SIZE("Ｗフィスト", PLAYER) - (ABL:Ａ拡張 + TEQUIP:弛緩/3) >= 2
	RETURN 0
RETURN 1

@COM_ABLE144
;放尿実行判定
SIF COMABLE_DEF(144)
	RETURN 0
;尿道が塞がっているとダメ
SIF TEQUIP:尿道バイブ
	RETURN 0
;尿意ゲージが5000以上でないとダメ
SIF BASE:尿意 < 5000
	RETURN 0
;相手がペニス持ちだと強制放尿は無理
SIF PENIS(TARGET) && CHECK_COM("強制放尿", 144)
	RETURN 0
;押し倒しの時には聖水プレイになる（とりあえずオンナのみ）
IF TEQUIP:押し倒し
	SIF TEQUIP:顔面騎乗 == 0 || PENIS(TARGET) || TEQUIP:Ｗ押し倒し
		RETURN 0
	SIF ABL:サドっ気 == 0 || ABL:露出癖 == 0 || ABL:欲望 < 3
		RETURN 0
	SIF ABL:サドっ気 < 2 && TALENT:倒錯的 == 0
		RETURN 0
	SIF ABL:サドっ気 + ABL:PLAYER:マゾっ気 < 5
		RETURN 0
ENDIF

CALL COM_ORDER_COM144, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE145
;剃毛実行判定
SIF COMABLE_DEF(145)
	RETURN 0
;押し倒している時or女王様プレイの時にはPLAYERの陰毛を剃ります！
IF TEQUIP:押し倒し || TEQUIP:女王様プレイ
	SIF TEQUIP:PLAYER:バイブ
		RETURN 0
	;技巧2LV、サドっ気 >= 3、PLAYERに毛が生えていないとダメ
	SIF COND("陰毛", PLAYER) <= 3 || ABL:技巧 < 2 || ABL:サドっ気 + ABL:PLAYER:マゾっ気 < 5
		RETURN 0
ELSE
	SIF TEQUIP:バイブ
		RETURN 0
	;毛が生えていないとダメ
	SIF COND("陰毛") <= 3
		RETURN 0
	;アソコ剥き出しにできないとダメ
	SIF !CHECK_CLO("Ｖ露出可")
		RETURN 0

	CALL COM_ORDER_COM145, 1
	RETURN LIMIT(RESULT, 0, 1)
ENDIF
RETURN 1

@COM_ABLE146
;各調教で一度だけ
SIF CHOSECOM(146)
	RETURN 0
;身嗜み管理実行判定
SIF COMABLE_DEF(146)
	RETURN 0
;何か指示できる状態でないとダメ
IF MARK:屈服刻印 >= 2
ELSEIF CONFIG("陰毛") && MARK:屈服刻印 >= 1
ELSEIF CONFIG("腋毛") && MARK:屈服刻印 >= 1
ELSEIF PENIS(TARGET) && COND("真性包茎") == 0 && MARK:屈服刻印 >= 1
ELSE
	RETURN 0
ENDIF
CALL COM_ORDER_COM146, 1
RETURN LIMIT(RESULT, 0, 1)

@COM_ABLE147
;精飲実行判定
SIF COMABLE_DEF(147)
	RETURN 0
;ボールギャグしているとダメ
SIF TEQUIP:口枷 == 1
	RETURN 0
;精液がたまっていないとダメ
SIF ITEM:ゴム内精液 == 0
	RETURN 0

CALL COM_ORDER_COM147, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE148
;ピアッシング実行判定
SIF COMABLE_DEF(148)
	RETURN 0
;アイテムがないとダメ
SIF ITEM:ピアスセット == 0
	RETURN 0
;あなた専用
SIF PLAYER != MASTER
	RETURN 0

CALL COM_ORDER_COM148, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE149
;カテーテル実行判定
SIF COMABLE_DEF(149)
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:カテーテル == 0
	RETURN 0
;アソコ剥き出しにできないとダメ
SIF !CHECK_CLO("Ｖ露出可")
	RETURN 0
CALL COM_ORDER_COM149, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE150
;ディープスロート実行判定
SIF COMABLE_DEF(150)
	RETURN 0
;解除させるのはいつでも
SIF TEQUIP:ディープスロート
	RETURN 1
;調教者にちんこが無いとダメ
SIF PENIS(PLAYER) == 0
	RETURN 0
;ボールギャグ使用中は不可
SIF TEQUIP:口枷 == 1
	RETURN 0
;ペニスが小さいとダメ
SIF SIZE("ペニス", PLAYER) == 0
	RETURN 0
;喉姦可能で無いとダメ（今後変えるかも）
SIF CHECK("喉姦") == 0
	RETURN 0

CALL COM_ORDER_COM150, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE151
;尿道ブジー実行判定
SIF COMABLE_DEF(151)
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:尿道ブジー == 0
	RETURN 0
;アソコ剥き出しにできないとダメ
SIF !CHECK_CLO("Ｖ露出可")
	RETURN 0
;尿道を使用済みだとダメ
SIF TEQUIP:カテーテル || TEQUIP:尿道バイブ
	RETURN 0

CALL COM_ORDER_COM151, 1
RETURN LIMIT(RESULT, 0, 1)


@COM_ABLE152
;尿道バイブ実行判定
SIF COMABLE_DEF(152)
	RETURN 0
;アイテムを持っていないとダメ
SIF ITEM:バイブセット == 0
	RETURN 0
;アソコ剥き出しにできないとダメ
SIF !CHECK_CLO("Ｖ露出可")
	RETURN 0
;カテーテル使用中はダメ
SIF TEQUIP:カテーテル
	RETURN 0
;既に使用中なら抜くだけなのでＯＫ
SIF TEQUIP:尿道バイブ
	RETURN 1
;尿道拡張LVが-1以上じゃないとダメ
SIF ABL:尿道拡張 <= -2
	RETURN 0

CALL COM_ORDER_COM152, 1
RETURN LIMIT(RESULT, 0, 1)

@COM_ABLE153
RETURN 0

@COM_ABLE154
RETURN 0

@COM_ABLE155
RETURN 0

@COM_ABLE156
RETURN 0

@COM_ABLE157
RETURN 0

@COM_ABLE158
;写真撮影実行判定　女あなたにはあまり対応してないかも…
SIF COMABLE_DEF(158)
	RETURN 0
;連続実行は駄目
SIF STR:前回コマンド == "写真撮影"
	RETURN 0
;SIF PLAYER != MASTER
;	RETURN 0
SIF COND("前Ｔと同ペア") == 0
	RETURN 0
SIF COND("合意：Ｈな写真") == 0 && EQUIP:Ｈなご褒美 == 0
	RETURN 0
;前ターン挿入してた場合可能　ハメ撮りや事後に分岐
SIF PREVPLAY("インサート")
	RETURN 1
;お掃除後も可能
SIF PREVPLAY("お掃除フェラ")
	RETURN 1
;口に出しても可能
SIF PREVEX("口内射精")
	RETURN 1
;見せ槍後も可能
SIF PREVPLAY("見せ槍")
	RETURN 1
;前回コマンド依存
SELECTCASE PREVCOM
;女あなたは指挿れ絶頂時も可能
CASE TNUM("指挿入れ")
	SIF TALENT:PLAYER:オンナ && PREVEX("Ｖ絶頂")
		RETURN 1
;くぱぁ後も可能
CASE TNUM("くぱぁさせる"), TNUM("Ａくぱぁさせる")
	SIF STR:青写真 != ""
		RETURN 1
;貝合わせ後絶頂時も可能
CASE TNUM("貝合わせ")
	SIF PREVEX("Ｃ絶頂")
		RETURN 1
ENDSELECT

RETURN 0

