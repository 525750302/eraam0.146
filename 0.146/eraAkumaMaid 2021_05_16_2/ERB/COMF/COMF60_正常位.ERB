@COM60
;正常位
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM60, ARG
CALL FLAG_INSERTION, "Ｖインサート", ARG, 100

@SOURCE_COM60, ARG
CALL SOURCE_INSERTION, "Ｖインサート", ARG, 100

CALL VARSET_COMF

SOURCE:0:露出 = 200
SOURCE:0:情愛 = 500

;根元まで受け入れられる場合にはクリを擦られてしまう
IF ABL:Ｖ拡張 > SIZE("ペニス", PLAYER) && PENIS(TARGET) == 0
	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 2, 30)
	;調教者の腰使いをみる
	SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "腰使い", PLAYER)
ENDIF

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:情愛, 0.30
CASE IS < PALAMLV:2
	TIMES SOURCE:0:情愛, 0.60
CASE IS < PALAMLV:3
	TIMES SOURCE:0:情愛, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:情愛, 1.50
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:情愛, 1.80
ENDSELECT

CALL CALC, "調教ソース", ARG

@TRAIN_MESSAGE_COM60
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
CALL TRAIN_MESSAGE_INSERTION
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

;前回、挿入前に暴発してしまった
IF PREVCOM == TNUM("挿入前暴発") && COND("前Ｔ調教者") == PLAYER
	PRINTFORM 気を取り直した%CALLNAME:PLAYER%は、
ELSE
	PRINTFORM %CALLNAME:PLAYER%は
ENDIF
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
	CALL ADD_TEXT, "性器から性器へ"
	PRINTFORM %CALLNAME:TARGET%の%TEXTS("Ｖ挿入前")%を
;穴開きでないパンツをはいていて、ずらされていないなら、必ずずらす
ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
	PRINTFORM %CALLNAME:TARGET%の
	CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET
	PRINTFORM 、%TEXTS("Ｖ挿入前")%を
;巨根分岐
ELSEIF ABL:PLAYER:サドっ気 && SIZE("ペニス", PLAYER) >= 2 && COND("視力") && CHOSECOMS("Ｖ挿入") == 0
	PRINTFORM %TEXTS("巨根：視覚", PLAYER)%をお腹にかざして
	PRINTFORM %TEXTR("どれほど深くまで犯す/何処まで入る/根元まで突き込んだらどういう事になる")%
	PRINTFORM かを%CALLNAME:TARGET%に見せつけたのち、腰を突き出して%TEXTS("Ｖ挿入前")%を
ELSE
	PRINTFORM %CALLNAME:TARGET%の%TEXTS("Ｖ挿入前")%を
ENDIF

IF TEQUIP:キス
	PRINTFORM 唇を奪いながら
	CALL SETFLAG, "追加キスフラグ", TARGET
ELSEIF NUM("ターン数") == 0
	PRINTFORM %TEXTR("前戯無しで/慣らしもせずに")%
ELSE
	SIF PREVCOM == 13
		PRINT 指で広げさせた体勢のまま
	SIF PALAM:Ｖ潤 < PALAMLV:4 && (PREVCOM == 91 || PREVCOM == 99 || PREVCOM == 140) && COND("前Ｔ調教者") == PLAYER && RAND:2 == 0
		PRINT 舐めて準備させたペニスで
ENDIF
IF CHOSECOM(SELECTCOM) == 0 && CHOSECOMS("挿入") && SIZE("ペニス", PLAYER) >= 2 && (TALENT:小柄 || SIZE("ペニス", PLAYER) >= 3)
	PRINTFORMW 深々と貫いて、子宮を位置が変わる程に押し上げた。
ELSE
	PRINTFORMW 貫いた。
ENDIF


@EQUIP_COM60
#DIM MEMO_SELECTCOM
#DIM MOTIVATION

;強度（通常は20）
MOTIVATION = 20

SELECTCASE SELECTCOM
CASE 60 TO 89, 120 TO 129
	RETURN 0
;何もしない
CASE 139
	IF COND("疲労")
		RETURN 0
	ELSE
		MOTIVATION = 5
		;騎乗位の場合はあんまり変わらない
		IF INSERT("騎乗位")
			MOTIVATION = 20
		;PLAYERが騎乗位している側
		ELSEIF INSERT("騎乗位")
			MOTIVATION = 5
		;PLAYERが犯されている時
		ELSEIF INSERT("挿入中")
			MOTIVATION = 20
		ENDIF
	ENDIF
ENDSELECT

MEMO_SELECTCOM = SELECTCOM


IF INSERT("Ｖインサート")
	IF INSERT("Ｖインサート", TARGET, PLAYER)
		SELECTCOM = INSERT("Ｖ対応コマンド")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
	ENDIF
	IF INSERT("Ｖインサート", TARGET, PLAYER:1)
		SWAP PLAYER, PLAYER:1
		SELECTCOM = INSERT("Ｖ対応コマンド")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
		SWAP PLAYER, PLAYER:1
	ENDIF
ENDIF
IF INSERT("Ａインサート")
	IF INSERT("Ａインサート", TARGET, PLAYER)
		SELECTCOM = INSERT("Ａ対応コマンド")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
	ENDIF
	IF INSERT("Ａインサート", TARGET, PLAYER:1)
		SWAP PLAYER, PLAYER:1
		SELECTCOM = INSERT("Ａ対応コマンド")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
		SWAP PLAYER, PLAYER:1
	ENDIF
ENDIF
IF INSERT("尿道インサート")
	IF INSERT("尿道インサート", TARGET, PLAYER)
		SELECTCOM = INSERT("尿道対応コマンド")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
	ENDIF
	IF INSERT("尿道インサート", TARGET, PLAYER:1)
		SWAP PLAYER, PLAYER:1
		SELECTCOM = INSERT("尿道対応コマンド")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
		SWAP PLAYER, PLAYER:1
	ENDIF
ENDIF
IF INSERT("Ｖインサート", PLAYER, TARGET)
	SELECTCOM = INSERT("Ｖ対応コマンド", PLAYER)
	CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
	CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
	CALL SAMEN_CHECK, MOTIVATION
ENDIF
IF INSERT("Ａインサート", PLAYER, TARGET)
	SELECTCOM = INSERT("Ａ対応コマンド", PLAYER)
	CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
	CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
	CALL SAMEN_CHECK, MOTIVATION
ENDIF
IF INSERT("尿道インサート", PLAYER, TARGET)
	SELECTCOM = INSERT("尿道対応コマンド", PLAYER)
	CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
	CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
	CALL SAMEN_CHECK, MOTIVATION
ENDIF

SELECTCOM = MEMO_SELECTCOM

RETURN 1

@EQUIP_COM60_2
#DIM A_JUST
#DIM V_JUST
#DIM U_JUST

CALL TEXT, "挿入具合の描写"

V_JUST = 0
A_JUST = 0
U_JUST = 0

;３ＰなどがあるのでTARGETとPLAYERを省略せずに記入（A_JUSTとJ_JUSTは片方しか1以上にはならない）
SIF INSERT("Ｖインサート", TARGET, PLAYER) && CHOSECOMS("Ｖ挿入") == 0
	V_JUST = CHECK("Ｖジャストフィット")
SIF INSERT("Ａインサート", TARGET, PLAYER) && CHOSECOMS("Ａ挿入") == 0
	A_JUST = CHECK("Ａジャストフィット")
SIF INSERT("尿道インサート", TARGET, PLAYER) && CHOSECOMS("尿道挿入") == 0
	U_JUST = CHECK("尿道ジャストフィット")

IF V_JUST || A_JUST || U_JUST
	CALL PRINT_STRL, "ハートピンク_＜ジャストフィット！＞"

	SELECTCASE MAX(V_JUST, A_JUST, U_JUST)
	CASE IS >= 90
		PRINT 完全に
	CASE IS >= 75
		PRINT 度重なる挿入で
	CASEELSE
		PRINT すっかり
	ENDSELECT
	PRINTFORM %CALLNAME:PLAYER%のペニス%TEXTR("の形にされて/を覚えて/専用になって")%しまった
	IF A_JUST
		PRINTFORM %CALLNAME:TARGET%のアナルが
	ELSEIF U_JUST
		PRINTFORM %CALLNAME:TARGET%の尿道が
	ELSE
		PRINTFORM %CALLNAME:TARGET%の膣が
	ENDIF
	PRINTFORML %TEXTR("得も言われぬ快感を与えて/隙間なく絡みついて/動きに合わせて吸い付いて")%くる！
	;CALL PRINT_STRL, "ハートピンク_H_お互いの快楽にボーナスが発生しています_ハートピンク_H"
ENDIF

VARSET LOCALS
IF INSERT("インサート") || SELECTCOM == 80
	IF COND("好きな体位中")
		SETCOLOR DEF_COLOR("ハートピンク")
		LOCALS = 好きな体位
	ENDIF

	SELECTCASE SELECTCOM
	CASE 60 TO 89
	;何もしない
	CASE 139
		SIF COND("疲労")
			RETURN 0
	CASEELSE
		SIF LOCALS != ""
			LOCALS = 好きな体位で
		IF INSERT("二穴挿入")
			LOCALS = %LOCALS%二穴挿入中
			EXP:Ｖ経験 += 1
			LOCALS:1 = Ｖ経験＋１
			EXP:Ａ経験 += 1
			LOCALS:2 = Ａ経験＋１
		ELSEIF INSERT("Ｖインサート")
			LOCALS = %LOCALS%インサート中
			EXP:Ｖ経験 += 1
			LOCALS:1 = Ｖ経験＋１
		ELSEIF INSERT("Ａインサート")
			LOCALS = %LOCALS%Ａインサート中
			EXP:Ａ経験 += 1
			LOCALS:2 = Ａ経験＋１
		ELSEIF INSERT("尿道インサート")
			LOCALS = %LOCALS%尿道インサート中
			EXP:尿道経験 += 1
			LOCALS:2 = 尿道経験＋１
		ENDIF
	ENDSELECT
ENDIF

SELECTCASE SELECTCOM
CASE 120 TO 129
CASEELSE
	IF INSERT("Ｖインサート", PLAYER, TARGET)
		LOCALS = 逆インサート中
		EXP:PLAYER:Ｖ経験 += 1
	ELSEIF INSERT("Ａインサート", PLAYER, TARGET)
		LOCALS = 逆Ａインサート中
		EXP:PLAYER:Ａ経験 += 1
	ENDIF
ENDSELECT

SIF LOCALS != ""
	PRINTFORML ＜%LOCALS%＞
RESETCOLOR
SIF LOCALS:1 != ""
	PRINTFORML %LOCALS:1%
SIF LOCALS:2 != ""
	PRINTFORML %LOCALS:2%

RETURN 1
