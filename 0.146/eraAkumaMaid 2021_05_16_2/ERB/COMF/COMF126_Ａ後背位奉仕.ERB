@COM126
;Ａ後背位奉仕
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 126

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%
;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM126, ARG
CALL FLAG_INSERTION, "Ａ逆インサート", ARG, 100

@SOURCE_COM126, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 126

SOURCE:0:性行動 = 350
SOURCE:0:達成感 = 900
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加") + 1000
SOURCE:0:屈従 = 200
SOURCE:0:逸脱 = 2200

;オトコかふたなり
IF PENIS(TARGET)
	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 4)
	SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "腰使い", PLAYER)
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM126
CALL SETFLAG, "下半身を脱がす", PLAYER
CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
;同時押し倒し
IF TEQUIP:Ｗ押し倒し
	;二穴挿入
	IF TRAIN_GENRE("Ｖ逆挿入", SELECTCOM:1)
		;二人とも前回と同じ行動
		IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
			PRINTFORM 二人は
			IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:3 == 0
				PRINT うっとりしながら
			ELSEIF RAND:2 == 0
				PRINT 夢中になって
			ELSE
				PRINT 楽しそうに
			ENDIF
			PRINTFORMW %CALLNAME:MASTER%の両穴を犯し続けている…！
		;助手が前回と同じ行動
		ELSEIF SELECTCOM:1 == PREVCOM:1
			PRINTFORM %CALLNAME:TARGET%は%CALLNAME:(TARGET:1)%に犯されている%CALLNAME:MASTER%のお尻をぐっと割り開くと、アナルを
			;ペニバン
			IF PENIS(TARGET) == 0
				IF ABL:サドっ気 >= 5
					PRINTFORM 凶悪な
				ELSEIF ABL:サドっ気 >= 3
					IF TALENT:MASTER:ふたなり || TALENT:MASTER:オトコ
						PRINTFORM %CALLNAME:MASTER%のモノとそっくりな
					ELSE
						PRINTFORM 大きめの
					ENDIF
				ENDIF
			ELSE
				IF RAND:3 == 0
					PRINT そそり立つ
				ELSEIF RAND:2 == 0 && TEQUIP:アイマスク == 0
					PRINT 硬く勃起した
				ELSE
					PRINT 脈打つ
				ENDIF
			ENDIF
			PRINTFORM %NAMES("ペニス")%で
			IF ABL:サドっ気 >= 3 && RAND:3 == 0
				PRINT 楽しそうに
			ELSEIF ABL:サドっ気 && RAND:2 == 0
				PRINT 荒々しく
			ELSEIF ABL:欲望 >= 3 && RAND:2 == 0
				PRINT 深々と
			ENDIF
			PRINTFORMW 犯しはじめた…！
		;役割交換
		ELSEIF TRAIN_GENRE("Ａ逆挿入", PREVCOM:1)
			PRINTFORMW %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%は犯す穴を交換したりして、%CALLNAME:MASTER%で愉しんでいる…！
		ELSE
			PRINTFORM %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%は
			IF RAND:2 == 0
				PRINTFORM 競うように
			ELSE
				PRINTFORM 協力して
			ENDIF
			PRINTFORMW %CALLNAME:MASTER%の両穴を犯し始めた…！
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:TARGET%は
		;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
		SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
			PRINTFORM 、%TEXTS("Ｗ押し倒し中")%

		IF SELECTCOM == PREVCOM
			PRINTFORMW %CALLNAME:MASTER%の肛門を思うがままに犯している…
		ELSE
			PRINTFORMW %CALLNAME:MASTER%のアナルを犯しはじめた…
		ENDIF
	ENDIF
ELSEIF TEQUIP:押し倒し
	;前回と同じ
	IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の肛門を思うがままに犯している…
	ELSE
		PRINTFORM %CALLNAME:TARGET%は
		SIF ABL:サドっ気 >= 3
			PRINTFORM 嗜虐的な笑みを浮かべると、
		;ペニバン
		IF PENIS(TARGET) == 0
			IF TRAIN_GENRE("Ｖ逆挿入", PREVCOM)
				PRINTFORM 愛液でトロトロの
			ELSEIF ABL:サドっ気 >= 5
				PRINTFORM 凶悪な
			ELSEIF ABL:サドっ気 >= 3
				IF PENIS(MASTER)
					PRINTFORM %CALLNAME:MASTER%のモノとそっくりな
				ELSE
					PRINTFORM 大きめの
				ENDIF
			ENDIF
		ELSE
			IF TRAIN_GENRE("Ｖ逆挿入", PREVCOM)
				PRINTFORM 愛液でトロトロの
			ELSEIF RAND:3 == 0
				PRINT そそり立つ
			ELSEIF RAND:2 == 0 && TEQUIP:アイマスク == 0
				PRINT 硬く勃起した
			ELSE
				PRINT 脈打つ
			ENDIF
		ENDIF
		PRINTFORM %NAMES("ペニス")%で%CALLNAME:PLAYER%のアヌスを
		IF ABL:サドっ気 >= 3 && RAND:3 == 0
			PRINT 楽しそうに
		ELSEIF ABL:サドっ気 && RAND:2 == 0
			PRINT 荒々しく
		ELSEIF ABL:欲望 >= 3 && RAND:2 == 0
			PRINT 深々と
		ENDIF
		IF ABL:サドっ気 >= 5 && RAND:2 == 0
			PRINTFORMW 陵辱した…
		ELSE
			PRINTFORMW 犯しはじめた…
		ENDIF
	ENDIF
;前回と今回の調教者が同じで、前回の調教が後背位であり、抜かないフラグがある場合
ELSEIF INSERT("Ａ後背位", PLAYER) && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の腰を掴んで、
	;恋慕で追加
	SIF TALENT:恋慕 && RAND:3
		PRINTFORM 首筋やうなじに舌を這わせながら
	IF ABL:サドっ気 >= 3 && RAND:3 == 0
		PRINTFORMW 立体的に腰をグラインドさせ、%CALLNAME:PLAYER%ごと体を揺らした…
	ELSEIF ABL:サドっ気 >= 3 && RAND:2 == 0
		PRINTFORMW 獣のように激しく腰を振りたてた…
	ELSE
		PRINTFORMW 再び腰を前後に動かした…
	ENDIF
;同様に前回の調教が正常位か騎乗位、逆レイプの場合
ELSEIF INSERT("Ａインサート", PLAYER) && COND("前Ｔ調教者") == PLAYER
	PRINTFORMW %CALLNAME:TARGET%はアナルから抜き去ることなく%CALLNAME:PLAYER%を組み伏せて、再び腰を動かし始めた…
ELSEIF INSERT("Ｖインサート", PLAYER) && COND("前Ｔ調教者") == PLAYER
	PRINTFORMW %CALLNAME:TARGET%は膣から抜いたモノを%CALLNAME:PLAYER%のアナルに一息に挿入して、再び腰を動かし始めた…
ELSE
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の前で四つん這いになり、
	IF TALENT:PLAYER:Ａ処女 == 1
		PRINTFORMW 男を受け入れた事のないアナルへの挿入をせがんだ…
	ELSE
		IF RAND:3 == 0
			PRINTFORM お尻を高く上げて
		ELSEIF TALENT:好色 || TALENT:淫乱
			PRINTFORM お尻を振って
		ENDIF
		PRINTFORMW %CALLNAME:TARGET%を誘った。
	ENDIF
	PRINTFORM %CALLNAME:TARGET%は誘われるままに、%CALLNAME:PLAYER%の
	SIF TALENT:恋慕
		PRINT 顔を見つめながら
	PRINTFORMW 直腸に%NAMES("ペニス")%を埋めて腰を動かし始めた…
ENDIF

@COM_ORDER_COM126, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

IF PENIS(TARGET)
	LOCAL += 25
	LOCALS = %LOCALS% + ペニス有り(25)
ENDIF
;貞操観念
IF TALENT:貞操観念
	LOCAL -= 10
	LOCALS = %LOCALS% - 貞操観念(10)
ENDIF
;貞操無頓着
IF TALENT:貞操無頓着
	LOCAL += 5
	LOCALS = %LOCALS% + 貞操無頓着(5)
ENDIF
;快感に素直
IF TALENT:快感に素直
	LOCAL += 2
	LOCALS = %LOCALS% + 快感に素直(2)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF
;調教者が処女
IF EXP:PLAYER:Ｖ経験 == 0 && TALENT:PLAYER:オトコ == 0
	LOCAL -= 15
	LOCALS = %LOCALS% - %CALLNAME:PLAYER%が処女(15)
ENDIF

;汚れあり
IF COM_ORDER_STAIN("Ａ", "Ｐ")/2
	LOCAL -= COM_ORDER_STAIN("Ａ", "Ｐ")/2
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ａ", "Ｐ")/2})
ENDIF

;合計を表示(60以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 60

RETURN RESULT
