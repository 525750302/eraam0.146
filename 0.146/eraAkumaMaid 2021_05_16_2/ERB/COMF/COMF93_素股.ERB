@COM93
;素股
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 93

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%
;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM93, ARG
IF CHECK_COM("見せ槍")
	CALL SETFLAG, "見せ槍フラグ", TARGET
ELSE
	;対象のＶ⇔調教者のＰの汚れが移動
	SIF CHECK_CLO("Ｖ露出")
		CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "Ｐ"
ENDIF
SIF ASSIPLAY == 0
	TFLAG:好感度ボーナス += 1

@SOURCE_COM93, ARG
CALL VARSET_COMF

SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:屈従 = 1200
SOURCE:0:逸脱 = 50

IF CHECK_COM("見せ槍") == 0
	CALL SOURCE_SERVICE_PLAY, "素股"

	CALL SOURCE_DOWNBASE_HP, 93

	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｖ")*50 + 60

	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 2)

	;PALAM:Ｖ潤
	SELECTCASE PALAM:Ｖ潤
	CASE IS < PALAMLV:1
		TIMES SOURCE:0:Ｃ快楽, 0.10
		TIMES SOURCE:0:性行動, 0.60
	CASE IS < PALAMLV:2
		TIMES SOURCE:0:Ｃ快楽, 0.30
		TIMES SOURCE:0:性行動, 0.80
	CASE IS < PALAMLV:3
		TIMES SOURCE:0:Ｃ快楽, 0.50
		TIMES SOURCE:0:性行動, 1.00
	CASE IS < PALAMLV:4
		TIMES SOURCE:0:性行動, 1.20
	CASE IS < PALAMLV:5
		TIMES SOURCE:0:Ｃ快楽, 0.90
		TIMES SOURCE:0:性行動, 1.40
	CASEELSE
		TIMES SOURCE:0:Ｃ快楽, 0.80
		TIMES SOURCE:0:性行動, 1.60
	ENDSELECT

	;アソコが露出していないと快Ｃ減
	IF CHECK_CLO("Ｖ露出") == 0 && COND("調教中")
		;ズボン等をはいていると快Ｃ激減
		IF CHECK_OUTER("ズボン") || CHECK_PANTIES("レザー")
			TIMES SOURCE:0:Ｃ快楽, 0.20
			TIMES SOURCE:0:露出, 0.20
		ELSE
			TIMES SOURCE:0:Ｃ快楽, 0.60
			TIMES SOURCE:0:露出, 0.50
		ENDIF
	ENDIF
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM93
;ちんこの大きさは２以上
IF CHECK_COM("見せ槍")
	PRINTFORMW %CALLNAME:PLAYER%は腰を突き出して%TEXTS("巨根：視覚", PLAYER)%を%CALLNAME:TARGET%に見せつけた。
	IF ABL:欲望 < 3 || EXP:Ｖ性交経験 + EXP:Ａ性交経験 + EXP:尿道性交経験 == 0
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスに気圧されつつも、目が離せないでいる……
	ELSE
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%のモノ%TEXTR("に腰を擦り付けつつ/をお腹にあてがい")%、
		PRINTFORMW %TEXTR("大きさに惚れ惚れし/これから起こることを想像し/どこまで入ってくるのかを確認し/気分を高め")%ている……
	ENDIF
ELSEIF CHECK_COM("Ｗ奉仕")
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 二人は
		IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:3 == 0
			PRINT うっとりしながら
		ELSEIF RAND:2 == 0
			PRINT 夢中になって
		ENDIF
		PRINTFORMW %CALLNAME:MASTER%のペニスに股間を擦り付け、刺激し続けている…
	ELSE
		PRINTFORM %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%は
		SIF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 + TALENT:淫乱 + TALENT:恋慕 && RAND:2 == 0
			PRINTFORM 上気した瞳で%CALLNAME:MASTER%を見つめながら、
		PRINTFORMW 協力してペニスを\@ PENIS(TARGET) + PENIS(TARGET:1) == 0 ? アソコ # 股間 \@で挟んで扱いてきた…
	ENDIF
ELSE
	IF TEQUIP:キス
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%%TEXTS("キス継続助詞付き")%ペニスを
		CALL SETFLAG, "追加キスフラグ", TARGET
	ELSE
		PRINTFORM %CALLNAME:TARGET%はペニスを
	ENDIF
	IF PREVCOM == SELECTCOM && PREVPLAY("見せ槍") == 0
		PRINTFORMW 股間で挟んで扱き続けている……
	ELSE
		;アソコ剥き出し
		IF CHECK_CLO("Ｖ露出") && TALENT:オトコ == 0
			IF RAND:2
				PRINT ぷっくりと膨らんだ柔らかい
			ELSEIF COND("陰毛") >= 6
				PRINT 濡れた陰毛の絡みつく
			ELSEIF COND("陰毛") == 5
				;PRINT 綺麗に剃り整えられた
				PRINT 柔らかな
			;ELSEIF COND("陰毛") == 4
			;	PRINT 生えかけの
			ELSEIF COND("陰毛") && COND("陰毛") <= 3
				PRINT ツルツルの
			ENDIF
		ENDIF
		PRINTFORM 股間で挟み、
		SIF ABL:腰使い >= 3
			PRINT 腰を淫靡にくねらせて
		IF CHOSECOM(SELECTCOM) == 0 && PENIS(PLAYER) && SIZE("ペニス", PLAYER) >= 2
			PRINTFORMW 扱き始めた。
			PRINTFORM %CALLNAME:TARGET%は奉仕の合間に%CALLNAME:PLAYER%のペニスを自分のお腹にあてがい、
			IF CHOSECOMS("挿入")
				PRINTFORMW どこまで入っていたのか確認している……
			ELSE
				PRINTFORMW どこまで入ってくるのかを確認している……
			ENDIF
		ELSE
			PRINTFORMW 扱きあげた…
		ENDIF
	ENDIF
ENDIF

@COM_ORDER_COM93, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 3
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 3})
ENDIF

;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	LOCALS = %LOCALS% - 恥じらい(3)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF

;媚薬
IF TEQUIP:媚薬
	LOCAL += 6
	LOCALS = %LOCALS% + 媚薬(6)
ENDIF

;汚れあり
IF COM_ORDER_STAIN("Ｐ", "Ｖ")/3
	LOCAL -= COM_ORDER_STAIN("Ｐ", "Ｖ")/3
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｐ", "Ｖ")/3})
ENDIF

;合計を表示(20以上で実行)
IF CHECK_COM("見せ槍", 93)
	CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 5
ELSE
	CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 20
ENDIF

RETURN RESULT
