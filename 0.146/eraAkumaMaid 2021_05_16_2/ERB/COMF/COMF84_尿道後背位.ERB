@COM84
;尿道後背位
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM84, ARG
CALL FLAG_INSERTION, "尿道インサート", ARG, 110

@SOURCE_COM84, ARG
CALL SOURCE_INSERTION, "尿道インサート", ARG, 110

CALL VARSET_COMF

SOURCE:0:露出 = 1000
;動物耳
SIF TALENT:動物耳
	SOURCE:0:露出 = 200
SOURCE:0:情愛 = 350

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:情愛, 0.30
CASE IS < PALAMLV:2
	TIMES SOURCE:0:情愛, 0.60
CASE IS < PALAMLV:3
	TIMES SOURCE:0:情愛, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:情愛, 1.50
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:情愛, 1.80
ENDSELECT

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM84
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
CALL TRAIN_MESSAGE_INSERTION
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

PRINTFORM %CALLNAME:PLAYER%は
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
	CALL ADD_TEXT, "性器から性器へ"
;尿道が露出していないで、パンツをずらしていないなら自動的にずらす
ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
	SIF TEQUIP:拘束
		PRINTFORM 拘束されている
	PRINTFORM %CALLNAME:TARGET%の
	CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET
;尿道セックスに慣れている、かつ欲情LV4以上で縛られてないと誘ってきます
ELSEIF CHECK("尿道インサート") && PALAM:欲情 >= PALAMLV:4 && TEQUIP:拘束 == 0 && RAND:2 == 0
	IF PREVCOM == 13
		PRINT アソコを指で広げて
	ELSEIF PREVCOM == 14
		PRINT お尻を指で押し広げて
	;淫乱
	ELSEIF (TALENT:淫乱 || ABL:尿道感覚 >= 3) && RAND:2 == 0
		IF RAND:2
			IF TALENT:小尻
				PRINT 小さな
			ELSEIF TALENT:巨尻
				PRINT 大きな
			ENDIF
			PRINT お尻を割り開いて見せ付けながら
		ELSE
			PRINT 大事な場所を見せ付けながら
		ENDIF
	ELSE
		PRINTFORM %TEXTR("恥ずかしい所が丸見えの/股間をさらけ出した")%格好で
	ENDIF
	;小悪魔
	IF TALENT:小悪魔 && RAND:3
		PRINT ニヤニヤしている
	;(反抗的でなくて淫乱) or(反抗的かつ恋慕)
	ELSEIF  ( (TALENT:淫乱 && TALENT:反抗的 == 0) || (TALENT:反抗的 && TALENT:恋慕) ) && RAND:3
		SIF TALENT:小悪魔 && RAND:2 == 0
			PRINT 、生意気にも
		PRINT 誘っている
	;反抗的かつ淫乱
	ELSEIF TALENT:反抗的 && TALENT:淫乱
		PRINT そっぽを向きながら顔を赤くしている
	;恥じらい
	ELSEIF TALENT:恥じらい
		PRINT 恥ずかしそうにしている
	ELSE
		PRINTFORM %TEXTR("微笑んでいる/横たわっている")%
	ENDIF
	PRINTFORM %CALLNAME:TARGET%の
ELSE
	;縛ってますか？
	IF TEQUIP:拘束
		PRINTFORM 拘束されている%CALLNAME:TARGET%の
	;ラミアですか？
	ELSEIF COND("種族：ラミア")
		SIF CHECK("尿道インサート") && RAND:2 == 0
			PRINT 擦り寄ってきた
		PRINTFORM %CALLNAME:TARGET%の
	;尿道セックスに慣れているなら誘ってきます
	ELSEIF CHECK("尿道インサート") && RAND:2 == 0
		SIF TALENT:恥じらい
			PRINT 恥ずかしがりながら
		IF CONFIG("立位")
			PRINTFORM お尻を向け
		ELSE
			PRINTFORM 四つん這いになっ
		ENDIF
		;淫乱かつ欲情4以上
		IF TALENT:淫乱 && PALAM:欲情 >= PALAMLV:4 && RAND:2 == 0
			PRINT て、お尻を振りながら誘っている
		ELSE
			PRINT た
		ENDIF
		PRINTFORM %CALLNAME:TARGET%の
	ELSE
		IF TALENT:反抗的 && ABL:尿道感覚 <= 2 && TALENT:恋慕 == 0
			PRINT 嫌がる
		ELSEIF TALENT:恥じらい
			PRINT 恥ずかしがる
		ENDIF
		PRINTFORM %CALLNAME:TARGET%を四つん這いにさせて
		SIF TALENT:恥じらい || (TALENT:反抗的 && ABL:尿道感覚 <= 2 && TALENT:恋慕 == 0)
			PRINT 、
	ENDIF
	PRINTFORM %TEXTS("尿道")%
ENDIF
PRINTFORM %TEXTR("おしっこの穴/尿道")%に
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
ELSE
	PRINTFORM %NAMES("ペニス", PLAYER)%を
ENDIF
;PLAYERがサドかつ尿道経験20以下
IF ABL:PLAYER:サドっ気 >= 3 && EXP:尿道経験 <= 20
	PRINT 力任せに
;PLAYERがサドor尿道セックスに慣れている
ELSEIF (CHECK("尿道インサート") || ABL:PLAYER:サドっ気 >= 3) && RAND:2 == 0
	PRINT 一気に
ELSEIF RAND:2 == 0 || EXP:尿道経験 <= EXPLV:3
	PRINT 慎重に
ENDIF
;PLAYERがサドor尿道経験20以上or恋慕かつ尿道感覚Lv3以上
SIF ABL:PLAYER:サドっ気 >= 3 || MIN(EXP:尿道経験, EXP:尿道性交経験) >= 20 || (TALENT:恋慕 && ABL:尿道感覚 >= 3)
	PRINT 根元まで
;PLAYERがサド
IF ABL:PLAYER:サドっ気 >= 3
	PRINT 押し込み、
ELSE
	PRINT 挿入し、
ENDIF
IF TALENT:妊娠 && RAND:2 == 0
	PRINT 大きなお腹をさすりながら
ELSEIF TALENT:妊娠
	PRINT 負担がかからないよう注意しながら
ELSEIF RAND:2
	PRINTFORM お尻を%TEXTR("撫でながら/鷲掴んで/揉みしだきながら")%
;PLAYERがサドorA経験20以上or恋慕かつA感覚Lv3以上
ELSEIF TALENT:小柄 && (ABL:PLAYER:サドっ気 >= 3 || MIN(EXP:尿道経験, EXP:尿道性交経験) >= 20 || (TALENT:恋慕 && ABL:尿道感覚 >= 3) )
	PRINT おなかを掴んで
ELSEIF ABL:PLAYER:サドっ気 >= 3 || MIN(EXP:尿道経験, EXP:尿道性交経験) >= 20 || (TALENT:恋慕 && ABL:尿道感覚 >= 3)
	PRINT くびれを掴んで
ENDIF
;PLAYERがサドor妊娠していない、かつ尿道経験3以上or恋慕かつ尿道感覚Lv4以上
IF (ABL:PLAYER:サドっ気 >= 3 || TALENT:妊娠 == 0) && (MIN(EXP:尿道経験, EXP:尿道性交経験) >= 20 || (TALENT:恋慕 && ABL:尿道感覚 >= 3) ) && RAND:3 == 0
	PRINT 荒々しく
ELSEIF (ABL:PLAYER:サドっ気 >= 3 || MIN(EXP:尿道経験, EXP:尿道性交経験) >= 20 || ABL:尿道感覚 >= 3 || ABL:PLAYER:腰使い) && SIZE("ペニス", PLAYER) && RAND:2 == 0
	SIF ABL:PLAYER:腰使い && RAND:2
		PRINT 入り口から奥まで責める
	PRINT 大きなストロークで
ELSEIF MAX(EXP:尿道経験, EXP:尿道性交経験) < 50
	PRINTFORM %TEXTR("浅めに/ゆっくりと")%
ELSE
	PRINTFORM %TEXTR("敏感な粘膜をゴシゴシ擦りながら/激しく")%
ENDIF
;PLAYERがサドor尿道経験20以上or尿道感覚Lv3以上
IF (ABL:PLAYER:サドっ気 >= 3 || MIN(EXP:尿道経験, EXP:尿道性交経験) >= 20 || ABL:尿道感覚 >= 3) && RAND:3 == 0
	PRINTFORMW 腰を打ちつけた…
ELSE
	PRINTFORMW %TEXTR("出し入れした/腰を前後させた/腰を振った")%…
ENDIF
