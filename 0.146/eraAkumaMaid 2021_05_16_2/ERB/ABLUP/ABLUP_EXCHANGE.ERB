;ARGは１度に交換する単位。初期は１万
@JUEL_TO_EXP, ARG
#DIM LCOUNT
#DIM MEMO_EXP
#DIM MEMO_JUEL, 16

;まずは経験値と各種珠の量を記録
MEMO_EXP = EXP:経験値
FOR LCOUNT, 0, 16
	MEMO_JUEL:LCOUNT = JUEL:LCOUNT
NEXT

CALL JUEL_TO_EXP_CALC, ARG, JUEL:0, JUEL:1, JUEL:2, JUEL:3, JUEL:4, JUEL:5, JUEL:6, JUEL:7, JUEL:8, JUEL:9, JUEL:10, JUEL:11, JUEL:12, JUEL:13, JUEL:14, JUEL:15

;やり直す？
IF RESULT == 99
	EXP:経験値 = MEMO_EXP
	FOR LCOUNT, 0, 16
		JUEL:LCOUNT = MEMO_JUEL:LCOUNT
	NEXT
	RESTART
ENDIF

;交換単位は最小１万。ARG < 0ならば全部交換
@JUEL_TO_EXP_CALC, ARG, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9, ARG:10, ARG:11, ARG:12, ARG:13, ARG:14, ARG:15, ARG:16
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

PRINTFORML 
PRINTFORML ここでは各種宝珠１万個を経験値10Ｐに交換する事ができます
PRINTFORML 割の良い交換レートではないですが、宝珠が余って仕方ない場合にどうぞ
PRINTFORML 
PRINTFORM 交換後の経験値は{EXP:経験値}Ｐで、
IF ARG < 0
	PRINTFORML 選択された宝珠をすべて交換します。
	PRINTFORML どれを交換していきますか？
ELSE
	PRINTFORML 単位は{ARG/10000}万です。どれを交換していきますか？
	PRINTFORML （交換単位未満のものは１万刻みで可能なだけ交換をします）
ENDIF

FOR LCOUNT, 0, 16
	SIF PALAMNAME:LCOUNT == "" || PALAMNAME:LCOUNT == "反感"
		CONTINUE

	SELECTCASE ARG:(LCOUNT+1)
	CASE IS >= 10000
		PRINTFORM  [{LCOUNT, 2}] 
	CASEELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORM  [×] 
	ENDSELECT

	PRINTFORM %PALAMNAME:LCOUNT%の珠:{ARG:(LCOUNT+1), 8}
	SIF JUEL:LCOUNT != ARG:(LCOUNT+1)
		PRINTFORM  → {JUEL:LCOUNT, 8} {(ARG:(LCOUNT+1) - JUEL:LCOUNT)/1000, 5}Ｐ get
	PRINTL 
	RESETCOLOR
NEXT

SELECTCASE ARG
CASE 10000 TO 1000000
	PRINTFORML  [90] 交換単位を{ARG/1000}万に増やす
CASE 10000000
	PRINTFORML  [90] すべての宝珠を経験値に変換するようにする
CASEELSE
	PRINTFORML  [90] 交換単位を１万に戻す
ENDSELECT

PRINTFORML  [99] 交換状況をリセットする
PRINTFORML [100] 終了

CALL INPUT_SELECT, 16, 90, 99, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	REDRAW 1
	RETURN 1
CASE 99
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	REDRAW 1
	RETURN 99
CASE 90
	SELECTCASE ARG
	CASE 10000 TO 1000000
		ARG *= 10
	CASE 10000000
		ARG = -1
	CASEELSE
		ARG = 10000
	ENDSELECT
CASEELSE
	IF PALAMNAME:CHOICE != "" && PALAMNAME:CHOICE != "反感"
		IF JUEL:CHOICE < 10000

		ELSEIF ARG > 0 && JUEL:CHOICE >= ARG
			JUEL:CHOICE -= ARG
			EXP:経験値 += 10 * ARG / 10000
		ELSE
			LOCAL = JUEL:CHOICE / 10000

			JUEL:CHOICE -= 10000 * LOCAL
			EXP:経験値 += 10 * LOCAL
		ENDIF
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART
