;-------------------------------------------------
;ABL:技巧 のLVUP処理
;-------------------------------------------------
@ABLUP2
#DIM LCOUNT
#DIM NUM_CHOICES
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP2
;何もできない場合
IF RESULT == 0 && ABL:技巧 >= 99
	PRINTFORMW 既にMAXです。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
SETCOLOR DEF_COLOR("黄色")
;フェラ経験〜クンニ経験の合計によるEXPLVによる軽減
SELECTCASE GET_EXP_LV(EXP:キス経験 + (EXP:フェラ経験+EXP:手淫経験+EXP:足コキ経験+EXP:腰使い経験+EXP:パイズリ経験+EXP:Ａ責め経験+EXP:クンニ経験)*5) - ABL:技巧*2
CASE IS >= 12
	PRINTFORML ※技巧に関わる経験の合計回数が著しく多いので、必要な珠が80％減少しています※
CASE IS >= 9
	PRINTFORML ※技巧に関わる経験の合計回数が非常に多いので、必要な珠が60％減少しています※
CASE IS >= 6
	PRINTFORML ※技巧に関わる経験の合計回数がとても多いので、必要な珠が40％減少しています※
CASE IS >= 3
	PRINTFORML ※技巧に関わる経験の合計回数が多いので、必要な珠が20％減少しています※
ENDSELECT
RESETCOLOR

VARSET NUM_JUEL
NUM_CHOICES = 0
FOR LCOUNT, 0, 16
	SIF JUEL:0:LCOUNT == 0
		CONTINUE

	IF JUEL:LCOUNT >= JUEL:0:LCOUNT
		PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
		RESETCOLOR
	ENDIF
	NUM_JUEL:NUM_CHOICES = LCOUNT
	NUM_CHOICES += 1
NEXT

PRINTL [100] 戻る

CALL INPUT_SELECT, NUM_CHOICES, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:RESULT) >= JUEL:0:(NUM_JUEL:RESULT)
		JUEL:(NUM_JUEL:RESULT) -= JUEL:0:(NUM_JUEL:RESULT)
		ABL:技巧 += 1
		PRINTFORMW 技巧が{ABL:技巧}LVになりました。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に戻る
CALL IS_ABLUP2
SIF RESULT
	RESTART



;ABL:技巧のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP2, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:技巧
CASE 0
	JUEL:0:習得 = 100
CASE 1
	JUEL:0:習得 = 1000
CASE 2 TO 9
	JUEL:0:習得 = 1000 + (ABL:技巧 - 1)* 3000
CASEELSE
	JUEL:0:習得 = 30000 + (ABL:技巧 - 10)* 20000
ENDSELECT

;フェラ経験〜クンニ経験の合計によるEXPLVによる軽減
SELECTCASE GET_EXP_LV(EXP:キス経験 + (EXP:フェラ経験+EXP:手淫経験+EXP:足コキ経験+EXP:腰使い経験+EXP:パイズリ経験+EXP:Ａ責め経験+EXP:クンニ経験)*5) - ABL:技巧*2
CASE IS >= 12
	TIMES JUEL:0:習得, 0.20
CASE IS >= 9
	TIMES JUEL:0:習得, 0.40
CASE IS >= 6
	TIMES JUEL:0:習得, 0.60
CASE IS >= 3
	TIMES JUEL:0:習得, 0.80
ENDSELECT

;最大LV未満か？
SIF ABL:技巧 >= 99
	RETURN 0

;条件のどれかをクリアしてる？
SIF JUEL:0:習得 && JUEL:習得 >= JUEL:0:習得
	RETURN 1
RETURN 0

