;-------------------------------------------------
;ABL:Ｂ感覚 のLVUP処理
;-------------------------------------------------
@ABLUP13
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP13
;何もできない場合
IF RESULT == 0 && ABL:Ｂ感覚 >= 99
	PRINTFORMW 既にMAXです。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
;SETCOLOR DEF_COLOR("黄色")

;RESETCOLOR
VARSET NUM_JUEL
NUM_CHOICES = 0

IF ABL:Ｂ感覚 < 99
	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF
;特殊な選択肢
SET_CHOICE = 0
;淫乳を得る
IF TALENT:淫乳 == 0
	IF COND("淫乳取得可能")
		PRINTFORML  [10] [淫乳]を獲得する
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] [淫乳]を獲得する(要:快Ｂの珠{NUM("宝珠：淫乳取得")}、Ｂ感覚4LV、今回のＢ絶頂数20)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 戻る

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快Ｂ >= NUM("宝珠：淫乳取得") && TALENT:淫乳 == 0
		JUEL:快Ｂ -= NUM("宝珠：淫乳取得")
		TALENT:淫乳 = 1
		PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい…
		IF CONFIG("ニプルファック") && TALENT:巨乳
			PRINTFORMW %CALLNAME:TARGET%はすっかりいやらしく開花した乳首に指をずぶずぶと潜り込ませ、軽い自慰行為を見せ付けています…
			PRINTFORMW …これならば胸を使ったもっとハードなプレイもできそうです！
			CALL PRINT_STR, "ハートピンク_※ニプルファックを行う事ができるようになりました※_W"
		ELSEIF TALENT:恋慕 || TALENT:淫乱
			PRINTFORMW %CALLNAME:TARGET%は行為が終わった後にもかかわらず、%CALLNAME:MASTER%に胸を弄って欲しいとねだってきた…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%の胸は%CALLNAME:MASTER%によって開発され、敏感になってしまったようだ…
		ENDIF
		PRINTL 
		SETCOLOR DEF_COLOR("ハートピンク")
		PRINTFORMW %CALLNAME:TARGET%は[淫乳]を得た
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:Ｂ感覚 += 1
		PRINTFORMW Ｂ感覚が{ABL:Ｂ感覚}LVになりました。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に戻る
CALL IS_ABLUP13
SIF RESULT
	RESTART



;ABL:Ｂ感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP13, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:Ｂ感覚
CASE 0
	JUEL:0:快Ｂ = 500
CASE 1
	JUEL:0:快Ｂ = 1000
CASE 2
	JUEL:0:快Ｂ = 3000
CASE 3 TO 4
	JUEL:0:快Ｂ = (ABL:Ｂ感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快Ｂ = 10000 + (ABL:Ｂ感覚 - 4) * 10000
CASEELSE
	JUEL:0:快Ｂ = 70000 + (ABL:Ｂ感覚 - 9) * 30000
ENDSELECT

;淫乳が獲得できる？
SIF COND("淫乳取得可能") && ARGS != "ＬＶ限定"
	RETURN 1
;最大LV未満か？
SIF ABL:Ｂ感覚 >= 99
	RETURN 0

;条件のどれかをクリアしてる？
SIF JUEL:0:快Ｂ && JUEL:快Ｂ >= JUEL:0:快Ｂ
	RETURN 1
RETURN 0

