;-------------------------------------------------
;ABL:Ｃ感覚 のLVUP処理
;-------------------------------------------------
@ABLUP10
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP10
;何もできない場合
IF RESULT == 0 && ABL:Ｃ感覚 >= 99
	PRINTFORMW 既にMAXです。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
;SETCOLOR DEF_COLOR("黄色")

;RESETCOLOR

VARSET NUM_JUEL
NUM_CHOICES = 0

IF ABL:Ｃ感覚 < 99
	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF
;特殊な選択肢
SET_CHOICE = 0
;淫核を得る
IF TALENT:淫核 == 0
	IF COND("淫核取得可能")
		PRINTFORML  [10] [淫核]を獲得する
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] [淫核]を獲得する(要:快Ｃの珠{NUM("宝珠：淫核取得")}、\@ PENIS(TARGET) ? Ｐ # Ｃ \@感覚4LV、今回のＣ絶頂数20)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 戻る

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快Ｃ >= NUM("宝珠：淫核取得") && TALENT:淫核 == 0
		JUEL:快Ｃ -= NUM("宝珠：淫核取得")
		TALENT:淫核 = 1
		PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい…
		IF TALENT:恋慕 || TALENT:淫乱
			PRINTFORM %CALLNAME:TARGET%は行為が終わった後にもかかわらず、%CALLNAME:MASTER%に
			PRINTFORMW \@ PENIS(TARGET) ? ペニス # クリトリス \@の疼きを鎮めて欲しいとせがんできた…
		ELSE
			PRINTFORM %CALLNAME:TARGET%は%CALLNAME:MASTER%のほうをじっと見ながら、手で自分の
			PRINTFORMW \@ PENIS(TARGET) ? 陰茎 # 陰豆 \@をこねくり回している…
		ENDIF
		PRINTL 
		SETCOLOR DEF_COLOR("ハートピンク")
		PRINTFORMW %CALLNAME:TARGET%は[淫核]を得た
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:Ｃ感覚 += 1
		ABL:Ｐ感覚 += 1
		IF PENIS(TARGET)
			PRINTFORMW Ｐ感覚が{ABL:Ｐ感覚}LVになりました。
		ELSE
			PRINTFORMW Ｃ感覚が{ABL:Ｃ感覚}LVになりました。
		ENDIF
	ENDIF
ENDSELECT

;Ｐ感覚とＣ感覚は等しい
ABL:Ｐ感覚 = MAX(ABL:Ｃ感覚, ABL:Ｐ感覚)
ABL:Ｃ感覚 = MAX(ABL:Ｃ感覚, ABL:Ｐ感覚)

;まだレベルが上げられたりする場合には最初に戻る
CALL IS_ABLUP10
SIF RESULT
	RESTART



;ABL:Ｃ感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP10, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:Ｃ感覚
CASE 0
	JUEL:0:快Ｃ = 500
CASE 1
	JUEL:0:快Ｃ = 1000
CASE 2
	JUEL:0:快Ｃ = 3000
CASE 3 TO 4
	JUEL:0:快Ｃ = (ABL:Ｃ感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快Ｃ = 10000 + (ABL:Ｃ感覚 - 4) * 10000
CASEELSE
	JUEL:0:快Ｃ = 70000 + (ABL:Ｃ感覚 - 9) * 30000
ENDSELECT

;淫核が獲得できる？
SIF COND("淫核取得可能") && ARGS != "ＬＶ限定"
	RETURN 1
;最大LV未満か？
SIF ABL:Ｃ感覚 >= 99
	RETURN 0

;条件のどれかをクリアしてる？
SIF JUEL:0:快Ｃ && JUEL:快Ｃ >= JUEL:0:快Ｃ
	RETURN 1
RETURN 0

