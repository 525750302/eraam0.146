;能力アップ可否判定追加版
@SHOW_JUEL
#DIM MEMO_FLAG
;フラグ記憶
MEMO_FLAG = FLAG:状況判定

PRINTL 
;調教中フラグON
SETBIT FLAG:状況判定, 10

CALL NEW_PRINT_STATE, TARGET, "ABLUP"

;フラグ戻す
FLAG:状況判定 = MEMO_FLAG


@SHOW_ABLUP_SELECT
CALL PRINT_SHOW_ABLUP_SELECT


;ARGSに"予定"と入っているとレベルアップ可能かどうかなどだけ表示される（宝珠取得予定数で用いる）
@PRINT_SHOW_ABLUP_SELECT, ARGS
#DIM LCOUNT
#DIMS NOTICE_ABLUP
#DIM ADDCOND_RESULT

FOR LCOUNT, 0, 32
	;性別の問題などで表示されないものを選別
	SELECTCASE ABLNAME:LCOUNT
	CASE "", "Ｐ感覚"
		CONTINUE
	CASE "Ｖ感覚"
		SIF TALENT:オトコ
			CONTINUE
	CASE "Ｖ拡張"
		SIF TALENT:オトコ
			CONTINUE
		;ＶＡ拡張度を手動であげない場合は表示しない
		SIF CONFIG("ＶＡ拡張の手動ＬＶＵＰ") == 0
			CONTINUE
	CASE "Ａ拡張"
		;ＶＡ拡張度を手動であげない場合は表示しない
		SIF CONFIG("ＶＡ拡張の手動ＬＶＵＰ") == 0
			CONTINUE
	ENDSELECT

	VARSET NOTICE_ABLUP

	TRYCALLFORM IS_ABLUP{LCOUNT}, "ＬＶ限定"
	SIF RESULT && ABL:LCOUNT < 99
		NOTICE_ABLUP = ☆レベルアップ可能☆%BL(1)%

	;特殊な習得
	SELECTCASE ABLNAME:LCOUNT
	CASE "信頼"
		IF COND("恋慕と淫乱の自動獲得可能") == 0
			STR:実行条件 = 
			RESULT = 0
			TRYCALLFORM ADDCOND_K{NO:TARGET}("恋慕")
			ADDCOND_RESULT = RESULT
			SIF STR:実行条件 == "" && (COND("恋慕取得可能") || ADDCOND_RESULT)
				NOTICE_ABLUP = %NOTICE_ABLUP%☆恋慕☆
		ENDIF
	CASE "欲望"
		IF COND("恋慕と淫乱の自動獲得可能") == 0
			STR:実行条件 = 
			RESULT = 0
			TRYCALLFORM ADDCOND_K{NO:TARGET}("淫乱")
			ADDCOND_RESULT = RESULT
			SIF STR:実行条件 == "" && (COND("淫乱取得可能") || ADDCOND_RESULT)
				NOTICE_ABLUP = %NOTICE_ABLUP%☆淫乱☆
		ENDIF
	CASE "Ｃ感覚"
		SIF COND("淫核取得可能")
			NOTICE_ABLUP = %NOTICE_ABLUP%☆淫核☆
	CASE "Ｖ感覚"
		SIF COND("淫壷取得可能")
			NOTICE_ABLUP = %NOTICE_ABLUP%☆淫壷☆
	CASE "Ａ感覚"
		SIF COND("尻穴狂い取得可能")
			NOTICE_ABLUP = %NOTICE_ABLUP%☆尻穴狂い☆
	CASE "Ｂ感覚"
		SIF COND("淫乳取得可能")
			NOTICE_ABLUP = %NOTICE_ABLUP%☆淫乳☆
	CASE "Ｍ感覚"
		SIF COND("蕩唇取得可能")
			NOTICE_ABLUP = %NOTICE_ABLUP%☆蕩唇☆
	CASE "尿道感覚"
		SIF COND("尿道狂い取得可能")
			NOTICE_ABLUP = %NOTICE_ABLUP%☆尿道狂い☆
	ENDSELECT

	SELECTCASE ARGS
	CASE "予定"
		IF NOTICE_ABLUP != ""
			PRINTFORM %TEXT_RJ(ABLNAME:LCOUNT, 8)%
			SETCOLOR DEF_COLOR("イエロー")
			PRINTFORML  %NOTICE_ABLUP%
			RESETCOLOR
		ENDIF
	CASEELSE
		SELECTCASE ABLNAME:LCOUNT
		CASE "Ｃ感覚"
			ABL:Ｐ感覚 = MAX(ABL:Ｃ感覚, ABL:Ｐ感覚)
			ABL:Ｃ感覚 = MAX(ABL:Ｃ感覚, ABL:Ｐ感覚)
			IF PENIS(TARGET)
				PRINTFORM  [{LCOUNT, 2}] %TEXT_RJ("Ｐ感覚", 8)%-{ABL:LCOUNT, 2}LV 
			ELSE
				PRINTFORM  [{LCOUNT, 2}] %TEXT_RJ(ABLNAME:LCOUNT, 8)%-{ABL:LCOUNT, 2}LV 
			ENDIF
		CASEELSE
			PRINTFORM  [{LCOUNT, 2}] %TEXT_RJ(ABLNAME:LCOUNT, 8)%-{ABL:LCOUNT, 2}LV 
		ENDSELECT

		CALL PRINT_RANK, ABL:LCOUNT, NUM("Ｓランク", LCOUNT)
		IF NOTICE_ABLUP != ""
			SETCOLOR DEF_COLOR("イエロー")
			PRINTFORM  %NOTICE_ABLUP%
			RESETCOLOR
		ENDIF
		PRINTL 
	ENDSELECT
NEXT

SELECTCASE ARGS
CASE "予定"
	VARSET NOTICE_ABLUP
	CALL IS_ABLUP98
	SIF RESULT
		NOTICE_ABLUP = ☆レベルアップ可能☆
	IF NOTICE_ABLUP != ""
		PRINTFORM %TEXT_RJ("屈服刻印", 8)%
		SETCOLOR DEF_COLOR("イエロー")
		PRINTFORML  %NOTICE_ABLUP%
		RESETCOLOR
	ENDIF
CASEELSE
	PRINTFORM  [90] 経験値を使って素質を獲得する
	SIF COND("素質獲得可能")
		CALL PRINT_STR, "イエロー_ ☆素質獲得可能☆"
	PRINTL 
	PRINTFORM  [98] 屈服刻印-{MARK:屈服刻印, 2}LV 
	CALL PRINT_BAR, "黄色ゲージ", MARK:屈服刻印, 3, 3
	CALL IS_ABLUP98
	SIF RESULT
		CALL PRINT_STR, "イエロー_　 ☆レベルアップ可能☆"
	PRINTL  

	PRINTL [100] 自動レベルアップ（素質は獲得しない）
	PRINTL [200] 不要な宝珠を経験値に変換する
	PRINTL [999] 能力値アップの終了
ENDSELECT


;入力が0〜99の範囲外である場合はここに来る
@USERABLUP
SELECTCASE RESULT
CASE 999
	;TARGETの素質変動など
	SIF TARGET
		CALL EVENTCHECK_T
	SIF COND("手動ABLUP")
		RETURN 999

	;ASSIの素質変動など
	CALL EVENTCHECK_A
	;MASTERの素質変動など
	CALL EVENTCHECK_M

	SELECTCASE FLAG:助手を調教した
	CASE 1
		FLAG:助手を調教した = 2
		TARGET = ASSI
		BEGIN AFTERTRAIN
	CASE 2
		FLAG:助手を調教した = 0
		ASSI = TARGET
		TARGET = FLAG:調教開始時の対象
	ENDSELECT

	BEGIN TURNEND

	RETURN 999
CASE 100
	CALL AUTO_LVUP
CASE 200
	CALL JUEL_TO_EXP, 10000
ENDSELECT

RETURN 0


;ＶＡ拡張LVは除く
@AUTO_LVUP
#DIM LCOUNT
#DIM MEMO_ABL, 30
#DIM MEMO_MARK
#DIM MEMO_LINECOUNT

FOR LCOUNT, 0, 30
	MEMO_ABL:LCOUNT = ABL:LCOUNT
NEXT
MEMO_MARK = MARK:屈服刻印

CALL AUTO_LVUP_MAIN

MEMO_LINECOUNT = LINECOUNT
FOR LCOUNT, 0, 30
	SIF MEMO_ABL:LCOUNT == ABL:LCOUNT
		CONTINUE
	PRINTFORML %TEXT_RJ(ABLNAME:LCOUNT, 8)%が{ABL:LCOUNT, 2}LVになった！(+{ABL:LCOUNT - MEMO_ABL:LCOUNT})
NEXT
SIF MEMO_MARK < MARK:屈服刻印
	PRINTFORML 屈服刻印が{MARK:屈服刻印, 2}LVになった！(+{MARK:屈服刻印 - MEMO_MARK})
SIF LINECOUNT > MEMO_LINECOUNT
	WAIT

@AUTO_LVUP_MAIN
#DIM LCOUNT
#DIM IS_AUTOLVUP

IS_AUTOLVUP = 0
;優先順位に従ってＬＶを上げる

;屈服刻印は最優先
CALL IS_ABLUP98, "ＬＶ限定"
IF RESULT && MARK:屈服刻印 < 3
	JUEL:屈服 -= JUEL:0:屈服
	MARK:屈服刻印 += 1
	IS_AUTOLVUP = 1
ENDIF

;精液中毒
CALL IS_ABLUP23, "ＬＶ限定"
IF RESULT && ABL:精液中毒 < 99
	IF JUEL:欲情 >= JUEL:0:欲情
		JUEL:欲情 -= JUEL:0:欲情
	ELSEIF JUEL:屈服 >= JUEL:0:屈服
		JUEL:屈服 -= JUEL:0:屈服
	ENDIF
	ABL:精液中毒 += 1
	IS_AUTOLVUP = 1
ENDIF
;自慰中毒
CALL IS_ABLUP22, "ＬＶ限定"
IF RESULT && ABL:自慰中毒 < 99
	IF JUEL:恥情 >= JUEL:0:恥情
		JUEL:恥情 -= JUEL:0:恥情
	ELSEIF JUEL:欲情 >= JUEL:0:欲情
		JUEL:欲情 -= JUEL:0:欲情
	ENDIF
	ABL:自慰中毒 += 1
	IS_AUTOLVUP = 1
ENDIF
;マゾっ気
CALL IS_ABLUP20, "ＬＶ限定"
IF RESULT && ABL:マゾっ気 < 99
	IF JUEL:苦痛 >= JUEL:0:苦痛
		JUEL:苦痛 -= JUEL:0:苦痛
	ELSEIF JUEL:恐怖 >= JUEL:0:恐怖
		JUEL:恐怖 -= JUEL:0:恐怖
	ELSEIF JUEL:恥情 >= JUEL:0:恥情
		JUEL:恥情 -= JUEL:0:恥情
	ELSEIF JUEL:欲情 >= JUEL:0:欲情
		JUEL:欲情 -= JUEL:0:欲情
	ENDIF
	ABL:マゾっ気 += 1
	IS_AUTOLVUP = 1
ENDIF
;サドっ気
CALL IS_ABLUP21, "ＬＶ限定"
IF RESULT && ABL:サドっ気 < 99
	IF JUEL:習得 >= JUEL:0:習得
		JUEL:習得 -= JUEL:0:習得
	ELSEIF JUEL:欲情 >= JUEL:0:欲情
		JUEL:欲情 -= JUEL:0:欲情
	ENDIF
	ABL:サドっ気 += 1
	IS_AUTOLVUP = 1
ENDIF

;信頼
CALL IS_ABLUP0, "ＬＶ限定"
IF RESULT && ABL:信頼 < 99
	JUEL:恭順 -= JUEL:0:恭順
	ABL:信頼 += 1
	IS_AUTOLVUP = 1
ENDIF
;欲望
CALL IS_ABLUP1, "ＬＶ限定"
IF RESULT && ABL:欲望 < 99
	JUEL:欲情 -= JUEL:0:欲情
	ABL:欲望 += 1
	IS_AUTOLVUP = 1
ENDIF
;技巧
CALL IS_ABLUP2, "ＬＶ限定"
IF RESULT && ABL:技巧 < 99
	JUEL:習得 -= JUEL:0:習得
	ABL:技巧 += 1
	IS_AUTOLVUP = 1
ENDIF
;奉仕精神
CALL IS_ABLUP3, "ＬＶ限定"
IF RESULT && ABL:奉仕精神 < 99
	IF JUEL:恭順 >= JUEL:0:恭順
		JUEL:恭順 -= JUEL:0:恭順
	ELSEIF JUEL:習得 >= JUEL:0:習得
		JUEL:習得 -= JUEL:0:習得
	ELSEIF JUEL:屈服 >= JUEL:0:屈服
		JUEL:屈服 -= JUEL:0:屈服
	ENDIF
	ABL:奉仕精神 += 1
	IS_AUTOLVUP = 1
ENDIF
;露出癖
CALL IS_ABLUP4, "ＬＶ限定"
IF RESULT && ABL:露出癖 < 99
	JUEL:恥情 -= JUEL:0:恥情
	ABL:露出癖 += 1
	IS_AUTOLVUP = 1
ENDIF
;ＣＶＡＢＭ尿道感覚
FOR LCOUNT, 10, 16
	CALLFORM IS_ABLUP{LCOUNT}, "ＬＶ限定"
	IF RESULT && ABL:LCOUNT < 99
		JUEL:LCOUNT -= JUEL:0:LCOUNT
		ABL:LCOUNT += 1
		IS_AUTOLVUP = 1
	ENDIF
NEXT

;LVUPが発生したなら、もう一回何かのLVが上がるか試す
SIF IS_AUTOLVUP
	RESTART

RETURN 1



;BEGINではないABLUP
@ABLUP_CHARA, ARG
#DIM MEMO_TARGET
;手動ABLUPフラグON
SETBIT FLAG:状況判定, 15
SELECTCASE ARG
CASE MASTER
	CALL ABLUP_MASTER, "能力変更"
CASEELSE
	MEMO_TARGET = TARGET
	TARGET = ARG
	CALL ABLUP_CHARA_MAIN
	TARGET = MEMO_TARGET
ENDSELECT

CLEARBIT FLAG:状況判定, 15


@ABLUP_CHARA_MAIN
CALL SHOW_JUEL
CALL SHOW_ABLUP_SELECT

INPUT

SELECTCASE RESULT
CASE 0 TO 99
	TRYCALLFORM ABLUP{RESULT}
CASEELSE
	CALL USERABLUP
	SIF RESULT == 999
		RETURN 0
ENDSELECT

RESTART

