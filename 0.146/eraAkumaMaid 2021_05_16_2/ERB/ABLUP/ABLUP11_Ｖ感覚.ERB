;-------------------------------------------------
;ABL:Ｖ感覚 のLVUP処理
;-------------------------------------------------
@ABLUP11
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

SIF TALENT:オトコ
	RETURN 0

;必要な珠などの値を取得
CALL IS_ABLUP11
;何もできない場合
IF RESULT == 0 && ABL:Ｖ感覚 >= 99
	PRINTFORMW 既にMAXです。
	RETURN 0
ENDIF

DRAWLINE
VARSET NUM_JUEL
NUM_CHOICES = 0

IF ABL:Ｖ感覚 < 99
	;珠以外で必要な条件や軽減の内訳の記載
	SETCOLOR DEF_COLOR("黄色")
	SELECTCASE EXP:Ｖ経験
	CASE IS >= EXP_LV(ABL:Ｖ感覚 + 4)
		PRINTFORML ※Ｖ経験が非常に多いので、要求される珠が80％減少しています※
	CASE IS >= EXP_LV(ABL:Ｖ感覚 + 3)
		PRINTFORML ※Ｖ経験がとても多いので、要求される珠が50％減少しています※
	CASE IS >= EXP_LV(ABL:Ｖ感覚 + 2)
		PRINTFORML ※Ｖ経験が多いので、要求される珠が20％減少しています※
	;著しく足りていない
	CASE IS < EXP_LV(ABL:Ｖ感覚 + 1)
		PRINTFORML ※Ｖ経験が少なすぎるので、要求される珠が３倍に増加しています※
	ENDSELECT
	RESETCOLOR

	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF

;特殊な選択肢
SET_CHOICE = 0
;淫壷を得る
IF TALENT:淫壷 == 0
	IF COND("淫壷取得可能")
		PRINTFORML  [10] [淫壷]を獲得する
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] [淫壷]を獲得する(要:快Ｖの珠{NUM("宝珠：淫壷取得")}、Ｖ感覚4LV、今回のＶ絶頂数20)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 戻る

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快Ｖ >= NUM("宝珠：淫壷取得") && TALENT:淫壷 == 0
		JUEL:快Ｖ -= NUM("宝珠：淫壷取得")
		TALENT:淫壷 = 1
		PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい…
		IF TALENT:恋慕 || PENIS(MASTER) == 0
			PRINTFORMW %CALLNAME:TARGET%は行為が終わった後にもかかわらず、%CALLNAME:MASTER%に子宮の渇きを癒してほしいと縋りついてきた…
		ELSE
			PRINTFORMW %CALLNAME:TARGET%の蜜壷は開発し尽くされ、すっかり%CALLNAME:MASTER%のモノに順応してしまったようだ…
		ENDIF
		PRINTL 
		SETCOLOR DEF_COLOR("ハートピンク")
		PRINTFORMW %CALLNAME:TARGET%は[淫壷]を得た
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:Ｖ感覚 += 1
		PRINTFORMW Ｖ感覚が{ABL:Ｖ感覚}LVになりました。
	ENDIF
ENDSELECT

;子宮性感の獲得
IF TALENT:子宮性感 == 0 && ABL:Ｖ感覚 >= 5 && (ABL:Ｖ感覚 >= 9 || EXP:子宮口経験 >= 100)
	CALL PRINT_STRL, @"%CALLNAME:TARGET%は_黄色_[子宮性感]_を得た"
	TALENT:子宮性感 = 1
	FORCEWAIT
ENDIF

;まだレベルが上げられたりする場合には最初に戻る
CALL IS_ABLUP11
SIF RESULT
	RESTART



;ABL:Ｖ感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP11, ARGS
SIF TALENT:オトコ
	RETURN 0

CALL VARSET_ABLUP
SELECTCASE ABL:Ｖ感覚
CASE 0
	JUEL:0:快Ｖ = 500
CASE 1
	JUEL:0:快Ｖ = 1000
CASE 2
	JUEL:0:快Ｖ = 3000
CASE 3 TO 4
	JUEL:0:快Ｖ = (ABL:Ｖ感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快Ｖ = 10000 + (ABL:Ｖ感覚 - 4) * 10000
CASEELSE
	JUEL:0:快Ｖ = 70000 + (ABL:Ｖ感覚 - 9) * 30000
ENDSELECT

;Ｖ経験による軽減
SELECTCASE EXP:Ｖ経験
CASE IS >= EXP_LV(ABL:Ｖ感覚 + 4)
	TIMES JUEL:0:快Ｖ, 0.20
CASE IS >= EXP_LV(ABL:Ｖ感覚 + 3)
	TIMES JUEL:0:快Ｖ, 0.50
CASE IS >= EXP_LV(ABL:Ｖ感覚 + 2)
	TIMES JUEL:0:快Ｖ, 0.80
;著しく足りていない
CASE IS < EXP_LV(ABL:Ｖ感覚 + 1)
	TIMES JUEL:0:快Ｖ, 3.00
ENDSELECT


;淫壷が獲得できる？
SIF COND("淫壷取得可能") && ARGS != "ＬＶ限定"
	RETURN 2
;最大LV未満か？
SIF ABL:Ｖ感覚 >= 99
	RETURN 0

;条件のどれかをクリアしてる？
SIF JUEL:0:快Ｖ && JUEL:快Ｖ >= JUEL:0:快Ｖ
	RETURN 1
RETURN 0

