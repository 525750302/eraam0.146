;-------------------------------------------------
;ABL:マゾっ気 のLVUP処理
;-------------------------------------------------
@ABLUP20
#DIM LCOUNT
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP20
;何もできない場合
IF RESULT == 0 && ABL:マゾっ気 >= 99
	PRINTFORMW 既にMAXです。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
SETCOLOR DEF_COLOR("黄色")
;被虐快楽経験による軽減
SELECTCASE EXP:被虐快楽経験
CASE IS >= EXP_LV(ABL:マゾっ気 + 5)
	PRINTFORML ※被虐快楽経験が非常に多いので、要求される珠が80％減少しています※
CASE IS >= EXP_LV(ABL:マゾっ気 + 4)
	PRINTFORML ※被虐快楽経験がとても多いので、要求される珠が50％減少しています※
CASE IS >= EXP_LV(ABL:マゾっ気 + 3)
	PRINTFORML ※被虐快楽経験が多いので、要求される珠が20％減少しています※
;著しく足りていない
CASE IS < EXP_LV(ABL:マゾっ気 + 2)
	PRINTFORML ※被虐快楽経験が少なすぎるので、要求される珠が３倍に増加しています※
ENDSELECT
RESETCOLOR

VARSET NUM_JUEL
NUM_CHOICES = 0
FOR LCOUNT, 0, 16
	SIF JUEL:0:LCOUNT == 0
		CONTINUE

	IF JUEL:LCOUNT >= JUEL:0:LCOUNT
		PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
		RESETCOLOR
	ENDIF
	NUM_JUEL:NUM_CHOICES = LCOUNT
	NUM_CHOICES += 1
NEXT

;特殊な選択肢
SET_CHOICE = 0

PRINTL [100] 戻る

CALL INPUT_SELECT, NUM_CHOICES, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:RESULT) >= JUEL:0:(NUM_JUEL:RESULT)
		JUEL:(NUM_JUEL:RESULT) -= JUEL:0:(NUM_JUEL:RESULT)
		ABL:マゾっ気 += 1
		PRINTFORMW マゾっ気が{ABL:マゾっ気}LVになりました。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に戻る
CALL IS_ABLUP20
SIF RESULT
	RESTART



;ABL:マゾっ気のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP20, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:マゾっ気
CASE 0
	JUEL:0:欲情 = 2000
CASE 1
	JUEL:0:欲情 = 5000
CASE 2
	JUEL:0:欲情 = 10000
CASE 3
	JUEL:0:欲情 = 20000
CASE 4
	JUEL:0:欲情 = 30000
CASEELSE
	JUEL:0:欲情 = 40000 + (ABL:マゾっ気 - 5) * 30000
ENDSELECT

SIF TALENT:高慢
	TIMES JUEL:0:欲情, 2.00
SIF TALENT:謙虚 || TALENT:倒錯的
	TIMES JUEL:0:欲情, 0.50

;被虐快楽経験による軽減
SELECTCASE EXP:被虐快楽経験
CASE IS >= EXP_LV(ABL:マゾっ気 + 5)
	TIMES JUEL:0:欲情, 0.20
CASE IS >= EXP_LV(ABL:マゾっ気 + 4)
	TIMES JUEL:0:欲情, 0.50
CASE IS >= EXP_LV(ABL:マゾっ気 + 3)
	TIMES JUEL:0:欲情, 0.80
;著しく足りていない
CASE IS < EXP_LV(ABL:マゾっ気 + 2)
	TIMES JUEL:0:欲情, 3.00
ENDSELECT

JUEL:0:恐怖 = JUEL:0:欲情 / 2
JUEL:0:恥情 = JUEL:0:欲情 * 3 / 4
JUEL:0:苦痛 = JUEL:0:欲情 / 2

;最大LV未満か？
SIF ABL:マゾっ気 >= 99
	RETURN 0

;条件のどれかをクリアしてる？

SIF JUEL:0:恐怖 && JUEL:恐怖 >= JUEL:0:恐怖
	RETURN 1
SIF JUEL:0:恥情 && JUEL:恥情 >= JUEL:0:恥情
	RETURN 1
SIF JUEL:0:欲情 && JUEL:欲情 >= JUEL:0:欲情
	RETURN 1
SIF JUEL:0:苦痛 && JUEL:苦痛 >= JUEL:0:苦痛
	RETURN 1
RETURN 0

