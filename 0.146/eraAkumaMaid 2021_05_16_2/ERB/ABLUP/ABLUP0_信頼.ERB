;-------------------------------------------------
;ABL:信頼 のLVUP処理
;-------------------------------------------------
@ABLUP0
#DIM LCOUNT
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10
#DIM ADDCOND_RESULT

;必要な珠などの値を取得
CALL IS_ABLUP0
;何もできない場合
IF RESULT == 0 && ABL:信頼 >= 99
	PRINTFORMW 既にMAXです。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
;SETCOLOR DEF_COLOR("黄色")

;RESETCOLOR

VARSET NUM_JUEL
NUM_CHOICES = 0
IF ABL:信頼 < 99
	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF

;特殊な選択肢
SET_CHOICE = 0
;恋慕に目覚める
IF TALENT:恋慕 == 0 && COND("恋慕と淫乱の自動獲得可能") == 0
	STR:実行条件 = 
	RESULT = 0
	TRYCALLFORM ADDCOND_K{NO:TARGET}("恋慕")
	ADDCOND_RESULT = RESULT

	;ダメだった場合
	IF STR:実行条件 != ""
		CALL PRINT_STRL, @"灰色_[×] 恋慕に目覚める(%STR:実行条件%)"
	ELSEIF COND("恋慕取得可能") || ADDCOND_RESULT
		PRINTFORML  [10] 恋慕に目覚める
		SET_CHOICE = 10
	ELSE
		CALL PRINT_STRL, "灰色_[×] 恋慕に目覚める(要:信頼5LV、好感度2500)"
	ENDIF
ENDIF

PRINTL [100] 戻る

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASE 10
	CALL SETFLAG, "恋慕"
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:RESULT) >= JUEL:0:(NUM_JUEL:RESULT)
		JUEL:(NUM_JUEL:RESULT) -= JUEL:0:(NUM_JUEL:RESULT)
		ABL:信頼 += 1
		PRINTFORMW 信頼が{ABL:信頼}LVになりました。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に戻る
CALL IS_ABLUP0
SIF RESULT
	RESTART



;ABL:信頼のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
;なお恋慕の獲得もここに含める
@IS_ABLUP0, ARGS
#DIM ADDCOND_RESULT

CALL VARSET_ABLUP
;まずは必要な珠などの計算
SELECTCASE ABL:信頼
CASE 0
	JUEL:0:恭順 = 100
CASE 1 TO 4
	JUEL:0:恭順 = ABL:信頼 * 1000
CASE 5 TO 9
	JUEL:0:恭順 = 4000 + (ABL:信頼 - 4) * 5000
CASEELSE
	JUEL:0:恭順 = 30000 + (ABL:信頼 - 10) * 20000
ENDSELECT

;恋慕が獲得できる？
IF TALENT:恋慕 == 0 && COND("恋慕と淫乱の自動獲得可能") == 0 && ARGS != "ＬＶ限定"
	STR:実行条件 = 
	RESULT = 0
	TRYCALLFORM ADDCOND_K{NO:TARGET}("恋慕")
	ADDCOND_RESULT = RESULT
	SIF STR:実行条件 == "" && (COND("恋慕取得可能") || ADDCOND_RESULT)
		RETURN 1
ENDIF
;最大LV未満か？
SIF ABL:信頼 >= 99
	RETURN 0

;条件のどれかをクリアしてる？
SIF JUEL:0:恭順 && JUEL:恭順 >= JUEL:0:恭順
	RETURN 1
RETURN 0
