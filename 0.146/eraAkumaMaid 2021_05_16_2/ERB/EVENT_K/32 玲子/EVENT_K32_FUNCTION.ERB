;
;  玲子専用のCOM_CHECK
;    コマンドの派生確認に使う
;
@RYOKO_COM_CHECK(ARGS)
#FUNCTION
	; TARGETの確認
	SIF NO:TARGET != 32
		RETURNF 0

	SELECTCASE ARGS
		CASE "素股誤挿入"
			SIF SELECTCOM != 93
				RETURNF 0
			SIF FIRSTCOM(SELECTCOM) == 0
				RETURNF 0
			SIF EXP:Ｖ性交経験 == 0
				RETURNF 0
			SIF CHECK_COM("見せ槍")
				RETURNF 0
			SIF CEVENT("調教：素股誤挿入") || CEVENT("調教：素股誤挿入発生済み")
				RETURNF 0
			SIF !CHECK_CLO("Ｖ露出")
				RETURNF 0
			SIF PALAM:Ｖ潤 < 3000
				RETURNF 0
			;SIF RAND:4
			;	RETURNF 0
			
			RETURNF 1
	ENDSELECT

	RETURNF 0

@RYOKO_ALTNATE_COM(ARGS)

	; TARGETの確認
	SIF NO:TARGET != 32
		RETURN 0

	SELECTCASE ARGS
		CASE "素股誤挿入"
			; 素股中に挿入してしまったというシチュエーション
			; 擬似的な正常位 or 騎乗位になる

			SIF SELECTCOM != 93
				RETURN 0

			TFLAG:地の文スキップ = 1
			CALL SET_CEVENT, "調教：素股誤挿入"
			CALL SET_CEVENT, "調教：素股誤挿入発生済み"

			; 素股の地の文をマニュアル表示
			CALL TRAIN_MESSAGE_COM93

			; 口上を表示
			CALL KOJO_MESSAGE_COM_K32

			; 派生描画はここで行う
			PRINTW …………
			PRINTFORM %CALLNAME:TARGET%は%TEXTR("愛液/淫らな蜜")%滴る%TEXTR("アソコ/女性器/秘裂")%で、%CALLNAME:PLAYER%の%TEXTR("陰茎/ペニス/肉竿")%
			IF TEQUIP:押し倒し
				PRINTFORM を%TEXTR("押さえつけ/押し倒し")%アイス%TEXTR("＿/キャンディ")%を舐めるかのように%TEXTR("入口のひだひだで扱いて/柔らかな膣口を擦り付けて")%くる……
			ELSE
				PRINTFORM を%TEXTR("しゃぶりつく/舐めまわす/むしゃぶりつく")%ように吸い付こうとしてくる……
			ENDIF
			PRINTFORMW 
			PRINTFORMW %CALLNAME:PLAYER%の%TEXTR("腰の動き/ストローク")%に合わせて%CALLNAME:TARGET%は%TEXTR("もどかしそうに/たじろぐように/物足りなさそうに")%身を捩っているが、そのせいで%CALLNAME:PLAYER%の%TEXTR("ペニス/男性器")%が%TEXTR("女性器/肉壷")%に侵入しそうになっている……
			PRINTFORMW %CALLNAME:TARGET%は%TEXTR("亀頭だけ/浅く")%%TEXTR("入っては抜ける/抽挿")%を繰り返す動作に%TEXTR("複雑そうな/恥ずかしそうな/批難じみた")%顔をしているが、%TEXTR("歓迎するような/迎え入れるような")%腰の動きを止めるつもりは無さそうだ……
			PRINTFORMW やがて、
			PRINTL
			CALL PRINT_STRW,  "　　　　　　　　_ハートピンク_にゅるん_BHH_"
			PRINTL
			PRINTFORMW 　　　　と%TEXTR("形になっていない/柔らか肉の/愛液に満ちた肉壷の")%抵抗を感じながら、%CALLNAME:PLAYER%の%TEXTR("ペニス/イチモツ")%は%TEXTR("根本まで/余す所なく")%%CALLNAME:TARGET%の%TEXTR("肉壷/女性器/胎内/膣/アソコ")%に呑み込まれてしまった……

			; コマンドの派生を表示
			PRINTL 
			PRINT 素股　→　素股誤挿入
			IF TEQUIP:押し倒し
				PRINTL 騎乗位
			ELSE
				PRINTL 正常位
			ENDIF
			PRINTL 

			; 追加地の文
			CALL TEXT,"追加地の文：Ｖ挿入後",TARGET

			; 実コマンドの変更
			SELECTCOM = 60
			SIF TEQUIP:押し倒し
				SELECTCOM = 65

	ENDSELECT

	RETURN 0

; 何らかの処理をまとめた関数
; 描画ではなく、値の管理のみを行う
@RYOKO_SET_FLAG(ARGS)
#DIM  MEMO
#DIMS MEMOS
#DIM  SAMEN_VAL
#DIMS TMPS
#DIM  CNT
	; TARGETの確認
	SIF NO:TARGET != 32
		RETURN 0

SELECTCASE ARGS
	CASE "着せ替え：服を着替えた"
		; 服を着替えさせたときに発生する諸々の一括処理
		IF CHECK_SHIRT("巫女")
			IF !CHECK_CLO("ノーパン")
				CALL SET_PANTIES, ""
				CFLAG:下着確認済 = 3
			ENDIF
			SIF EQUIP:ブラ
				CALL SET_BRA, ""
		ENDIF
	CASE "性処理：膣内使用済みコンドーム破れ"
		; 性処理後、膣内に残していたコンドームが破れた

		; フローがよくわからぬ……
		;   汎用処理とは異なる動作をさせたいので、結局、自作
		; Ref.
		;   CALL SETFLAG, "膣内精液経験", PLAYER, TARGET

		DEBUGPRINTFORML CSTR:Ｖ中出され = %CSTR:Ｖ中出され%
		DEBUGPRINTFORML 　　　　　　　　　　　　↓

		; 射精（されていた）量の決定
		MEMO       = 1
		MEMOS      = %TOSTR(BASE:PLAYER:識別番号)%：
		SAMEN_VAL  = 3
		SAMEN_VAL  = (SAMEN_VAL * 3 / 2) + 1
		SAMEN_VAL *= MEMO
		SAMEN_VAL  = MULTIPLY(SAMEN_VAL, 100 + RAND:51)

		; 経験の増加
		EXP:TARGET:膣内精液経験 += MEMO
		CFLAG:TARGET:Ｖ中出し済 += SAMEN_VAL
		SIF TALENT:妊娠
			BASE:胎児への栄養 += MEMO

		;妊娠用判定
		CALL CHECK_PREGNANT, TARGET, MEMO, MEMOS

		CALL ADD_STAIN, TARGET, "Ｖ", "精液"
		EX:Ｖ中出し      += 1
		NOWEX:Ｖ中出し   += 1
		EX:Ｖ中出し量    += SAMEN_VAL
		NOWEX:Ｖ中出し量 += SAMEN_VAL

		;Ｖ液体追加のソース
		SOURCE:Ｖ液体追加 += 100 * MEMO

		;記録を分解する
		VARSET LOCALS
		SPLIT CSTR:Ｖ中出され, "/", LOCALS
		TMPS = 

		FOR CNT, 0, 30
			SIF LOCALS:CNT == ""
				BREAK

			;今回と同じ相手のものかどうか
			IF MEMOS != "" && MEMOS == SUBSTRING(LOCALS:CNT, 0, STRLENS(MEMOS) )
				;相手：量 となっているので"量"部分を抜き出して、今回の数値を加算する
				LOCALS:CNT = %MEMOS%%TOSTR(TOINT(EXTSTR(LOCALS:CNT, "：") ) + SAMEN_VAL)%

				;MEMOSは加算して用済みなので初期化
				MEMOS = 

				;記録する文字列を再構築
				TMPS = %LOCALS:CNT%/%TMPS%
			ELSE
				TMPS = %TMPS%%LOCALS:CNT%/
			ENDIF
		NEXT

		;初めての相手ならMEMOSは文字列が入っている
		SIF MEMOS != ""
			TMPS = %MEMOS%%TOSTR(SAMEN_VAL)%/%TMPS%
		CSTR:Ｖ中出され = %TMPS%

		; 確認用
		DEBUGPRINTFORML CSTR:Ｖ中出され = %CSTR:Ｖ中出され%

		; ついでに潤滑も増える
		;   増加量については要調整
		PALAM:Ｖ潤 += (500 + RAND:100) * (RAND:((ABL:精液中毒 + 1)) + 1)

ENDSELECT
RETURN 1

@RYOKO_COND(ARGS, ARG)
#FUNCTION

	SIF ARG == 0 && TARGET > 0
		ARG = TARGET
	; TARGETの確認
	SIF NO:ARG != 32
		RETURNF 0

	SELECTCASE ARGS
		CASE "淫紋：愛液過多"
			SIF MARK:ARG:淫紋 < 2
				RETURNF 0
			RETURNF 1
		CASE "処女"
			SIF EXP:ARG:Ｖ性交経験 > 0
				RETURNF 0
			RETURNF 1
		CASE "告白可能"
			SIF !TALENT:ARG:恋慕
				RETURNF 0
			SIF !CEVENT("イベント：初妊娠", ARG)
				RETURNF 0
			RETURNF 1
	ENDSELECT

	RETURNF 0


;===========================================================
; RYOKO_SWEET_VOICE_RAND
;   汎用喘ぎ関数
;   「%RYOKO_SWEET_VOICE_RAND()%」としての使用を想定
;===========================================================
@RYOKO_SWEET_VOICE_RAND()
#FUNCTIONS
#DIMS VOICE
#DIMS SITUATION

	VARSET VOICE
	VARSET SITUATION

	RETURNF VOICE

;===========================================================
; RYOKO_SWEET_VOICE_RAND_SITUATION
;   汎用喘ぎ関数のシチュエーション判断
;   〇〇/○○/○○/... 形式
;===========================================================
@RYOKO_SWEET_VOICE_RAND_SITUATION
#FUNCTIONS
#DIMS SITUATION

	VARSET SITUATION

	; 大本のシチュエーション
	SIF COND("調教中")
		SITUATION += "調教中/"
	SIF COND("デート中")
		SITUATION += "デート中/"
	SIF COND("性処理中")
		SITUATION += "性処理中/"
	SIF COND("朝駆け相手") && COND("朝")
		SITUATION += "朝駆け中/"

	; プレイ内容
	IF INSERT("Ｖインサート")
		SITUATION += "Ｖインサート/"
		IF TEQUIP:PLAYER:コンドーム
			SITUATION += "ゴムハメ中/"
		ELSE
			IF COND("危険日")
				SITUATION += "危険日ナマハメ中/"
			ELSE
				SITUATION += "ナマハメ中/"
			ENDIF
		ENDIF
	ENDIF
	SIF INSERT("Ａインサート")
		SITUATION += "Ａインサート/"
	SIF COND("好きな体位中")
		SITUATION += "好きな体位/"
	SIF COND("Ｈないちゃつき中", MASTER)
		SITUATION += "Ｈないちゃつき中/"

	; 服装系
	SIF CHECK_CLO("ハダカ")
		SITUATION += "ハダカ/"
	SIF CHECK_CLO("下着姿")
		SITUATION += "下着姿/"
	SIF CHECK_CLO("トップレス")
		SITUATION += "トップレス/"
	SIF CHECK_CLO("ボトムレス")
		SITUATION += "ボトムレス/"
	SIF CHECK_CLO("ノーパン")
		SITUATION += "ノーパン/"
	SIF CHECK_CLO("ノーブラ")
		SITUATION += "ノーブラ/"
	SIF CHECK_CLO("水着姿")
		SITUATION += "水着姿/"

	; 場所
	IF PLACE("野外")
		SITUATION += "屋外/%SAVESTR:現在位置%/"
	ELSE
		SITUATION += "屋内/%SAVESTR:現在位置%/"
	ENDIF

	; 他情報
	SIF COND("危険日")
		SITUATION += "危険日/"
	SIF MARK:淫紋 == 1
		SITUATION += "淫紋シール/"
	SIF MARK:淫紋 >= 2
		SITUATION += "淫紋/"

	RETURNF SITUATION
