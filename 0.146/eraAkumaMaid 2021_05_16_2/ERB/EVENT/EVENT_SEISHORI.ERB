;-------------------------------------------------
;性処理関連が複雑化してきたのでまとめる
;-------------------------------------------------
;@CANTRAINの条件付きver
;ARGSにはTRAINNAMEが入る。性欲解消(あなたがパートナーの性処理に回る)の際にはTRAINNAMEの前に"性欲解消"と付ける。例)ARGS = 性欲解消正常位
;具現については[本人が具現の使い手]＆[両刀]の場合に限り性処理でも使用可とする。
;-------------------------------------------------
@CANSS(ARGS, ARG)
#FUNCTION
#DIM IS_REV
;竿役
#DIM ACTOR_USEP

SIF ARG < 0 || ARGS == ""
	RETURNF 0
SIF ARG == 0 && TARGET > 0
	ARG = TARGET

ACTOR_USEP = 0
IS_REV = 0

;性欲解消？
IF STRCOUNT(ARGS, "性欲解消")
	ARGS = %SUBSTRINGU(ARGS, 4, -1)%
	IS_REV = 1
ENDIF

;CANTRAINを満たしている事が前提なので処女の場合は無視して良い
SIF CANTRAIN(ARGS, ARG) == 0
	RETURNF 0

;性別条件などがあるなら見る。性欲解消は該当箇所の感覚３ＬＶが目安
SELECTCASE ARGS
CASE "クンニ"
	SIF TALENT:ARG:オトコ
		RETURNF 0
	IF IS_REV
		SIF ABL:ARG:Ｃ感覚 < 3 || TALENT:ARG:Ｃ性向 + TALENT:ARG:淫核 + COND("陰核肥大", ARG) == 0
			RETURNF 0
	ENDIF
CASE "手淫", "パイズリ"
	ACTOR_USEP = PLAYER
CASE "フェラチオ"
	ACTOR_USEP = PLAYER
	IF IS_REV
		SIF ABL:ARG:精液中毒 < 3 || COND("精飲に弱い", ARG) + TALENT:ARG:においに弱い == 0
			RETURNF 0
	ENDIF
CASE "正常位", "後背位", "屈曲位", "対面座位", "背面座位", "騎乗位"
	ACTOR_USEP = PLAYER
	SIF TALENT:ARG:オトコ
		RETURNF 0
	SIF ABL:ARG:Ｖ拡張 < SIZE("ペニス", PLAYER)
		RETURNF 0
	IF IS_REV
		SIF ABL:ARG:Ｖ感覚 < 3 || COND("Ｖ熟達", ARG) == 0
			RETURNF 0
	ENDIF
CASE "アナル正常位", "アナル後背位", "アナル屈曲位", "アナル対面座位", "アナル背面座位", "アナル騎乗位"
	ACTOR_USEP = PLAYER
	SIF ABL:ARG:Ａ拡張 < SIZE("ペニス", PLAYER)
		RETURNF 0
	IF IS_REV
		SIF ABL:ARG:Ａ感覚 < 3 || COND("Ａ熟達", ARG) == 0 || TALENT:ARG:Ａ性向 + TALENT:ARG:尻穴狂い + TALENT:ARG:オトコ == 0
			RETURNF 0
	ENDIF
CASE "尿道正常位", "尿道後背位", "尿道屈曲位", "尿道対面座位", "尿道背面座位", "尿道騎乗位"
	ACTOR_USEP = PLAYER
	SIF ABL:ARG:尿道拡張 < SIZE("ペニス", PLAYER)
		RETURNF 0
	IF IS_REV
		SIF ABL:ARG:尿道感覚 < 3 || COND("尿道熟達", ARG) == 0 || TALENT:ARG:尿道性感 + TALENT:ARG:尿道狂い == 0
			RETURNF 0
	ENDIF
CASE "ニプルファック"
	ACTOR_USEP = PLAYER
	SIF COND("Ｂ熟達", ARG) == 0
		RETURNF 0
CASE "奉仕クンニ"
	SIF TALENT:PLAYER:オトコ
		RETURNF 0
CASE "フェラする"
	ACTOR_USEP = ARG
	IF IS_REV
		SIF ABL:ARG:Ｃ感覚 < 3 || TALENT:ARG:Ｃ性向 + TALENT:ARG:淫核 + TALENT:ARG:オトコ == 0
			RETURNF 0
	ENDIF
CASE "正常位奉仕", "後背位奉仕", "騎乗位で犯す"
	ACTOR_USEP = ARG
	SIF TALENT:PLAYER:オトコ
		RETURNF 0
	IF IS_REV
		SIF TALENT:ARG:Ｃ性向 + TALENT:ARG:オトコ == 0
			RETURNF 0
	ELSE
		SIF COND("Ｖ熟達", PLAYER) == 0
			RETURNF 0
	ENDIF
CASE "Ａ正常位奉仕", "Ａ後背位奉仕", "Ａ騎乗位で犯す"
	ACTOR_USEP = ARG
	IF IS_REV
		SIF TALENT:ARG:Ｃ性向 + TALENT:ARG:オトコ == 0
			RETURNF 0
	ELSE
		SIF COND("Ａ熟達", PLAYER) == 0
			RETURNF 0
	ENDIF

;それ以外は性処理コマンドではないのでダメ
CASEELSE
	RETURNF 0
ENDSELECT

;要竿役なら条件を満たすかチェック
IF ACTOR_USEP
	SIF PENIS(ACTOR_USEP) || (TALENT:ACTOR_USEP:具現 && TALENT:ACTOR_USEP:両刀)
		RETURNF 1
	RETURNF 0
ENDIF

RETURNF 1

