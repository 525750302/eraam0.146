;文化祭についての話題があったので、できるとこまでやってやろうじゃねえか！　という試み
;製作者  道端のメロンパン
;-------------------------------------------------
;文化祭出し物の解説文
;-------------------------------------------------
@MESSAGE_BUNKASAI, ARGS

SELECTCASE ARGS
CASE "バンド演奏"
	PRINTFORMW %CALLNAME:TARGET%は体育館でバンドを組んで演奏していた。
CASE "演劇"
	PRINTFORMW %CALLNAME:TARGET%は体育館で%ARGS%を行っていた。
CASE "占いの館"
	PRINTFORMW どうやら%CALLNAME:TARGET%のクラスの出し物は%CALLNAME:TARGET%による占いのようだ。
CASE "お化け屋敷"
	PRINTFORMW どうやら%CALLNAME:TARGET%のクラスは%ARGS%を出し物とすることに決めたようだ。
;以下はまだ本文が無い物
CASE "お悩み相談室"
	PRINTFORMW どうやら%CALLNAME:TARGET%は教室の一角を借りてお悩み相談を行っているらしい。
CASE "ぱふぱふ屋"
	PRINTFORMW %CALLNAME:TARGET%のクラスは%ARGS%なるものを行っているようだ。
CASE "メイド喫茶", "執事喫茶", "レストラン"
	PRINTFORMW どうやら%CALLNAME:TARGET%のクラス%TEXTR(@"の出し物は%ARGS%のようだ/は%ARGS%を行っているらしい/は%ARGS%を行っているようだ/の出し物は本格的な%ARGS%らしい")%。
ENDSELECT
;台詞
CALL KOJO_DATE, ARGS

;-------------------------------------------------
;文化祭出し物の本文
;-------------------------------------------------
@DAILY_MESSAGE_BUNKASAI, ARGS

SELECTCASE ARGS
CASE "バンド演奏"
	PRINTFORM %CALLNAME:TARGET%は
	SELECTCASE RAND:5
	CASE 0
		PRINTFORM ボーカル担当のようで、
		IF TALENT:内気
			PRINTFORM 普段とは別人のように激しい
		ELSEIF TALENT:目立ちたがり
			PRINTFORM 生き生きとした表情で
		ELSEIF TALENT:淫乱
			PRINTFORM 蠱惑的に腰をうねらせながら
		ELSE
			PRINTFORM 若さに溢れた
		ENDIF
		PRINTFORMW パフォーマンスを行っている……
	CASE 1
		PRINTFORM ギター担当のようで、
		IF ABL:技巧 >= 5
			PRINTFORMW 目にもとまらぬ早弾きを披露して観客を驚かせている……
		ELSE
			PRINTFORMW 必死にコードをかきならしている……
		ENDIF
	CASE 2
		PRINTFORM ベース担当らしく、
		IF TALENT:目立ちたがり
			PRINTFORMW テクニカルなベースを披露している……
		ELSE
			PRINTFORMW 堅実なプレイでバンドの土台となっている……
		ENDIF
	CASE 3
		PRINTFORMW ドラム担当のようで、ときおり派手なパフォーマンスをしながらバンドのグルーヴを生み出している……
	CASE 4
		PRINTFORMW キーボードを担当しているらしく、周りにある様々なキーボードを弾きこなしている……
	ENDSELECT
CASE "演劇"
	SELECTCASE RAND:3
	CASE 0
		PRINTFORMW どうやら漫画を原作にした劇を行うようだ。
	CASE 1
		PRINTFORMW どうやら古典的な演劇を行うことにしたらしい。
	CASE 2
		PRINTFORMW どうやら創作演劇を行うらしい。
	ENDSELECT
	PRINTFORM %CALLNAME:TARGET%は%TEXTR("主役/ヒロイン/脇役/悪役")%を演じているらしく、
	IF TALENT:目立ちたがり
		PRINTFORM 生き生きとした
	ELSEIF TALENT:内気
		PRINTFORM 緊張が隠しきれない
	ELSE
		PRINTFORM 堂々とした
	ENDIF
	PRINTFORM 演技で
	IF TALENT:謎の魅力 || TALENT:魅力
		PRINTFORMW 観客を魅了している……
	ELSE
		PRINTFORMW 観客を釘付けにしている……
	ENDIF
CASE "占いの館"
	PRINTFORMW 机の上には水晶玉が置かれており、彼女が魔法使いであるということを知らない生徒でも、本物の占い師であると確信してしまいそうなオーラを放っている。
	PRINTFORMW ……そろそろ%CALLNAME:MASTER%の番のようだ。
	PRINTFORMW 目の前に現れた%CALLNAME:MASTER%の姿に驚きながらも、%CALLNAME:TARGET%は馴れた手つきで占いを始めた。
	PRINTFORMW しばらくすると、%CALLNAME:MASTER%の運勢が分かったと耳の近くに顔を近づける。
	PRINTFORMW ……今夜は期待できそうだ。

CASE "お化け屋敷"
	PRINTFORMW 中々凝った造りをしているらしく、入口から既におどろおどろしさをかもし出している。
	PRINTFORMW %CALLNAME:MASTER%と%CALLNAME:PARTNER()%がどのような内容か楽しみに中に入ると、真っ暗な闇が広がっていた。
	PRINTFORMW 先の見えない迷路を手探りで歩いていると、目の前に明かりが見える。
	PRINTFORM まるで夜の虫のようにそこに近づくと、真横から
	SELECTCASE RAND:3
	CASE 0
		PRINTFORM 血まみれの
	CASE 1
		PRINTFORM 幽霊に化けた
	CASEELSE
		PRINTFORM 特殊メイクですぐにはわからないが、
	ENDSELECT
	PRINTFORMW %CALLNAME:TARGET%が飛び出してきた！
CASEELSE
	PRINTFORMW 出し物の出来は……%TEXTR("まずまず、と言った感じか。/少々雑だが、活気は有ったのでそんな悪くはなかった。/専門的な知識を持った生徒でも居たのか、びっくりするぐらい良かった。本気出し過ぎだろう……")%
ENDSELECT

;-------------------------------------------------
;文化祭の出し物を細かく設定する関数
;素質や種族で内容が変わったりする……といいな
;-------------------------------------------------
@SHOP_BUNKASAI()
#FUNCTIONS
#DIM LCOUNT
#DIMS SHOP_TABLE

;まずは固定の出し物
SHOP_TABLE = メイド喫茶/執事喫茶/お化け屋敷/バンド演奏/演劇/

;次に種族によって出る出し物を加える
SIF FIND_COND("お悩み相談室")
	SHOP_TABLE += "お悩み相談室/"
SIF FIND_COND("占いの館")
	SHOP_TABLE += "占いの館/"

;素質によって出る出し物を加える
SIF FIND_COND("レストラン")
	SHOP_TABLE += "レストラン/"
SIF FIND_COND("ぱふぱふ屋")
	SHOP_TABLE += "ぱふぱふ屋/"

;もう巡った場所は除外
VARSET LOCALS
SPLIT SHOP_TABLE, "/", LOCALS
SHOP_TABLE = 
FOR LCOUNT, 0, 10
	SIF CEVENT(@"学園祭：%LOCALS:LCOUNT%", MASTER)
		CONTINUE
	SHOP_TABLE += @"%LOCALS:LCOUNT%/"
NEXT

RETURNF SHOP_TABLE

;-------------------------------------------------
;出し物ARGSをやっているARGをふさわしい格好に着替えさせる
;-------------------------------------------------
@SET_COORDINATE_BUNKASAI, ARGS, ARG
;外套は脱ぐ
CALL RESET_OTHER, "外套", ARG

SELECTCASE ARGS
CASE "レストラン"
	CALL SET_SHIRT, "ゴシックワンピース", ARG
CASE "メイド喫茶"
	CALL SET_COORDINATE, "メイド服", ARG
CASE "執事喫茶"
	CALL SET_SHIRT, "ネクタイブラウス/黒", ARG
CASE "演劇"
	IF COND("ガチオトコ", ARG)
		CALL SET_SHIRT, "中世チュニック", ARG
	ELSE
		CALL SET_SHIRT, "甘ロリワンピース", ARG
	ENDIF
CASE "ぱふぱふ屋"
	CALL SET_SHIRT, "ベビードール", ARG
ENDSELECT


