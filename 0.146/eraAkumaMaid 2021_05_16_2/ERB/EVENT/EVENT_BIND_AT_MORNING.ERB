;-------------------------------------------------
;拘束されて弄ばれるイベント
;欲求不満イベントに変更してみる
;-------------------------------------------------
@BIND_AT_MORNING
#DIM LCOUNT
#DIM MEMO_ABL_MASTER
;ランダム選択に用いる
#DIM NUM_FIND
;行為の回数
#DIM NUM_PLAY, 3
;乱交参加者の人数
#DIM CHARANUM_RANKOU
;乱交プレイ参加者の名前の羅列。%CALLNAME:TARGET%と%CALLNAME:(TARGET:1)% など
#DIMS CNAMES_RANKOU, 2
#DIMS MEMO_EVENT

;日曜朝だけ＆デートの日は起きない
SIF TEXTS("曜日") != "日" || CHARA_NUM("デートの約束")
	RETURN 0

;そもそもこの種類のイベントが起きるかのチェック
CALL CHECK_BIND_AT_MORNING, TARGET, ""

;TARGETがプレイの条件を満たしているか？
SIF CSTR:朝駆け == ""
	RETURN 0

;複数キャラを使うイベント用の対象フラグリセット
VARSET TARGET, 0, 1, 4

;一時的にイベント名を記録
MEMO_EVENT = %CSTR:朝駆け%

NUM_FIND = 1
;仲間を呼ぶ
FOR LCOUNT, 2, CHARANUM
	SIF LCOUNT == TARGET || LCOUNT == MASTER
		CONTINUE

	CALL CHECK_BIND_AT_MORNING, LCOUNT, CSTR:朝駆け
	;選択されているプレイを許容できるなら欲求不満値を記録して、参加候補入り（ＭＡＸ二人）
	SIF CSTR:朝駆け != MEMO_EVENT
		CONTINUE

	IF TARGET:1 == 0
		TARGET:1 = LCOUNT
	ELSEIF TARGET:2 == 0
		TARGET:2 = LCOUNT
	ELSEIF RAND:NUM_FIND <= 1
		TARGET:(RAND:2 + 1) = LCOUNT
	ENDIF
	NUM_FIND += 1
NEXT

CSTR:朝駆け = %MEMO_EVENT%

CNAMES_RANKOU = %CALLNAME:TARGET%
CNAMES_RANKOU:1 = %CALLNAME:TARGET%
NUM_PLAY = MIN(5, ABL:欲望/2) + RAND:(MIN(5, ABL:サドっ気) + 5) + TALENT:淫乱*2 + TALENT:恋慕

IF TARGET:1 > 0
	CNAMES_RANKOU = %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%
	CNAMES_RANKOU:1 = %CALLNAME:TARGET%達
	NUM_PLAY:1 = MIN(5, ABL:(TARGET:1):欲望/2) + RAND:(MIN(5, ABL:(TARGET:1):サドっ気) + 1) + TALENT:(TARGET:1):淫乱*2 + TALENT:(TARGET:1):恋慕
ENDIF
IF TARGET:2 > 0
	CNAMES_RANKOU = %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%と%CALLNAME:(TARGET:2)%
	CNAMES_RANKOU:1 = %CALLNAME:TARGET%達
	NUM_PLAY:2 = MIN(5, ABL:(TARGET:2):欲望/2) + RAND:(MIN(5, ABL:(TARGET:2):サドっ気) + 1) + TALENT:(TARGET:2):淫乱*2 + TALENT:(TARGET:2):恋慕
ENDIF

CHARANUM_RANKOU = 1 + (TARGET:1 > 0) + (TARGET:2 > 0)
DRAWLINE

CVARSET TEQUIP, GETNUM(TEQUIP, "押し倒し"), 1
;拘束されているということで技巧を下げる
MEMO_ABL_MASTER = ABL:MASTER:技巧
ABL:MASTER:技巧 = 1

CALL SIMULATE_TRAIN_FIRST, CHARANUM_RANKOU

SELECTCASE CSTR:朝駆け
CASE "朝ＳＭ"
	PRINTFORMW 朝、目が覚めると身体が動かない……
	PRINTFORMW 何時の間にか裸でベッドに縛り付けられている
	PRINTFORMW こちらを見下ろしている%CNAMES_RANKOU%が、嗜虐と情欲に濁った眼差しで挨拶してきた……
	CALL KOJO_EVENT, CSTR:朝駆け
	PRINTL 
	PRINTFORMW そのまま夜になるまで%CNAMES_RANKOU:1%に弄ばれた……
	FOR LCOUNT, 0, CHARANUM_RANKOU
		SWAP TARGET, TARGET:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 1, MAX(NUM_PLAY:LCOUNT/(2 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, MAX(NUM_PLAY:LCOUNT/(3 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, MAX(NUM_PLAY:LCOUNT/(2 + RAND:4), 1)
		IF RAND:2 && COND("女性主導生セックス")
			CALL COM_ABLE65
			SIF RESULT && TALENT:処女 + TALENT:MASTER:童貞 == 0 && ABL:信頼+ABL:欲望+ABL:Ｖ感覚+ABL:奉仕精神 > 14 && PENIS(MASTER)
				CALL SIMULATE_TRAIN_COM, 65, MAX(NUM_PLAY:LCOUNT/(5 + RAND:5), 1 + RAND:3)
		ENDIF
		IF RAND:3 == 0
			CALL COM_ABLE75
			SIF RESULT && TALENT:Ａ処女 == 0 && EXP:PLAYER:Ａ挿入経験 && ABL:Ａ感覚 >= MAX(3, ABL:Ｖ感覚) && ABL:信頼+ABL:欲望+ABL:Ａ感覚+ABL:奉仕精神 > 14 && PENIS(MASTER)
				CALL SIMULATE_TRAIN_COM, 75, MAX(NUM_PLAY:LCOUNT/(5 + RAND:5), 1 + RAND:3)
		ENDIF
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 90, NUM_PLAY:LCOUNT/(3 + RAND:3)
		CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY:LCOUNT/(4 + RAND:3)
		SIF RAND:2 && TALENT:巨乳
			CALL SIMULATE_TRAIN_COM, 92, MAX(NUM_PLAY:LCOUNT/(4 + RAND:3), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 95, NUM_PLAY:LCOUNT/(3 + RAND:3)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 96, NUM_PLAY:LCOUNT/(4 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
CASE "朝拘束騎乗位"
	PRINTFORMW 朝、目が覚めると身体が重い……\@ TARGET:1 > 0 ? だけでなく、呼吸も苦しい… # \@
	IF TARGET:1
		PRINTFORM …状況の把握に時間がかかったが、どうやら%CALLNAME:(TARGET:1)%が顔に
		SIF TARGET:2
			PRINTFORM 、%CALLNAME:(TARGET:2)%が腕に、
		PRINTFORMW 股間を夢中で擦りつけているようだ
		PRINTFORMW その上、%CALLNAME:TARGET%が騎乗位で楽しんでいるらしく、股間は色々な液体で凄い有様だ…
		PRINTL 
		PRINTFORMW 目が覚めた%CALLNAME:MASTER%に気づいた%CNAMES_RANKOU:1%の嗜虐と情欲に濁った眼差しを見るに、当分解放してはもらえなさそうだ…
	ELSE
		PRINTFORMW どうやら、何時の間にか裸でベッドに縛り付けられているようだ…
		PRINTFORMW 重さのかかっている場所を見遣ると、腰に跨っている%CALLNAME:TARGET%と目が合ってしまった
		PRINTFORMW 結構な時間楽しんだ後の様で、その結合部は泡立った液体がブクブクと垂れ、ドロドロになっている…
		PRINTL 
		PRINTFORMW %CALLNAME:TARGET%の嗜虐と情欲に濁った眼差しを見るに、当分解放してはもらえなさそうだ…
	ENDIF

	CALL KOJO_EVENT, CSTR:朝駆け
	PRINTL 
	PRINTFORMW そのまま夜になるまで%CNAMES_RANKOU:1%に絞られ続け、弄ばれた……
	FOR LCOUNT, 0, CHARANUM_RANKOU
		SWAP TARGET, TARGET:LCOUNT
		CALL SIMULATE_TRAIN_COM, 1, NUM_PLAY:LCOUNT/(2 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, NUM_PLAY:LCOUNT/(4 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, NUM_PLAY:LCOUNT/(2 + RAND:4)
		CALL SIMULATE_TRAIN_COM, 65, NUM_PLAY:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY:LCOUNT/(3 + RAND:3)
		SIF RAND:2 && TALENT:巨乳
			CALL SIMULATE_TRAIN_COM, 92, NUM_PLAY:LCOUNT/(4 + RAND:3)
		SIF RAND:4 == 0
			CALL SIMULATE_TRAIN_COM, 95, NUM_PLAY:LCOUNT/(5 + RAND:3)
		SIF RAND:5 == 0
			CALL SIMULATE_TRAIN_COM, 96, NUM_PLAY:LCOUNT/(5 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
CASE "朝拘束Ａ騎乗位"
	PRINTFORMW 朝、目が覚めると身体が重い……\@ TARGET:1 > 0 ? だけでなく、呼吸も苦しい… # \@
	IF TARGET:1
		PRINTFORM …状況の把握に時間がかかったが、どうやら%CALLNAME:(TARGET:1)%が顔に
		SIF TARGET:2
			PRINTFORM 、%CALLNAME:(TARGET:2)%が腕に、
		PRINTFORMW 股間を夢中で擦りつけているようだ
		PRINTFORMW その上、%CALLNAME:TARGET%がお尻の穴で楽しんでいるらしく、股間は色々な液体で凄い有様だ…
		PRINTL 
		PRINTFORMW 目が覚めた%CALLNAME:MASTER%に気づいた%CNAMES_RANKOU:1%の嗜虐と情欲に濁った眼差しを見るに、当分解放してはもらえなさそうだ…
	ELSE
		PRINTFORMW どうやら、何時の間にか裸でベッドに縛り付けられているようだ…
		PRINTFORMW 重さのかかっている場所を見遣ると、腰に跨ってアナルにペニスを咥え込んでいる%CALLNAME:TARGET%と目が合ってしまった
		PRINTFORMW 結構な時間楽しんだ後の様で、その結合部は泡立った液体がブクブクと垂れ、ドロドロになっている…
		PRINTL 
		PRINTFORMW %CALLNAME:TARGET%の嗜虐と情欲に濁った眼差しを見るに、当分解放してはもらえなさそうだ…
	ENDIF

	CALL KOJO_EVENT, CSTR:朝駆け
	PRINTL 
	PRINTFORMW そのまま夜になるまで%CNAMES_RANKOU:1%に絞られ続け、弄ばれた……
	FOR LCOUNT, 0, CHARANUM_RANKOU
		SWAP TARGET, TARGET:LCOUNT
		CALL SIMULATE_TRAIN_COM, 1, MAX(NUM_PLAY:LCOUNT/(2 + RAND:4), 1)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 6, NUM_PLAY:LCOUNT/(4 + RAND:4)
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 9, NUM_PLAY:LCOUNT/(2 + RAND:4)
		CALL SIMULATE_TRAIN_COM, 75, NUM_PLAY:LCOUNT
		SIF RAND:2
			CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY:LCOUNT/(3 + RAND:3)
		SIF RAND:2 && TALENT:巨乳
			CALL SIMULATE_TRAIN_COM, 92, NUM_PLAY:LCOUNT/(4 + RAND:3)
		SIF RAND:4 == 0
			CALL SIMULATE_TRAIN_COM, 95, NUM_PLAY:LCOUNT/(5 + RAND:3)
		SIF RAND:5 == 0
			CALL SIMULATE_TRAIN_COM, 96, NUM_PLAY:LCOUNT/(5 + RAND:3)
		SWAP TARGET, TARGET:LCOUNT
	NEXT
;今のところこの下は無し
CASE "朝拘束逆レイプ"
	PRINTFORMW 朝、目が覚めると身体が動かない……
	PRINTFORMW 何時の間にか裸でベッドに縛り付けられている
	PRINTFORMW こちらを見下ろしている%CNAMES_RANKOU%が、嗜虐と情欲に濁った眼差しで挨拶してきた……
	CALL KOJO_EVENT, CSTR:朝駆け
	PRINTFORMW そのまま夜になるまで%CNAMES_RANKOU:1%に弄ばれた……
CASE "朝拘束逆アナル"
	PRINTFORMW 朝、目が覚めると身体が動かない……
	PRINTFORMW 何時の間にか裸でベッドに縛り付けられている
	PRINTFORMW こちらを見下ろしている%CNAMES_RANKOU%が、嗜虐と情欲に濁った眼差しで挨拶してきた……
	CALL KOJO_EVENT, CSTR:朝駆け
	PRINTFORMW そのまま夜になるまで%CNAMES_RANKOU:1%に弄ばれた……
ENDSELECT

CALL SIMULATE_TRAIN_CALC
CALL SIMULATE_TRAIN_SHOW
WAIT

CALL KOJO_GREETING, "朝拘束", TARGET

CALL CALC, "欲求不満解消", TARGET, (NUM_PLAY + NUM_PLAY:1 + NUM_PLAY:2)*15
IF TARGET:1 > 0
	CALL CALC, "欲求不満解消", TARGET:1, (NUM_PLAY + NUM_PLAY:1 + NUM_PLAY:2)*10
	;仲良くなる
	CALL CHANGE_RELATION, 5, TARGET, TARGET:1, TARGET:2
	CSTR:(TARGET:1):朝駆け = CSTR:朝駆け
ENDIF
IF TARGET:2 > 0
	CALL CALC, "欲求不満解消", TARGET:2, (NUM_PLAY + NUM_PLAY:1 + NUM_PLAY:2)*10
	CSTR:(TARGET:2):朝駆け = CSTR:朝駆け
ENDIF

CVARSET TEQUIP, GETNUM(TEQUIP, "押し倒し")

ABL:MASTER:技巧 = MEMO_ABL_MASTER
RETURN 1


@CHECK_BIND_AT_MORNING, ARG, ARGS
#DIM LCOUNT
;起きうるイベントの判定に使う
#DIMS EVENT_TABLE

;欲求不満度が限界までは行ってないor欲望が5LV未満、好感度が500未満
SIF COND("欲求不満", ARG) < 3 || ABL:ARG:欲望 < 5 || CFLAG:ARG:好感度 < 500	|| COND("Ｈはあなたとだけ")
	RETURN 0

VARSET EVENT_TABLE

;サドっ気5LVならＳＭコース
IF ABL:ARG:サドっ気 >= 5
	IF ARGS == "朝ＳＭ"
		CSTR:朝駆け = 朝ＳＭ
		RETURN 0
	ENDIF
	EVENT_TABLE += "朝ＳＭ/"
ENDIF

CALL COM_ABLE65
IF RESULT && PENIS(MASTER) && TALENT:ARG:処女 + TALENT:MASTER:童貞 == 0 && ABL:ARG:Ｖ感覚 >= 3 && EXP:ARG:Ｖ性交経験 >= 10 && EXP:ARG:腰使い経験 >= 20 && COND("女性主導生セックス", ARG)
	IF ARGS == "朝拘束騎乗位"
		CSTR:朝駆け = 朝拘束騎乗位
		RETURN 0
	ENDIF
	EVENT_TABLE += "朝拘束騎乗位/"
ENDIF

CALL COM_ABLE75
IF RESULT && PENIS(MASTER) && EXP:MASTER:Ａ挿入経験 && COND("Ａ熟達", ARG)
	IF ARGS == "朝拘束Ａ騎乗位"
		CSTR:朝駆け = 朝拘束Ａ騎乗位
		RETURN 0
	ENDIF
	EVENT_TABLE += "朝拘束Ａ騎乗位/"
ENDIF

CSTR:朝駆け = %TEXTR(EVENT_TABLE)%

RETURN 0

