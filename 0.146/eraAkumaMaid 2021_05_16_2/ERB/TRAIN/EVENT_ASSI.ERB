;アドバイザー能力ARGSの解説
@COMMENT_ADVISER(ARGS)
#FUNCTIONS

VARSET LOCALS
SELECTCASE ARGS
CASE "自動補充"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、消耗品を無料で補充します
CASE "魔性の香"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、ムードと快楽にボーナス
CASE "精力増大"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、あなたの精力+10
CASE "ロマンチック"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、初期ムード+_HH_HH
CASE "リジェネレイト"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、毎ターン全員の体力が50回復
CASE "ファシネイション"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、時間毎に欲求不満+10％
CASE "好奇の視線"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、時間毎に欲求不満+５％＆恥情の珠の取得+５％
CASE "天才肌"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、習得の珠の取得+５％
CASE "淫蕩薬師"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、媚薬の効果+50％
CASE "縁結び"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、毎朝ランダムな相手との好感度が増す
CASE "金運招来Ⅱ"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、毎朝財布の重みが増す
CASE "金運招来"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、毎朝財布の重みが増す
CASE "マナ蒐集"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、時間毎の魂回復量+0.1
CASE "包容力"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、パートナーの恐怖と反感を軽減
CASE "理性主義"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、あなたは恍惚状態になりません
CASE "ソロプレイヤー"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、自慰系コマンドの効果が少し上がる
CASE "毎日にぎやか"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、時間経過で全員の好感度が少し上昇
CASE "不毛な相談"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、実行値にボーナス＆ムードにペナルティ
CASE "サプレッション"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、時間毎に全員の欲求不満-10％
CASE "見習いの意地"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、習得の珠の取得+５％
CASE "淫精の伝道師"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、Ｈでの射精量ＵＰ
;淫精の伝道師の類似能力
CASE "射精煽り"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、Ｈでの射精量ＵＰ
CASE "堕天使の甘言"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、甘い言葉の成功率ＵＰ＆継続時間が増加
CASE "クールダウン"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、時間毎に全員の欲求不満-５％
CASE "動画撮影"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、パートナーの恥情の上昇量+50％＆実行値-５
CASE "媚薬精製"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、何故か媚薬が増えることがあります
CASE "天然潤滑液"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、何故かローションが増えることがあります
CASE "無能"
	LOCALS = なにもない、なにもできない、だから無能。
CASE "頑張る先輩"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、パートナーの恭順+10％ 反感-15％
CASE "テンプテーション"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、パートナーの欲情+50％＆体力消費増
CASE "狩猟の美学"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、パートナーの欲情+70％＆体力消費減
CASE "痛いの痛いのとんでけ"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、パートナーの痛みと恐れを軽減
CASE "小悪魔の囁き"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、甘い言葉で感度ＵＰ＆攻めＵＰ
CASE "浄化の火"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、毎ターン全員の体力が100、%CALLNAME:MASTER%の精力が5回復
CASE "ナチュラリスト"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、薄着になる取引にボーナス＆条件を緩和
CASE "解放感"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、野外での実行値にボーナス
CASE "トリックスター"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、実行値に少しボーナス
CASE "甘えん坊"
	LOCALS = %TEXTS("助手の名称")%に選ばれている時に限り、胸に関わるコマンドの効果が少し上がる
ENDSELECT
RETURNF LOCALS

@EVENTCHECK_A
SIF ASSI <= 0
	RETURN 0

;-------------------------------------------------------------------------------------------------
;技能の成長
;-------------------------------------------------------------------------------------------------
CALL EX_ABLUP, ASSI

;助手のLVUP
SELECTCASE ABL:ASSI:助手
CASE 0
	SIF EXP:ASSI:助手経験 < 50
		RETURN 0
CASE 1
	SIF EXP:ASSI:助手経験 < 200
		RETURN 0
CASE 2
	SIF EXP:ASSI:助手経験 < 500
		RETURN 0
CASE 3
	SIF EXP:ASSI:助手経験 < 1000
		RETURN 0
CASE 4
	SIF EXP:ASSI:助手経験 < 2000
		RETURN 0
CASEELSE
	RETURN 0
ENDSELECT

ABL:ASSI:助手 += 1
CALL BLANKLINE
PRINTFORML %CALLNAME:ASSI%は%TEXTS("助手の名称")%の役割に対する理解が深まった
PRINTFORMW %CALLNAME:ASSI%の%TEXTS("助手の名称")%LVが{ABL:ASSI:助手}になった！
;助手への指示呼び出し
CALL ASSI_CHANGE_INDICATE, ASSI


;-------------------------------------------------
;アドバイザーの開始時特典
;-------------------------------------------------
@EVENTTRAIN_BONUS_ADVISER
SIF ASSI:1 <= 0
	RETURN 0

;アドバイザー能力発動
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "魔性の香")
	DRAWLINE
	PRINTL ＜魔性の香＞
	PRINTFORMW %CALLNAME:(ASSI:1)%は昂揚の作用があるという複雑な匂いの香を焚いた。

	FLAG:ムード += 60
	PALAM:欲情 += 5000

	CALL TEXT, "ムード変化", 60
	PRINTFORML 欲情+5000
	IF ASSI
		PALAM:ASSI:欲情 += 5000
		PRINTFORML 欲情+5000(%CALLNAME:ASSI%)
	ENDIF
	WAIT
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "ロマンチック")
	DRAWLINE
	PRINTL ＜ロマンチック＞
	PRINTFORMW あたりにロマンチックなムードが漂う…
	FLAG:ムード += 60
	CALL PRINT_STR, "ムード+ _HH_HH_W"
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "精力増大")
	DRAWLINE
	PRINTL ＜精力増大＞
	PRINTFORMW %CALLNAME:(ASSI:1)%から力が流れ込み、%CALLNAME:MASTER%の精力が高まる！
	MAXBASE:MASTER:精力 += 10
	BASE:MASTER:精力 += 10
	PRINTFORMW 精力+10(%CALLNAME:MASTER%)
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "包容力")
	DRAWLINE
	PRINTL ＜包容力＞
	IF TARGET != ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%が見守る事により、%CALLNAME:TARGET%の恐怖と反感の上昇が大幅に抑えられます。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の恐怖と反感の上昇が大幅に抑えられます。
	ENDIF
	TEQUIP:包容力 = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "理性主義")
	DRAWLINE
	PRINTL ＜理性主義＞
	PRINTFORMW %CALLNAME:MASTER%が一時の快楽に流されてしまった際に、%CALLNAME:(ASSI:1)%が音を立てて理性を取り戻させてくれます。
	TEQUIP:MASTER:理性主義 = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "ソロプレイヤー")
	DRAWLINE
	PRINTL ＜ソロプレイヤー＞
	IF TARGET != ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%がこの場に居る事により、%CALLNAME:TARGET%の自慰系コマンドの効果が少し上がります。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の自慰系コマンドの効果が少し上がります。
	ENDIF
	TEQUIP:ソロプレイヤー = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "不毛な相談")
	DRAWLINE
	PRINTL ＜不毛な相談＞
	IF TARGET != ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%が雑談混じりで相談に乗ってくれます。思ったより役に立ちますがムードはぶち壊し！
	ELSE
		PRINTFORML %CALLNAME:(ASSI:1)%とのエッチは雑談混じりとなります。
		PRINTFORMW 相談の上なので多少無理を聞いてくれますが……ムードはぶち壊し！
	ENDIF
	TEQUIP:MASTER:不毛な相談 = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "淫精の伝道師")
	DRAWLINE
	PRINTL ＜淫精の伝道師＞
	PRINTFORML %CALLNAME:(ASSI:1)%が傍に居る事により、皆の射精量がもりっと増えます。
	SIF PENIS(ASSI:1)
		PRINTFORML ※%CALLNAME:(ASSI:1)%も例外ではありません※
	WAIT
	TEQUIP:MASTER:淫精の伝道師 = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "射精煽り")
	DRAWLINE
	PRINTL ＜射精煽り＞
	PRINTFORML %CALLNAME:(ASSI:1)%が傍に居る事により、皆の射精量がもりっと増えます。
	SIF PENIS(ASSI:1)
		PRINTFORML ※%CALLNAME:(ASSI:1)%も例外ではありません※
	WAIT
	TEQUIP:MASTER:淫精の伝道師 = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "堕天使の甘言")
	DRAWLINE
	PRINTL ＜堕天使の甘言＞
	IF TARGET != ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%の助言により、甘い言葉の成功率ＵＰ＆継続時間が増加します。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%とのＨでは甘い言葉の成功率が上がり、継続時間も増加します。
	ENDIF
	TEQUIP:堕天使の甘言 = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "動画撮影")
	DRAWLINE
	IF TARGET != ASSI:1
		PRINTL ＜動画撮影＞
		PRINTFORMW %CALLNAME:(ASSI:1)%がスマホで動画撮影する事により、恥情の上昇量+50％＆実行値-５となります。
		TEQUIP:動画撮影 = 1
	ELSE
		PRINTL ＜動画撮影＞
		PRINTFORMW %CALLNAME:TARGET%とのＨでは、スマホで自撮りする事により恥情の上昇量+50％＆実行値-５となります。
		TEQUIP:動画撮影 = 1
	ENDIF
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "頑張る先輩")
	DRAWLINE
	PRINTL ＜頑張る先輩＞
	IF TARGET != ASSI:1
		PRINTFORML %CALLNAME:(ASSI:1)%が頑張ってお手伝いします。それだけです。
		PRINTFORMW ですがその姿は%CALLNAME:TARGET%の心を解して恭順の上昇量を＋10％し、反感の上昇を抑えます。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%がいつも以上に頑張って恭順の上昇量を＋10％し、反感の上昇を抑えます。
	ENDIF
	TEQUIP:頑張る先輩 = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "テンプテーション")
	DRAWLINE
	PRINTL ＜テンプテーション＞
	IF TARGET != ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%の醸し出す空気により、欲情の上昇量+50％＆体力の消費が増加します。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の醸し出す空気により、欲情の上昇量+50％＆体力の消費が増加します。
	ENDIF
	TEQUIP:テンプテーション = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "狩猟の美学")
	DRAWLINE
	PRINTL ＜狩猟の美学＞
	IF TARGET != ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%の豊富な知識により、欲情の上昇量+50％＆体力の消費が低減されます。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の豊富な知識により、欲情の上昇量+70％＆体力の消費が低減されます。
	ENDIF
	TEQUIP:狩猟の美学 = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "痛いの痛いのとんでけ")
	DRAWLINE
	PRINTL ＜痛いの痛いのとんでけ＞
	IF TARGET != ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%が見守る事により、%CALLNAME:TARGET%の痛みと恐れの上昇が大幅に抑えられます。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の痛みと恐れの上昇が大幅に抑えられます。
	ENDIF
	TEQUIP:痛いの痛いのとんでけ = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "小悪魔の囁き")
	DRAWLINE
	PRINTFORML ＜小悪魔の囁き＞
	PRINTFORML %CALLNAME:(ASSI:1)%の助力により、甘い言葉がより相手を誘惑します。
	PRINTFORMW 甘い言葉の効果が発揮している相手は、感度が上がる代わりに攻めが激しくなります。
	TEQUIP:MASTER:小悪魔の囁き = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "解放感") && PLACE("野外")
	DRAWLINE
	PRINTL ＜解放感＞
	IF TARGET != ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%は外での露出行為に慣れているため、%CALLNAME:TARGET%の緊張を解してくれます。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は外での露出行為にある程度慣れているようです。
	ENDIF
	TEQUIP:解放感 = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "トリックスター")
	DRAWLINE
	PRINTL ＜トリックスター＞
	IF TARGET != ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%の助力により、%CALLNAME:TARGET%達は少しだけ場に流されやすくなります。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%の振る舞いにより、%CALLNAME:MASTER%達は少しだけ場に流されやすくなります。
	ENDIF
	TEQUIP:トリックスター = 1
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "甘えん坊")
	DRAWLINE
	PRINTL ＜甘えん坊＞
	IF TARGET != ASSI:1
		PRINTFORML %CALLNAME:(ASSI:1)%がこの場に居る事により、母性本能が刺激され胸に関わるコマンドの効果が少し上がります。
	ELSE
		PRINTFORML %CALLNAME:(ASSI:1)%の振る舞いにより、母性本能が刺激され胸に関わるコマンドの効果が少し上がります。
	ENDIF
	SIF COND("ガチオトコ", MASTER) == 0 && STRCOUNT(CSTR:MASTER:アドバイス, "甘えん坊") == 0
		TEQUIP:MASTER:甘えん坊 = 1
	SIF COND("ガチオトコ") == 0 && STRCOUNT(CSTR:アドバイス, "甘えん坊") == 0
		TEQUIP:甘えん坊 = 1
ENDIF

;アドバイザーのエッチ中のターンごとのボーナス
@TURN_BONUS_ADVISER
#DIM LCOUNT
SIF ASSI:1 <= 0
	RETURN 0
;リジェネレイト
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "リジェネレイト")
	PRINTFORML ＜リジェネレイト＞
	FOR LCOUNT, 1, CHARANUM
		SELECTCASE LCOUNT
		CASE MASTER, TARGET, ASSI
			BASE:LCOUNT:体力 = MIN(BASE:LCOUNT:体力 + 50, MAXBASE:LCOUNT:体力)
		ENDSELECT
	NEXT
	PRINTFORML 全員の体力が50回復した
ENDIF
;浄化の火
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "浄化の火")
	PRINTFORML ＜浄化の火＞
	FOR LCOUNT, 1, CHARANUM
		SELECTCASE LCOUNT
		CASE MASTER
			BASE:LCOUNT:体力 = MIN(BASE:LCOUNT:体力 + 100, MAXBASE:LCOUNT:体力)
			BASE:LCOUNT:精力 = MIN(BASE:LCOUNT:精力 + 5, MAXBASE:LCOUNT:精力)
		CASE TARGET, ASSI
			BASE:LCOUNT:体力 = MIN(BASE:LCOUNT:体力 + 100, MAXBASE:LCOUNT:体力)
		ENDSELECT
	NEXT
	PRINTFORML 全員の体力が100回復し、%CALLNAME:MASTER%の精力が5回復した。
ENDIF

;アドバイザーの終了時特典
@AFTERTRA_BONUS_ADVISER
#DIM LCOUNT
#DIM MEMO_LINECOUNT

SIF ASSI:1 <= 0
	RETURN 0
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "自動補充") && NUM("消耗品使用数")
	CALL SETFLAG, "消耗品補充"
	DRAWLINE
	PRINTFORML ＜自動補充＞
	PRINTFORMW 今回使った消耗品を%CALLNAME:(ASSI:1)%が自腹で補充しました。
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "天才肌")
	DRAWLINE
	PRINTFORML ＜天才肌＞
	IF TARGET == ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%の閃きにより、習得の珠の取得量に５％のボーナスが発生。
	ELSE
		PRINTFORMW %CALLNAME:(ASSI:1)%の助言により、習得の珠の取得量に５％のボーナスが発生。
	ENDIF
ENDIF
IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "好奇の視線")
	DRAWLINE
	PRINTFORML ＜好奇の視線＞
	IF TARGET == ASSI:1
		PRINTFORMW %CALLNAME:(ASSI:1)%の妄想力により、恥情の珠の取得量に５％のボーナスが発生。
	ELSE
		PRINTFORMW %CALLNAME:(ASSI:1)%の態度により、恥情の珠の取得量に５％のボーナスが発生。
	ENDIF
ENDIF
;精力増大終了
SIF STRCOUNT(CSTR:(ASSI:1):アドバイス, "精力増大")
	CALL SET_COUNTER_BASE

;アドバイザー能力の終了
@RESET_ABL_ADVISER
CVARSET TEQUIP, GETNUM(TEQUIP, "包容力")
CVARSET TEQUIP, GETNUM(TEQUIP, "理性主義")
CVARSET TEQUIP, GETNUM(TEQUIP, "ソロプレイヤー")
CVARSET TEQUIP, GETNUM(TEQUIP, "不毛な相談")
CVARSET TEQUIP, GETNUM(TEQUIP, "淫精の伝道師")
CVARSET TEQUIP, GETNUM(TEQUIP, "堕天使の甘言")
CVARSET TEQUIP, GETNUM(TEQUIP, "動画撮影")
CVARSET TEQUIP, GETNUM(TEQUIP, "小悪魔の囁き")
CVARSET TEQUIP, GETNUM(TEQUIP, "解放感")
CVARSET TEQUIP, GETNUM(TEQUIP, "トリックスター")
CVARSET TEQUIP, GETNUM(TEQUIP, "甘えん坊")


;アドバイザーのSHOP画面特典
@SHOP_BONUS_ADVISER
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM ACTOR

SIF ASSI:1 <= 0
	RETURN 0

MEMO_LINECOUNT = LINECOUNT

IF COND("昼") && STRCOUNT(CSTR:(ASSI:1):アドバイス, "縁結び")
	ACTOR = RAND:(CHARANUM - 2) + 2
	LOCAL = RAND:10 + 1
	CFLAG:ACTOR:好感度 += LOCAL
	PRINTFORML ＜縁結び＞
	PRINTFORML %CALLNAME:(ASSI:1)%の神通力により、%CALLNAME:ACTOR%と仲良くなった。
	PRINTFORMW 好感度+{LOCAL}（%CALLNAME:ACTOR%）
ENDIF

IF COND("昼") && STRCOUNT(CSTR:(ASSI:1):アドバイス, "金運招来")
	LOCAL = 1 + RAND:100 + RAND:(MAX(CFLAG:(ASSI:1):好感度/100, 1) )
	IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "金運招来Ⅱ")
		PRINTFORML ＜金運招来Ⅱ＞
		LOCAL += 1 + RAND:10
	ELSE
		PRINTFORML ＜金運招来＞
	ENDIF
	MONEY += LOCAL
	PRINTFORMW %CALLNAME:(ASSI:1)%の神通力により、%CALLNAME:MASTER%の財布の重みが増した……具体的には%MONEY_C(LOCAL)%ほど。
ENDIF

IF COND("夜") && TEXTS("曜日") != "日" && STRCOUNT(CSTR:(ASSI:1):アドバイス, "毎日にぎやか")
	PRINTFORML ＜毎日にぎやか＞
	PRINTFORML %CALLNAME:(ASSI:1)%の言動と引き起こす珍事によって部員達の距離が縮まっています。
	PRINTFORMW 全員の好感度+10＆仲良し度+1

	FOR LCOUNT, 2, CHARANUM
		CFLAG:LCOUNT:好感度 += 10
	NEXT
	CALL CHANGE_RELATION_ALL, 1
ENDIF

IF COND("夜") && STRCOUNT(CSTR:(ASSI:1):アドバイス, "サプレッション")
	PRINTFORML ＜サプレッション＞
	PRINTFORMW %CALLNAME:(ASSI:1)%が身近な人物の性的欲求を吸収している……全員の欲求不満が10％減退した。
	FOR LCOUNT, 2, CHARANUM
		CALL CALC, "欲求不満解消", LCOUNT, 10
	NEXT
ENDIF

IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "クールダウン")
	PRINTFORML ＜クールダウン＞
	PRINTFORMW %CALLNAME:(ASSI:1)%が温度を調節し、快適な空間を提供している……全員の欲求不満が5％減退した。
	FOR LCOUNT, 2, CHARANUM
		CALL CALC, "欲求不満解消", LCOUNT, 5
	NEXT
ENDIF

IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "ファシネイション")
	PRINTFORML ＜ファシネイション＞
	PRINTFORMW %CALLNAME:(ASSI:1)%の身近な人物の性的欲求が溜まっていく……全員の欲求不満が10％上昇した。
	FOR LCOUNT, 2, CHARANUM
		BASE:LCOUNT:欲求不満 = LIMIT(BASE:LCOUNT:欲求不満 + 10, BASE:LCOUNT:欲求不満, 100)
	NEXT
ELSEIF STRCOUNT(CSTR:(ASSI:1):アドバイス, "好奇の視線")
	PRINTFORML ＜好奇の視線＞
	PRINTFORMW %CALLNAME:(ASSI:1)%の身近な人物の性的欲求が溜まっていく……全員の欲求不満が５％上昇した。
	FOR LCOUNT, 2, CHARANUM
		BASE:LCOUNT:欲求不満 = LIMIT(BASE:LCOUNT:欲求不満 + 5, BASE:LCOUNT:欲求不満, 100)
	NEXT
ENDIF

IF COND("昼") && TEXTS("曜日") != "日" && STRCOUNT(CSTR:(ASSI:1):アドバイス, "媚薬精製") && ITEM:媚薬 < 99 && !RAND:3
	PRINTFORML ＜媚薬精製＞
	IF CFLAG:ASSI:3900 == 0
		GETFONT
		CHKFONT "ＭＳ Ｐゴシック"
		SIF RESULT
			SETFONT "ＭＳ Ｐゴシック"
			PRINTFORML 　 〓..〓..〓..〓.〓
			PRINTFORML 　／　　　　　　　 ＼
			PRINTFORML ｜　　　　　　　　　 ｜
			PRINTFORML ｜　―――――　｜
			PRINTFORML ｜│　・　ω ・　│｜
			PRINTFORML ｜　―――――　｜
			PRINTFORML ｜　　　　　　　　　 ｜
			PRINTFORML ｜　　　　　　　　　 ｜
			PRINTFORML ｜　　　　　　　　　 ｜
			PRINTFORML 　￣￣￣￣￣￣￣
		SETFONT RESULTS
		CALL BLANKLINE
		PRINTFORML 普通の媚薬の横に変なラベルの容器が置いてある…
		PRINTFORML 中身を確認してみたが、やっぱり媚薬が入っている…それも質の良い媚薬だ…
		CFLAG:ASSI:3900 = 1
	ELSE
		SELECTCASE RAND:3
		CASE 0
			PRINTFORML 知らない間に新しい媚薬が増えている…
		CASE 1
			PRINTFORML 普通のローションの横に見覚えのある変な容器が置いてある…
		CASE 2
			GETFONT
			CHKFONT "ＭＳ Ｐゴシック"
			SIF RESULT
				SETFONT "ＭＳ Ｐゴシック"
				PRINTFORML 　 〓..〓..〓..〓.〓
				PRINTFORML 　／　　　　　　　 ＼
				PRINTFORML ｜　　　　　　　　　 ｜
				PRINTFORML ｜　―――――　｜
				PRINTFORML ｜│　・　ω ・　│｜
				PRINTFORML ｜　―――――　｜
				PRINTFORML ｜　　　　　　　　　 ｜
				PRINTFORML ｜　　　　　　　　　 ｜
				PRINTFORML ｜　　　　　　　　　 ｜
				PRINTFORML 　￣￣￣￣￣￣￣
			SETFONT RESULTS
			CALL BLANKLINE
			PRINTFORML また変なラベルの容器が並んでいる…
		ENDSELECT
	ENDIF
	PRINTFORMW 媚薬が１つ増えました。
	ITEM:媚薬 += 1
ENDIF

IF COND("昼") && TEXTS("曜日") != "日" && STRCOUNT(CSTR:(ASSI:1):アドバイス, "天然潤滑液") && ITEM:ローション < 99 && !RAND:3
	PRINTFORML ＜天然潤滑液＞
		IF CFLAG:ASSI:4300 == 0
			PRINTFORML 捨てるために綺麗に洗っておいたローションの空き容器が何故かいっぱいになっている…
			PRINTFORML 中身を確認してみたが、やっぱりローションが入っている…それも質の良いローションだ…
			CFLAG:ASSI:4300 = 1
		ELSE
			SELECTCASE RAND:3
			CASE 0
				PRINTFORML 知らない間に新しいローションが増えている…
			CASE 1
				PRINTFORML また空だったローションがいっぱいになっている…
			CASE 2
				PRINTFORML 普通のローションの横に見覚えのあるスポーツドリンクの形の容器が並んでいる…
			ENDSELECT
		ENDIF
	PRINTFORMW ローションが１つ増えました。
	ITEM:ローション += 1
ENDIF

SIF LINECOUNT > MEMO_LINECOUNT
	DRAWLINE

;アドバイザーの時間経過特典
@EVENTTURNEND_BONUS_ADVISER
#DIM LCOUNT
SIF ASSI:1 <= 0
	RETURN 0

IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "マナ蒐集") && BASE:MASTER:魂 < MAXBASE:MASTER:魂
	DRAWLINE
	PRINTFORML ＜マナ蒐集＞
	PRINTFORM %CALLNAME:MASTER%の魂が0.1回復した（%TEXTS("0.01", BASE:MASTER:魂)% 
	BASE:MASTER:魂 = MIN(BASE:MASTER:魂 + 10, MAXBASE:MASTER:魂)
	PRINTFORML → %TEXTS("0.01", BASE:MASTER:魂)%）
ENDIF

;リジェネレイト
IF STRCOUNTS(CSTR:(ASSI:1):アドバイス, "リジェネレイト", "浄化の火") && CHARA_NUM("Ｖ損傷") + CHARA_NUM("Ａ損傷")
	DRAWLINE
	IF STRCOUNT(CSTR:(ASSI:1):アドバイス, "浄化の火")
		PRINTFORML ＜浄化の火＞
	ELSE
		PRINTFORML ＜リジェネレイト＞
	ENDIF

	SETCOLOR DEF_COLOR("黄色")
	FOR LCOUNT, 1, CHARANUM
		SIF TALENT:LCOUNT:Ｖ損傷 == 0 && TALENT:LCOUNT:Ａ損傷 == 0
			CONTINUE

		TALENT:LCOUNT:Ｖ損傷 = 0
		TALENT:LCOUNT:Ａ損傷 = 0
		IF LCOUNT == ASSI:1
			PRINTFORMW %CALLNAME:(ASSI:1)%の体内の損傷が癒された。
		ELSE
			PRINTFORMW %CALLNAME:(ASSI:1)%は%CALLNAME:LCOUNT%の損傷を治療した。
		ENDIF
	NEXT
	RESETCOLOR
ENDIF

;-------------------------------------------------
;助手への指示の処理
;優先順位はアルカナ＞アイテム＞その他
;ARGSには 調教開始、調教中、お掃除 などが入る
;-------------------------------------------------
@EVENT_ASSI_AUTO, ARGS
;助手調教中や助手が居ないときはダメ
SIF ASSI <= 0 || ASSIPLAY
	RETURN 0
;Ｗ押し倒しや３Ｐに参加している場合もやってこない
SIF TEQUIP:Ｗ押し倒し && SELECTCOM:1 >= 0 && TARGET:1 == ASSI
	RETURN 0
SIF SELECTCOM:2 >= 0 && PLAYER:1 == ASSI
	RETURN 0
;指示していないor精力が少ない等
SIF STATE("撃沈", ASSI) || CFLAG:ASSI:助手の指示 == 0 || BASE:ASSI:精力 <= 5
	RETURN 0
;行動済み
SIF ARGS != "調教開始" && TFLAG:助手行動済
	RETURN 0

LOCAL = TFLAG:地の文分岐

SELECTCASE ARGS
CASE "お掃除"
	CALL BLANKLINE
	CALL EVENT_ASSI_OSOUJI
CASE "調教開始"
	CALL EVENT_ASSI_ITEM, ARGS
	CALL EVENT_ASSI_OSOUJI
	IF TFLAG:助手行動済
		CALL KOJO_GUEST, "助手：調教開始前の準備後台詞", ASSI
		CALL BLANKLINE
		PRINTFORMW %CALLNAME:ASSI%は二人の会話を横目に、自らの仕事を終えたようだ…
	ENDIF
CASEELSE
	CALL EVENT_ASSI_ITEM, ARGS
ENDSELECT

TFLAG:地の文分岐 = LOCAL

@EVENT_ASSI_OSOUJI
;誰々の～が分かるならそれを記述
#DIMS ACTOR_OF

VARSET LOCAL
;主人をお掃除
IF GETBIT(CFLAG:ASSI:助手の指示, 40) && TEQUIP:MASTER:コンドーム == 0
	IF PLAYER == MASTER && COND("絶頂", PLAYER) && INSERT("インサート") == 0 && PENIS(MASTER)
		SIF SHOOT("膣内") && CHECK_STAIN(STAIN:MASTER:Ｐ, "Ｖ")
			LOCAL = 1
		SIF SHOOT("アナル") && CHECK_STAIN(STAIN:MASTER:Ｐ, "Ａ")
			LOCAL = 2
		SIF SHOOT("尿道") && CHECK_STAIN(STAIN:MASTER:Ｐ, "尿")
			LOCAL = 3
	ENDIF
	SIF PLAYER == MASTER && CHECK_STAIN(STAIN:MASTER:Ｐ, "Ｐ恥垢") && COND("真性包茎", MASTER) == 0
		LOCAL = 4
ENDIF
;対象をお掃除
IF GETBIT(CFLAG:ASSI:助手の指示, 41)
	IF COND("絶頂", PLAYER) && INSERT("インサート") == 0
		SIF SHOOT("膣内") && STAIN:Ｖ != SET_STAIN(0, "Ｖ")
			LOCAL:1 = 1
		SIF SHOOT("アナル") && STAIN:Ａ != SET_STAIN(0, "Ａ")
			LOCAL:1 = 2
		IF SHOOT("尿道")
			IF PENIS(TARGET)
				SIF STAIN:Ｐ != SET_STAIN(0, "Ｐ")
					LOCAL:1 = 3
			ELSE
				SIF STAIN:Ｖ != SET_STAIN(0, "Ｖ")
					LOCAL:1 = 3
			ENDIF
		ENDIF
	ENDIF
	SIF TALENT:オンナ && CHECK_CLO("Ｖ露出") && CHECK_STAIN(STAIN:Ｖ, "尿") && NOWEX:絶頂お漏らし
		LOCAL:1 = 4
ENDIF

IF LOCAL && RANDIF(1, LOCAL:1)
	ACTOR_OF = 
	SIF COND("Ｐ汚れの相手", MASTER)
		ACTOR_OF = %CONDS("Ｐ汚れの相手", MASTER)%の
	SELECTCASE LOCAL
	CASE 1
		PRINTFORM %CALLNAME:ASSI%は%ACTOR_OF%膣から抜き出されたばかりの%CALLNAME:MASTER%のペニスを
		;汚れの状態で判定
		IF CHECK_STAIN(STAIN:MASTER:Ｐ, "Ａ") && TALENT:ASSI:汚れ無視 == 0
			PRINTFORMW 蒸しタオルで念入りに拭き清めた…
		ELSEIF (ABL:ASSI:奉仕精神 >= 3 || ABL:ASSI:精液中毒 >= 3) && COND("口の使用", ASSI)
			PRINTFORMW 口で受け止めて、念入りに舐め清めた……
			CALL KOJO_GUEST, "助手：主人にお掃除フェラ", ASSI
			CALL SETFLAG, "お掃除フェラ", ASSI, MASTER
		ELSE
			PRINTFORMW 清潔な布で丁寧に拭き清めた…
		ENDIF
	CASE 2
		PRINTFORM %CALLNAME:ASSI%は%ACTOR_OF%アナルから抜き出されたばかりの%CALLNAME:MASTER%のペニスを
		;汚れの状態で判定
		IF CHECK_STAIN(STAIN:MASTER:Ｐ, "Ａ") && TALENT:ASSI:汚れ無視 == 0
			PRINTFORMW 蒸しタオルで念入りに拭き清めた…
		ELSEIF (ABL:ASSI:奉仕精神 >= 3 || ABL:ASSI:精液中毒 >= 3) && COND("口の使用", ASSI)
			PRINTFORMW 口で受け止めて、念入りに舐め清めた……
			CALL KOJO_GUEST, "助手：主人にお掃除フェラ", ASSI
			CALL SETFLAG, "お掃除フェラ", ASSI, MASTER
		ELSE
			PRINTFORMW 清潔な布で丁寧に拭き清めた…
		ENDIF
	CASE 3
		IF PENIS(TARGET)
			PRINTFORM %CALLNAME:ASSI%は%CALLNAME:TARGET%のペニスから抜き出されたばかりの%CALLNAME:MASTER%のソレ
		ELSE
			PRINTFORM %CALLNAME:ASSI%は%ACTOR_OF%尿道から抜き出されたばかりの%CALLNAME:MASTER%のペニスを
		ENDIF
		;汚れの状態で判定
		IF CHECK_STAIN(STAIN:MASTER:Ｐ, "Ａ") && TALENT:ASSI:汚れ無視 == 0
			PRINTFORMW 蒸しタオルで念入りに拭き清めた…
		ELSEIF (ABL:ASSI:奉仕精神 >= 3 || ABL:ASSI:精液中毒 >= 3) && COND("口の使用", ASSI)
			PRINTFORMW 口で受け止めて、念入りに舐め清めた……
			CALL KOJO_GUEST, "助手：主人にお掃除フェラ", ASSI
			CALL SETFLAG, "お掃除フェラ", ASSI, MASTER
		ELSE
			PRINTFORMW 清潔な布で丁寧に拭き清めた…
		ENDIF
	CASE 4
		PRINTFORM %CALLNAME:ASSI%は%CALLNAME:MASTER%の股間の前に跪いて鼻を鳴らすと、ペニスを取り出して
		;奉仕精神と献身で判定
		IF ABL:ASSI:奉仕精神 + TALENT:ASSI:献身 >= 4 && COND("口の使用", ASSI)
			PRINTFORMW 皮を引き降ろした
			PRINTFORMW そして溜まっている恥垢を舌でこそげ取って綺麗にすると、ゴクリと喉を鳴らして飲み下した……
			CALL KOJO_GUEST, "助手：主人にＰ恥垢お掃除フェラ", ASSI
			CALL SETFLAG, "お掃除フェラ", ASSI, MASTER
		ELSE
			PRINTFORMW 清潔な布で丁寧に拭き清めた…
		ENDIF
	ENDSELECT
	CALL RESET_STAIN_EX, "Ｐ", MASTER
ELSEIF LOCAL:1
	SELECTCASE LOCAL:1
	CASE 1
		PRINTFORM %CALLNAME:ASSI%は犯されてドロドロの%CALLNAME:TARGET%のアソコを押し広げ、
		IF (TALENT:ASSI:汚れ無視 || ABL:ASSI:奉仕精神 >= 3) && COND("口の使用", ASSI)
			PRINTFORMW 舌で丁寧に汚れを舐めとった…
			CALL KOJO_GUEST, "助手：パートナーにお掃除クンニ", ASSI
			CALL SETFLAG, "お掃除クンニ", ASSI, TARGET
		ELSE
			PRINTFORMW 蒸しタオルで丁寧に拭き清めた…
		ENDIF
		STAIN:Ｖ = SET_STAIN(0, "Ｖ")
	CASE 2
		PRINTFORM %CALLNAME:ASSI%は犯されて口を開けている%CALLNAME:TARGET%のアヌスを押し広げ、
		IF (TALENT:ASSI:汚れ無視 || ABL:ASSI:奉仕精神 >= 7) && COND("口の使用", ASSI)
			PRINTFORMW 舌を差し込んで丁寧に汚れを舐めとった…
			CALL KOJO_GUEST, "助手：パートナーにお掃除Ａ舐め", ASSI
			CALL SETFLAG, "お掃除Ａ舐め", ASSI, TARGET
		ELSE
			PRINTFORMW 蒸しタオルで丁寧に拭き清めた…
		ENDIF
		STAIN:Ａ = SET_STAIN(0, "Ａ")
	CASE 3
		IF PENIS(TARGET)
			PRINTFORM %CALLNAME:ASSI%は%CALLNAME:TARGET%の口を開けたままになっているペニスを
			IF (TALENT:ASSI:汚れ無視 || ABL:ASSI:奉仕精神 >= 5) && COND("口の使用", ASSI)
				PRINTFORMW 咥え、汚れを啜り上げた後に丁寧に舌で舐め清めた…
				CALL KOJO_GUEST, "助手：パートナーにお掃除フェラ", ASSI
				CALL SETFLAG, "お掃除フェラ", ASSI, TARGET
			ELSE
				PRINTFORMW 蒸しタオルで丁寧に拭き清めた…
			ENDIF
			STAIN:Ｐ = SET_STAIN(0, "Ｐ")
		ELSE
			PRINTFORM %CALLNAME:ASSI%は犯されてドロドロの%CALLNAME:TARGET%のアソコを押し広げ、尿道をグッと剥き出しにすると
			IF (TALENT:ASSI:汚れ無視 || ABL:ASSI:奉仕精神 >= 5) && COND("口の使用", ASSI)
				PRINTFORMW 、舌で丁寧に汚れを舐めとった…
				CALL KOJO_GUEST, "助手：パートナーにお掃除クンニ", ASSI
				CALL SETFLAG, "お掃除クンニ", ASSI, TARGET
			ELSE
				PRINTFORMW 蒸しタオルで丁寧に拭き清めた…
			ENDIF
			STAIN:Ｖ = SET_STAIN(0, "Ｖ")
		ENDIF
	CASE 4
		PRINTFORM %CALLNAME:ASSI%は大股開きでお漏らしをしてしまった%CALLNAME:TARGET%のアソコを押し広げ、
		IF (TALENT:ASSI:汚れ無視 || ABL:ASSI:奉仕精神 >= 5) && COND("口の使用", ASSI)
			PRINTFORMW 舌で丁寧に舐めとった後に尿道をちゅぅっと吸い上げた…
			CALL KOJO_GUEST, "助手：パートナーにお掃除クンニ", ASSI
			CALL SETFLAG, "お掃除クンニ", ASSI, TARGET
		ELSE
			PRINTFORMW 蒸しタオルで丁寧に拭き清めた…
		ENDIF
		STAIN:Ｖ = SET_STAIN(0, "Ｖ")
	ENDSELECT
ENDIF
IF LOCAL + LOCAL:1
	CALL BLANKLINE
	TFLAG:助手行動済 = 1
	RETURN 1
ENDIF


@EVENT_ASSI_ITEM, ARGS
;行動済み
SIF ARGS != "調教開始" && TFLAG:助手行動済
	RETURN 0

IF ARGS == "調教開始" && GETBIT(CFLAG:ASSI:助手の指示, 45)
	PRINTL ＜衣服を脱がす＞
	CALL SETFLAG, "衣装全脱ぎ", TARGET
	PRINTFORML %CALLNAME:ASSI%は%CALLNAME:TARGET%を一糸纏わぬ姿にした！
ENDIF

;ローション
IF GETBIT(CFLAG:ASSI:助手の指示, 20) && TEQUIP:ローション == 0 && ITEM:ローション && MIN(PALAM:Ｖ潤, PALAM:Ａ潤) < 10000
	PRINTFORML ＜\@ ARGS != "調教開始" ? オート # \@ローション＞
	CALL SETFLAG, "調教外潤滑追加", 10000

	TEQUIP:ローション = 1
	ITEM:ローション -= 1
	TFLAG:助手行動済 = 1
	IF ARGS != "調教開始"
		PRINTFORML %CALLNAME:ASSI%は%CALLNAME:TARGET%にローションをぬりたくった…
		CALL BLANKLINE
		RETURN 1
	ENDIF
ENDIF

;撮影(撮影されたことがある相手に対してのみ)
IF ARGS == "調教開始" && GETBIT(CFLAG:ASSI:助手の指示, 27) && TEQUIP:ビデオカメラ == 0 && ITEM:撮影機材 && FIRSTCOM(170)
	PRINTL ＜ビデオ撮影開始＞
	TEQUIP:ビデオカメラ = 1
	TFLAG:助手行動済 = 1
ENDIF


@ASSI_CHANGE_INDICATE, ARG
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
REDRAW 0
CALL BLANKLINE
PRINTFORML %NAME:ARG%は{ABL:ARG:助手}個まで助手の仕事を適切にやる能力があります。
PRINTFORML 以下のどれを指示しますか？
VARSET LOCALS

LOCALS:20 = [20] \@ GETBIT(CFLAG:ARG:助手の指示, 20) ? ○ # － \@ 開始時にローション使用
LOCALS:21 = [21] \@ GETBIT(CFLAG:ARG:助手の指示, 21) ? ○ # － \@ 開始時に媚薬使用

;LOCALS:22 = [22] \@ GETBIT(CFLAG:ARG:助手の指示, 22) ? ○ # － \@ 適度に利尿剤使用

;LOCALS:24 = [24] \@ GETBIT(CFLAG:ARG:助手の指示, 24) ? ○ # － \@ 適度に排卵誘発剤使用

SIF ITEM:撮影機材
	LOCALS:27 = [27] \@ GETBIT(CFLAG:ARG:助手の指示, 27) ? ○ # － \@ 開始時撮影開始（撮影経験がある相手のみ）

IF ABL:ARG:奉仕精神 >= 3
	LOCALS:40 = [40] \@ GETBIT(CFLAG:ARG:助手の指示, 40) ? ○ # － \@ 主人の汚れを拭う
	LOCALS:41 = [41] \@ GETBIT(CFLAG:ARG:助手の指示, 41) ? ○ # － \@ 調教相手の汚れを拭う
ENDIF

LOCALS:45 = [45] \@ GETBIT(CFLAG:ARG:助手の指示, 45) ? ○ # － \@ 開始時に調教相手を全裸にする

;SIF ABL:ARG:奉仕精神 >= 3
;	LOCALS:50 = [50] \@ GETBIT(CFLAG:ARG:助手の指示, 50) ? ○ # － \@ 御用商人をもてなす

FOR LOCAL, 0, 100
	SIF LOCALS:LOCAL != ""
		PRINTFORML  %LOCALS:LOCAL%
NEXT
PRINTFORML [100] これで良い

$INPUT_LOOP
INPUT

IF RESULT == 100
	REDRAW 1
	RETURN 0
ELSEIF RESULT >= 0 && RESULT < 100 && LOCALS:RESULT != ""
ELSE
	GOTO INPUT_LOOP
ENDIF

LOCAL = RESULT
INVERTBIT CFLAG:ARG:助手の指示, LOCAL

IF NUM_BIT(CFLAG:ARG:助手の指示) > ABL:ARG:助手
	PRINTFORMW 与える指示の数が能力を超えています
	INVERTBIT CFLAG:ARG:助手の指示, LOCAL
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


;助手に指示する画面
@ASSI_LIST_INDICATE, ARG
IF ABL:ARG:助手 == 0
	PRINTFORM %TEXTS("助手の名称")%LVが０なので指示できません
	RETURN 0
ENDIF

SIF GETBIT(CFLAG:ARG:助手の指示, 20)
	PRINT [ローション]
;SIF GETBIT(CFLAG:ARG:助手の指示, 21)
;	PRINT [媚薬]
;SIF GETBIT(CFLAG:ARG:助手の指示, 22)
;	PRINT [利尿剤]

;SIF GETBIT(CFLAG:ARG:助手の指示, 24)
;	PRINT [排卵誘発剤]

SIF GETBIT(CFLAG:ARG:助手の指示, 27)
	PRINT [撮影]

SIF GETBIT(CFLAG:ARG:助手の指示, 40)
	PRINT [主人をお掃除]
SIF GETBIT(CFLAG:ARG:助手の指示, 41)
	PRINT [対象をお掃除]
SIF GETBIT(CFLAG:ARG:助手の指示, 45)
	PRINT [対象を全裸に]
SIF GETBIT(CFLAG:ARG:助手の指示, 50)
	PRINT [もてなし]

LOCAL:1 = ABL:ARG:助手 - NUM_BIT(CFLAG:ARG:助手の指示)
IF LOCAL:1 > 0
	FOR LOCAL, 0, LOCAL:1
		PRINT []
	NEXT
ENDIF


@ASSI_LIST_ABL, ARG
#DIM LCOUNT

SIF ARG != MASTER
	PRINTPLAINFORM 精{BASE:ARG:精力, 2} 
PRINTPLAINFORM 技{ABL:ARG:技巧, 2} 
SIF ARG != MASTER
	PRINTPLAINFORM %SUBSTRING(TEXTS("助手の名称"), 0, 2)%{ABL:ARG:助手} 

;相性。効果が微妙なものにはテコ入れorコメントアウト
IF TARGET
	IF ARG != TARGET
		IF RELATION:(NO:ARG) >= 200
			PRINTBUTTON "相性☆ ", "相性☆"
		ELSEIF RELATION:(NO:ARG) >= 150
			PRINTBUTTON "相性◎ ", "相性◎"
		ELSEIF RELATION:(NO:ARG) >= 120
			PRINTBUTTON "相性○ ", "相性○"
		ENDIF
	ENDIF
ENDIF

SIF COND("Ｈはあなたとだけ", ARG)
	PRINTBUTTON "３Ｐ禁止 ", "３Ｐ禁止"

SIF TALENT:ARG:錬金術
	PRINTBUTTON "調合 ", "調合"

;SIF TALENT:ARG:汚れ無視
;	PRINTBUTTON "汚れ無視 ", "汚れ無視"
;SIF TALENT:ARG:両刀
;	PRINTBUTTON "両刀 ", "両刀"

SIF TALENT:ARG:鼓舞
	PRINTBUTTON "鼓舞 ", "鼓舞"
SIF TALENT:ARG:ふたなり
	PRINTBUTTON "ふたなり ", "ふたなり"
SIF TALENT:ARG:オトコ
	PRINTBUTTON "♂ ", "♂"
;SIF TALENT:ARG:禁断の知識
;	PRINTBUTTON "禁断の知識 ", "禁断の知識"
SIF TALENT:ARG:具現
	PRINTBUTTON "具現 ", "具現"
SIF TALENT:ARG:調教知識
	PRINTBUTTON @"%CONDS("調教知識", ARG)% ", CONDS("調教知識", ARG)
SIF TALENT:ARG:甘い言葉
	PRINTBUTTON "甘い言葉 ", "甘い言葉"

VARSET LOCALS
SPLIT CSTR:ARG:アドバイス, "＆", LOCALS

FOR LCOUNT, 0, 10
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASE "無し"
		CONTINUE
	CASEELSE
		PRINTBUTTON @"%LOCALS:LCOUNT% ", LOCALS:LCOUNT
	ENDSELECT
NEXT
