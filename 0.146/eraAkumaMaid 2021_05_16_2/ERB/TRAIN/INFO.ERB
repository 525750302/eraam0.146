@SHOW_STATUS
REDRAW 0

;この値をもとにCLEARLINEを行う
TFLAG:LINECOUNT記録 = LINECOUNT

DRAWLINE
CALL SHOW_STATUS_MAIN

CALL SETFLAG, "助手相手の恋慕外し開始"

;@COMORDERで求める実行値の初期化
CVARSET TCVAR, GETNUM(TCVAR, "コマンド制限")
CVARSET TCVAR, GETNUM(TCVAR, "基本実行値")

FLAG:COM_ABLEの一括OFF = 1


@SHOW_STATUS_MAIN
#DIM LCOUNT
#DIMS SHOW_CONFIG
#DIMS ITEMS

;射精ゲージ設定
FOR LCOUNT, 1, CHARANUM
	SELECTCASE LCOUNT
	CASE MASTER, ASSI, PLAYER, PLAYER:1, TARGET, TARGET:1
		CALL SETFLAG, "射精ゲージ", LCOUNT
	ENDSELECT
NEXT


;季節、年表示部分
PRINTFORM %TEXTS("日付表示")% 
IF TEQUIP:お風呂プレイ
	PRINTFORM -お風呂場-
ELSE
	PRINTFORM -%TEXTS("現在位置")%-
ENDIF
SIF EQUIP:Ｈなご褒美
	CALL PRINT_STR, "ハートピンク_ [Ｈなご褒美中]"
;クリスマスツリーチェック
IF DAYEV("クリスマス期間") && NUM("クリスマスツリー")
	SETCOLOR DEF_COLOR("ハートピンク")
	PRINTFORM %UNICODE(0xD83C)%%UNICODE(0xDF84)%
	RESETCOLOR
ENDIF
PRINT  ムード:
CALL TEXT, "ムードハート"
;ムード記録
TFLAG:ターン開始時のムード = MOOD()
;全体に効果の出ているものはここに書く
;現在効果を発揮しているアドバイザー能力
IF ASSI:1 && CSTR:(ASSI:1):アドバイス != ""
	VARSET LOCALS
	SPLIT CSTR:(ASSI:1):アドバイス, "＆", LOCALS
	FOR LCOUNT, 0, 10
		SELECTCASE LOCALS:LCOUNT
		CASE ""
			BREAK
		CASEELSE
			IF STATE("撃沈", ASSI:1)
				SETCOLOR DEF_COLOR("灰色")
			ELSE
				SETCOLOR DEF_COLOR("黄色")
			ENDIF

			CALL PRINT_LIST, LOCALS:LCOUNT
		ENDSELECT
	NEXT
	RESETCOLOR
ENDIF
PRINTL 

ITEMS = 
;消耗品
FOR LCOUNT, 50, 60
	SIF ITEMNAME:LCOUNT == ""
		CONTINUE
	ITEMS = %ITEMS%%ITEMNAME:LCOUNT% ×{ITEM:LCOUNT}/
NEXT
;ゴム
SIF ITEM:使用済みゴム
	ITEMS = %ITEMS%使用済みゴム ×{ITEM:使用済みゴム}

CALL PRINT_LIST, ITEMS
PRINTL 
PRINTFORM %NAME:PLAYER% 
;危険日
CALL TEXT, "生理周期", PLAYER
CALL SHOW_EQUIP, PLAYER
PRINTFORM \@ PENIS(PLAYER) ? 射精 # 絶頂 \@(%CALLNAME:PLAYER%)
CALL PRINT_BAR, "溜めゲージ", BASE:PLAYER:絶頂, MAXBASE:PLAYER:絶頂, 15
PRINTFORM ({BASE:PLAYER:絶頂}/{MAXBASE:PLAYER:絶頂})
;主人精力表示
PRINTFORM  精力
CALL PRINT_BAR, "減少ゲージ", BASE:PLAYER:精力, MAXBASE:PLAYER:精力, 6
PRINTFORM ({BASE:PLAYER:精力}/{MAXBASE:PLAYER:精力})
;溜まっている
;SIF BASE:PLAYER:精力 > MAXBASE:PLAYER:精力 && PENIS(PLAYER)
;	CALL PRINT_COLORTEXT, "(たまっている) ", DEF_COLOR("ハートピンク")
IF PLAYER != MASTER
	PRINTFORM  集めた魂:
ELSE
	PRINTFORM  魂
	CALL PRINT_BAR, "減少ゲージ", BASE:PLAYER:魂, MAXBASE:PLAYER:魂, 6
ENDIF
PRINTFORM %TEXTS("0.01", BASE:PLAYER:魂)% 
CALL TEXT, "尿意", PLAYER
PRINTL 

CALL SHOW_STATUS_CHARA, TARGET
IF TEQUIP:Ｗ押し倒し
	PRINTL 
	CALL SHOW_STATUS_CHARA, TARGET:1
ENDIF


;助手がいて、ダウンしていないなら表示
IF ASSI && STATE("撃沈", ASSI) == 0 && TEQUIP:Ｗ押し倒し == 0
	PRINTFORM \@ PENIS(ASSI) ? 射精 # 絶頂 \@(%CALLNAME:ASSI%)
	CALL PRINT_BAR, "溜めゲージ", BASE:ASSI:絶頂, MAXBASE:ASSI:絶頂, 15
	PRINTFORM ({BASE:ASSI:絶頂}/{MAXBASE:ASSI:絶頂})
	;助手精力表示
	PRINTFORM  精力
	CALL PRINT_BAR, "減少ゲージ", BASE:ASSI:精力, MAXBASE:ASSI:精力, 6
	PRINTFORM ({BASE:ASSI:精力}/{MAXBASE:ASSI:精力})
	;溜まっている
	;SIF BASE:ASSI:精力 > MAXBASE:ASSI:精力 && PENIS(ASSI)
	;	CALL PRINT_COLORTEXT, "(たまっている) ", DEF_COLOR("ハートピンク")
	PRINTL 
ENDIF


@SHOW_STATUS_CHARA, ARG
#DIMS MEMOS_SEMEN

PRINTFORM %NAME:ARG% 
CALL TEXT, "生理周期", ARG

;これはTARGETの数値を見ればよい
IF TEQUIP:Ｗ押し倒し
	PRINTFORM 好:{CFLAG:ARG:好感度}
ELSE
	PRINTFORM 好感度:{CFLAG:ARG:好感度}
ENDIF
;CALL PRINT_RANK, CFLAG:ARG:好感度, 2500
CALL SHOW_EQUIP, ARG

BASE:ARG:体力 = MAX(BASE:ARG:体力, 0)

PRINT 体力
CALL PRINT_BAR, "減少ゲージ", BASE:ARG:体力, MAXBASE:ARG:体力, 32
PRINTFORM ({BASE:ARG:体力}/{MAXBASE:ARG:体力}) 
;精力表示
PRINT 精力
CALL PRINT_BAR, "減少ゲージ", BASE:ARG:精力, MAXBASE:ARG:精力, 8
PRINTFORML ({BASE:ARG:精力}/{MAXBASE:ARG:精力}) 

CALL NEW_PRINT_PALAM, ARG

IF PENIS(ARG)
	PRINTFORM 射精
	CALL PRINT_BAR, "溜めゲージ", BASE:ARG:絶頂, MAXBASE:ARG:絶頂, 24
	PRINTFORM ({BASE:ARG:絶頂}/{MAXBASE:ARG:絶頂})　
ENDIF
IF TALENT:ARG:母乳体質
	SIF MAXBASE:ARG:母乳 == 0
		MAXBASE:ARG:母乳 = 10000
	PRINTFORM 母乳
	CALL PRINT_BAR, "溜めゲージ", BASE:ARG:母乳, MAXBASE:ARG:母乳, 24
	PRINTFORM ({BASE:ARG:母乳}/{MAXBASE:ARG:母乳})
ENDIF

SIF PENIS(ARG) || TALENT:ARG:母乳体質
	PRINTL 

IF EX:ARG:処女喪失 || EX:ARG:Ｃ絶頂 || EX:ARG:尿道絶頂 || EX:ARG:Ｖ絶頂 || EX:ARG:Ａ絶頂 || EX:ARG:Ｂ絶頂 || EX:ARG:Ｍ絶頂 || EX:ARG:射精 || EX:ARG:噴乳 || EX:ARG:エナジードレイン
	PRINT < 
	SIF EX:ARG:処女喪失
		CALL PRINT_COLORTEXT, "処女喪失 ", DEF_COLOR("ハートピンク")
	SIF EX:ARG:尿道絶頂
		PRINTFORM 尿道絶頂:{EX:ARG:尿道絶頂}回 
	SIF EX:ARG:Ｃ絶頂
		PRINTFORM Ｃ絶頂:{EX:ARG:Ｃ絶頂}回 
	SIF EX:ARG:Ｖ絶頂
		PRINTFORM Ｖ絶頂:{EX:ARG:Ｖ絶頂}回 
	SIF EX:ARG:Ａ絶頂
		PRINTFORM Ａ絶頂:{EX:ARG:Ａ絶頂}回 
	SIF EX:ARG:Ｂ絶頂
		PRINTFORM Ｂ絶頂:{EX:ARG:Ｂ絶頂}回 
	SIF EX:ARG:Ｍ絶頂
		PRINTFORM Ｍ絶頂:{EX:ARG:Ｍ絶頂}回 
	SIF EXP_GAIN("射精経験", ARG)
		PRINTFORM 射精:{EXP_GAIN("射精経験", ARG)}回 
	SIF EX:ARG:トコロテン
		PRINTFORM トコロテン:{EX:ARG:トコロテン}回 
	SIF EXP_GAIN("噴乳経験", ARG)
		PRINTFORM 噴乳:{EXP_GAIN("噴乳経験", ARG)}回 
	SIF EX:ARG:エナジードレイン
		PRINTFORM ＥＤ:{EX:ARG:エナジードレイン}回 
	PRINT >
	PRINTL 
ENDIF
IF PALAM:ARG:Extacy || EX:ARG:絶対搾精量
	IF EX:ARG:絶対搾精量
		PRINTFORM 搾精量:{EX:ARG:絶対搾精量}ml

		MEMOS_SEMEN = 
		SIF EX:ARG:Ｖ中出し量
			MEMOS_SEMEN += @"膣内{EX:ARG:Ｖ中出し量}ml"
		SIF EX:ARG:Ａ中出し量
			MEMOS_SEMEN += @"肛内{EX:ARG:Ａ中出し量}ml"
		SIF EX:ARG:口内射精量
			MEMOS_SEMEN += @"精飲{EX:ARG:口内射精量}ml"
		SIF EX:ARG:乳内射精量
			MEMOS_SEMEN += @"乳内{EX:ARG:乳内射精量}ml"
		SIF EX:ARG:尿道内射精量
			MEMOS_SEMEN += @"膀胱{EX:ARG:尿道内射精量}ml"
		SIF COND("ゴム搾精量", ARG)
			MEMOS_SEMEN += @"ゴム内射精{COND("ゴム搾精量", ARG)}ml"

		SIF MEMOS_SEMEN != ""
			PRINTFORM (%MEMOS_SEMEN%) 
	ENDIF
	;オバフロ対策
	IF PALAM:Extacy >= 10000000000000000
		PRINTFORM Extacy:１京ＯＶＥＲ！ 
	ELSEIF PALAM:Extacy
		PRINTFORM Extacy:{PALAM:ARG:Extacy} 
	ENDIF
	SELECTCASE PALAM:ARG:連続絶頂COMBO数
	CASE IS >= 100
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！！！！ 
	CASE IS >= 50
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！！！ 
	CASE IS >= 20
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！！ 
	CASE IS >= 10
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！！ 
	CASE IS >= 2
		PRINTFORM … 連続絶頂 {PALAM:ARG:連続絶頂COMBO数}COMBO！ 
	ENDSELECT
	PRINTL 
ENDIF

CALL CHECK_SATISFACTION, ARG

CALL TEXT, "性欲", ARG
CALL TEXT, "精液欲", ARG
CALL TEXT, "胎内精液量", ARG
CALL TEXT, "尿意", ARG
CALL TEXT, "排泄欲", ARG

PRINTL 

;ARGの状況の表示
@SHOW_EQUIP, ARG
IF TALENT:ARG:オトコ
	PRINTPLAINFORM [%CONDS("体型", ARG)%]
ELSE
	PRINTPLAINFORM [%CONDS("体型", ARG)%(%CONDS("バストカップ", ARG)%)]
ENDIF
;日焼け有り？
IF TEXTS("日焼け跡", ARG) != ""
	CALL PRINT_STR, @"[_茶色_%TEXTS("日焼け跡", ARG)%_]"
ELSEIF TALENT:ARG:肌の色
	PRINTFORM [%CONDS("肌の色", ARG)%]
ENDIF

IF STATE("具現", ARG)
	PRINT [具現]
ELSEIF STATE("ペニス陰核化", ARG)
	PRINT [陰核化]
ENDIF
IF PENIS(ARG)
	SELECTCASE SIZE("ペニス", ARG)
	CASE 0
		PRINTFORM [小
	CASE 1
		PRINTFORM [並
	CASE 2
		PRINTFORM [巨根
	CASE 3
		PRINTFORM [馬並み
	CASE 4
		PRINTFORM [腕並み
	CASEELSE
		PRINTFORM [規格外
	ENDSELECT

	PRINTFORM (大きさLV{SIZE("ペニス", ARG)})]

	IF ARG == PLAYER
		PRINTFORM Ｐ潤%TEXTS("調教パラメータのＬＶ", GET_PALAMLV(PALAM:ARG:Ｐ潤) )%
		CALL PRINT_BAR, "ＰＡＬＡＭ", PALAM:ARG:Ｐ潤, 0, 4
	ENDIF
ELSE
	IF COND("陰核露出", ARG)
		PRINTFORM [常時クリ露出]
	ELSEIF CALCF("Ｃ発情", ARG) >= 2
		CALL PRINT_STR, "ハートピンク_[陰核露出]"
	ENDIF
ENDIF
;陥没乳首が勃起
IF COND("陥没乳首", ARG)
	IF CALCF("Ｂ発情", ARG)
		CALL PRINT_STR, "ハートピンク_[乳首露出]"
	ELSE
		PRINTFORM [陥没乳首]
	ENDIF
ENDIF

SIF CFLAG:ARG:甘い言葉
	PRINT [ときめき]
SIF CFLAG:ARG:褒める
	PRINT [上機嫌]
SIF STATE("恍惚", ARG)
	CALL PRINT_STR, "ピンク_[恍惚]"
SIF EX:ARG:イくのをこらえた
	CALL PRINT_STR, "ハートピンク_[イきそう]"
SIF COND("発情期", ARG)
	CALL PRINT_STR, "ハートピンク_[発情中]"

IF STATE("悪酔い", ARG)
	CALL PRINT_STR, "[_青_悪酔い_]"
ELSEIF STATE("酔い", ARG)
	CALL PRINT_STR, "[_ハートピンク_酔い_]"
ENDIF

SELECTCASE TEQUIP:ARG:弛緩
CASE 1
	CALL PRINT_STR, "黄色_[緊張緩和]"
CASE 2
	CALL PRINT_STR, "黄色_[弛緩]"
CASE 3
	CALL PRINT_STR, "ハートピンク_[陶酔]"
ENDSELECT

IF ARG != PLAYER
	SIF COND("Ｖ潤十分", ARG) && FIRSTCOMS("Ｖ挿入") && CHOSECOMS("Ｖ挿入") == 0
		CALL PRINT_STR, "ハートピンク_[Ｖ潤十分]"
	SIF COND("Ａ潤十分", ARG) && FIRSTCOMS("Ａ挿入") && CHOSECOMS("Ａ挿入") == 0
		CALL PRINT_STR, "ハートピンク_[Ａ潤十分]"
ENDIF
IF INSERT("インサート", ARG)
	VARSET LOCALS
	IF INSERT("Ｖ二本挿入", ARG)
		LOCALS = Ｖ二本挿入
	ELSEIF INSERT("Ａ二本挿入", ARG)
		LOCALS = Ａ二本挿入
	ELSEIF INSERT("二穴挿入", ARG)
		LOCALS = 二穴挿入
	ELSE
		IF INSERT("Ａインサート", ARG)
			LOCALS = Ａ%CONDS("体位", ARG)%
		ELSEIF INSERT("尿道インサート", ARG)
			LOCALS = 尿道%CONDS("体位", ARG)%
		ELSE
			LOCALS = %CONDS("体位", ARG)%
		ENDIF
		SELECTCASE INSERT("インサート相手", ARG)
		CASE PLAYER
		CASE ASSI
			LOCALS += @"(%TEXTS("助手の名称")%)"
		CASE MASTER
			LOCALS += @"(主人)"
		ENDSELECT
	ENDIF
	IF COND("好きな体位中", ARG)
		CALL PRINT_STR, @"[_ハートピンク_%LOCALS%☆_]"
	ELSE
		CALL PRINT_STR, @"[_黄色_%LOCALS%_]"
	ENDIF
ELSEIF INSERT("挿入中", ARG)
	IF INSERT("Ａ挿入中", ARG)
		PRINTFORM [Ａ挿入中(%CALLNAME:INSERT("挿入中", ARG)%)]
	ELSEIF INSERT("尿道挿入中", ARG)
		PRINTFORM [尿道挿入中(%CALLNAME:INSERT("挿入中", ARG)%)]
	ELSE
		PRINTFORM [Ｖ挿入中(%CALLNAME:INSERT("挿入中", ARG)%)]
	ENDIF
ENDIF
SIF TEQUIP:ARG:手加減
	CALL PRINT_STR, "[_ハートピンク_焦らされ中_]"
SIF TEQUIP:ARG:コンドーム
	PRINT [ゴム装着中]

;これはTARGETの値を見る
IF TEQUIP:Ｗ押し倒し
	SIF ARG == PLAYER
		PRINT [Ｗ押し倒され中]
ELSEIF TEQUIP:ARG:シックスナイン
	PRINTFORM [\@ TEQUIP:ARG:押し倒し ? 押し倒し # \@６９(%TRAIN_NAME_STANDARD(TEQUIP:ARG:シックスナイン)%)中]
ELSEIF TEQUIP:ARG:押し倒し
	PRINT [押し倒し
	IF TEQUIP:PLAYER:拘束
		PRINT 拘束
	ELSEIF TEQUIP:ARG:顔面騎乗
		PRINT 顔面騎乗
	ENDIF
	PRINT 中]
ENDIF
SIF TEQUIP:ARG:電話中
	PRINT [電話中]

SIF COND("指の使用", ARG) == 0
	PRINT [腕使用不可]
SIF TEQUIP:ARG:キス
	PRINT [隙あらばキス]

SIF TEQUIP:ARG:ビデオカメラ
	PRINTFORM [撮影中(残{10 - TEQUIP:ARG:ビデオカメラ + 1}回)]
IF TEQUIP:ARG:野外プレイ
	SIF COND("人里", ARG)
		PRINT [人里]
	SIF COND("森林", ARG)
		PRINT [森林]
	SIF COND("花園", ARG)
		PRINT [花園]
	SIF COND("水辺", ARG)
		PRINT [水辺]
	SIF COND("海", ARG)
		PRINT [海]
ENDIF
SIF TEQUIP:ARG:室内露出プレイ
	PRINT [室内露出中]
IF TEQUIP:ARG:女王様プレイ
	IF TALENT:ARG:オトコ
		PRINTFORM [ＳＭプレイ LV{TEQUIP:ARG:女王様プレイ}]
	ELSE
		PRINTFORM [女王様プレイ LV{TEQUIP:ARG:女王様プレイ}]
	ENDIF
ENDIF

SIF TEQUIP:ARG:お風呂プレイ
	PRINT [お風呂場]
SIF TEQUIP:ARG:新妻プレイ
	PRINT [新妻]

IF TEQUIP:ARG:ディープスロート == PLAYER
	PRINT [ディープスロート中]
ELSEIF TEQUIP:ARG:ディープスロート
	PRINTFORM [ディープスロート中(%CALLNAME:(TEQUIP:ARG:ディープスロート)%)]
ENDIF

SIF TEQUIP:ARG:甘い言葉 >= 1 && TEQUIP:ARG:甘い言葉 <= 8
	PRINTFORM [甘い言葉]
SIF TEQUIP:ARG:排卵誘発剤
	PRINTFORM [排卵誘発剤]

SELECTCASE TEQUIP:ARG:媚薬
CASE IS >= 200
	PRINTFORM [媚薬(激)]
CASE IS >= 150
	PRINTFORM [媚薬(強)]
CASE IS > 50
	PRINTFORM [媚薬]
CASE IS > 0
	PRINTFORM [媚薬(弱)]
ENDSELECT

SIF TEQUIP:ARG:利尿剤
	PRINTFORM [利尿剤]
IF TEQUIP:ARG:筋弛緩剤
	PRINTFORM [筋弛緩剤
	SIF GETBIT(TEQUIP:ARG:筋弛緩剤, 0)
		PRINTFORM (Ａ)
	SIF GETBIT(TEQUIP:ARG:筋弛緩剤, 1)
		PRINTFORM (尿道)
	SIF GETBIT(TEQUIP:ARG:筋弛緩剤, 2)
		PRINTFORM (Ｖ)
	SIF GETBIT(TEQUIP:ARG:筋弛緩剤, 3)
		PRINTFORM (子宮口)
	PRINTFORM ]
ENDIF
IF TEQUIP:ARG:バイブ
	IF COND("双頭バイブ", ARG)
		PRINTFORM [双頭バイブ]
	ELSE
		SELECTCASE SIZE("バイブ", ARG)
		CASE 6
			PRINTFORM [規格外バイブ]
		CASE 5
			PRINTFORM [馬型バイブ]
		CASE 4
			PRINTFORM [腕並バイブ]
		CASE 3
			PRINTFORM [極太バイブ]
		CASEELSE
			PRINTFORM [バイブ]
		ENDSELECT
	ENDIF
ENDIF

SIF TEQUIP:ARG:カテーテル
	PRINTFORM [カテーテル]

IF TEQUIP:ARG:アナルバイブ
	IF COND("アナルパール", ARG)
		PRINTFORM [アナルパール]
	ELSE
		VARSET LOCALS
		SIF COND("ロングバイブ", ARG)
			LOCALS = (結腸挿入中)
		SELECTCASE SIZE("Ａバイブ", ARG)
		CASE 6
			PRINTFORM [Ａ規格外バイブ%LOCALS%]
		CASE 5
			PRINTFORM [Ａ馬型バイブ%LOCALS%]
		CASE 4
			PRINTFORM [Ａ腕並バイブ%LOCALS%]
		CASE 3
			PRINTFORM [Ａ極太バイブ%LOCALS%]
		CASEELSE
			PRINTFORM [アナルバイブ%LOCALS%]
		ENDSELECT
	ENDIF
ENDIF

IF TEQUIP:ARG:尿道バイブ
	IF COND("尿道パール", ARG)
		PRINTFORM [尿道パール]
	ELSE
		SELECTCASE SIZE("尿道バイブ", ARG)
		CASE 6
			PRINTFORM [尿道規格外バイブ]
		CASE 5
			PRINTFORM [尿道馬型バイブ]
		CASE 4
			PRINTFORM [尿道腕並バイブ]
		CASE 3
			PRINTFORM [尿道極太バイブ]
		CASEELSE
			PRINTFORM [尿道バイブ]
		ENDSELECT
	ENDIF
ENDIF

SIF TEQUIP:ARG:拘束
	PRINTFORM [%BINDS(TEQUIP:ARG:拘束)%]

IF TEQUIP:ARG:浣腸
	PRINTFORM [浣腸

	SIF COND("精液浣腸", ARG)
		PRINTFORM (精液)
	SIF COND("温泉浣腸", ARG)
		PRINTFORM (尿)
	SIF COND("空気浣腸", ARG)
		PRINTFORM (空気)

	SIF TEQUIP:ARG:浣腸 && COND("アナルプラグ", ARG)
		PRINTFORM ＋アナルプラグ

	PRINTFORM ]
ENDIF

IF TEQUIP:ARG:アイマスク || TEQUIP:ARG:鼻フック
	IF TEQUIP:ARG:アイマスク && TEQUIP:ARG:鼻フック
		PRINTFORM [アイマスク＆鼻フック]
	ELSEIF TEQUIP:ARG:アイマスク
		PRINTFORM [アイマスク]
	ELSEIF TEQUIP:ARG:鼻フック
		PRINTFORM [鼻フック]
	ENDIF
ENDIF

IF TEQUIP:ARG:口枷 == 1
	PRINTFORM [ボールギャグ]
ELSEIF TEQUIP:ARG:口枷 == 2
	PRINTFORM [リングギャグ]
ENDIF

SIF TEQUIP:ARG:絆創膏
	PRINTFORM [絆創膏]
	
PRINTL  
;着せ替え表示
SIF ARG != PLAYER
	CALL PRINT_DRESSL, ARG, "短縮表示"


@STAIN_INFO
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT

REDRAW 0
PRINTL 
PRINTFORML 以下が各々の汚れの状態です。
SIF TEQUIP:押し倒し == 0
	PRINTFORML お望みならば、手や口の汚れ等を綺麗にすることもできます。
;誰かが真性包茎
IF COND("真性包茎") || COND("真性包茎", MASTER) || COND("真性包茎", ASSI)
	SETCOLOR DEF_COLOR("イエロー")
	PRINTFORML ※真性包茎の人はＰに常に<Ｐ恥垢>汚れが有ります。洗っても掃除しても落ちません
	RESETCOLOR
ENDIF
PRINTFORML （入浴中・お風呂上り状態だと汚れが移りづらく、奉仕調教も実行しやすくなります）
CALL STAIN_INFO_SHOW, MASTER
CALL STAIN_INFO_SHOW, TARGET
CALL STAIN_INFO_SHOW, TARGET:1
DRAWLINE
IF TEQUIP:押し倒し
	WAIT
ELSE
	PRINTBUTTON "[Enter] 閉じる", "Enter"

	ONEINPUTS

	SELECTCASE RESULTS
	CASE "0"
		STAIN:MASTER:Ｍ = 0
	CASE "1"
		STAIN:MASTER:手 = 0
	CASE "2"
		IF CHECK_ADD_STAIN("Ｐ", MASTER)
			CALL RESET_STAIN_EX, "Ｐ", MASTER
			SIF INSERT("挿入中", MASTER)
				CALL RESET_INSERT, MASTER
		ENDIF
	CASE "3"
		STAIN:Ｍ = 0
	CASE "4"
		STAIN:手 = 0
	CASE "5"
		IF CHECK_ADD_STAIN("Ｐ", TARGET)
			CALL RESET_STAIN_EX, "Ｐ", TARGET
			SIF INSERT("挿入中", TARGET)
				CALL RESET_INSERT, TARGET
		ENDIF
	CASE "6"
		SIF ASSI
			STAIN:ASSI:Ｍ = 0
	CASE "7"
		SIF ASSI
			STAIN:ASSI:手 = 0
	CASE "8"
		IF ASSI
			IF CHECK_ADD_STAIN("Ｐ", ASSI)
				CALL RESET_STAIN_EX, "Ｐ", ASSI
				SIF INSERT("挿入中", ASSI)
					CALL RESET_INSERT, ASSI
			ENDIF
		ENDIF
	CASEELSE
		REDRAW 1
		RETURN 0
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

REDRAW 1

@STAIN_INFO_SHOW, ARG
#DIM LCOUNT

SIF ARG <= 0
	RETURN 0
IF TEQUIP:ARG:お風呂上り
	;ここはTARGETの値を見る
	IF TARGET && TEQUIP:お風呂プレイ
		LOCALS = □ %NAME:ARG%（入浴中）□
	ELSE
		LOCALS = □ %NAME:ARG%（お風呂上り）□
	ENDIF
ELSE
	LOCALS = □ %NAME:ARG% □
ENDIF
CALL DRAWLINES, LOCALS

;選択肢に使う
SELECTCASE ARG
CASE MASTER
	LOCAL = 0
CASE TARGET
	LOCAL = 3
CASE TARGET:1
	LOCAL = 6
ENDSELECT

FOR LCOUNT, 0, 7
	LOCALS = 
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｖ")
		LOCALS += "<Ｖ>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｖ恥垢")
		LOCALS += "<Ｖ恥垢>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｐ")
		LOCALS += "<Ｐ>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ｐ恥垢")
		LOCALS += "<Ｐ恥垢>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "精液")
		LOCALS += "<精>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "Ａ")
		LOCALS += "<Ａ>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "乳")
		LOCALS += "<乳>"
	SIF CHECK_STAIN(STAIN:ARG:LCOUNT, "尿")
		LOCALS += "<尿>"

	;SIF LCOUNT == 4 && (COND("身嗜み：腸洗浄", ARG) || EX:ARG:排泄 >= 2)
	;	LOCALS += "（洗浄済み）"

	SELECTCASE LCOUNT
	CASE 0
		PRINTFORM 　口：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:押し倒し == 0 && LOCALS != ""
			PRINTFORM [{LOCAL + LCOUNT}] 口を\@ ARG == MASTER ? すすぐ # すすがせる \@
	CASE 1
		PRINTFORM 　手：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:押し倒し == 0 && LOCALS != ""
			PRINTFORM [{LOCAL + LCOUNT}] 指を拭う
	CASE 2
		SIF PENIS(ARG) == 0
			CONTINUE
		PRINTFORM 　Ｐ：%TEXT_LJ(LOCALS, 32)%
		SIF TEQUIP:押し倒し == 0 && CHECK_ADD_STAIN("Ｐ", ARG)
			PRINTFORM [{LOCAL + LCOUNT}] ペニスを拭う \@ INSERT("挿入中", ARG) ? (インサートは解除) # \@
	CASE 3
		SIF TALENT:ARG:オトコ
			CONTINUE
		PRINTFORM 　Ｖ：%TEXT_LJ(LOCALS, 32)%
	CASE 4
		PRINTFORM 　Ａ：%TEXT_LJ(LOCALS, 32)%
	CASE 5
		;SIF TALENT:ARG:オトコ
		;	CONTINUE
		PRINTFORM 　Ｂ：%TEXT_LJ(LOCALS, 32)%
	CASE 6
		PRINTFORM 　脚：%TEXT_LJ(LOCALS, 32)%
	ENDSELECT


	PRINTL 
NEXT
PRINTL 
