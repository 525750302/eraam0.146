;-------------------------------------------------
;調教コマンド実行前のパラメータの記録
;TCVAR:100〜119を"調教コマンド実行前の重要パラメータの記録"用として確保してある
;-------------------------------------------------
@SAVE_PALAM_BEFORECOM
#DIM LCOUNT
FOR LCOUNT, 1, CHARANUM
	;射精or絶頂ゲージの開始値＆増加量
	TCVAR:LCOUNT:絶頂開始前 = BASE:LCOUNT:絶頂
	TCVAR:LCOUNT:絶頂増加 = 0
	;母乳ゲージの開始値＆増加量
	TCVAR:LCOUNT:母乳開始前 = BASE:LCOUNT:母乳
	TCVAR:LCOUNT:母乳増加 = 0
	;魂の開始値
	TCVAR:LCOUNT:魂開始前 = BASE:LCOUNT:魂
	;精力の開始値＆減少量
	TCVAR:LCOUNT:精力開始前 = BASE:LCOUNT:精力
	TCVAR:LCOUNT:精力減少 = 0
	;受精確率
	TCVAR:LCOUNT:受精確率 = COND("受精確率", LCOUNT)
NEXT


;-------------------------------------------------
;PALAMの増減を表示させる（Ｐ潤は表示しない）
;対応する珠の増加分も表示するように変更
;ARGにはTARGETかASSIかTARGET:1しか入らない
;-------------------------------------------------
@NEW_UPCHECK_PALAM, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;このターンでの増減を行う前のPALAMの値の記録に使う
#DIM PALAM_PRE, 30


;Ｗ押し倒しの時には名前を表示させる
IF TEQUIP:Ｗ押し倒し && SELECTCOM:1 >= 0
	PRINTFORML ＜%CALLNAME:ARG%＞
ELSE
	PRINTL 
ENDIF

MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 30
	PALAM_PRE:LCOUNT = PALAM:ARG:LCOUNT
	PALAM:ARG:LCOUNT = MIN(PALAM_LV(16), PALAM:ARG:LCOUNT + CUP:ARG:LCOUNT - CDOWN:ARG:LCOUNT)
NEXT

FOR LCOUNT, 0, 30
	SIF CUP:ARG:LCOUNT == 0 && CDOWN:ARG:LCOUNT == 0
		CONTINUE

	PRINTFORM 　%TEXT_LJ(PALAMNAME:LCOUNT, 8)% = {PALAM_PRE:LCOUNT, 8} + 

	SELECTCASE GET_PALAMLV(CUP:ARG:LCOUNT)
	CASE 3
		SETCOLOR DEF_COLOR("黄色")
	CASE IS >= 4
		SETCOLOR DEF_COLOR("ピンク")
	ENDSELECT
	PRINTFORM {CUP:ARG:LCOUNT, 7}
	RESETCOLOR

	PRINTFORM  - {CDOWN:ARG:LCOUNT, 7} = {PALAM:ARG:LCOUNT, 8}　

	;宝珠を獲得するようならそれを表示
	IF GET_JUEL(LCOUNT)
		PRINTFORM %PALAMNAME:LCOUNT%の珠＋
		SETCOLOR DEF_COLOR("黄色")
		PRINTFORM {GET_JUEL(LCOUNT), 5}
	ENDIF

	RESETCOLOR
	PRINTL 
NEXT

;変動したPALAMを表示した場合には改行
SIF LINECOUNT != MEMO_LINECOUNT
	PRINTL 


;-------------------------------------------------
;表示を整形したUPCHECK
;-------------------------------------------------
@NEW_UPCHECK
#DIM MEMO_LINECOUNT
#DIM LCOUNT
#DIM BASE_PRE, 30
#DIM BASE_AFT, 30
#DIM UP_BASE, 30
#DIM DOWN_BASE, 30
#DIM BASE_MAX, 30
#DIMS NAME_BASE, 30

;改行をするかどうかに用いる
MEMO_LINECOUNT = LINECOUNT

CALL NEW_UPCHECK_PALAM, TARGET

SIF TEQUIP:Ｗ押し倒し && SELECTCOM:1 >= 0
	CALL NEW_UPCHECK_PALAM, TARGET:1

SIF LINECOUNT == MEMO_LINECOUNT
	PRINTL 

;PLAYERのペニスのＰ潤の処理
CALL SETFLAG, "Ｐ潤変動処理", MASTER
IF COND("３Ｐ")
	SWAP SELECTCOM, SELECTCOM:2
	CALL SETFLAG, "Ｐ潤変動処理", PLAYER:1
	SWAP SELECTCOM, SELECTCOM:2
ENDIF

VARSET BASE_PRE
VARSET BASE_AFT
VARSET UP_BASE
VARSET DOWN_BASE
VARSET BASE_MAX
VARSET NAME_BASE

;射精or絶頂
BASE_AFT:0 = BASE:MASTER:絶頂
BASE_PRE:0 = TCVAR:MASTER:絶頂開始前
UP_BASE:0 = MAX(TCVAR:MASTER:絶頂増加, 0)
DOWN_BASE:0 = BASE_PRE:0 + UP_BASE:0 - BASE_AFT:0
BASE_MAX:0 = MAXBASE:MASTER:絶頂
NAME_BASE:0 = \@ PENIS(MASTER) ? 射精 # 絶頂 \@

;精力
BASE_AFT:1 = BASE:MASTER:精力
BASE_PRE:1 = TCVAR:MASTER:精力開始前
DOWN_BASE:1 = TCVAR:MASTER:精力減少
;UP_BASE:1 = MAX(BASE_AFT:1 + UP_BASE:1 - BASE_PRE:1, 0)
UP_BASE:1 = MAX(BASE_AFT:1 + DOWN_BASE:1 - BASE_PRE:1, 0)
NAME_BASE:1 = 精力

;魂
BASE_AFT:2 = BASE:MASTER:魂
BASE_PRE:2 = TCVAR:MASTER:魂開始前
DOWN_BASE:2 = BASE_PRE:2 - BASE_AFT:2
UP_BASE:2 = MAX(BASE_AFT:2 - BASE_PRE:2, 0)
NAME_BASE:2 = 　魂

;射精or絶頂（助手）
IF ASSI
	BASE_AFT:10 = BASE:ASSI:絶頂
	BASE_PRE:10 = TCVAR:ASSI:絶頂開始前
	UP_BASE:10 = MAX(TCVAR:ASSI:絶頂増加, 0)
	DOWN_BASE:10 = BASE_PRE:10 + UP_BASE:10 - BASE_AFT:10
	BASE_MAX:10 = MAXBASE:ASSI:絶頂
	NAME_BASE:10 = \@ PENIS(ASSI) ? 射精 # 絶頂 \@

	;精力（助手）
	BASE_AFT:11 = BASE:ASSI:精力
	BASE_PRE:11 = TCVAR:ASSI:精力開始前
	DOWN_BASE:11 = TCVAR:ASSI:精力減少
	UP_BASE:11 = MAX(BASE_AFT:11 + DOWN_BASE:11 - BASE_PRE:11, 0)
	NAME_BASE:11 = 精力(助)
ENDIF

;射精（パートナー）
IF PENIS(TARGET)
	BASE_AFT:20 = BASE:絶頂
	BASE_PRE:20 = TCVAR:絶頂開始前
	UP_BASE:20 = MAX(TCVAR:絶頂増加, 0)
	DOWN_BASE:20 = BASE_PRE:20 + UP_BASE:20 - BASE_AFT:20
	BASE_MAX:20 = MAXBASE:絶頂
	NAME_BASE:20 = 射精(調)
ENDIF

;精力（パートナー）
BASE_AFT:21 = BASE:精力
BASE_PRE:21 = TCVAR:精力開始前
DOWN_BASE:21 = TCVAR:精力減少
UP_BASE:21 = MAX(BASE_AFT:21 + DOWN_BASE:21 - BASE_PRE:21, 0)
NAME_BASE:21 = 精力(調)

;母乳（パートナー）
IF TALENT:母乳体質
	BASE_AFT:22 = BASE:母乳
	BASE_PRE:22 = TCVAR:母乳開始前
	UP_BASE:22 = MAX(TCVAR:母乳増加, 0)
	DOWN_BASE:22 = BASE_PRE:22 + UP_BASE:22 - BASE_AFT:22
	BASE_MAX:22 = MAXBASE:母乳
	NAME_BASE:22 = 母乳(調)
ENDIF

MEMO_LINECOUNT = LINECOUNT

FOR LCOUNT, 0, 30
	SIF NAME_BASE:LCOUNT == ""
		CONTINUE
	SIF UP_BASE:LCOUNT == 0 && DOWN_BASE:LCOUNT == 0
		CONTINUE

	;魂は小数点表示する
	SELECTCASE NAME_BASE:LCOUNT
	CASE "　魂"
		PRINTFORML 　%TEXT_LJ(NAME_BASE:LCOUNT, 8)% = %TEXT_RJ(TEXTS("0.01", BASE_PRE:LCOUNT), 8)% + %TEXT_RJ(TEXTS("0.01", UP_BASE:LCOUNT), 7)% - %TEXT_RJ(TEXTS("0.01", DOWN_BASE:LCOUNT), 7)% = %TEXT_RJ(TEXTS("0.01", BASE_AFT:LCOUNT), 8)%　%NAME_BASE:LCOUNT%
	CASEELSE
		PRINTFORM 　%TEXT_LJ(NAME_BASE:LCOUNT, 8)% = {BASE_PRE:LCOUNT, 8} + 

		;射精の時などは色を変える
		IF BASE_MAX:LCOUNT
			SELECTCASE UP_BASE:LCOUNT
			CASE IS >= BASE_MAX:LCOUNT*4/5
				SETCOLOR DEF_COLOR("ピンク")
			CASE IS >= BASE_MAX:LCOUNT/4
				SETCOLOR DEF_COLOR("黄色")
			ENDSELECT
		ENDIF

		PRINTFORM {UP_BASE:LCOUNT, 7}

		RESETCOLOR

		PRINTFORML  - {DOWN_BASE:LCOUNT, 7} = {BASE_AFT:LCOUNT, 8}　%NAME_BASE:LCOUNT%
	ENDSELECT
NEXT

SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

IF BASE:PLAYER:精力 <= 0 && ASSIPLAY && STATE("撃沈", ASSI)
	PRINTL  
	PRINTFORMW ★調教者を%CALLNAME:MASTER%に交代しました。★
	TEQUIP:押し倒し = 0
	SIF TARGET:1 == ASSI
		CALL SETFLAG, "Ｗ押し倒し解除"
	CALL REMOVE_STATE, ASSI, "恍惚"
	CALL SETFLAG, "調教者交代", 1
ENDIF
