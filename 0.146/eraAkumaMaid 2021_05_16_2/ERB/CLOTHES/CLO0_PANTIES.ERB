;-------------------------------------------------
;キャラARGにパンツARGSを着せる。ARGS == ""なら脱がす
;-------------------------------------------------
@SET_PANTIES, ARGS, ARG
SIF ARG == 0 && TARGET > 0
	ARG = TARGET

;汚れ＆ずれたりしたフラグ等を初期化して新品にする
STAIN:ARG:パンツ = 0

CALL PALAM_PANTIES, ARGS, ARG

;コモンフラグを設定する
CSTR:ARG:パンツ = %SET_CLO_COMMON(CSTR:ARG:パンツ, ARG)%
CSTR:ARG:ブラ = %SET_CLO_COMMON(CSTR:ARG:ブラ, ARG)%

EQUIP:ARG:パンツ = STRLENS(CSTR:ARG:パンツ)
EQUIP:ARG:ブラ = STRLENS(CSTR:ARG:ブラ)

;自分の下着に着替えた場合には色を合わせる
SIF TFLAG:自分の下着に着替えた
	CALL EQ_CLOCOLOR, TARGET

;ボディスーツは脱がす
SIF EQUIP:ARG:パンツ && EQUIP:ARG:ボディスーツ
	CALL SET_BODYS, "", ARG

TFLAG:着替えた = 1

;-------------------------------------------------
;ARGの穿いているパンツの名前。ARGSに"略"と入れると略称になる
;-------------------------------------------------
@PANTIES(ARG, ARGS)
#FUNCTIONS
#DIMS COLOR_ABOUT

VARSET LOCALS
SELECTCASE ARGS
CASE "リボン"
	SIF CHECK_PANTIES("リボン", ARG) == 0
		RETURNF ""
	RETURNF EXTSTR(CSTR:ARG:パンツ, "リボン", "/")
CASE "CIPフロント"
	IF CHECK_PANTIES("Ｖ露出", ARG) || CHECK_PANTIES("Ｃストリング", ARG)
		RETURNF ""
	ELSEIF CHECK_PANTIES("前貼り", ARG)
		LOCALS = 前貼り
	ELSEIF CHECK_PANTIES("ドロワーズ", ARG)
		LOCALS = ドロワーズ
	ELSEIF CHECK_PANTIES("ハイレグブルマ", ARG)
		LOCALS = ハイレグブルマ
	ELSEIF CHECK_PANTIES("葉っぱパンツ", ARG)
		LOCALS = 葉っぱパンツ
	ELSEIF CHECK_PANTIES("褌", ARG)
		LOCALS = 褌
	ELSEIF CHECK_PANTIES("子供パンツ", ARG)
		LOCALS = 子供パンツ
	ELSEIF CHECK_PANTIES("ボーイレッグ", ARG)
		LOCALS = ボーイレッグ
	ELSEIF CHECK_PANTIES("ティアドロップ", ARG)
		LOCALS = ティアドロップ
	ELSEIF CHECK_PANTIES("マイクロ", ARG) && CHECK_PANTIES("ビキニ", ARG) == 0
		LOCALS = マイクロショーツ
	ELSEIF CHECK_PANTIES("Ｇストリング", ARG) || CHECK_PANTIES("マイクロ", ARG)
		LOCALS = マイクロ
	ELSEIF CHECK_PANTIES("ローレグ", ARG)
		LOCALS = ローレグ
	ELSEIF CHECK_PANTIES("ローライズパンツ", ARG)
		LOCALS = ローライズ
	ELSEIF CHECK_PANTIES("ハイレグ", ARG)
		LOCALS = ハイレグ
	ELSEIF CHECK_PANTIES("フリル", ARG)
		LOCALS = フリル
	ELSEIF CHECK_PANTIES("レース", ARG) || CHECK_PANTIES("フリル", ARG) || CHECK_PANTIES("シースルー", ARG)
		LOCALS = レース
	ELSEIF CHECK_PANTIES("縞々", ARG)
		LOCALS = 縞パン
	ELSEIF CHECK_PANTIES("Ｔバック", ARG) || CHECK_PANTIES("紐パン", ARG)
		LOCALS = Ｔバック
	ELSEIF CHECK_PANTIES("ショーツ", ARG) || CHECK_PANTIES("フルバック", ARG)
		LOCALS = ショーツ
	ELSEIF CHECK_PANTIES("パンティ", ARG)
		LOCALS = パンティ
	ELSEIF EQUIP:ARG:パンツ == 0
		SIF PENIS(ARG)
			RETURNF ""
		LOCALS = すじ
	ELSE
		RETURNF ""
	ENDIF
CASE "CIPバック"
	IF CHECK_PANTIES("Ｃストリング", ARG)
		RETURNF ""
	ELSEIF CHECK_PANTIES("ティアドロップ", ARG) || CHECK_PANTIES("Ｇストリング", ARG) || CHECK_PANTIES("マイクロ", ARG)
		LOCALS = Ｇストリング
	ELSEIF CHECK_PANTIES("ローレグ", ARG)
		RETURNF ""
	ELSEIF CHECK_PANTIES("Ｔバック", ARG)
		LOCALS = Ｔバック
	ELSE
		RETURNF ""
	ENDIF
CASE "CIP種類"
	IF CHECK_PANTIES("Ｃストリング", ARG)
		RETURNF ""
	ELSEIF CHECK_PANTIES("前貼り", ARG)
		LOCALS = 前貼り
	ELSEIF CHECK_PANTIES("ドロワーズ", ARG)
		LOCALS = ドロワーズ
	ELSEIF CHECK_PANTIES("ハイレグブルマ", ARG)
		LOCALS = ハイレグブルマ
	ELSEIF CHECK_PANTIES("葉っぱパンツ", ARG)
		LOCALS = 葉っぱパンツ
	ELSEIF CHECK_PANTIES("褌", ARG)
		LOCALS = 褌
	ELSEIF CHECK_PANTIES("子供パンツ", ARG)
		LOCALS = 子供パンツ
	ELSEIF CHECK_PANTIES("ボーイレッグ", ARG)
		LOCALS = ボーイレッグ
	ELSEIF CHECK_PANTIES("ティアドロップ", ARG)
		LOCALS = ティアドロップ
	ELSEIF CHECK_PANTIES("Ｇストリング", ARG)
		LOCALS = Ｇストリング
	ELSEIF CHECK_PANTIES("マイクロ", ARG) && CHECK_PANTIES("ビキニ", ARG) == 0
		LOCALS = マイクロショーツ
	ELSEIF CHECK_PANTIES("マイクロ", ARG)
		LOCALS = マイクロ
	ELSEIF CHECK_PANTIES("ローレグ", ARG)
		LOCALS = ローレグ
	ELSEIF CHECK_PANTIES("ローライズパンツ", ARG)
		LOCALS = ローライズ
	ELSEIF CHECK_PANTIES("ハイレグ", ARG)
		LOCALS = ハイレグ
	ELSEIF CHECK_PANTIES("フリル", ARG)
		LOCALS = フリル
	ELSEIF CHECK_PANTIES("レース", ARG)
		LOCALS = レース
	ELSEIF CHECK_PANTIES("Ｔバック", ARG)
		LOCALS = Ｔバック
	ELSEIF CHECK_PANTIES("縞々", ARG)
		LOCALS = 縞パン
	ELSEIF CHECK_PANTIES("ショーツ", ARG) || CHECK_PANTIES("フルバック", ARG)
		LOCALS = ショーツ
	ELSEIF CHECK_PANTIES("パンティ", ARG)
		LOCALS = パンティ
	ELSEIF EQUIP:ARG:パンツ == 0
		SIF PENIS(ARG)
			RETURNF ""
		LOCALS = すじ
	ELSE
		RETURNF ""
	ENDIF
CASE "CIPカラー：種類", "CIPカラー：フロント", "CIPカラー：バック"
	SELECTCASE PANTIES(ARG, @"CIP%EXTSTR(ARGS, "：")%")
	;色指定が無いもの
	CASE "", "すじ"
		RETURNF ""
	;特殊
	CASE "子供パンツ"
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		;デフォなので変化させなくてよい
		CASE "白"
			RETURNF ""
		CASEELSE
			RETURNF CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		ENDSELECT
	CASE "前貼り"
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASEELSE
			RETURNF "白"
		ENDSELECT
	CASE "ハイレグブルマ"
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		CASE "赤"
			RETURNF "赤"
		CASE "水色"
			RETURNF "水色"
		CASE "紺色"
			RETURNF "紺色"
		CASE "緑色"
			RETURNF "緑色"
		CASEELSE
			RETURNF "紺色"
		ENDSELECT
	;白赤黒紫青分岐
	CASE "マイクロショーツ"
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		;デフォなので変化させなくてよい
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤", "薄桃色"
			RETURNF "赤"
		CASE "青", "紺色", "薄青色"
			RETURNF "青"
		;用意されていない色
		CASE "緑色", "薄緑", "黄色", "橙色"
			RETURNF ""
		ENDSELECT
	CASE "Ｔバック"
		IF STRCOUNT(CSTR:ARG:パンツ, "水玉模様")
			SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
			CASE "黒"
				RETURNF "水玉黒"
			CASE "ピンク"
				RETURNF "水玉ピンク"
			CASE "薄青色", "水色"
				RETURNF "水玉水色"
			CASE "黄色", "橙色"
				RETURNF "水玉黄色"
			ENDSELECT
		ELSEIF STRCOUNT(CSTR:ARG:パンツ, "豹柄")
			RETURNF "豹柄"
		ENDIF
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		;デフォなので変化させなくてよい
		CASE "黒"
			RETURNF ""
		CASE "紫"
			RETURNF "紫"
		CASE "真紅"
			RETURNF "真紅"
		CASE "赤", "薄桃色"
			RETURNF "赤"
		CASE "ピンク"
			RETURNF "ピンク"
		CASE "青", "紺色"
			RETURNF "青"
		CASE "薄青色", "水色"
			RETURNF "水色"
		CASE "緑色"
			RETURNF "緑色"
		CASE "薄緑"
			RETURNF "薄緑"
		CASE "灰色"
			RETURNF "灰色"
		CASE "黄色", "橙色"
			RETURNF "黄色"
		ENDSELECT
	CASE "Ｇストリング", "マイクロ", "ティアドロップ"
		SIF STRCOUNT(CSTR:ARG:パンツ, "豹柄")
			RETURNF "豹柄"
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		;デフォなので変化させなくてよい
		CASE "白", "純白"
			RETURNF ""
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤", "薄桃色"
			RETURNF "赤"
		CASE "青", "紺色", "薄青色"
			RETURNF "青"
		;用意されていない色
		CASE "緑色", "薄緑", "黄色", "橙色"
			RETURNF ""
		ENDSELECT
	CASE "縞パン"
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		CASE "紅白", "緑と白", "黄色と白", "黒と白"
			RETURNF CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		ENDSELECT
	;ピンクが無い
	CASE "ローレグ"
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤"
			RETURNF "赤"
		CASE "薄桃色", "桃"
			RETURNF ""
		CASE "青", "紺色", "薄青色"
			RETURNF "青"
		CASE "緑色", "薄緑"
			RETURNF "緑色"
		CASE "黄色", "橙色"
			RETURNF "黄色"
		ENDSELECT
	;赤が無い
	CASE "ローライズパンツ", "フリル", "ボーイレッグ"
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤"
			RETURNF ""
		CASE "薄桃色", "桃"
			RETURNF "桃"
		CASE "青", "紺色", "薄青色"
			RETURNF "青"
		CASE "緑色", "薄緑"
			RETURNF "緑色"
		CASE "黄色", "橙色"
			RETURNF "黄色"
		ENDSELECT
	;その他は８カラ―
	CASEELSE
		SELECTCASE CLOCOLOR(CSTR:ARG:パンツ, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤"
			RETURNF "赤"
		CASE "薄桃色", "桃"
			RETURNF "桃"
		CASE "青", "紺色", "薄青色", "縞々", "水玉"
			RETURNF "青"
		CASE "緑色", "薄緑"
			RETURNF "緑色"
		CASE "黄色", "橙色", "豹柄"
			RETURNF "黄色"
		ENDSELECT
	ENDSELECT
	RETURNF ""

CASE "パンツ", "下着", "ぱんつ"
	IF CHECK_PANTIES("水着", ARG)
		LOCALS = 水着
	ELSEIF CHECK_PANTIES("ブルマ", ARG)
		LOCALS = ブルマ
	ELSE
		LOCALS = %ARGS%
	ENDIF
CASE "略"
	IF CHECK_PANTIES("貞操帯", ARG)
		LOCALS = 貞操帯
	ELSEIF CHECK_PANTIES("ブルマ", ARG)
		LOCALS = ブルマ
	ELSEIF CHECK_PANTIES("ボクサーパンツ", ARG)
		LOCALS = ボクサーパンツ
	ELSEIF CHECK_PANTIES("ブリーフ", ARG)
		LOCALS = ブリーフ
	ELSEIF CHECK_PANTIES("トランクス", ARG)
		LOCALS = トランクス
	ELSEIF CHECK_PANTIES("褌", ARG)
		LOCALS = 褌
	;ビキニって呼称は上下合わせないとちょっと微妙かも
	ELSEIF CHECK_PANTIES("ビキニ", ARG)
		IF CHECK_PANTIES("ティアドロップ", ARG)
			LOCALS = ティアドロップビキニ
		ELSEIF CHECK_PANTIES("マイクロ", ARG)
			LOCALS = マイクロビキニ
		ELSE
			LOCALS = ビキニパンツ
		ENDIF
	;ELSEIF CHECK_PANTIES("スパッツ", ARG)
	;	LOCALS = スパッツ
	ELSEIF CHECK_PANTIES("ドロワーズ", ARG)
		LOCALS = ドロワーズ
	ELSEIF CHECK_PANTIES("Ｃストリング", ARG)
		LOCALS = Ｃストリング
	ELSEIF CHECK_PANTIES("前貼り", ARG)
		LOCALS = 前貼り
	;ELSEIF CHECK_PANTIES("絆創膏", ARG)
	;	LOCALS = 絆創膏
	ELSE
		LOCALS = パンツ
	ENDIF
CASE "種類"
	;LOCALSに名称が入る
	SPLIT CSTR:ARG:パンツ, "/", LOCALS
CASEELSE
	;LOCALSに名称が入る
	SPLIT CSTR:ARG:パンツ, "/", LOCALS
	COLOR_ABOUT = %CHECK_CLOCOLOR(CSTR:ARG:パンツ)%

	IF CHECK_PANTIES("男物", ARG)
		LOCALS = \@ TALENT:ARG:オトコ == 0 ? 男物の # \@%LOCALS%
	;名称と色が被ってる場合は省略
	ELSEIF STRCOUNT(LOCALS, COLOR_ABOUT)
		RETURNF LOCALS
	;一部の柄物
	ELSEIF STRCOUNTS(CSTR:ARG:パンツ, "縞々", "水玉模様", "豹柄")
		SELECTCASE COLOR_ABOUT
		CASE ""
		CASE "赤", "青", "黒", "白"
			LOCALS = %COLOR_ABOUT%い%LOCALS%
		CASEELSE
			LOCALS = %COLOR_ABOUT%の%LOCALS%
		ENDSELECT
	ELSE
		LOCALS = %CLOCOLOR(CSTR:ARG:パンツ, "形容詞")%%LOCALS%
	ENDIF
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;ARG番のパンツの名称。着せ替えなどで表を作る際に使う
;-------------------------------------------------
@NAME_PANTIES(ARG)
#FUNCTIONS
VARSET LOCALS

SELECTCASE ARG
CASE 0
	LOCALS = ショーツ
CASE 1
	LOCALS = パンティ
CASE 2
	LOCALS = フルバックパンツ
CASE 3
	LOCALS = ローライズパンツ
CASE 4
	LOCALS = ローレグパンツ
CASE 5
	LOCALS = ハイレグパンツ
CASE 6
	LOCALS = Ｔバック
CASE 7
	LOCALS = Ｇストリング
CASE 8
	LOCALS = Ｃストリング
CASE 9
	LOCALS = ボーイレッグ
CASE 10
	LOCALS = オープンクロッチパンツ
CASE 11
	LOCALS = Ｏバック
CASE 12
	LOCALS = マイクロショーツ

CASE 15
	LOCALS = 縞々パンツ
CASE 16
	LOCALS = 水玉模様のパンツ
CASE 17
	LOCALS = 豹柄パンツ

CASE 20
	LOCALS = レースのパンツ
CASE 21
	LOCALS = シースルーパンツ
CASE 22
	LOCALS = フリルのパンツ
CASE 23
	LOCALS = 紐パン
CASE 24
	LOCALS = レザーパンツ
CASE 25
	LOCALS = 褌
CASE 26
	LOCALS = 貞操帯
CASE 27
	LOCALS = 前貼り
CASE 28
	LOCALS = 子供パンツ

CASE 30
	LOCALS = ビキニ
CASE 31
	LOCALS = マイクロビキニ
CASE 32
	LOCALS = ティアドロップビキニ

CASE 35
	LOCALS = トランクス
CASE 36
	LOCALS = ボクサーパンツ
CASE 37
	LOCALS = ハイレグブルマ

CASE 39
	LOCALS = Ｈな下着
ENDSELECT

RETURNF LOCALS


;-------------------------------------------------
;キャラARGにパンツARGSを着せた場合のCSTR処理
;名前/色/属性/…/属性/ というルールで記述される(一番左は名前、次は色で固定)
;後で属性を追加したい際には CSTR:パンツ += "Ｖ露出/" のように単純に追加すればよい。同じ属性が複数あっても問題ない
;-------------------------------------------------
@PALAM_PANTIES, ARGS, ARG
#DIM MEMO_TARGET
;追加で記述された属性を一時的に保管する
#DIMS STR_ADD

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

STR_ADD = %EXTSTR(ARGS, "/")%
ARGS = %EXTSTR(ARGS, "", "/")%

MEMO_TARGET = TARGET
TFLAG:自分の下着に着替えた = 0

SELECTCASE ARGS
CASE "", "ノーパン"
	CSTR:ARG:パンツ = 
;キャラ衣装
CASE "自分の下着"
	TARGET = ARG

	TRYCALLFORM CLOTHES_K{NO:TARGET}, "下着"

	TFLAG:自分の下着に着替えた = 1
	TARGET = MEMO_TARGET
CASE "しまった下着"
	VARSET LOCALS
	SPLIT CSTR:ARG:しまった下着, "_", LOCALS
	CSTR:ARG:パンツ = %LOCALS:0%
	CSTR:ARG:ブラ = %LOCALS:1%
	CSTR:ARG:しまった下着 = 

CASE "ショーツ"
	CSTR:ARG:パンツ = ショーツ/おとなしい色/シンプル/
CASE "パンティ"
	CSTR:ARG:パンツ = パンティ/おとなしい色/フロントリボン/
CASE "フルバックパンツ"
	CSTR:ARG:パンツ = フルバックパンツ/おとなしい色/
CASE "ローライズパンツ"
	CSTR:ARG:パンツ = ローライズパンツ/おとなしい色/
;本作ではローレグ＆ローライズの小さいパンツをローレグパンツとする。not子供パンツ
CASE "ローレグパンツ"
	CSTR:ARG:パンツ = ローレグパンツ/おとなしい色/
CASE "ハイレグパンツ"
	CSTR:ARG:パンツ = ハイレグパンツ/ランダムカラー/
CASE "Ｔバック"
	CSTR:ARG:パンツ = Ｔバック/ギャル/
CASE "Ｇストリング"
	CSTR:ARG:パンツ = Ｇストリング/ギャル/ハイレグ/Ｔバック/
CASE "Ｃストリング"
	CSTR:ARG:パンツ = Ｃストリング/ランダムカラー/Ｔバック/ずれ不可/
CASE "ボーイレッグ"
	CSTR:ARG:パンツ = ボーイレッグ/おとなしい色/
CASE "オープンクロッチパンツ"
	CSTR:ARG:パンツ = オープンクロッチパンツ/Ｈな色/Ｖ露出/Ａ露出/
CASE "Ｏバック"
	CSTR:ARG:パンツ = Ｏバック/ギャル/Ａ露出/ローレグ/
CASE "マイクロショーツ"
	CSTR:ARG:パンツ = マイクロショーツ/ランダムカラー/
CASE "ドロワーズ"
	CSTR:ARG:パンツ = ドロワーズ/白/ずれ不可/

CASE "縞々パンツ"
	CSTR:ARG:パンツ = 縞々パンツ/縞々色/
CASE "水玉模様のパンツ"
	CSTR:ARG:パンツ = 水玉模様のパンツ/水玉色/
CASE "水玉模様のＴバック"
	CSTR:ARG:パンツ = 水玉模様のＴバック/水玉色/
CASE "豹柄パンツ"
	CSTR:ARG:パンツ = 豹柄パンツ/豹柄/Ｔバック/
CASE "豹柄のＴバック"
	CSTR:ARG:パンツ = 豹柄のＴバック/豹柄/
CASE "肉球プリントパンツ"
	CSTR:ARG:パンツ = 肉球プリントパンツ/黒/紐/

CASE "レースのパンツ"
	CSTR:ARG:パンツ = レースのパンツ/Ｈな色/Ｔバック/シースルー/ランジェリー/
CASE "シースルーパンツ"
	CSTR:ARG:パンツ = シースルーパンツ/おとなしい色/ランジェリー/
CASE "フリルのパンツ"
	CSTR:ARG:パンツ = フリルのパンツ/おとなしい色/フロントリボン紐/ランジェリー/
CASE "紐パン"
	CSTR:ARG:パンツ = 紐パン/おとなしい色/
CASE "レザーパンツ"
	CSTR:ARG:パンツ = レザーパンツ/黒/ハイレグ/Ｔバック/
CASE "葉っぱパンツ"
	CSTR:ARG:パンツ = 葉っぱパンツ/緑色/ローレグ/

CASE "ビキニ"
	CSTR:ARG:パンツ = ビキニ/ランダムカラー/ハイレグ/紐/
CASE "白いビキニ"
	CSTR:ARG:パンツ = 白いビキニ/白/
CASE "黒いビキニ"
	CSTR:ARG:パンツ = 黒いビキニ/黒/Ｔバック/紐/
CASE "マイクロビキニ"
	CSTR:ARG:パンツ = マイクロビキニ/ランダムカラー/紐/
CASE "ティアドロップビキニ"
	CSTR:ARG:パンツ = ティアドロップビキニ/ランダムカラー/紐/Ｔバック/マイクロ/

CASE "メンズビキニ"
	CSTR:ARG:パンツ = メンズビキニ/黒/水着/
CASE "サーフパンツ"
	CSTR:ARG:パンツ = サーフパンツ/黒/水着/

CASE "褌"
	CSTR:ARG:パンツ = 褌/白/ハイレグ/Ｔバック/
CASE "貞操帯"
	CSTR:ARG:パンツ = 貞操帯/黒/レザー/ずれ不可/脱衣不可/
CASE "前貼り"
	CSTR:ARG:パンツ = 前貼り/白/Ａ露出/ずれ不可/
CASE "子供パンツ"
	CSTR:ARG:パンツ = 子供パンツ/子供パンツカラー/

CASE "トランクス"
	CSTR:ARG:パンツ = トランクス/男物の色/男物/
CASE "ボクサーパンツ"
	CSTR:ARG:パンツ = ボクサーパンツ/男物の色/男物/

CASE "ハイレグブルマ"
	CSTR:ARG:パンツ = ハイレグブルマ/%CLOCOLOR_MAIDFACE("ブルマ", "", ARG)%/
CASE "Ｈな下着"
	CSTR:ARG:パンツ = Ｈな下着/Ｈな色/

CASEELSE
	CSTR:ARG:パンツ = %ARGS%/ランダムカラー/
ENDSELECT

CSTR:ARG:パンツ = %MERGE_STR(CSTR:ARG:パンツ, STR_ADD)%


;-------------------------------------------------
;ARGの穿いているパンツの詳細
;-------------------------------------------------
@DETAIL_PANTIES(ARG, ARGS)
#FUNCTIONS
;種類名：下着かパンツかビキニかブルマ
#DIMS TYPE_PANTU
SIF ARG == 0 && TARGET
	ARG = TARGET

IF CHECK_PANTIES("ブルマ", ARG)
	TYPE_PANTU = ブルマ
ELSEIF CHECK_PANTIES("ビキニ", ARG)
	TYPE_PANTU = ビキニ
ELSE
	TYPE_PANTU = %TEXTR("パンツ/下着")%
ENDIF
VARSET LOCALS
SELECTCASE ARGS
;触っただけでパンツを類推する場合
CASE "感触"
	IF CHECK_PANTIES("Ｖ露出", ARG)
		IF CHECK_PANTIES("Ａ露出", ARG)
			LOCALS += "股間が前から後ろまで完全に露出している"
		ELSE
			LOCALS += "クロッチ部分に生地が無い"
		ENDIF
	ELSEIF CHECK_PANTIES("Ａ露出", ARG)
		LOCALS += "お尻の穴の周りだけ生地が無い"
	ELSEIF CHECK_PANTIES("スリット", ARG)
		LOCALS += "クロッチ部分にスリットが隠された"
	;マイクロ（Ｔバックでもある）
	ELSEIF CHECK_PANTIES("マイクロ", ARG)
		LOCALS += "布面積が異常に少ない"
	ELSEIF CHECK_PANTIES("ファスナー", ARG)
		LOCALS += "クロッチ部分にファスナーのついた"
	ELSEIF CHECK_PANTIES("紐パン", ARG)
		LOCALS += "サイドが紐で結ばれた"
	ELSEIF CHECK_PANTIES("レース", ARG)
		LOCALS += "レースで彩られた"
	ENDIF

	IF CHECK_PANTIES("前貼り", ARG)
		RETURNF "前貼り"
	ELSEIF CHECK_PANTIES("褌", ARG)
		RETURNF "褌"
	ELSEIF CHECK_PANTIES("ティアドロップ", ARG)
		LOCALS += "所謂ティアドロップの"
	ELSEIF CHECK_PANTIES("Ｃストリング", ARG)
		LOCALS += "Ｃストリングの"
	ELSEIF CHECK_PANTIES("Ｇストリング", ARG)
		LOCALS += "Ｇストリングの"
	ELSEIF CHECK_PANTIES("Ｔバック", ARG)
		LOCALS += "Ｔバックの"
	;ローorハイ
	ELSEIF CHECK_PANTIES("ローレグ", ARG)
		LOCALS += "ローレグの"
	ELSEIF CHECK_PANTIES("ハイレグ", ARG)
		LOCALS += "ハイレグの"
	ELSEIF LOCALS != ""
		LOCALS += "フルバックの"
	ENDIF
	SIF LOCALS == ""
		LOCALS = %TEXTR("スタンダードな/普通の/安定感のある")%フルバックの
	LOCALS = %LOCALS%%TYPE_PANTU%

CASEELSE
	;Ｈな下着
	IF CHECK_PANTIES("Ｈな下着", ARG) || CHECK_PANTIES("Ｖ露出", ARG) || CHECK_PANTIES("Ａ露出", ARG)
		SIF CHECK_PANTIES("レース", ARG)
			LOCALS += "レースで彩られた"

		IF CHECK_PANTIES("シースルー", ARG)
			LOCALS += "シースルー素材の"
		ELSEIF CHECK_PANTIES("シルク", ARG)
			LOCALS += "シルク製の"
		ENDIF

		IF CHECK_PANTIES("Ｖ露出", ARG)
			IF CHECK_PANTIES("Ａ露出", ARG)
				LOCALS += "股間が完全に露出している"
			ELSE
				LOCALS += "クロッチ部分に生地が無い"
			ENDIF
		ELSEIF CHECK_PANTIES("Ａ露出", ARG)
			LOCALS += "お尻の穴の周りだけ生地が無い"
		;マイクロ（Ｔバックでもある）
		ELSEIF CHECK_PANTIES("マイクロ", ARG)
			LOCALS += "布面積が異常に少ない"
		ELSEIF CHECK_PANTIES("ファスナー", ARG)
			LOCALS += "クロッチ部分にファスナーのついた"
		ELSEIF CHECK_PANTIES("スリット", ARG)
			LOCALS += "クロッチ部分にスリットが隠された"
		ENDIF

		SIF LOCALS == ""
			LOCALS = エロチックな

		IF CHECK_PANTIES("Ｇストリング", ARG)
			LOCALS += "Ｇストリング"
			TYPE_PANTU = 
		ELSEIF CHECK_PANTIES("Ｔバック", ARG)
			LOCALS += "Ｔバック"
			TYPE_PANTU = 
		;ローorハイ
		ELSEIF CHECK_PANTIES("ローレグ", ARG)
			LOCALS += "ローレグ"
		ELSEIF CHECK_PANTIES("ハイレグ", ARG)
			LOCALS += "ハイレグ"
		ENDIF
		;紐
		IF CHECK_PANTIES("紐", ARG)
			LOCALS += "紐"
			SIF CHECK_PANTIES("ビキニ", ARG) == 0
				TYPE_PANTU = パン
		ENDIF
		LOCALS = %LOCALS%%TYPE_PANTU%
	ELSEIF CHECK_PANTIES("マイクロ", ARG)
		IF RAND:2
			LOCALS = 小さな布と形容した方がいいような、物凄くきわどい\@ CHECK_PANTIES("ビキニ", ARG) ? ビキニ # パンツ \@
		ELSE
			LOCALS = 大事なところを全然隠せていない極小生地の\@ CHECK_PANTIES("ビキニ", ARG) ? 水着 # 下着 \@
		ENDIF
	ELSEIF CHECK_PANTIES("ビキニ", ARG)
		LOCALS = 大胆なビキニ
	ELSEIF CHECK_PANTIES("Ｃストリング", ARG)
		LOCALS = パンツと言って良いものか迷う逸品、Ｃストリング
	;前貼り
	ELSEIF CHECK_PANTIES("前貼り", ARG)
		LOCALS = 股間にぴったりと%TEXTR("フィットして/くっついて")%%TEXTR("大事な所/アソコ")%を隠している前貼り
	;ELSEIF CHECK_PANTIES("褌", ARG)

	;ELSEIF CHECK_PANTIES("貞操帯", ARG)

	ELSEIF CHECK_PANTIES("肉球プリントパンツ", ARG)
		LOCALS = 前面に小さな猫耳があしらわれたパンツ
	ELSEIF CHECK_PANTIES("縞々", ARG)
		IF RAND:2
			LOCALS = 股間の%TEXTR("陰影/ライン")%を%TEXTR("柔らかく/控えめに")%%TEXTR("浮かば/主張さ")%せる縞々%TEXTR("パンツ/の下着")%
		ELSE
			LOCALS = %TEXTR("お尻/股間")%のふくらみに沿って模様が歪んでいる縞パン
		ENDIF
	ELSEIF CHECK_PANTIES("水玉模様", ARG)
		LOCALS = 可愛らしい水玉模様の%TEXTR("パンツ/下着")%
	ELSEIF CHECK_PANTIES("豹柄", ARG)
		SELECTCASE AGE(ARG)
		CASE IS >= 4
			LOCALS = %TEXTR("セクシーな/エロチックな/大人の香り漂う")%
		CASE 3
			LOCALS = %TEXTR("セクシーな/挑発的な/ギャルギャルしい")%
		CASE 2
			LOCALS = %TEXTR("大人びた/可愛らしい/挑発的な/")%
		CASE 1
			LOCALS = %TEXTR("見た目とのアンバランスさがたまらない/可愛らしい/挑発的な")%
		ENDSELECT
		LOCALS += @"%PANTIES(ARG)%"
	ELSEIF CHECK_PANTIES("レースのパンツ", ARG)
		IF RAND:4 == 0
			LOCALS = セクシーな%CLOCOLOR(CSTR:ARG:パンツ, "形容詞")%レース生地の
		ELSEIF RAND:3 == 0
			LOCALS = 大人の香り漂う%CLOCOLOR(CSTR:ARG:パンツ, "形容詞")%レースの
		ELSEIF RAND:2
			LOCALS = 上品でお洒落なレース生地の
		ELSE
			LOCALS = 大人っぽくも可愛らしいレースの
		ENDIF
		LOCALS = %LOCALS%%TYPE_PANTU%
	ELSEIF CHECK_PANTIES("フリルのパンツ", ARG)
		LOCALS = %TEXTR("可愛らしい/清楚な")%フリル%TEXTR("で飾られた/付きの")%%TYPE_PANTU%
	ELSEIF CHECK_PANTIES("紐パン", ARG)
		IF RAND:2
			LOCALS = 脱がしやすそうな紐パン
		ELSE
			LOCALS = %TEXTR("両サイドの結び目/三角形の布地部分")%が目を引く紐パン
		ENDIF
	ELSEIF CHECK_PANTIES("Ｇストリング", ARG)
		LOCALS = お尻部分が紐になっている大胆なＧストリング
	ELSEIF CHECK_PANTIES("Ｔバック", ARG)
		LOCALS = お尻のラインが可愛いＴバック
	ELSEIF CHECK_PANTIES("ローライズ", ARG)
		LOCALS = 股上が浅いゴム履きのローライズ
	ELSEIF CHECK_PANTIES("シースルーパンツ", ARG)
		IF RAND:3 == 0
			LOCALS = %CALLNAME:ARG%の%TEXTR("下腹部/お尻")%をうっすらと透かしている下着
		ELSEIF RAND:2 == 0
			LOCALS = 大胆に透けている下着
		ELSE
			LOCALS = なんとも淫靡なシースルーのパンツ
		ENDIF
	ELSEIF CHECK_PANTIES("ハイレグブルマ", ARG)
		LOCALS = %TEXTR("柔らかな恥丘を際立たせる/丸見えの鼠径部が目を引く/お洒落な/股間の切れ込みが深い")%ハイレグブルマ
	ELSEIF CHECK_PANTIES("ハイレグパンツ", ARG)
		LOCALS = %TEXTR("柔らかな恥丘を際立たせる/丸見えの鼠径部が目を引く/お洒落な/股間の切れ込みが深い")%ハイレグパンツ
	ELSEIF CHECK_PANTIES("レザーパンツ", ARG)
		LOCALS = 股間にファスナーのついた、きわどい革製のハイレグパンツ
	ELSEIF CHECK_PANTIES("ローレグ", ARG)
		SELECTCASE COND("陰毛", ARG)
		CASE 1, 2
			LOCALS = 剥き卵の様な恥丘
		CASE 3
			LOCALS = ツルツルの恥丘
		CASE 4
			IF CONDS("陰毛", ARG) == "剃り跡"
				LOCALS = ジョリジョリの剃り跡
			ELSE
				LOCALS = 生えかけの陰毛
			ENDIF
		CASE 5
			IF CEVENTS("陰毛の名称", ARG) != "" && RAND:2
				LOCALS = %CEVENTS("陰毛の名称", ARG)%に剃り整えられた陰毛
			ELSE
				LOCALS = ばっちりお手入れされた陰毛
			ENDIF
		CASE IS >= 6
			LOCALS = 陰毛の茂み
		ENDSELECT
		IF LOCALS != ""
			IF RAND:3 == 0
				LOCALS = %LOCALS%を見せ付けるようなデザインの
			ELSEIF RAND:2
				IF AGE(ARG) >= 3
					LOCALS = %LOCALS%は勿論、それどころかクリトリスの鞘が見えている
				ELSE
					LOCALS = %LOCALS%は勿論、それどころかスジの上端が見えかけている
				ENDIF
			ELSE
				LOCALS = %LOCALS%\@ ABL:ARG:露出癖 >= 3 ? を見せ付けるのに最適な形状の # がよく見える \@
			ENDIF
		ELSE
			LOCALS = 股上が異常に浅い
		ENDIF
		LOCALS = %LOCALS%ローレグパンツ
	ELSEIF CHECK_PANTIES("フロントリボン", ARG)
		LOCALS = %TEXTR("ワンポイントのリボンがお洒落な/前面に小さなリボンの付いた/可愛らしいリボン付きの")%%CLOCOLOR(CSTR:ARG:パンツ, "形容詞")%%TYPE_PANTU%
	ELSEIF CHECK_PANTIES("シンプル", ARG)
		LOCALS = 飾り気の少ない%CLOCOLOR(CSTR:ARG:パンツ, "形容詞")%%TYPE_PANTU%
	ELSE
		LOCALS = %PANTIES(ARG)%
	ENDIF
ENDSELECT

RETURNF LOCALS




;-------------------------------------------------
;ARGがARGSのパンツを好きor嫌いかチェックする
;-------------------------------------------------
@FAV_PANTIES(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
RETURNF STRCOUNT(CSTR:ARG:好きなパンツ, ARGS)

@DISLIKE_PANTIES(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
RETURNF STRCOUNT(CSTR:ARG:嫌いなパンツ, ARGS)

;-------------------------------------------------
;ARGのパンツが条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_PANTIES(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF CSTR:ARG:パンツ == ""
	RETURNF 0

SELECTCASE ARGS
CASE "はみパン可能"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ハイレグ", "Ｔバック", "Ｃストリング", "マイクロ", "前貼り", "貞操帯")
		RETURNF 0
	RETURNF 1
CASE "半脱ぎ時太もも保持可能"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "Ｃストリング", "褌", "貞操帯", "前貼り")
		RETURNF 0
	RETURNF 1
CASE "ワイドクロッチ"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ボクサーパンツ", "ブリーフ", "トランクス", "ボーイレッグ")
		RETURNF 1
;主に"アソコのチラ見せ撮影"の判定で使う
CASE "めくり可能"
	;男物パンツは股間の布地が横幅あり過ぎてめくれない
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ボクサーパンツ", "ブリーフ", "トランクス", "ボーイレッグ")
		RETURNF 0
	;めくれなさそうなもの
	SIF STRCOUNTS(CSTR:ARG:パンツ, "Ｃストリング", "褌", "貞操帯", "前貼り", "Ｖ露出")
		RETURNF 0
	RETURNF 1
;ずれ不可でなく、ずらしていない
CASE "ずらし可", "ずらせる"
	IF STRCOUNTS(CSTR:ARG:パンツ, "ファスナー")
		SIF STRCOUNTS(CSTR:ARG:パンツ, "ファスナー開")
			RETURNF 0
		SIF STRCOUNTS(CSTR:ARG:パンツ, "ファスナー閉")
			RETURNF 1
	ENDIF
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ずらした")
		RETURNF 0
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ずれ不可")
		RETURNF 0
	RETURNF 1
;ファスナーがあり、あけていない
CASE "ファスナー閉"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ファスナー開")
		RETURNF 0
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ファスナー")
		RETURNF 1
CASE "ＶＡ露出"
	SIF STRCOUNT(CSTR:ARG:パンツ, "Ｖ露出") && STRCOUNT(CSTR:ARG:パンツ, "Ａ露出")
		RETURNF 1
CASE "Ａのみ露出"
	SIF STRCOUNT(CSTR:ARG:パンツ, "Ｖ露出") == 0 && STRCOUNT(CSTR:ARG:パンツ, "Ａ露出")
		RETURNF 1
CASE "クロッチ", "Ｖクロッチ", "尿クロッチ"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ファスナー開", "ずらした", "破いた", "Ｖ露出", "スリット")
		RETURNF 0
	RETURNF 1
CASE "Ａクロッチ"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ファスナー開", "ずらした", "破いた", "Ａ露出", "スリット")
		RETURNF 0
	RETURNF 1
CASE "食い込み"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ずらした", "ファスナー開", "Ｖ露出", "スリット", "破いた")
		RETURNF 0
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ハイレグ", "ティアドロップ", "マイクロ")
		RETURNF 1
CASE "Ｈ"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "Ｈ", "シースルー", "ハイレグ", "Ｔバック", "ファスナー", "スリット", "オープン", "Ｏバック")
		RETURNF 1
CASE "セクシー"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "Ｈ", "シースルー", "ハイレグ", "Ｔバック", "ファスナー", "スリット")
		RETURNF 1
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ストリング", "マイクロ", "Ｖ露出", "Ａ露出")
		RETURNF 1
CASE "マイクロ"
	SIF STRCOUNT(CSTR:ARG:パンツ, "マイクロ")
		RETURNF 1
	SIF STRCOUNT(CSTR:ARG:パンツ, "ハイレグ") && STRCOUNT(CSTR:ARG:パンツ, "Ｔバック") && STRCOUNT(CSTR:ARG:パンツ, "ローレグ")
		RETURNF 1
CASE "蒸れ"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ずらした", "ファスナー開", "Ｖ露出", "スリット", "破いた")
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:パンツ, "蒸れ")
		RETURNF 1
CASE "染み"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ずらした", "ファスナー開", "Ｖ露出", "スリット", "破いた", "通気性無し")
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:パンツ, "濡れ")
		RETURNF 1
CASE "男物"
	SIF STRCOUNTS(CSTR:ARG:パンツ, "ボクサーパンツ", "ブリーフ", "トランクス")
		RETURNF 1
CASEELSE
	SIF STRCOUNT(CSTR:ARG:パンツ, ARGS)
		RETURNF 1
ENDSELECT
RETURNF 0
