;-------------------------------------------------
;ARGSが色を表す文字列かを判定する。色の種類を増やしたらここにも追加する事
;各色についての名称はここに書いたものに統一する。紺色、緑色には注意
;-------------------------------------------------
@IS_CLOCOLOR(ARGS)
#FUNCTION
SELECTCASE ARGS
CASE "紅白", "緑と白", "青と白", "黄色と白", "黒と白", "ピンク", "オレンジ"
	RETURNF 1
CASE "いちご柄", "くま柄", "さかな柄", "ねこ柄", "ひよこ柄", "りぼん柄", "水玉模様"
	RETURNF 1
CASE "黄色", "橙色", "黒", "紫", "濃紫", "青", "紺色", "薄青色", "真紅", "赤", "薄桃色", "桃", "白", "純白", "緑色", "薄緑", "灰色", "水色", "茶色", "焦茶色", "ベージュ"
	RETURNF 1
CASE "縞々色", "水玉色", "豹柄", "子供パンツカラー", "おとなしい色", "Ｈな色", "ランダムカラー", "ギャル", "男物の色"
	RETURNF 1
;色のあるなしがバグの温床なので全てＯＫとして、後で本体側が使いやすい方を自動選択させる
CASE "オレンジ色", "黒色", "紫色", "青色", "赤色", "桃色", "白色", "薄緑色", "縞々色", "水玉色", "ギャル色"
	RETURNF 1
CASE "黄", "橙", "紺", "薄青", "薄桃", "緑", "灰", "水", "茶", "焦茶"
	RETURNF 1
;特殊
CASE "藤の花"
	RETURNF 1
CASEELSE
	RETURNF 0
ENDSELECT

@TEXT_ALLCOLOR()
#FUNCTIONS
LOCALS = 紅白/緑と白/青と白/黄色と白/黒と白/ピンク/オレンジ/
LOCALS += "いちご柄/くま柄/さかな柄/ねこ柄/ひよこ柄/りぼん柄/水玉模様/"
LOCALS += "黄色/橙色/黒/紫/薄青色/青/紺色/真紅/赤/薄桃色/純白/白/緑色/薄緑/灰色/水色/茶色/焦茶色/ベージュ/藤の花/"
;これは色に変換する前の指示語なので除く
;LOCALS += "縞々色/水玉色/豹柄/子供パンツカラー/おとなしい色/Ｈな色/ランダムカラー/ギャル/男物の色/"
RETURNF LOCALS

;-------------------------------------------------
;衣装文字列ARGSにARGS:1を、以下のルールを守りながら混ぜる
;・色に関してはARGS:1の要素で上書きさせる
;・共通属性追加済/は末尾に付け直す
;-------------------------------------------------
@MERGE_STR(ARGS, ARGS:1)
#FUNCTIONS
#DIM LCOUNT
#DIMS STR_ADD

SIF ARGS:1 == ""
	RETURNF ARGS

STR_ADD = 
;ARGS:1の色文字列を使える状態に変換しながら、ARGSに混ぜる文字列STR_ADDに作り変える
VARSET LOCALS
SPLIT ARGS:1, "/", LOCALS
FOR LCOUNT, 0, 10
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		;色文字列は使える状態に変換
		SIF IS_CLOCOLOR(LOCALS:LCOUNT)
			LOCALS:LCOUNT = %SET_CLOCOLOR(LOCALS:LCOUNT)%
		STR_ADD += @"%LOCALS:LCOUNT%\/"
	ENDSELECT
NEXT

;STR_ADDが色に関する場合は、ARGSから色文字列を抜く
SIF STR_ADD != RESET_CLOCOLOR(STR_ADD)
	ARGS = %RESET_CLOCOLOR(ARGS)%

;共通属性追加済みを末尾に付け直す処理
IF STRCOUNT(ARGS, "共通属性追加済")
	ARGS = %EXTSTR(ARGS, "", "共通属性追加済/")%
	STR_ADD += "共通属性追加済/"
ENDIF

ARGS += @"%STR_ADD%"

RETURNF ARGS

;-------------------------------------------------
;衣装文字列ARGSから色に関するものを消し去る
;色に関わる文字列かどうかは@IS_CLOCOLORで判定する
;-------------------------------------------------
@RESET_CLOCOLOR(ARGS)
#FUNCTIONS
#DIM LCOUNT

VARSET LOCALS
SPLIT ARGS, "/", LOCALS

ARGS = 
FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF IS_CLOCOLOR(LOCALS:LCOUNT)
			CONTINUE
		ARGS += @"%LOCALS:LCOUNT%\/"
	ENDSELECT
NEXT
RETURNF ARGS

;-------------------------------------------------
;衣装文字列ARGSから色に関するものを返す
;色に関わる文字列かどうかは@IS_CLOCOLORで判定する
;-------------------------------------------------
@CHECK_CLOCOLOR(ARGS)
#FUNCTIONS
#DIM LCOUNT

;文字列に / を含まない場合はアクセサリなので特殊な判定をする
SIF STRCOUNT(ARGS, "/") == 0
	RETURNF CHECK_OTHERCOLOR(ARGS)

VARSET LOCALS
SPLIT ARGS, "/", LOCALS

FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF IS_CLOCOLOR(LOCALS:LCOUNT)
			RETURNF LOCALS:LCOUNT
	ENDSELECT
NEXT
RETURNF ""

;-------------------------------------------------
;衣装文字列ARGSが色に関するものを含んでいるだけでそれを返す。主にアクセサリ用
;-------------------------------------------------
@CHECK_OTHERCOLOR(ARGS)
#FUNCTIONS
#DIM LCOUNT

VARSET LOCALS
SPLIT TEXT_ALLCOLOR(), "/", LOCALS

FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF STRCOUNT(ARGS, LOCALS:LCOUNT)
			RETURNF LOCALS:LCOUNT
	ENDSELECT
NEXT
RETURNF ""

;-------------------------------------------------
;衣装文字列ARGSから色を返させる。ARGS:1に"形容詞"等を入れる事で変化を出せる
;-------------------------------------------------
@CLOCOLOR(ARGS, ARGS:1)
#FUNCTIONS
#DIMS COLOR_ABOUT
#DIMS COLOR_PATTERN

COLOR_PATTERN = 
;柄物チェック
IF STRCOUNT(ARGS, "縞々パンツ")
	COLOR_PATTERN = 縞々
ELSEIF STRCOUNT(ARGS, "水玉模様")
	COLOR_PATTERN = 水玉模様
ELSEIF STRCOUNT(ARGS, "豹柄")
	COLOR_PATTERN = 豹柄
ENDIF

COLOR_ABOUT = %CHECK_CLOCOLOR(ARGS)%

VARSET LOCALS
SELECTCASE ARGS:1
CASE "色のみ"
	LOCALS = %COLOR_ABOUT%
CASE "形容詞"
	SIF COLOR_PATTERN != ""
		COLOR_ABOUT = %COLOR_PATTERN%

	SELECTCASE COLOR_ABOUT
	CASE ""
	CASE "赤", "青", "黒", "白"
		LOCALS = %COLOR_ABOUT%い
	CASEELSE
		LOCALS = %COLOR_ABOUT%の
	ENDSELECT
CASEELSE
	SIF COLOR_PATTERN != ""
		COLOR_ABOUT = %COLOR_PATTERN%
	LOCALS = %COLOR_ABOUT%
ENDSELECT
RETURNF LOCALS


;-------------------------------------------------
;ARGS:1には衣装の文字列が入っている事があり参照できる（水玉模様のレースのパンツ、等が起きないように参照するため）
;-------------------------------------------------
@SET_CLOCOLOR(ARGS, ARGS:1)
#FUNCTIONS
SIF SAVESTR:下着の指定色 != ""
	RETURNF SAVESTR:下着の指定色

;まずは間違いが起きやすい色(緑色or緑、など)の変換
SELECTCASE ARGS
CASE "オレンジ色", "黒色", "紫色", "青色", "赤色", "白色", "薄緑色", "縞々色", "水玉色", "ギャル色"
	ARGS = %SUBSTRINGU(ARGS, 0, STRLENSU(ARGS) - 1)%
CASE "黄", "橙", "紺", "薄青", "薄桃", "緑", "灰", "水", "茶"
	ARGS = %ARGS%色
ENDSELECT

SELECTCASE ARGS
;絶対に変更が起きないもの
CASE "水玉模様"
	RETURNF ARGS
CASE "縞々色"
	LOCALS = %TEXTR("紅白/緑と白/青と白/黄色と白/黒と白")%
CASE "水玉色"
	LOCALS = %TEXTR("ピンク/薄緑/オレンジ/青/黒")%
CASE "おとなしい色"
	LOCALS = %TEXTR("薄桃色/薄緑/橙色/薄青色/白/純白")%
CASE "ギャル"
	LOCALS = %TEXTR("豹柄/黒/紫/真紅/赤")%
CASE "Ｈな色"
	LOCALS = %TEXTR("黒/紫/真紅/赤/純白")%
CASE "男物の色"
	LOCALS = %TEXTR("黒/紺色")%
CASE "子供パンツカラー"
	LOCALS = %TEXTR("白/いちご柄/くま柄/さかな柄/ねこ柄/ひよこ柄/りぼん柄/水玉模様/緑色")%
CASE "ランダムカラー"
	LOCALS = %TEXTR("黄色/橙色/黒/紫/青/紺色/薄青色/真紅/赤/薄桃色/白/純白/緑色/薄緑")%
CASEELSE
	LOCALS = %ARGS%
ENDSELECT
;大人の事情により色制限がかかるものの管理。ARGS:1 == ""ならLOCALSが返る
IF STRCOUNTS(ARGS:1, "タンクトップ")
	LOCALS = %TEXTR("灰色/青/緑色/白")%
ELSEIF STRCOUNTS(ARGS:1, "マイクロショーツ")
	SELECTCASE LOCALS
	CASE "黒", "紫", "紺色", "青", "真紅", "赤", "白", "純白"
	CASEELSE
		LOCALS = %TEXTR("黒/紫/真紅/赤/青/紺色/白/純白")%
	ENDSELECT
ELSEIF STRCOUNTS(ARGS:1, "Ｇストリング", "マイクロビキニ", "ティアドロップ")
	SELECTCASE LOCALS
	CASE "黒", "紫", "紺色", "青", "真紅", "赤", "白", "純白", "豹柄"
	CASEELSE
		LOCALS = %TEXTR("黒/紫/真紅/赤/青/紺色/白/純白/豹柄/豹柄")%
	ENDSELECT
ELSEIF STRCOUNT(ARGS:1, "褌")
	SELECTCASE LOCALS
	CASE "黒", "真紅", "赤", "薄桃色", "桃", "白"
	CASEELSE
		LOCALS = %TEXTR("黒/真紅/赤/薄桃色/白/純白")%
	ENDSELECT
ELSEIF STRCOUNT(ARGS:1, "Ｔバック")
	IF STRCOUNT(ARGS:1, "水玉")
		SELECTCASE LOCALS
		CASE "ピンク", "黒"
		CASEELSE
			LOCALS = %TEXTR("ピンク/黄色/黒/水色")%
		ENDSELECT
	ELSE
		SELECTCASE LOCALS
		CASE "黄色", "黒", "紫", "薄青色", "青", "真紅", "赤", "白", "純白", "緑色", "薄緑", "豹柄"
		CASEELSE
			LOCALS = %TEXTR("黄色/黒/紫/真紅/水色/青/赤/白/薄緑/緑色/灰色/豹柄/豹柄")%
		ENDSELECT
	ENDIF
ELSEIF STRCOUNTS(ARGS:1, "ローライズパンツ", "フリル", "ボーイレッグ")
	SELECTCASE LOCALS
	CASE "黄色", "黒", "紫", "薄青色", "青", "薄桃色", "白", "純白", "緑色", "薄緑"
	CASEELSE
		LOCALS = %TEXTR("黒/紫/薄桃色/緑色/青/黄色/薄緑/薄青色/白/純白")%
	ENDSELECT
ELSEIF STRCOUNT(ARGS:1, "ローレグ")
	SELECTCASE LOCALS
	CASE "黄色", "黒", "紫", "薄青色", "真紅", "赤", "白", "純白", "緑色", "薄緑"
	CASEELSE
		LOCALS = %TEXTR("黄色/黒/紫/薄青色/真紅/赤/緑色/薄緑/白/純白")%
	ENDSELECT
ENDIF

RETURNF LOCALS

;-------------------------------------------------
;キャラARGのパンツの色をARGSにし、その後ブラの色をパンツに合わせる
;-------------------------------------------------
@EQ_CLOCOLOR, ARG, ARGS
#DIM LCOUNT
SIF ARG <= 0 && TARGET > 0
	ARG = TARGET
;パンツの色をARGSに変える
IF EQUIP:ARG:パンツ && ARGS != ""
	CSTR:ARG:パンツ = %RESET_CLOCOLOR(CSTR:ARG:パンツ)%%ARGS%/
	EQUIP:ARG:パンツ = STRLENS(CSTR:ARG:パンツ)
ENDIF
;ブラの色をパンツに合わせる
IF EQUIP:ARG:ブラ && (EQUIP:ARG:パンツ || ARGS != "")
	IF ARGS != ""
		CSTR:ARG:ブラ = %RESET_CLOCOLOR(CSTR:ARG:ブラ)%
		CSTR:ARG:ブラ += @"%ARGS%/"
	ELSEIF EQUIP:ARG:パンツ && CHECK_PANTIES("ブルマ", ARG) == 0
		;パンツが縞々や水玉や豹柄の場合には、ブラジャーでない場合には柄を合わせない
		IF STRCOUNTS(CSTR:ARG:パンツ, "縞々", "水玉模様", "豹柄") && CHECK_BRA("ブラ", ARG) == 0
			RETURN 0
		ELSE
			CSTR:ARG:ブラ = %RESET_CLOCOLOR(CSTR:ARG:ブラ)%
			CSTR:ARG:ブラ += @"%CHECK_CLOCOLOR(CSTR:ARG:パンツ)%/"
		ENDIF
	ENDIF
	EQUIP:ARG:ブラ = STRLENS(CSTR:ARG:ブラ)
ENDIF


;-------------------------------------------------
;ARGが服装ARGSを着る際にどの色を着るかを決める
;ARGS:1に色が入っている場合にはそれを優先する
;-------------------------------------------------
@CLOCOLOR_MAIDFACE(ARGS, ARGS:1, ARG)
#FUNCTIONS
#DIM LCOUNT
;その衣装で使うことが出来る色を/区切りで記述して、CSTR:好きな色 に対応できるようにする
#DIMS COLOR_USABLE
#DIMS COLOR_CHOICES
#DIMS COLOR_DEFO

COLOR_DEFO = 
COLOR_CHOICES = 
COLOR_USABLE = 

SELECTCASE ARGS
;萌え袖グループ
CASE "カーディガン", "肩出しジャケット"
	COLOR_USABLE = 橙/灰色/赤/青/緑色/濃紫/桃/白/黒

CASE "Ｔシャツ"
	COLOR_USABLE = 黄色/白/黒/赤/緑色/青/紫/桃/橙
CASE "フリルサスペンダー"
	COLOR_USABLE = 白/黒/桃
CASE "サスペンダー"
	COLOR_USABLE = 黒/白/茶色/赤/青/焦茶色
CASE "ダッフルコート"
	COLOR_USABLE = 茶色/白/青/黒/焦茶色
CASE "コート"
	COLOR_USABLE = 茶色/赤/白
CASE "パジャマ"
	COLOR_USABLE = 薄緑/白/紺/桃/水色
CASE "パーカー"
	COLOR_USABLE = 黒/灰色/白/青/赤
CASE "どてら"
	COLOR_USABLE = 茶色/灰色/赤/緑色/紺色
CASE "ゴスロリドレス"
	COLOR_USABLE = 黒/赤/白
CASE "サマードレス"
	COLOR_USABLE = 黒/緑/赤/白
CASE "チュニック"
	COLOR_USABLE = 黄色/紺色
CASE "執事服"
	COLOR_USABLE = 黒/灰色/緑色
CASE "メイド服"
	COLOR_USABLE = 青/黒/赤/緑色/桃/水色
CASE "胸開きチャイナドレス"
	COLOR_USABLE = 黒/赤/青/緑色/白
CASE "チャイナドレス"
	COLOR_USABLE = 赤/青/黒/緑色
CASE "ネクタイブラウス"
	COLOR_USABLE = 赤/青/黒
CASE "リボンブラウス"
	COLOR_USABLE = 赤/青/黒/緑色
CASE "フリルブラウス"
	COLOR_USABLE = 黒/白
CASE "シャツジャケット", "前開きシャツ"
	COLOR_USABLE = 白/黒/緑色/紺色
CASE "セーラー", "ノースリーブセーラー"
	IF CHECK_SHIRT("セーラー夏服", ARG)
		COLOR_USABLE = 青/白
	ELSE
		COLOR_USABLE = 青/紺色/白/黒/赤/灰色
	ENDIF
CASE "ブレザー"
	COLOR_USABLE = 灰色/青/赤/茶色/緑色/黒
CASE "ベスト"
	COLOR_USABLE = 茶色/青/赤/灰色/緑色/黒
CASE "ジャージ"
	COLOR_USABLE = 黒/青/赤/緑色
CASE "詰襟ジャージ"
	COLOR_USABLE = 黄色/青/赤/白/緑色/橙
CASE "タンクトップ"
	COLOR_USABLE = 灰色/白/青/緑色
CASE "キャミソール"
	COLOR_USABLE = 黒/青/赤/白/緑色/橙
CASE "オフショルダー"
	COLOR_USABLE = ベージュ/黒/白/灰色
CASE "ニットセーター"
	COLOR_USABLE = 茶色/緑色/白/赤/紺色/水色
CASE "縦セーター"
	COLOR_USABLE = 茶色/灰色/紺色/赤/茶色/緑色/黒/紫
CASE "振袖"
	COLOR_USABLE = 赤/黒/紫/青/緑色
CASE "浴衣"
	COLOR_USABLE = 黄色/黒/青/赤/緑色
CASE "和服"
	COLOR_USABLE = 黒/青/赤/白/緑色/橙
CASE "ベレー帽"
	COLOR_USABLE = 赤/白/緑色/茶色
CASE "キャスケット帽"
	COLOR_USABLE = 茶色/黒/灰色/白/赤/青/緑色
CASE "競泳水着"
	COLOR_USABLE = 黒/青/赤/緑色
CASE "ベビードール"
	COLOR_USABLE = 黒/赤/緑色
CASE "ネグリジェ"
	COLOR_USABLE = 白/黒/桃
CASE "ボンデージ"
	COLOR_USABLE = 黒/赤
CASE "両側リボン", "片側リボン"
	COLOR_USABLE = 赤/白/黄/青/緑
CASE "片側リボン２", "片側リボン２お団子"
	COLOR_USABLE = 白/黒/桃
CASE "両側リボンお団子", "片側リボンお団子"
	COLOR_USABLE = 白/黒
CASE "ブルマ"
	COLOR_USABLE = 赤/紺色/水色/緑色
CASE "バニースーツ"
	COLOR_USABLE = 赤/黒/青/紫/白
ENDSELECT

SIF ARGS:1 != "" && STRCOUNT(COLOR_USABLE, ARGS:1)
	RETURNF ARGS:1

VARSET LOCALS
SPLIT COLOR_USABLE, "/", LOCALS
;各衣裳のデフォカラーをここで決めておく
COLOR_DEFO = %LOCALS:0%

FOR LCOUNT, 0, 10
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF STRCOUNT(CSTR:ARG:好きな色, LOCALS:LCOUNT) == 0
			CONTINUE

		COLOR_CHOICES = %LOCALS:LCOUNT%
		BREAK
	ENDSELECT
NEXT

SIF COLOR_CHOICES != ""
	RETURNF COLOR_CHOICES

;学年や立場などでの自動決定があるならここに書く
SELECTCASE ARGS
CASE "セーラー"
	SIF CHECK_SHIRT("セーラー夏服", ARG)
		RETURNF "白"
	SELECTCASE CSTR:ARG:学年
	CASE "初等部", "中等部"
		RETURNF "紺色"
	CASEELSE
		RETURNF "青"
	ENDSELECT
CASE "ブレザー"
	IF COND("教員", ARG)
		RETURNF "赤"
	ELSEIF CHECK_SHIRT("男物", ARG)
		RETURNF "灰色"
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			RETURNF "青"
		CASEELSE
			RETURNF "茶色"
		ENDSELECT
	ENDIF
CASE "ネクタイブラウス"
	IF COND("教員", ARG)
		RETURNF "青"
	ELSEIF CHECK_SHIRT("男物", ARG)
		RETURNF "黒"
	ELSE
		RETURNF "赤"
	ENDIF
CASE "リボンブラウス"
	IF COND("教員", ARG)
		RETURNF "青"
	ELSEIF CHECK_SHIRT("男物", ARG)
		RETURNF "黒"
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			RETURNF "緑色"
		CASEELSE
			RETURNF "赤"
		ENDSELECT
	ENDIF
CASE "ジャージ"
	IF COND("教員", ARG)
		IF STRCOUNT(CSTR:ARG:学年, "体育")
			RETURNF "緑色"
		ELSE
			RETURNF "黒"
		ENDIF
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部"
			RETURNF "青"
		CASE "中等部"
			RETURNF "青"
		CASE "１年生"
			RETURNF "緑色"
		CASE "２年生"
			RETURNF "赤"
		CASE "３年生"
			RETURNF "黒"
		CASEELSE
			RETURNF "赤"
		ENDSELECT
	ENDIF
CASE "詰襟ジャージ"
	IF COND("教員", ARG)
		RETURNF "橙"
	ELSEIF CHECK_SHIRT("男物", ARG)
		RETURNF "黄色"
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			RETURNF "緑色"
		CASEELSE
			RETURNF "赤"
		ENDSELECT
	ENDIF
CASE "ブルマ"
	IF COND("教員", ARG)
		IF STRCOUNT(CSTR:ARG:学年, "体育")
			RETURNF "紺色"
		ELSE
			RETURNF "赤"
		ENDIF
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部"
			RETURNF "水色"
		CASE "中等部"
			RETURNF "緑色"
		CASE "１年生"
			RETURNF "紺色"
		CASE "２年生"
			RETURNF "紺色"
		CASE "３年生"
			RETURNF "紺色"
		CASEELSE
			RETURNF "赤"
		ENDSELECT
	ENDIF
CASE "スパッツ"
	SELECTCASE CSTR:ARG:学年
	CASE "初等部", "中等部", "１年生", "２年生", "３年生"
		RETURNF ""
	CASEELSE
		RETURNF "柄"
	ENDSELECT
CASE "競泳水着"
	IF COND("教員", ARG)
		IF STRCOUNT(CSTR:ARG:学年, "体育")
			RETURNF "赤"
		ELSE
			RETURNF "黒"
		ENDIF
	ELSE
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			RETURNF "赤"
		CASE "１年生", "２年生", "３年生"
			RETURNF "青"
		CASEELSE
			RETURNF "黒"
		ENDSELECT
	ENDIF
CASE "チャイナドレス"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "黒"
		RETURNF "赤"
	CASEELSE
		IF COND("教員", ARG)
			RETURNF "黒"
		ELSE
			RETURNF "赤"
		ENDIF
	ENDSELECT
CASE "バニースーツ"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "黒"
		RETURNF "赤"
	CASE "金"
		RETURNF "青"
	CASEELSE
		IF COND("教員", ARG)
			RETURNF "黒"
		ELSE
			RETURNF "赤"
		ENDIF
	ENDSELECT
CASE "振袖"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "黒"
		SELECTCASE NORM(1315, ARG)%4
		CASE 0
			RETURNF "紫"
		CASE 1
			RETURNF "青"
		CASE 2
			RETURNF "赤"
		CASE 3
			RETURNF "緑色"
		ENDSELECT
	CASE "赤"
		SELECTCASE NORM(1315, ARG)%4
		CASE 0
			RETURNF "黒"
		CASE 1
			RETURNF "紫"
		CASE 2
			RETURNF "青"
		CASE 3
			RETURNF "緑色"
		ENDSELECT
	CASEELSE
		SELECTCASE NORM(1315, ARG)%5
		CASE 0
			RETURNF "黒"
		CASE 1
			RETURNF "紫"
		CASE 2
			RETURNF "青"
		CASE 3
			RETURNF "赤"
		CASE 4
			RETURNF "緑色"
		ENDSELECT
	ENDSELECT
CASE "浴衣"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "金"
		SELECTCASE NORM(1316, ARG)%4
		CASE 0
			RETURNF "黒"
		CASE 1
			RETURNF "赤"
		CASE 2
			RETURNF "緑色"
		CASE 3
			RETURNF "青"
		ENDSELECT
	CASEELSE
		SELECTCASE NORM(1316, ARG)%5
		CASE 0
			RETURNF "黄色"
		CASE 1
			RETURNF "黒"
		CASE 2
			RETURNF "赤"
		CASE 3
			RETURNF "緑色"
		CASE 4
			RETURNF "青"
		ENDSELECT
	ENDSELECT
CASE "和服"
	SELECTCASE NORM(1317, ARG)%5
	CASE 0
		RETURNF "黒"
	CASE 1
		RETURNF "青"
	CASE 2
		RETURNF "赤"
	CASE 3
		RETURNF "白"
	CASE 4
		RETURNF "緑色"
	ENDSELECT
CASE "ベレー帽"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "青"
		RETURNF "茶色"
	CASE "黒", "銀", "緑"
		RETURNF "赤"
	CASE "金", "赤", "茶", "桃"
		RETURNF "緑色"
	CASEELSE
		RETURNF "白"
	ENDSELECT
CASE "両側リボン", "片側リボン"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "桃", "紫"
		RETURNF "白"
	CASE "青", "緑"
		RETURNF "黄"
	CASE "金"
		RETURNF "青"
	CASE "赤"
		RETURNF "緑"
	CASEELSE
		RETURNF "赤"
	ENDSELECT
CASE "片側リボン２", "片側リボン２お団子"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "赤", "桃", "紫", "黒", "茶", "真鍮", "焦茶", "紺"
		RETURNF "白"
	CASE "白"
		RETURNF "黒"
	CASEELSE
		RETURNF "桃"
	ENDSELECT
CASE "両側リボンお団子", "片側リボンお団子"
	SELECTCASE CONDS("髪色の種類", ARG)
	CASE "赤", "桃", "紫", "黒", "茶"
		RETURNF "白"
	CASEELSE
		RETURNF "黒"
	ENDSELECT
ENDSELECT

;まだ決まっていない場合はデフォカラーを採用
RETURNF COLOR_DEFO


