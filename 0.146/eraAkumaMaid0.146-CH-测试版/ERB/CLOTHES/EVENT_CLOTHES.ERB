;-------------------------------------------------
;パンツコレクション
;ARGのパンツARGSをコレクションする
;-------------------------------------------------
@COLLECT_PANTU, ARGS, ARG
SIF ARG == 0 && TARGET > 0
	ARG = TARGET

;現在のところは重複は無しとする
SIF CPANTU(ARGS, ARG)
	RETURN 0

SIF CSTR:ARG:胖次收藏 == ""
	CSTR:ARG:胖次收藏 = \/

CSTR:ARG:胖次收藏 = %CSTR:ARG:胖次收藏%%ARGS%\/

;-------------------------------------------------
;ARGからパンツARGSをコレクションしたか判定する
;-------------------------------------------------
@CPANTU(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET > 0
	ARG = TARGET

LOCALS = \/%ARGS%\/

RETURNF STRCOUNT(CSTR:ARG:胖次收藏, LOCALS)


;-------------------------------------------------
;パンツチェック時のテキスト
;ARGSは何の下から出てきたか部分
;電源ボックス使用中の時にはそれの描写も
;-------------------------------------------------
@TEXT_PANTU_CHECK, ARGS, ARGS:1
SELECTCASE ARGS:1
CASE "外套露出"
	CALL PRINT_CUTIN, "外套", TARGET
CASE "贴身制服露出"
CASEELSE
	CALL PRINT_CUTIN, "胖次", TARGET
ENDSELECT
;すでにパンツ知ってる場合
IF COND("胖次形状確認")
	IF ARGS:1 == "连褲襪露出" && EQUIP:胖次
		PRINTFORMW 隔著半透明的%OUTER(TARGET)%能清楚看見的%PANTIES(TARGET)%顯得格外醒目……
	ELSEIF ARGS:1 == "外套露出"
 		IF CHECK_OUTER("長褲") == 0
			PRINTFORMW 包裹著股間的過分合身的%OUTER(TARGET, "略")%顯得格外醒目……
		ELSE
			PRINTFORMW 借着难得的机会仔细端详了被%OUTER(TARGET, "略")%包裹的腰围。
		ENDIF
	ELSEIF EQUIP:胖次 == 0
		IF ARGS:1 == "连褲襪露出"
			PRINTFORMW 薄薄的%OUTER(TARGET, "略")%隔着的股間顯得格外醒目……
		ELSE
			PRINTFORMW 仔细地观察着%CALLNAME:TARGET%什么都没有穿的無防備的股間……
		ENDIF
	ELSE
		PRINTFORMW 包裹着股間的%PANTIES(TARGET)%顯得格外醒目……
	ENDIF
	RETURN 1
ENDIF

SELECTCASE ARGS
CASE "外套"
	PRINTFORM 從%OUTER(TARGET)%下露出的……
CASEELSE
	PRINTFORM 從%ARGS%下露出的……
ENDSELECT

;オープンパンストだとここには来ない
IF ARGS:1 == "连褲襪露出" && EQUIP:胖次
	PRINTFORMW 是隔著半透明的%OUTER(TARGET)%能清楚看見的%PANTIES(TARGET)%！
	CALL TEXT_PANTU_ANAL
ELSEIF ARGS:1 == "外套露出"
 	IF CHECK_OUTER("長褲") == 0
		PRINTFORMW 是包裹著股間的過分合身的%OUTER(TARGET, "略")%！
	ELSEIF CHECK_OUTER("热褲")
		PRINTFORMW 是被称为裤子而言有些過激的%OUTER(TARGET, "略")%！
	ELSE
		PRINTFORMW 是看起来很好动的%OUTER(TARGET, "略")%。
	ENDIF
	IF CHECK_OUTER("股間の隙間")
		;スパッツ＆ノーパン
		IF EQUIP:胖次 == 0 && CHECK_OUTER("紧身短褲")
			PRINTFORMW 大概布料下面就什麼也沒有吧……%CALLNAME:TARGET%的股間、如果仔細一點看的話、是連\@ PENIS(TARGET) ? 陰莖 # 陰唇 \@都可以明顯的勾勒出輪廓的程度了！！
		ELSEIF TALENT:大力士 && COND("豐腴") == 0
			PRINTFORMW %CALLNAME:TARGET%的股間与紧绷的大腿之間连接出来的逆三角形的隙間%TEXTR("令人移不開眼/非常的有魅力/酝酿着健康的魅力")%……
		ELSEIF COND("苗条")
			PRINTFORMW %CALLNAME:TARGET%的股間和纤细的大腿之間连接出来的逆三角形的隙間%TEXTR("令人移不開眼/非常的有魅力/酝酿着健康的魅力")%……
		ENDIF
	ENDIF
ELSEIF ARGS:1 == "贴身制服露出"
	IF CHECK_CLO("勒紧的")
		PRINTFORMW 是被%BODYS(TARGET, "略")%勒入的%TEXTS("陰毛：表現短め")%股間！
	ELSE
		PRINTFORMW 是股間上十分合身的%BODYS(TARGET, "略")%！
	ENDIF
	SIF CHECK_BODYS("高開衩")
		PRINTFORMW %TEXTR("目光无法从完全暴露著的腹股沟上移開/完全暴露著的腹股沟真的很有魅力/完全暴露著的腹股沟的景色真是美丽")%……
ELSEIF EQUIP:胖次 == 0 && (TEQUIP:創口貼 || CFLAG:創口貼装着)
	PRINTFORMW 是在%TEXTR("窄缝/阴部/蜜裂")%上的創口貼！
	IF CFLAG:創口貼装着 > 6
		PRINTFORMW 由于贴在上面的时间太长了、浸透了汗水和其他液体的創口貼看起来快要脱落了……
	ELSE
		PRINTFORMW 恰好%TEXTR("没有缝隙地/紧紧")%贴在一起、突出地浮现着%TEXTR("重要地方/阴部")%的形状……
	ENDIF

ELSEIF EQUIP:胖次 == 0
	IF ARGS:1 == "连褲襪露出"
		PRINTFORM 越过薄薄半透明的%OUTER(TARGET, "略")%看见的、完全裸露的%TEXTS("陰毛：表現短め")%
	ELSE
		PRINTFORM 完全裸露的%TEXTS("陰毛：表現短め")%
	ENDIF
	IF PENIS(TARGET)
		PRINTFORMW %TEXTR("下腹部/股間")%被你欣赏了一番！
	ELSE
		PRINTFORMW %TEXTR("下腹部/恥丘")%被你欣赏了一番！
	ENDIF
	IF COND("自撮りと同じ胖次")
		PRINTFORMW 也许、是从发送自拍的时候開始就一直没有穿内裤……？
	ELSEIF ARGS == "外套"
		PRINTFORMW 大概是直接穿在身上的緣故吧、%OUTER(TARGET, "略")%股間的布料滲入了汗液和各種各樣的東西、散發著相當顯著的%TEXTS("氣味")%……
	ELSEIF ARGS:1 == "连褲襪露出"

	ENDIF
;前に確認したパンツと一緒
ELSEIF CSTR:胖次 == CSTR:PREV_胖次 && COND("夜")
	PRINTFORMW 在午間的部室看過之後、再一次觀賞著%DETAIL_PANTIES(TARGET)%。
	SIF COND("電源BOX") == 0 || COND("電源BOX発覚")
		PRINTFORMW 整整一天的污垢被積累起來、僅僅只論%TEXTS("氣味")%就已經%TEXTR("非常強烈的/刺鼻的/在股間迴響的")%……
ELSE
	IF COND("自撮りと同じ胖次")
		PRINTFORMW 和收到的自拍上一样、是%PANTIES(TARGET)%。
	ELSE
		PRINTFORMW %DETAIL_PANTIES(TARGET)%被你欣赏了一番！
	ENDIF
	IF CHECK_PANTIES("Ａのみ露出") && COND("身嗜み：Ａ拡張")
		CALL TEXT_PANTU_ANAL
	ELSEIF COND("電源BOX") == 0 || COND("電源BOX発覚")
		CALL SHOW_PUBICHAIR
	ENDIF
ENDIF
CALL TEXT_PANTU_VIBE

;-------------------------------------------------
;上のもののやや簡略版
;-------------------------------------------------
@TEXT_PANTU_CHECK_SHORT
CALL PRINT_CUTIN, "胖次", TARGET
;すでにパンツ知ってる場合
IF COND("胖次形状確認")
	PRINTFORMW 包裹着股間的%PANTIES(TARGET)%顯得格外醒目……
	RETURN 1
ENDIF
;前の時間で下着確認済
IF CSTR:PREV_胖次 != ""
	PRINTFORM %CALLNAME:TARGET%的胖次是……
ELSE
	PRINTFORM %CALLNAME:TARGET%今日的胖次是……
ENDIF
;前に確認したパンツと一緒
IF CSTR:胖次 == CSTR:PREV_胖次 && COND("夜")
	PRINTFORMW 和午間的部室時看到的同樣是%PANTIES(TARGET)%、今天似乎沒有進行更換的樣子。
	SIF COND("電源BOX") == 0 || COND("電源BOX発覚")
		PRINTFORMW 整整一天的污垢被積累起來、僅僅只論%TEXTS("氣味")%就已經%TEXTR("非常強烈的/刺鼻的/在股間迴響的")%……
ELSEIF CHECK_CLO("Ｖ露出")
	PRINTFORMW %DETAIL_PANTIES(TARGET)%被你欣赏了一番！
	;PRINTFORMW %CALLNAME:TARGET%那豐潤的陰唇自不必說、就連緊緊夾住陰核的閃耀著的小小圓環也變得一目了然……
	;大陰唇が織り成す縦筋が
	;小陰唇を装飾するピアス
ELSEIF CHECK_PANTIES("Ａのみ露出")
	PRINTFORMW %DETAIL_PANTIES(TARGET)%被你欣赏了一番！
	CALL TEXT_PANTU_ANAL
ELSE
	SIF COND("自撮りと同じ胖次")
		PRINTFORM 和收到的自拍上一样、
	PRINTFORMW %PANTIES(TARGET)%的樣子。
	SIF CHECK_CLO("ハミ毛：裙無視")
		CALL SHOW_PUBICHAIR
ENDIF
CALL TEXT_PANTU_VIBE

@TEXT_PANTU_ANAL
IF CHECK_PANTIES("Ａのみ露出")
	IF COND("身嗜み：Ａ拡張")
		PRINTFORMW 因为尻穴周辺没有布料、能看见肛門时常拡張用肛塞的底部顔色……
	ENDIF
ENDIF

@TEXT_PANTU_VIBE
SIF COND("電源BOX") == 0 || COND("電源BOX発覚")
	RETURN 0
IF EQUIP:胖次 == 0 || CHECK_CLO("Ｖ露出")
	IF AGE(TARGET) == 1
		PRINTFORM %CALLNAME:TARGET%的%TEXTR("縦筋/裂口/阴部")%
	ELSE
		PRINTFORM %CALLNAME:TARGET%的%TEXTR("膣口/裂口/阴部")%
	ENDIF
ELSE
	PRINTFORM 內衣開口部分的下方
ENDIF
PRINTFORM 延伸出來的長長的電源線、
IF CHECK_OTHER("腿环")
	PRINTFORM 被腿环%TEXTR("固定著/夾住了/忍耐著")%
ELSEIF CHECK_SOCKS("直到大腿")
	PRINTFORM 用%SOCKS(TARGET)%%TEXTR("固定著/夾住了/忍耐著")%
ELSE
	PRINTFORM 被固定在大腿那裡
ENDIF
PRINTFORMW 正接着電源……看起来正用膣内振動着的振动棒%TEXTR("享受中/沉浸于自慰行為/追求着快楽")%的様子。


;-------------------------------------------------
;衣服を脱いでもらった時のテキスト
;-------------------------------------------------
@EVENT_DATUI, ARGS
;淫紋が見えるようになったかをチェックする
#DIM IS_VISIBLE_INMON
#DIMS HADAKETA

;淫紋確認の地の文を出すかのチェックに使う
IS_VISIBLE_INMON = CHECK_CLO("淫紋露出")

SELECTCASE ARGS
CASE "裸体"
	PRINTFORM %CALLNAME:TARGET%
	IF COND("調教中") == 0 && CHARA_NUM("室内の邪魔者")
		PRINTFORM 毫不在意室內的旁觀者、
	;恥じらい
	ELSEIF TALENT:害羞 && RAND:3 == 0
		PRINT 不時害羞地微笑著並、
	;巨乳
	ELSEIF TALENT:巨乳 && RAND:3 == 0
		PRINT 炫耀著胸部和尻部的同時、
	;プライド高い、小悪魔、魅力
	ELSEIF (TALENT:高慢 || TALENT:小悪魔) && RAND:3 == 0
		PRINT 用眼神挑逗著%CALLNAME:PLAYER%的同時、
	;淫乱
	ELSEIF TALENT:淫乱 && EQUIP:胖次 && RAND:3 == 0
		PRINTFORM 讓%CALLNAME:PLAYER%充分觀賞自己只穿著%PANTIES(TARGET, "内衣")%的姿態的同时、
	ELSEIF RAND:2 == 0
		PRINTFORM 在羞澀的把眼神投向%CALLNAME:PLAYER%的同時、
	ELSE 
		PRINTFORM 看起來十分愉快的、
	ENDIF

	IF EQUIP:襪子 + EQUIP:靴 + EQUIP:手袋
		IF EQUIP:手袋
			IF EQUIP:襪子 + EQUIP:靴
				PRINTFORMW 只留下手和脚上的衣服、剩下的衣服全部脫下
			ELSE
				PRINTFORMW 只留下手套、剩下的衣服全部脫下
			ENDIF
		ELSEIF EQUIP:襪子
			IF EQUIP:靴
				PRINTFORMW 只留下%SHOES(TARGET, "略")%只留下%SOCKS(TARGET, "略")%、剩下的衣服全部脫下
			ELSE
				PRINTFORMW 只留下%SOCKS(TARGET, "略")%、剩下的衣服全部脫下
			ENDIF
		ELSE
			PRINTFORMW %SHOES(TARGET, "略")%以外的的衣服全部脫下
		ENDIF
	ELSEIF EQUIP:服 + EQUIP:贴身制服
		PRINTFORM 脫下衣服露出了赤裸的身體
	ELSEIF EQUIP:胖次 && RAND:2
		PRINTFORM 脫下%PANTIES(TARGET, "胖次")%露出了赤裸的身體
	ELSE
		PRINTFORM 慢慢地脫掉了
	ENDIF

	IF CHECK_CLO("乳頭露出") == 0 && (COND("陥没乳頭") || COND("巨乳輪") || COND("乳頭肥大") || TALENT:乳环)
		PRINTFORMW 。
		IF CHECK_CLO("泳装姿")
			PRINTFORM 在脫下泳衣之後、
		ELSEIF EQUIP:贴身制服
			PRINTFORM 在脫下%BODYS(TARGET)%之後、
		ELSE
			PRINTFORM 在脫下衣物之後、
		ENDIF
		PRINTFORM 隨著%CALLNAME:TARGET%充滿魅力的身體一併顯露出來的

		IF COND("陥没乳頭")
			IF COND("巨乳輪")
				PRINTFORM 略有%TEXTR("陷沒/埋沒")%現象的乳頭以及%TEXTR("充滿雌性魅力的/大號的/巨大的")%乳輪
			ELSE
				PRINTFORM 略有%TEXTR("陷沒/埋沒")%現象的乳頭
			ENDIF
		ELSE
			IF TALENT:乳环 && RAND:2
				PRINTFORM 被%TEXTR("閃爍金屬光澤的圓環/增添了一份妖媚氣氛的穿環")%装飾著的乳頭
			ELSEIF COND("巨乳輪") && (COND("乳頭肥大") == 0 || RAND:2)
				PRINTFORM 嬌小的美麗乳頭以及%TEXTR("充滿雌性魅力的/大號的/巨大的")%乳輪
			ELSEIF COND("乳頭肥大")
				PRINTFORM 碩大的乳頭
			ELSE
				PRINTFORM 乳頭
			ENDIF
		ENDIF
		PRINTFORMW 、無法抑制的吸引著視線……
	ELSE
		PRINTFORMW …
	ENDIF

CASE "衣服"
	PRINTFORM %CALLNAME:TARGET%
	;恥じらい
	IF TALENT:害羞 && RAND:3
		PRINT 不時害羞地微笑著並、
	;巨乳 && RAND:3
	ELSEIF TALENT:巨乳
		PRINT 炫耀著胸部和尻部的同時、
	;プライド高い、小悪魔、魅力
	ELSEIF (TALENT:高慢 || TALENT:小悪魔) && RAND:3
		PRINT 用眼神挑逗著的同時、
	;淫乱
	ELSEIF TALENT:淫乱 && EQUIP:胖次 && RAND:3
		PRINTFORM 讓%CALLNAME:PLAYER%充分觀賞自己只穿著%PANTIES(TARGET, "内衣")%的姿態的同时、
	ELSEIF RAND:2 || TALENT:好色 + TALENT:淫乱
		PRINTFORM 在羞澀的把眼神投向%CALLNAME:PLAYER%的同時、
	ELSE 
		PRINT 看起來十分愉快的、
	ENDIF
	IF EQUIP:服 + EQUIP:贴身制服
		IF EQUIP:服
			PRINTFORM 脱下了%SHIRT(TARGET, "略")%、
		ELSE
			PRINTFORM 脱下了%BODYS(TARGET, "略")%、
		ENDIF
		IF EQUIP:胖次
			PRINTFORM %PANTIES(TARGET, "内衣")%姿
		ELSE
			PRINTFORM 像是初生嬰孩一樣
		ENDIF
		PRINTW 的姿態…
	ELSEIF EQUIP:胖次
		PRINTFORMW 將衣服一枚一枚慢慢地脫到只剩下%PANTIES(TARGET, "胖次")%的樣子了…
	ELSE
		PRINTFORMW 慢慢地脫掉了…
	ENDIF

	CALL SETFLAG, "薄着化：内衣姿", TARGET

	;淫紋イベント
	SIF IS_VISIBLE_INMON == 0 && MARK:淫紋 >= 2 && CFLAG:淫紋の様子 == 0 && CHECK_CLO("淫紋露出")
		CALL TEXT, "淫紋の様子：脱衣後"

CASE "胸罩"
	PRINTFORMW %CALLNAME:TARGET%取下了\@ CHECK_CLO("泳装姿") ? 泳装的 #  \@文胸…
	CALL SET_BRA, ""
CASE "服を半脱て胸を見せ"
	;はだけられる服を着ている
	IF CHECK_SHIRT("半脱可")
		IF CHECK_SHIRT("提起裙角")
			HADAKETA = %SHIRT(TARGET, "略")%をたくし上げて
		ELSE
			HADAKETA = %SHIRT(TARGET, "略")%を半脱て
		ENDIF
		IF CHECK_BRA("胸罩") && CHECK_BRA("乳頭露出") == 0 && RAND:2
			IF RAND:2 && COND("胸罩形状確認") == 0
				IF CHECK_BRA("見栄え良し")
					PRINTFORM %HADAKETA%%BRA(TARGET)%を見せつけ
				ELSE
					PRINTFORM %HADAKETA%%BRA(TARGET)%を見せ
				ENDIF
			ELSE
				PRINTFORM %HADAKETA%%BRA(TARGET)%が見えるようにし
			ENDIF
			RETURN 1
		ELSE
			IF EQUIP:胸罩 && CHECK_BRA("胸罩") == 0
				PRINTFORM %BRA(TARGET, "略")%ごと
			ELSEIF EQUIP:胸罩
				PRINTFORM 着けている胸罩ごと
			ENDIF
			PRINTFORM %HADAKETA%
		ENDIF
	ELSEIF EQUIP:胸罩
		PRINTFORM %BRA(TARGET)%をずらして
	ENDIF
	IF TALENT:乳环
		PRINTFORM %TEXTS("乳頭：描写")%%TEXTR("乳頭/突起")%につけられた%TEXTS("乳环")%を見せ
	ELSE
		PRINTFORM %TEXTS("乳頭：描写")%%TEXTR("乳頭/突起")%を見せ
	ENDIF
	SIF EQUIP:胸罩
		CALL SETFLAG_DAILY, "胸罩形状確認"
CASE "胖次脱いで手渡し"
	IF TALENT:淫乱 && RAND:3 == 0
		PRINTFORM %TEXTR("勿体つけるように/雰囲気たっぷりに/慢慢地")%%CALLNAME:PLAYER%にお尻を向けた%CALLNAME:TARGET%は股間を見せ付けるように前屈しながら%PANTIES(TARGET, "胖次")%を降ろすと、
	ELSE
		PRINTFORM %CALLNAME:TARGET%
		IF (ABL:露出癖 >= 3 || TALENT:好色 || TALENT:献身) && CHECK_SKIRT("長")
			PRINTFORM 將%SKIRT(TARGET, "略")%的下端用嘴叼住、用完全展示出下腹部的姿势將胖次脫了下來、
		ELSE
			PRINTFORM 迅速地将%PANTIES(TARGET, "胖次")%脱下、
		ENDIF
	ENDIF
	IF PENIS(TARGET) == 0 && (ABL:露出癖 >= 3 || TALENT:好色) && CHECK_PANTIES("闷热的")
		IF CHECK_PANTIES("带污迹的") && RAND:3
			PRINTFORM %TEXTR("弄脏的/恥ずかしい带污迹的の出来た/愛液で湿的た")%
		ELSE
			PRINTFORM 散發著%CALLNAME:TARGET%身體%TEXTS("氣味")%的、
		ENDIF
		PRINTFORM 散發著溫熱的柔軟布料被
		IF TALENT:害羞
			PRINTFORM %TEXTR("耳まで赤くしながら/有点害臊的")%
		ELSE
			IF TALENT:開朗 + TALENT:好色 + TALENT:小悪魔 && RAND:3 == 0
				PRINT 一邊嬌媚的微笑著一邊清楚地展示著的
			ELSE
				PRINTFORM %TEXTR("ニヤニヤしながら/楽しそうに")%
			ENDIF
		ENDIF
		IF TALENT:開朗 + TALENT:好色 + TALENT:小悪魔 && RAND:3
			PRINTFORMW %CALLNAME:PLAYER%に見せ付けた後に手渡してきた。
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%に見せた後に手渡してきた。
		ENDIF
	ELSEIF CHECK_PANTIES("闷热的")
		PRINTFORMW 暖かく湿ったソレを%CALLNAME:PLAYER%に手渡してきた。
	ELSE
		PRINTFORMW 温もりの移ったソレを%CALLNAME:PLAYER%に手渡してきた。
	ENDIF
CASEELSE
	IF TALENT:淫乱 && RAND:3 == 0 && CHECK_CLO("胖次露出")
		PRINTFORMW %CALLNAME:TARGET%用尻部對準%CALLNAME:PLAYER%後、像是在展示股間一樣前屈著慢慢脫下了%PANTIES(TARGET, "胖次")%…
	ELSEIF (ABL:露出癖 - TALENT:害羞 + TALENT:不知羞恥 >= 3 || TALENT:好色) && CHECK_CLO("胖次露出")
		PRINTFORM %CALLNAME:TARGET%
		IF (ABL:露出癖 >= 3 || TALENT:好色 || TALENT:献身) && CHECK_SKIRT("長")
			PRINTFORM 將%SKIRT(TARGET, "略")%的下端用嘴叼住、用完全展示出下腹部的姿势將胖次脫了下來、
		ELSE
			PRINTFORM 迅速地将%PANTIES(TARGET, "胖次")%脱下、
		ENDIF
		IF RAND:2 && EQUIP:裙 && CHECK_SKIRT("長") == 0
			PRINTFORMW 將%SKIRT(TARGET, "略")%大膽的捲起、讓%CALLNAME:PLAYER%肆意的觀賞毫無防備的股間……
		ELSEIF PENIS(TARGET) == 0 && (ABL:露出癖 >= 3 || TALENT:好色) && CHECK_PANTIES("闷热的")
			IF CHECK_PANTIES("带污迹的") && RAND:3
				PRINTFORM %TEXTR("弄脏的/恥ずかしい带污迹的の出来た/愛液で湿的た")%
			ELSE
				PRINTFORM 散發著%CALLNAME:TARGET%身體%TEXTS("氣味")%的、
			ENDIF
			PRINTFORM 散發著溫熱的柔軟布料被
			IF TALENT:害羞
				PRINTFORM %TEXTR("耳まで赤くしながら/有点害臊的")%
			ELSE
				IF TALENT:開朗 + TALENT:好色 + TALENT:小悪魔 && RAND:3 == 0
					PRINT 一邊嬌媚的微笑著一邊清楚地展示著的
				ELSE
					PRINTFORM %TEXTR("ニヤニヤしながら/楽しそうに")%
				ENDIF
			ENDIF
			IF TALENT:開朗 + TALENT:好色 + TALENT:小悪魔 && RAND:3
				PRINTFORMW 清楚的向%CALLNAME:PLAYER%展示著……
			ELSE
				PRINTFORMW 猶豫地展示給%CALLNAME:PLAYER%了……
			ENDIF
		ELSEIF CHECK_PANTIES("闷热的")
			PRINTFORMW 溫暖而濕潤的小小布料被%CALLNAME:TARGET%當面交給%CALLNAME:PLAYER%了……
		ELSE
			PRINTFORMW 遺留微微熱度的小小布料被%CALLNAME:TARGET%當面交給%CALLNAME:PLAYER%了……
		ENDIF
	ELSEIF TALENT:害羞
		IF MARK:屈服刻印 < 3
			PRINTFORMW %CALLNAME:TARGET%一邊躲避著%CALLNAME:PLAYER%的視線、一邊扭扭捏捏的將%PANTIES(TARGET, "内衣")%脫下了…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%慢慢脫下了红着脸的%CALLNAME:TARGET%的%PANTIES(TARGET, "内衣")%…
		ENDIF
	ELSE
		PRINTFORMW %CALLNAME:TARGET%脱下了%PANTIES(TARGET, "内衣")%…
	ENDIF
	SELECTCASE ARGS
	CASE "胖次"
		CALL SET_PANTIES, ""
	CASE "内衣上下"
		CALL SET_BRA, ""
		CALL SET_PANTIES, ""
	ENDSELECT
ENDSELECT


;--------------------------------------------------------
;自発脱ぎ
;LOCALを自動脱ぎ度として用いる
;--------------------------------------------------------
@EVENT_AUTO_DATUI
;下着を足首に絡めたりで一時的に脱いだだけの場合
#DIM IS_TEMPDATUIPANTU

;自分で脱いでくるのはどうだろう
;脱がないようにお願い中or75%で脱いでこない
SIF CONFIG("禁止自動脱衣") || RAND:4 || (COND("疲労") && BASE:性欲 >= MAXBASE:性欲)
	RETURN 0
;ぱんつコキコマンド
SIF PREVCOM == 97 && (CHECK_COM("玩弄Ａ手交") || CHECK_COM("洗面奶手交") )
	RETURN 0
;シックスナイン、緊縛、アイマスク中は脱いでこない
SIF TEQUIP:眼罩 || TEQUIP:拘束 || TEQUIP:六九式 || TEQUIP:深喉
	RETURN 0
;インサート、絶頂中は脱いできません
SIF INSERT("挿入") || INSERT("挿入中") || NOWEX:絶頂数合計
	RETURN 0
;既にハダカ
SIF CHECK_CLO("裸体")
	RETURN 0
;コスプレ中
SIF CFLAG:Cosplay中
	RETURN 0

IS_TEMPDATUIPANTU = 0

;初期値は0～5
LOCAL = RAND:6

SELECTCASE SELECTCOM
;愛撫、胸愛撫、キスは我慢できずに脱いでくる確率アップ
CASE 0, 5, 6
	LOCAL += 5
;セックス系
CASE 60 TO 89, 120 TO 129
	RETURN 0
;コスプレ系
CASE 97, 175
	RETURN 0
ENDSELECT

LOCAL += TALENT:強気*10 + TALENT:好奇心*2 + TALENT:不在乎貞操*2 + TALENT:喜歡引人注目*3 + TALENT:不知羞恥*4 + TALENT:好色*5 + TALENT:淫乱*5
LOCAL -= TALENT:内気*3 + TALENT:冷静*2 + TALENT:保守的*2 + TALENT:害羞*3
LOCAL += (MIN(ABL:欲望, 10) + 1)/2 + (MIN(ABL:露出癖, 10) + 1)/2 + GET_PALAMLV(PALAM:Ｖ潤 + PALAM:欲情)
;媚薬
SIF TEQUIP:媚薬
	LOCAL += 5
;押し倒されている
SIF TEQUIP:推倒
	LOCAL += 3
;恍惚状態
SIF STATE("恍惚", PLAYER)
	LOCAL += 3

;ビデオカメラ
IF TEQUIP:攝像機
	LOCAL -= 5
	SIF COND("合意：露出PLAY")
		LOCAL += 5
ENDIF

;野外プレイ(人里)
IF COND("人里")
	LOCAL -= 10
	SIF COND("合意：露出PLAY")
		LOCAL += 5
	SIF COND("夜") == 0
		LOCAL -= 5
;野外プレイ(人里以外)
ELSEIF TEQUIP:野外PLAY
	LOCAL -= 3
	SIF COND("合意：露出PLAY")
		LOCAL += 5
	SIF COND("夜") == 0
		LOCAL -= 3
ENDIF

;服装による、脱いでくる条件の補正
;挿入系が選べない服装だと脱いできやすい
IF CHECK_PANTIES("Ｃ胖次")
	LOCAL += 10
ELSEIF CHECK_CLO("Ｖ露出可") == 0 && CHECK_CLO("Ａ露出可") == 0
	LOCAL += 10
ENDIF

;ムードが低いと脱いでこない
SELECTCASE MOOD_HEART()
CASE 0
	LOCAL /= 10
CASE IS < 3
	LOCAL /= 2
CASE IS >= 4
	LOCAL += 10
ENDSELECT

;まずは全裸化チェック
IF LOCAL >= 30 && RAND:3 == 0 && CHECK_CLO("泳装姿") == 0
	PRINTL 
	PRINTFORMW %CALLNAME:TARGET%脫下了衣服、露出了美麗的赤裸身體……
	CALL SETFLAG, "扒成裸体", TARGET
	CALL KOJO_EVENT, "自発脱ぎ"
	RETURN 0
ENDIF

;既に股間が露出している
SIF CHECK_CLO("Ｖ露出") || CHECK_CLO("Ａ露出")
	RETURN 0

IF CHECK_CLO("贴身制服露出")
	IF CHECK_BODYS("带拉链的閉") && LOCAL >= 20
		PRINTL 
		PRINTFORM %CALLNAME:TARGET%輕輕地張開雙腿、將%BODYS(TARGET, "略")%股間部分隱藏的拉鏈拉下……
		CSTR:贴身制服 += "带拉链的開/"
		IF (TALENT:恋慕 || TALENT:不知羞恥) && TALENT:男性 == 0 && CHECK_CLO("Ｖ露出")
			PRINTW 。
			PRINTFORMW 從手指稍微拓寬的狹小開口之中、可以隱約的看見%TEXTS("阴部：視覚")%……
		ELSE
			PRINTFORMW …
		ENDIF
		CALL KOJO_EVENT, "自発脱ぎ"
	ENDIF
ELSEIF CHECK_CLO("连褲襪露出")
	IF PALAM:欲情 < 30000
	ELSEIF LOCAL >= 15
		PRINTL 
		PRINTFORM %CALLNAME:TARGET%小心的撕開了%OUTER(TARGET, "略")%股間的部分……
		CSTR:外套 += "破開的/"
		IF CHECK_CLO("Ｖ露出")
			PRINTFORMW 美麗的\@ PENIS(TARGET) ? 股間 # 陰部 \@在纖細布料的破洞中暴露出來了…
		ELSE
			PRINTFORMW 在纖細布料的小小洞口之中、可以清楚地看見散發著微微熱氣的胖次……
		ENDIF
		CALL KOJO_EVENT, "自発脱ぎ"
	ENDIF
;ブルマは脱ぎません
ELSEIF CHECK_OUTER("布鲁马")
ELSEIF CHECK_CLO("外套露出")
	IF CHECK_OUTER("带拉链的")
		IF CHECK_OUTER("带拉链的閉") && LOCAL >= 20
			PRINTL 
			PRINTFORM %CALLNAME:TARGET%輕輕地張開雙腿、將%OUTER(TARGET, "略")%股間部分隱藏的拉鏈拉下……
			CSTR:外套 += "带拉链的開/"
			IF (TALENT:恋慕 || TALENT:不知羞恥) && TALENT:男性 == 0 && CHECK_CLO("Ｖ露出")
				PRINTW 。
				PRINTFORMW 從手指稍微拓寬的狹小開口之中、可以隱約的看見%TEXTS("阴部：視覚")%……
			ELSE
				PRINTFORMW …
			ENDIF
			CALL KOJO_EVENT, "自発脱ぎ"
		ENDIF
	;パンツも穿いている
	ELSEIF EQUIP:胖次 && LOCAL >= 30
		PRINTL 
		PRINTFORMW %CALLNAME:TARGET%小心的將%PANTIES(TARGET, "内衣")%連著%OUTER(TARGET, "略")%一起脫下來了…
		CALL SET_OUTER, ""
		CALL SET_PANTIES, ""
		CALL KOJO_EVENT, "自発脱ぎ"
	ELSEIF LOCAL >= 10
		PRINTL 
		PRINTFORM %CALLNAME:TARGET%%OUTER(TARGET, "略")%
		IF TALENT:男性 == 0 && TEQUIP:推倒 && TALENT:不知羞恥 + TALENT:好色 + TALENT:淫乱 + TALENT:小悪魔 - TALENT:害羞 - TALENT:清楚 > 0
			PRINTFORMW %CALLNAME:TARGET%將%OUTER(TARGET, "略")%從身上脫下、羞怒的丟在%CALLNAME:PLAYER%的臉上……
			SIF PALAM:Ｖ潤 >= 3000 && PALAM:欲情 >= 3000 && RAND:3 == 0
				PRINTFORMW 在%CALLNAME:PLAYER%下意識的深嗅一口後、屬於%CALLNAME:TARGET%的濕潤的%TEXTS("气味")%漸漸流入鼻腔……
		ELSE
			PRINTFORMW 慢慢脫下……
		ENDIF
		CALL SET_OUTER, ""
		CALL KOJO_EVENT, "自発脱ぎ"
	ENDIF
ELSEIF CHECK_CLO("胖次露出")
	;ファスナー優先
	IF CHECK_PANTIES("带拉链的閉") && LOCAL >= 20
		PRINTL 
		PRINTFORM %CALLNAME:TARGET%將內衣隱藏的拉鏈拉下……
		CSTR:胖次 += "带拉链的開/"
		IF TALENT:男性 == 0 && (TALENT:恋慕 || TALENT:不知羞恥) && CHECK_CLO("Ｖ露出")
			PRINTW 。
			PRINTFORMW 從手指稍微拓寬的狹小開口之中、可以隱約的看見%TEXTS("阴部：視覚")%……
		ELSE
			PRINTFORMW …
		ENDIF
		CALL KOJO_EVENT, "自発脱ぎ"
	ELSEIF CHECK_PANTIES("脱衣不可") == 0 && LOCAL >= 20
		PRINTL 
		IF TEQUIP:推倒 && TALENT:不知羞恥 + TALENT:好色 + TALENT:淫乱 + TALENT:小悪魔 - TALENT:害羞 - TALENT:清楚 > 0
			PRINTFORM %CALLNAME:TARGET%將%PANTIES(TARGET, "略")%
			IF CHECK_PANTIES("Ｃ胖次")
				PRINT 取下了、
			ELSEIF CHECK_PANTIES("紐")
				PRINT 的紐扣一個個慢慢地解開了、
			ELSE
				PRINT 慢慢脫下了、
			ENDIF
			IF TEQUIP:面部騎乗
				PRINTFORMW 在丟到一旁後又慢慢地坐在%CALLNAME:PLAYER%的臉上了……
			ELSE
				PRINTFORMW 輕笑著丟到%CALLNAME:PLAYER%的臉上了……
			ENDIF
			IF TALENT:男性 == 0 && PALAM:Ｖ潤 >= 3000 && PALAM:欲情 >= 3000 && RAND:3 == 0
				IF RAND:2 && CHECK_PANTIES("Ｃ胖次") == 0
					PRINTFORMW 已經沾滿情色污漬的\@ CHECK_CLO("勒紧的") ? 深深勒進股間的 # 因濡濕而緊貼股間的 \@小小布料、因為膣道內側溢出的%TEXTR("黏稠/淫靡")%汁液而完全變成了深色調……
				ELSE
					PRINTFORMW 小小布料發出的屬於%CALLNAME:TARGET%的濃郁%TEXTS("气味")%悄悄飄進了%CALLNAME:PLAYER%的鼻腔……
				ENDIF
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:TARGET%將%PANTIES(TARGET, "略")%
			IF CHECK_PANTIES("紐")
				PRINTFORMW 的紐扣一個個慢慢地解開了……
			ELSEIF TALENT:不知羞恥 && RAND:3 == 0
				PRINTFORMW 慢慢脫下、向%CALLNAME:PLAYER%仔細地展示過、得到許可後便丟到了遠處……
			ELSEIF ABL:露出癖 + TALENT:好色 && CHECK_PANTIES("男物") == 0 && CHECK_PANTIES("Ｃ胖次") == 0 && RAND:2
				PRINTFORMW 慢慢脫到小腿、拔出一隻腳後將它仔細地纏繞在另一隻腳的腳裸上……
				IS_TEMPDATUIPANTU = 1
			ELSEIF RAND:2 && TALENT:男性 == 0 && COND("陰毛") && COND("陰毛") != 6
				PRINTFORM 慢慢脫下、
				SIF RAND:2 && CHECK_PANTIES("Ｈ")
					PRINTFORM 被成熟風味的%PANTIES(TARGET, "内衣")%所掩藏著的
				SELECTCASE COND("陰毛")
				CASE 1
					PRINT 光潔無毛的
				CASE 2
					PRINT 完全脫毛的
				CASE 3
					PRINTFORM %TEXTR("剃毛済みの/光滑无毛の")%
				CASE 4
					;剃り跡
					IF CONDS("陰毛") == "剃り跡"
						PRINT 帶著微微毛茬兒的
					ELSE
						PRINT 生長著單薄陰毛的
					ENDIF
				CASE 5
					PRINTFORM %TEXTR("保养完毕みの/手入れの行き届いた")%
				CASE 6
					PRINT 被柔軟而茂密的陰毛遮蓋的
				CASE 7
					PRINT 被厚重的黑色密林深深掩藏的
				ENDSELECT
				PRINTFORMW %TEXTR("漂亮/淫靡")%的陰部顯露著……
			ELSE
				PRINTFORMW 慢慢脫下……
			ENDIF
			IF TALENT:男性 == 0 && PALAM:Ｖ潤 >= 3000 && PALAM:欲情 >= 3000 && RAND:3 == 0
				IF CHECK_PANTIES("通気性没有") && TALENT:男性 == 0
					PRINTFORMW 被毫無通氣性可言的布料緊緊密封的陰部中、雌性發情而產生的濃郁%TEXTS("气味")%、轉眼間在四周瀰漫開來……
				ELSEIF CHECK_PANTIES("男物") == 0
					PRINTFORMW %PANTIES(TARGET, "胖次")%的股間部分已然沾染了無法隱藏的色斑…
				ENDIF
			ENDIF
		ENDIF

		SIF IS_TEMPDATUIPANTU
			CALL SETFLAG, "内衣を一時的に脱掉", TARGET

		CALL KOJO_EVENT, "自発脱ぎ"

		SIF IS_TEMPDATUIPANTU == 0
			CALL SET_PANTIES, ""
	ENDIF
ENDIF


;ずらしたパンツとかを直す
@EVENT_ZURENAOSHI
;67%で直さない
SIF RAND:3 || COND("疲労") || TALENT:男性
	RETURN 0
;ずらしてない
SIF CHECK_CLO("移開的") == 0
	RETURN 0

SELECTCASE SELECTCOM
;愛撫の股間を重点的には弄らないもの
CASE 0, 5, 6
;愛撫系
CASE 0 TO 29
	RETURN 0
;道具系
CASE 30 TO 59
	RETURN 0
;セックス系
CASE 60 TO 89, 120 TO 129
	RETURN 0
;SM系
CASE 130 TO 159
	RETURN 0
;コスプレ系
CASE 97, 175
	RETURN 0
CASE TNUM("素股"), TNUM("尻素股")
	RETURN 0
ENDSELECT

;ぱんつコキコマンド
SIF PREVCOM == 97 && (CHECK_COM("玩弄Ａ手交") || CHECK_COM("洗面奶手交") )
	RETURN 0
;シックスナイン、緊縛、アイマスク中
SIF TEQUIP:面部騎乗 || TEQUIP:眼罩 || TEQUIP:拘束 || TEQUIP:六九式 || TEQUIP:深喉 || TEQUIP:PLAYER:自慰
	RETURN 0
;インサート中、絶頂
SIF INSERT("挿入") || INSERT("挿入中") || NOWEX:絶頂数合計
	RETURN 0

;ARGが0でないならテキストは出力しない
VARSET LOCALS
SIF EQUIP:贴身制服 && CHECK_BODYS("移開的")
	LOCALS = %BODYS(TARGET, "略")%と
SIF EQUIP:外套 && CHECK_OUTER("移開的")
	LOCALS = %LOCALS%%OUTER(TARGET, "略")%と
SIF EQUIP:胖次 && CHECK_PANTIES("移開的")
	LOCALS = %LOCALS%%PANTIES(TARGET, "略")%と

PRINTL 
PRINTFORMW %CALLNAME:TARGET%將%SUBSTRING(LOCALS, 0, STRLENS(LOCALS) - 2)%的位置不動聲色地調整好了……
CALL RESETFLAG_CLO, TARGET, "移開的"


;時間ごとに一度行うRAND_PANTU。時間帯などの調整が入っている
@RAND_PANTU_ALL
#DIM LCOUNT
#DIM MEMO_TIME
#DIM MEMO_DAY
#DIMS MEMO_PLACE

MEMO_TIME = TIME
MEMO_DAY = DAY
MEMO_PLACE = %SAVESTR:現在位置%

CALL SETFLAG, "次の時間帯のＴＰＯ"

FOR LCOUNT, 2, CHARANUM
	;パンツは謎にする
	CFLAG:LCOUNT:内衣確認済 = 0
	;日替わりと汚れが凄い場合には履き替える
	SIF COND("昼", LCOUNT) || (ABL:LCOUNT:欲望 >= 3 && TALENT:LCOUNT:容易濕 && RAND:2) || EQUIP:LCOUNT:胖次 == 0
		CALL RAND_PANTU, LCOUNT
NEXT

TIME = MEMO_TIME
DAY = MEMO_DAY
SAVESTR:現在位置 = %MEMO_PLACE%

;ARGSに"ノーパン禁止"とすると必ず何かを穿く
@RAND_PANTU, ARG, ARGS
;PRINTFORM %CALLNAME:ARG% %CSTR:ARG:ブラ% → 
CALL RAND_PANTU_MAIN, ARG, ARGS

;自分の下着に着替えた場合はブラもセットなのでスキップ
SIF TFLAG:自分の内衣に着替えた == 0
	CALL RAND_BRA, ARG, ARGS

;PRINTFORML \@ TFLAG:自分の下着に着替えた ? 自分の下着： # \@%CSTR:ARG:ブラ%

@RAND_PANTU_MAIN, ARG, ARGS
#DIMS MEMO_PANTU

MEMO_PANTU = %CSTR:ARG:胖次%

;パンツの着用禁止
IF COND("身嗜み：没穿内褲", ARG) && ARGS != "没穿内褲禁止"
	CALL SET_PANTIES, "", ARG
	RETURN 0
ENDIF
;パンツを穿き替えない理由いろいろ
SIF COND("脚が無い", ARG) || EQUIP:ARG:贴身制服 || CHECK_PANTIES("泳装", ARG) || CHECK_PANTIES("脱衣不可", ARG) || CHECK_PANTIES("兜襠布", ARG)
	RETURN 0
;アウターがＶorＡ穴開きならパンツははかない
IF EQUIP:ARG:外套 && CHECK_OUTER("连褲襪", ARG) == 0 && (CHECK_OUTER("Ｖ露出", ARG) || CHECK_OUTER("Ａ露出", ARG) )
	CALL SET_PANTIES, "", ARG
	RETURN 0
ENDIF

;ランダム下着を行わないフラグを立てている場合は自分の下着を身に着ける
IF CFLAG:ARG:随机内衣OFF
	CALL SET_PANTIES, "自分の内衣", ARG
	RETURN 1
ENDIF

;アウターがハイレグ
IF EQUIP:ARG:外套 && CHECK_OUTER("高開衩", ARG)
	;人によってはノーパン(ちんこある人はパンスト必須)
	IF ABL:ARG:露出癖 >= MAX(5, RAND:10) && ARGS != "没穿内褲禁止" && (CHECK_OUTER("连褲襪", ARG) || PENIS(ARG) == 0)
		CALL SET_PANTIES, "", ARG
		RETURN 0
	ELSEIF RAND:3 == 0 && (ABL:ARG:露出癖 >= 5 || TALENT:ARG:好色 || TALENT:ARG:淫乱) && DISLIKE_PANTIES("Ｃ胖次", ARG) == 0
		CALL SET_PANTIES, "Ｃ胖次", ARG
	ELSE
		CALL SET_PANTIES, "高開衩胖次", ARG
	ENDIF
;アウターがローライズ
ELSEIF EQUIP:ARG:外套 && CHECK_OUTER("低腰", ARG)
	;人によってはノーパン
	IF ABL:ARG:露出癖 >= MAX(5, RAND:10) && ARGS != "没穿内褲禁止"
		CALL SET_PANTIES, "", ARG
		RETURN 0
	ELSEIF RAND:2 && (ABL:ARG:露出癖 >= 3 || TALENT:ARG:好色 || TALENT:ARG:淫乱) && DISLIKE_PANTIES("超低腰胖次", ARG) == 0
		CALL SET_PANTIES, "超低腰胖次", ARG
	ELSE
		CALL SET_PANTIES, "低腰胖次", ARG
	ENDIF
ELSE
	CALL GET_RAND_PANTU, ARG, ARGS
ENDIF

;ペニスがある人はオープンクロッチはランダムだと穿かない
SIF PENIS(ARG) && CHECK_PANTIES("Ｖ露出", ARG)
	CALL SET_PANTIES, "後開型", ARG

;以下は染みパン判定。穿き替えてないorノーパンorクロッチ無しならここで終わり
SIF TALENT:ARG:男性 || COND("種族：人形", ARG) || EQUIP:ARG:胖次 == 0 || MEMO_PANTU == CSTR:ARG:胖次 || CHECK_PANTIES("開洞", ARG) == 0 || CHECK_PANTIES("泳装", ARG)
	RETURN 0
SIF TALENT:ARG:高贵 || TALENT:ARG:聰慧 || TALENT:ARG:清楚
	RETURN 0
SIF (TALENT:ARG:粗野 || TALENT:ARG:笨蛋 || TALENT:ARG:不知羞恥) && RAND:3 == 0
	CSTR:ARG:胖次 += "開洞带污迹的/"


@RAND_BRA, ARG, ARGS
;ノーブラ主義ならノーブラ
IF COND("无胸罩主義", ARG)
	CALL SET_BRA, "", ARG
;Ｈなパンツを選んだならブラも同様に
ELSEIF CHECK_PANTIES("Ｈ", ARG)
	CALL SET_BRA, "Ｈ的胸罩", ARG
;好みのブラが設定されているなら、高確率でそれを選ぶ
ELSEIF CSTR:ARG:喜欢的胸罩 != "" && RAND:7
	CALL SET_BRA, TEXTR(CSTR:ARG:喜欢的胸罩), ARG
;そうでないなら、パンツに合わせたブラを選ぶ
ELSE
	CALL COORDINATE_BRA, ARG
ENDIF

;ブラの色をあわせる
CALL EQ_CLOCOLOR, ARG


@GET_RAND_PANTU, ARG, ARGS
#DIM LCOUNT
;パンツの好き嫌いがあるので何を穿いているかを２段階で選別する
#DIMS PANTU_TABLE_PRE
#DIMS PANTU_TABLE

;何らかの下着を選ぶのでパンツリセット
CALL SET_PANTIES, "", ARG

;ノーパン(ちんこある人はＯバック)
IF RAND:10 == 0 && ABL:ARG:露出癖 >= 7 && ARGS != "没穿内褲禁止"
	IF PENIS(ARG)
		CALL SET_PANTIES, "後開型", ARG
	ELSE
		RETURN 1
	ENDIF
ENDIF

;25%で自分の下着
IF RAND:4 == 0
	CALL SET_PANTIES, "自分の内衣", ARG
	RETURN 1
ELSEIF TALENT:ARG:好色 + TALENT:ARG:淫乱 && ABL:ARG:露出癖 >= 5 && RAND:4 == 0 && DISLIKE_PANTIES("Ｈ的内衣", ARG) == 0
	IF PENIS(ARG) && COND("Ａ熟達", ARG) && RAND:3
		CALL SET_PANTIES, "後開型", ARG
	ELSE
		CALL SET_PANTIES, "Ｈ的内衣", ARG
	ENDIF
;好みのパンツが設定されているなら、高確率でそれを選ぶ
ELSEIF CSTR:ARG:喜欢的胖次 != "" && RAND:2
	CALL SET_PANTIES, TEXTR(CSTR:ARG:喜欢的胖次), ARG
ENDIF

;ここまでで選んだパンツが服装的に問題ないかをチェック
;丈が短いズボンだとトランクスなどは場違い
IF CHECK_OUTER("布鲁马", ARG) || CHECK_OUTER("热褲", ARG)
	SIF CHECK_PANTIES("男物", ARG) || CHECK_PANTIES("兜襠布", ARG) || CHECK_PANTIES("貞操帯", ARG)
		CALL SET_PANTIES, "", ARG
;ミニスカ
ELSEIF CHECK_SKIRT("迷你", ARG)
	SIF CHECK_PANTIES("男物", ARG)
		CALL SET_PANTIES, "", ARG
;性別と心情的なもの加味
ELSEIF TALENT:ARG:恋慕 + TALENT:ARG:淫乱 && TALENT:ARG:男性 == 0 && TALENT:MASTER:男性
	IF CHECK_PANTIES("男物", ARG)
		IF EQUIP:ARG:外套 && CHECK_OUTER("男物可", ARG) == 0
			CALL SET_PANTIES, "", ARG
		ELSEIF FLAG:叫出来Ｈ中 || COND("约会中", ARG) || RAND:2
			CALL SET_PANTIES, "", ARG
		ENDIF
	ENDIF
ENDIF
SIF EQUIP:ARG:胖次
	RETURN 1

;以下はランダム選択
PANTU_TABLE = 
PANTU_TABLE_PRE = 內褲/

;素質などで穿いているパンツにバリエーション
SIF (ABL:ARG:露出癖 >= 3 || TALENT:ARG:不知羞恥) && (TALENT:ARG:小悪魔 || AGE(ARG) <= 2)
	PANTU_TABLE_PRE += "超低腰胖次/繩系胖次/"
SIF (ABL:ARG:露出癖 >= 3 || TALENT:ARG:不知羞恥) && (TALENT:ARG:強気 || TALENT:ARG:開朗 || AGE(ARG) >= 3)
	PANTU_TABLE_PRE += "高開衩胖次/Ｔ字褲/Micro內褲/"
SIF (ABL:ARG:露出癖 >= 5 || TALENT:ARG:不知羞恥) && TALENT:ARG:好色 + TALENT:ARG:喜歡引人注目
	PANTU_TABLE_PRE += "丁字褲/"
SIF ABL:ARG:露出癖 >= 7 && TALENT:ARG:好色 + TALENT:ARG:淫乱
	PANTU_TABLE_PRE += "Ｃ胖次/"
SIF TALENT:ARG:害羞 || TALENT:ARG:粗野 || PENIS(ARG)
	PANTU_TABLE_PRE += "四角内褲/"
SIF TALENT:ARG:高贵 + TALENT:ARG:清楚
	PANTU_TABLE_PRE += "蕾丝的胖次/透明胖次/褶边の胖次/"
SIF TALENT:ARG:強気 || TALENT:ARG:開朗 || TALENT:ARG:笨蛋
	PANTU_TABLE_PRE += "豹纹胖次/"
SIF COND("Ａ熟達", ARG) && ABL:ARG:露出癖 >= 5 && COND("性処理中", ARG) + TALENT:ARG:好色 + TALENT:ARG:淫乱
	PANTU_TABLE_PRE += "後開型/"

;外見年齢による適性
SELECTCASE AGE(ARG)
;オトナ
CASE 4
	PANTU_TABLE_PRE += "全後衛胖次/蕾丝的胖次/透明胖次/繩系胖次/"
;お姉さん
CASE 3
	PANTU_TABLE_PRE += "三角胖次/條紋胖次/水玉模様の胖次/蕾丝的胖次/透明胖次/褶边の胖次/繩系胖次/"
;少女以下
CASEELSE
	PANTU_TABLE_PRE += "三角胖次/低腰胖次/條紋胖次/水玉模様の胖次/褶边の胖次/"
ENDSELECT

VARSET LOCALS
SPLIT PANTU_TABLE_PRE, "/", LOCALS

FOR LCOUNT, 0, 30
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		SIF DISLIKE_PANTIES(LOCALS:LCOUNT, ARG) == 0
			PANTU_TABLE += @"%LOCALS:LCOUNT%\/"
	ENDSELECT
NEXT
CALL SET_PANTIES, TEXTR(PANTU_TABLE), ARG

