;-------------------------------------------------
;現在の衣服の表記
;ARGS = 短縮表示 の時にはグローブなどをコンフィグによっては表示しない
;ARGS = 衣装表示 の時にはノーパンなどの不要な表示を省く
;ARGS = 拡大表示 の時にはフォントを弄らない＆２行表示
;-------------------------------------------------
@PRINT_DRESS, ARG, ARGS
#DIM LCOUNT
#DIMS DRESS

;衣装の汚れなどを表示前に計算
CALL STAIN_CALC_COSPLAY, ARG

DRESS = 

IF EQUIP:ARG:服
	DRESS = %DRESS%%SHIRT(ARG)%\@ CHECK_SHIRT("半脱的", ARG) ? (半脱) # \@\@ CHECK_STAIN(STAIN:ARG:服, "精液") ? <精> # \@
	IF CHECK_SHIRT("两件套", ARG) && EQUIP:ARG:裙 == 0
		SIF CHECK_OUTER("長褲", ARG) == 0
			DRESS += "(裙无)"
	ELSEIF CHECK_SHIRT("つなぎ")
		IF CHECK_OUTER("破開的", ARG)
			DRESS += "(破)"
		ELSEIF CHECK_OUTER("移開的", ARG)
			DRESS += "(移開)"
		ELSEIF CHECK_OUTER("带拉链的開", ARG)
			DRESS += "(開)"
		ELSEIF CHECK_OUTER("開衩", ARG)
			DRESS += "(開衩)"
		ELSEIF CHECK_OUTER("ＶＡ露出", ARG)
			DRESS += "(穴開き)"
		ELSEIF CHECK_OUTER("Ａ露出", ARG)
			DRESS += "(Ａ穴開き)"
		ELSEIF CHECK_OUTER("Ｖ露出", ARG)
			DRESS += "(Ｖ穴開き)"
		ELSEIF ARGS != "衣装表示"
			SIF CHECK_CLO("外套直穿き", ARG)
				DRESS += "(直穿き)"
			;汚れ
			IF !CHECK_CLO("Ｖ露出", ARG)
				IF CHECK_STAIN(STAIN:ARG:外套, "尿")
					DRESS += "<尿>"
				ELSEIF CHECK_STAIN(STAIN:ARG:外套, "湿的")
					DRESS += "<濡>"
				ELSEIF CHECK_STAIN(STAIN:ARG:外套, "闷热的")
					DRESS += "<蒸>"
				ENDIF
				;SIF CHECK_STAIN(STAIN:ARG:アウター, "精液")
				;	DRESS += "<精>"
			ENDIF
		ENDIF
	ENDIF
	DRESS += "/"
ENDIF

IF EQUIP:ARG:贴身制服
	DRESS = %DRESS%%BODYS(ARG)%
	IF CHECK_BODYS("移開的", ARG)
		DRESS += "(移開)"
	ELSEIF CHECK_BODYS("带拉链的開", ARG)
		DRESS += "(開)"
	ELSEIF CHECK_BODYS("带拉链的", ARG)
		DRESS += "(带拉链的)"
	ELSEIF CHECK_BODYS("ＶＡ露出", ARG)
		DRESS += "(穴開き)"
	ELSEIF CHECK_BODYS("Ａ露出", ARG)
		DRESS += "(Ａ穴開き)"
	ELSEIF CHECK_BODYS("Ｖ露出", ARG)
		DRESS += "(Ｖ穴開き)"
	;汚れ
	ELSEIF ARGS != "衣装表示"
		IF CHECK_STAIN(STAIN:ARG:贴身制服ボトム, "尿")
			DRESS += "<尿>"
		ELSEIF CHECK_STAIN(STAIN:ARG:贴身制服ボトム, "湿的")
			DRESS += "<濡>"
		ELSEIF CHECK_STAIN(STAIN:ARG:贴身制服ボトム, "闷热的")
			DRESS += "<蒸>"
		ENDIF
		SIF CHECK_STAIN(STAIN:ARG:贴身制服ボトム, "精液")
			DRESS += "<精>"
	ENDIF
	DRESS += "/"
ENDIF

SIF EQUIP:ARG:裙 && CHECK_SHIRT("連衣裙", ARG) == 0
	DRESS = %DRESS%%SKIRT(ARG)%/

;上半身の状態チェック
SIF CHECK_CLO("没穿外衣", ARG)
	DRESS += "没穿外衣/"

;ブラは、上に何か着ていたりする時などは非表示
SIF EQUIP:ARG:胸罩 && (ARGS != "短縮表示" || COND("胸罩透け", ARG) || CHECK_CLO("谷間露出", ARG) || CHECK_SHIRT("透明", ARG) || EQUIP:ARG:服 + EQUIP:ARG:贴身制服 == 0)
	DRESS = %DRESS%%BRA(ARG)%\@ CHECK_BRA("移開的", ARG) ? (移開) # \@/

IF EQUIP:ARG:外套 && CHECK_OUTER("つなぎ") == 0 && CHECK_CLO("外套未確認", ARG) == 0
	DRESS = %DRESS%%OUTER(ARG)%
	IF CHECK_OUTER("破開的", ARG)
		DRESS += "(破)"
	ELSEIF CHECK_OUTER("移開的", ARG)
		DRESS += "(移開)"
	ELSEIF CHECK_CLO("贴身制服露出", ARG)

	ELSEIF CHECK_OUTER("带拉链的開", ARG)
		DRESS += "(開)"
	ELSEIF CHECK_OUTER("带拉链的", ARG)
		DRESS += "(带拉链的)"
	ELSEIF CHECK_OUTER("開衩", ARG)
		DRESS += "(開衩)"
	ELSEIF CHECK_OUTER("ＶＡ露出", ARG) && CHECK_OUTER("開放型", ARG) == 0
		DRESS += "(穴開き)"
	ELSEIF CHECK_OUTER("Ａ露出", ARG) && CHECK_OUTER("開放型", ARG) == 0
		DRESS += "(Ａ穴開き)"
	ELSEIF CHECK_OUTER("Ｖ露出", ARG) && CHECK_OUTER("開放型", ARG) == 0
		DRESS += "(Ｖ穴開き)"
	ELSEIF ARGS != "衣装表示"
		SIF CHECK_CLO("外套直穿き", ARG) && CHECK_CLO("胖次未確認", ARG) == 0 && CHECK_OUTER("開放型", ARG) == 0
			DRESS += "(直穿き)"
		;汚れ
		IF !CHECK_CLO("Ｖ露出", ARG)
			IF CHECK_STAIN(STAIN:ARG:外套, "尿")
				DRESS += "<尿>"
			ELSEIF CHECK_STAIN(STAIN:ARG:外套, "湿的")
				DRESS += "<濡>"
			ELSEIF CHECK_STAIN(STAIN:ARG:外套, "闷热的")
				DRESS += "<蒸>"
			ENDIF
			SIF CHECK_STAIN(STAIN:ARG:外套, "精液")
				DRESS += "<精>"
		ENDIF
	ENDIF
	DRESS += "/"
ENDIF

;パンツは未確認の時には表示しない
IF CSTR:ARG:脱掉的胖次 != ""
	SWAP CSTR:ARG:胖次, CSTR:ARG:脱掉的胖次
	DRESS = %DRESS%%PANTIES(ARG)%(足首)\/
	SWAP CSTR:ARG:胖次, CSTR:ARG:脱掉的胖次
ELSEIF EQUIP:ARG:胖次 && CHECK_CLO("胖次未確認", ARG) == 0
	DRESS = %DRESS%%PANTIES(ARG)%
	IF CHECK_PANTIES("移開的", ARG)
		DRESS += "(移開)"
	ELSEIF CHECK_PANTIES("带拉链的開", ARG)
		DRESS += "(開)"
	ELSEIF CHECK_PANTIES("ＶＡ露出", ARG)
		SIF CHECK_PANTIES("開放型開洞", ARG) == 0
			DRESS += "(穴開き)"
	ELSEIF CHECK_PANTIES("Ａ露出", ARG)
		SIF CHECK_PANTIES("後開型", ARG) == 0
			DRESS += "(Ａ穴開き)"
	ELSEIF CHECK_PANTIES("Ｖ露出", ARG)
		SIF CHECK_PANTIES("開放型開洞", ARG) == 0
			DRESS += "(Ｖ穴開き)"
	ELSEIF ARGS != "衣装表示"
		;SIF CHECK_PANTIES("クロッチ染み", ARG)
		;	DRESS += "<染み>"
		IF CHECK_STAIN(STAIN:ARG:胖次, "尿")
			DRESS += "<尿>"
		ELSEIF CHECK_STAIN(STAIN:ARG:胖次, "湿的")
			DRESS += "<濡>"
		ELSEIF CHECK_STAIN(STAIN:ARG:胖次, "汗")
			DRESS += "<汗>"
		ELSEIF CHECK_STAIN(STAIN:ARG:胖次, "闷热的")
			DRESS += "<蒸>"
		ENDIF
		SIF CHECK_STAIN(STAIN:ARG:胖次, "精液")
			DRESS += "<精>"
	ENDIF
	DRESS += "/"
;色だけ分かっている場合
ELSEIF EQUIP:ARG:胖次 && COND("胖次色確認", ARG)
	DRESS += @"%CLOCOLOR(CSTR:ARG:胖次, "形容詞")%胖次？\/"
ENDIF

IF CHECK_CLO("裸体", ARG)
	DRESS += "裸体/"
ELSEIF CHECK_CLO("没穿下衣", ARG)
	DRESS += "没穿下衣/"
ELSEIF CHECK_CLO("没穿内褲", ARG)
	SIF ARGS != "衣装表示" && CHECK_CLO("胖次未確認", ARG) == 0 && EQUIP:ARG:贴身制服 == 0
		DRESS += "没穿内褲/"
ENDIF

IF ARGS == "拡大表示" && EQUIP:ARG:服 && EQUIP:ARG:胖次 && EQUIP:ARG:胸罩
	CALL PRINT_LIST, DRESS, ARGS
	DRESS = 
ENDIF

SIF EQUIP:ARG:襪子
	DRESS = %DRESS%%SOCKS(ARG)%\@ CHECK_STAIN(STAIN:ARG:襪子, "精液") ? <精> # \@/

SIF EQUIP:ARG:靴
	DRESS = %DRESS%%SHOES(ARG)%\@ CHECK_STAIN(STAIN:ARG:靴, "精液") ? <精> # \@/

SIF EQUIP:ARG:手袋
	DRESS = %DRESS%%GLOVE(ARG)%\@ CHECK_STAIN(STAIN:ARG:靴, "精液") ? <精> # \@/

IF EQUIP:ARG:飾品
	IF STRCOUNT(CSTR:ARG:飾品, "黒色颈环")
		DRESS = %DRESS%黄色\/%CSTR:ARG:飾品%\/
	ELSE
		DRESS = %DRESS%%CSTR:ARG:飾品%\/
	ENDIF
ENDIF
IF ARG != MASTER && ARGS != "衣装表示"
	IF COND("調教中")
		SIF COND("腋毛", ARG) && CHECK_CLO("腋露出", ARG)
			DRESS += @"%TEXTS("腋")%毛:%CONDS("腋毛")%\/"
		IF COND("陰毛", ARG) && (CHECK_CLO("陰毛露出", ARG) || CHECK_CLO("Ｖ露出") )
			IF CONDS("陰毛", ARG) == "保养完毕" && CEVENTS("陰毛の名称", ARG) != ""
				DRESS += @"陰毛:%CONDS("陰毛", ARG)%(%CEVENTS("陰毛の名称", ARG)%)\/"
			ELSE
				DRESS += @"陰毛:%CONDS("陰毛", ARG)%\/"
			ENDIF
		ENDIF
	ELSE
		IF COND("胖次詳細確認", ARG)
			SIF TALENT:ARG:常時Ｖ振動棒
				DRESS += "Ｖ振動棒/"
			SIF TALENT:ARG:常時Ａ肛塞
				DRESS += "Ａ塞子/"
			SIF TALENT:ARG:常時尿道塞
				DRESS += "尿道塞子/"
		ENDIF
	ENDIF
ENDIF

SIF DRESS == ""
	RETURN 0

SIF ARGS == "拡大表示" && EQUIP:ARG:服 && EQUIP:ARG:胖次 && EQUIP:ARG:胸罩
	PRINTL 

CALL PRINT_LIST, DRESS, ARGS

@PRINT_DRESSL, ARG, ARGS
CALL PRINT_DRESS, ARG, ARGS
PRINTL 

@PRINT_DRESSW, ARG, ARGS
CALL PRINT_DRESS, ARG, ARGS
PRINTW 


;-------------------------------------------------
;ARGの衣装全てから条件を判定して文字列を出力する。
;-------------------------------------------------
@CLO(ARGS, ARG)
#FUNCTIONS
#DIMS MEMO_CLO

LOCALS = 
SIF ARG == 0 && TARGET
	ARG = TARGET
SELECTCASE ARGS
CASE "股間"
	IF CHECK_CLO("贴身制服露出", ARG)
		LOCALS = %BODYS(ARG, "略")%
	ELSEIF CHECK_CLO("外套露出", ARG)
		LOCALS = %OUTER(ARG, "略")%
	ELSEIF CHECK_CLO("胖次露出", ARG)
		LOCALS = %PANTIES(ARG, "略")%
	ENDIF
CASE "お尻"
	IF CHECK_CLO("Ｔ字褲臀圍", ARG)
		IF EQUIP:ARG:胖次 && CHECK_CLO("胖次視認可能", ARG)
			IF CHECK_PANTIES("Ｃ胖次", ARG) || CHECK_PANTIES("丁字褲", ARG) || CHECK_PANTIES("兜襠布", ARG)
				RETURNF PANTIES(ARG)
			ELSEIF CHECK_PANTIES("Ｔ字褲", ARG)
				LOCALS = Ｔ字褲の
			ENDIF
		ELSEIF CHECK_OUTER("Ｔ字褲", ARG) || CHECK_BODYS("Ｔ字褲", ARG)
			LOCALS = Ｔ字褲の
		ENDIF
	ENDIF
	IF CHECK_CLO("贴身制服露出", ARG)
		LOCALS += @"%BODYS(ARG, "略")%"
	ELSEIF CHECK_CLO("外套露出", ARG)
		LOCALS += @"%OUTER(ARG, "略")%"
	ELSEIF CHECK_CLO("胖次露出", ARG)
		LOCALS += @"%PANTIES(ARG, "略")%"
	ENDIF
CASE "内褲冲"
	IF EQUIP:ARG:外套 && !CHECK_OUTER("不可手交", ARG)
		LOCALS = %OUTER(ARG, "略")%
	ELSEIF EQUIP:ARG:胖次 && !CHECK_PANTIES("不可手交", ARG)
		LOCALS = %PANTIES(ARG, "略")%
	ENDIF
CASE "掀起裙子", "掀起裙子：詳細"
	SELECTCASE SKIRT(ARG, "略")
	CASE "下摆"
		IF EQUIP:ARG:服 && ARGS == "掀起裙子：詳細"
			LOCALS = %SHIRT(ARG, "略")%の下摆
		ELSE
			LOCALS = %SKIRT(ARG)%
		ENDIF
	CASEELSE
		LOCALS = %SKIRT(ARG, "略")%
	ENDSELECT
CASE "颜绘衣装の色"
	LOCALS = %CLO("颜绘衣装", ARG)%
	RETURNF SUBSTRINGU(LOCALS, STRLENSU(LOCALS) - 1, 1)
CASE "颜绘衣装"
	IF STR:颜绘衣装 != ""
		IF STRCOUNT(STR:颜绘衣装, "/")
			RETURNF SUBSTRING( STR:颜绘衣装, 0, STRFIND(STR:颜绘衣装, "/") )
		ELSE
			RETURNF STR:颜绘衣装
		ENDIF
	ELSEIF ARG == MASTER
		IF STRCOUNT(CLOTH_MAIDFACE_MASTER(), "/")
			RETURNF SUBSTRING( CLOTH_MAIDFACE_MASTER(), 0, STRFIND(CLOTH_MAIDFACE_MASTER(), "/") )
		ELSE
			RETURNF CLOTH_MAIDFACE_MASTER()
		ENDIF
	ENDIF
	;アクセサリの外套その他枠をまず見る
	IF CHECK_OTHER("浴衣", ARG) && EQUIP:ARG:服 == 0
		LOCALS = 浴衣
	ELSEIF CHECK_OTHER("粗呢外套", ARG)
		LOCALS = 粗呢外套%CLOCOLOR_MAIDFACE("粗呢外套", CHECK_CLOCOLOR(OTHER(CSTR:ARG:飾品, "外套") ), ARG )%
		SIF CHECK_SHIRT("水手", ARG)
			LOCALS = 水手%CLOCOLOR_MAIDFACE("水手", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%＆%LOCALS%
	ELSEIF CHECK_OTHER("外套", ARG)
		IF EQUIP:ARG:服 == 0
			LOCALS = 裸外套%CLOCOLOR_MAIDFACE("外套", CHECK_CLOCOLOR(OTHER(CSTR:ARG:飾品, "外套") ), ARG )%
		ELSE
			LOCALS = 外套%CLOCOLOR_MAIDFACE("外套", CHECK_CLOCOLOR(OTHER(CSTR:ARG:飾品, "外套") ), ARG )%
		ENDIF
	ELSEIF CHECK_OTHER("前開連帽風衣", ARG)
		IF EQUIP:ARG:服 == 0
			LOCALS = 恤没有連帽風衣%CLOCOLOR_MAIDFACE("連帽風衣", CHECK_CLOCOLOR(OTHER(CSTR:ARG:飾品, "外套") ), ARG )%
		ELSE
			LOCALS = 前開連帽風衣%CLOCOLOR_MAIDFACE("連帽風衣", CHECK_CLOCOLOR(OTHER(CSTR:ARG:飾品, "外套") ), ARG )%
		ENDIF
	ELSEIF CHECK_OTHER("連帽風衣", ARG)
		LOCALS = 連帽風衣%CLOCOLOR_MAIDFACE("連帽風衣", CHECK_CLOCOLOR(OTHER(CSTR:ARG:飾品, "外套") ), ARG )%
	ELSEIF CHECK_OTHER("褞袍", ARG)
		LOCALS = 褞袍%CLOCOLOR_MAIDFACE("褞袍", CHECK_CLOCOLOR(OTHER(CSTR:ARG:飾品, "外套") ), ARG )%

	;次いで服
	ELSEIF CHECK_SHIRT("Ｔ恤", ARG)
		LOCALS = Ｔ恤%CLOCOLOR_MAIDFACE("Ｔ恤", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("巫女服", ARG)
		LOCALS = 巫女服
	ELSEIF CHECK_SHIRT("弄脏的連衣裙", ARG)
		LOCALS = 弄脏的連衣裙
	ELSEIF CHECK_SHIRT("白色和金色装饰的民族衣装", ARG)
		IF CHECK_SHIRT("腋露出", ARG)
			LOCALS = 白色和金色装饰的民族衣装夏服
		ELSE
			LOCALS = 白色和金色装饰的民族衣装
		ENDIF
	ELSEIF CHECK_SHIRT("红黑色无袖連帽風衣", ARG)
		LOCALS = 红黑色无袖連帽風衣
	ELSEIF CHECK_SHIRT("圍裙", ARG)
		LOCALS = 圍裙
	ELSEIF CHECK_SHIRT("外套", ARG)
		IF EQUIP:ARG:服 == 0
			LOCALS = 裸外套%CLOCOLOR_MAIDFACE("外套", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
		ELSE
			LOCALS = 外套%CLOCOLOR_MAIDFACE("外套", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
		ENDIF
	ELSEIF CHECK_SHIRT("睡衣", ARG)
		LOCALS = 睡衣%CLOCOLOR_MAIDFACE("睡衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("前開連帽風衣", ARG)
		LOCALS = 前開連帽風衣%CLOCOLOR_MAIDFACE("連帽風衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("連帽風衣", ARG)
		LOCALS = 連帽風衣%CLOCOLOR_MAIDFACE("連帽風衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("褞袍", ARG)
		LOCALS = 褞袍%CLOCOLOR_MAIDFACE("褞袍", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("宽大的Ｔ恤", ARG)
		LOCALS = 宽大的Ｔ恤
	ELSEIF CHECK_SHIRT("吊帯衫", ARG)
		LOCALS = 吊帯衫%CLOCOLOR_MAIDFACE("吊帯衫", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("婚纱礼服", ARG)
		LOCALS = 婚纱礼服
	ELSEIF CHECK_SHIRT("修道服", ARG)
		LOCALS = 修道服
	ELSEIF CHECK_SHIRT("护士服", ARG)
		LOCALS = 护士服
	ELSEIF CHECK_SHIRT("针织露肩衣", ARG)
		LOCALS = 针织露肩衣
	ELSEIF CHECK_SHIRT("オフショル", ARG)
		LOCALS = 露肩外衣%CLOCOLOR_MAIDFACE("露肩外衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("无袖毛衣", ARG)
		LOCALS = 无袖毛衣
	ELSEIF CHECK_SHIRT("针织毛衣", ARG)
		LOCALS = 针织毛衣%CLOCOLOR_MAIDFACE("针织毛衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("縦毛衣", ARG)
		LOCALS = 縦毛衣%CLOCOLOR_MAIDFACE("縦毛衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("開胸高領毛衣", ARG)
		LOCALS = 開胸高領毛衣
	ELSEIF CHECK_SHIRT("高領毛衣", ARG)
		LOCALS = 高領毛衣
	ELSEIF CHECK_SHIRT("毛衣", ARG)
		LOCALS = 縦毛衣%CLOCOLOR_MAIDFACE("縦毛衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("哥特蘿莉礼服", ARG)
		LOCALS = 哥特蘿莉礼服%CLOCOLOR_MAIDFACE("哥特蘿莉礼服", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("夏日礼服", ARG)
		LOCALS = 夏日礼服%CLOCOLOR_MAIDFACE("夏日礼服", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("白色連衣裙", ARG)
		LOCALS = 白色連衣裙
	ELSEIF CHECK_SHIRT("哥特礼服", ARG)
		LOCALS = 哥特礼服
	ELSEIF CHECK_SHIRT("哥特連衣裙", ARG)
		LOCALS = 哥特連衣裙
	ELSEIF CHECK_SHIRT("洛丽塔連衣裙", ARG)
		LOCALS = 洛丽塔連衣裙
	ELSEIF CHECK_SHIRT("中世紧身高袍", ARG)
		LOCALS = 中世紧身高袍赤
	ELSEIF CHECK_SHIRT("紧身高袍", ARG)
		LOCALS = 紧身高袍%CLOCOLOR_MAIDFACE("紧身高袍", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("恤夹克", ARG)
		LOCALS = 恤夹克%CLOCOLOR_MAIDFACE("恤夹克", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("前開的衬衫", ARG)
		LOCALS = 前開的衬衫%CLOCOLOR_MAIDFACE("前開的衬衫", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("校服", ARG)
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			LOCALS = 校服Ａ
		CASEELSE
			LOCALS = 校服Ｂ
		ENDSELECT
	ELSEIF CHECK_SHIRT("水手", ARG)
		IF CHECK_OTHER("运动衫", ARG)
			LOCALS = 水手%CLOCOLOR_MAIDFACE("水手", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%＆运动衫%CLOCOLOR_MAIDFACE("运动衫", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
		ELSEIF CHECK_SHIRT("腋露出", ARG)
			LOCALS = 无袖水手%CLOCOLOR_MAIDFACE("水手", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
		ELSE
			LOCALS = 水手%CLOCOLOR_MAIDFACE("水手", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
		ENDIF
	ELSEIF CHECK_SHIRT("背心", ARG)
		LOCALS = 背心%CLOCOLOR_MAIDFACE("背心", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("V领毛衣", ARG)
		LOCALS = V领毛衣%CLOCOLOR_MAIDFACE("V领毛衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("西装外衣", ARG)
		LOCALS = 西装外衣%CLOCOLOR_MAIDFACE("西装外衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("褶边女式衬衫", ARG)
		LOCALS = 褶边女式衬衫%CLOCOLOR_MAIDFACE("褶边女式衬衫", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("无袖女式衬衫", ARG)
		IF CHECK_OTHER("肩出し夹克", ARG)
			LOCALS = 无袖女式衬衫＆肩出し夹克%CLOCOLOR_MAIDFACE("肩出し夹克", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
		ELSEIF CHECK_SHIRT("无袖女式衬衫Ａ", ARG)
			LOCALS = 无袖女式衬衫Ａ
		ELSEIF CHECK_SHIRT("无袖女式衬衫Ｂ", ARG) || AGE(ARG) >= 3
			LOCALS = 无袖女式衬衫Ｂ
		ELSE
			LOCALS = 无袖女式衬衫Ａ
		ENDIF
	ELSEIF CHECK_SHIRT("宽松女式衬衫", ARG) || CHECK_SHIRT("没系扣子的女式衬衫", ARG) || CHECK_SHIRT(@"%CALLNAME:MASTER%の衬衫", ARG)
		LOCALS = 宽松女式衬衫
	ELSEIF CHECK_SHIRT("制服", ARG)
		IF TALENT:ARG:男性
			LOCALS = 領帯女式衬衫黒
		ELSE
			LOCALS = 西装外衣灰色
		ENDIF
	ELSEIF CHECK_SHIRT("領帯女式衬衫", ARG)
		LOCALS = 領帯女式衬衫%CLOCOLOR_MAIDFACE("領帯女式衬衫", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("衬衫", ARG) || CHECK_SHIRT("女式衬衫", ARG)
		IF CHECK_OTHER("蝴蝶結領帯丝巾", ARG)
			LOCALS = 蝴蝶結女式衬衫%CLOCOLOR_MAIDFACE("蝴蝶結女式衬衫", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
		ELSE
			LOCALS = 女式衬衫白
		ENDIF
	ELSEIF CHECK_SHIRT("学生服", ARG)
		LOCALS = 領帯女式衬衫赤
	ELSEIF CHECK_SHIRT("运动衫", ARG) || CHECK_SHIRT("体操服", ARG)
		IF DAYEV("夏服期間") || CHECK_SHIRT("体操服", ARG) || CHECK_PANTIES("布鲁马", ARG) || CHECK_OUTER("布鲁马", ARG) || CEVENTS("着替えをした", ARG) == "泳装"
			LOCALS = 运动衫%CLOCOLOR_MAIDFACE("运动衫", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
		ELSE
			LOCALS = 詰襟运动衫%CLOCOLOR_MAIDFACE("詰襟运动衫", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
		ENDIF
	ELSEIF CHECK_SHIRT("執事服", ARG)
		LOCALS = 執事服%CLOCOLOR_MAIDFACE("執事服", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("女仆装", ARG)
		LOCALS = 女仆装%CLOCOLOR_MAIDFACE("女仆装", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("胸開き旗袍", ARG)
		LOCALS = 胸開き旗袍%CLOCOLOR_MAIDFACE("胸開き旗袍", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("旗袍", ARG)
		LOCALS = 旗袍%CLOCOLOR_MAIDFACE("旗袍", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("情趣内衣")
		LOCALS = 情趣内衣%CLOCOLOR_MAIDFACE("情趣内衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("晨衣")
		LOCALS = 晨衣%CLOCOLOR_MAIDFACE("晨衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("振袖", ARG)
		LOCALS = 振袖%CLOCOLOR_MAIDFACE("振袖", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("浴衣", ARG)
		LOCALS = 浴衣%CLOCOLOR_MAIDFACE("浴衣", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("和服", ARG) || CHECK_SHIRT("和服", ARG)
		LOCALS = 和服%CLOCOLOR_MAIDFACE("和服", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CHECK_SHIRT("啦啦队制服", ARG)
		LOCALS = 啦啦队制服
	ELSEIF CHECK_SHIRT("吊帯背心", ARG)
		LOCALS = 吊帯背心%CLOCOLOR_MAIDFACE("吊帯背心", CHECK_CLOCOLOR(CSTR:ARG:服), ARG)%
	ELSEIF CLO("颜绘衣装：内衣と泳装", ARG) != ""
		LOCALS = %CLO("颜绘衣装：内衣と泳装", ARG)%
	ELSEIF EQUIP:ARG:服 || EQUIP:ARG:贴身制服
		LOCALS = 恤
	ENDIF
CASE "颜绘衣装：内衣と泳装"
	IF CHECK_BODYS("拘束衣", ARG)
		LOCALS = 拘束衣%CLOCOLOR_MAIDFACE("拘束衣", CHECK_CLOCOLOR(CSTR:ARG:贴身制服), ARG)%
	ELSEIF CHECK_BODYS("兔", ARG)
		LOCALS = 兔女郎制服%CLOCOLOR_MAIDFACE("兔女郎制服", CHECK_CLOCOLOR(CSTR:ARG:贴身制服), ARG)%
	ELSEIF CHECK_BODYS("競泳泳装", ARG)
		LOCALS = 競泳%CLOCOLOR_MAIDFACE("競泳泳装", CHECK_CLOCOLOR(CSTR:ARG:贴身制服), ARG)%
	ELSEIF CHECK_BODYS("死库水", ARG)
		SELECTCASE CSTR:ARG:学年
		CASE "初等部", "中等部"
			LOCALS = 新死库水紺色
		CASEELSE
			LOCALS = 死库水青
		ENDSELECT
	ELSEIF CHECK_BODYS("連体泳装", ARG) || CHECK_BODYS("拘束衣", ARG) || CHECK_BODYS("緊身内衣", ARG)
		LOCALS = 连体泳装黒
	ELSEIF CHECK_BODYS("泳装", ARG)
		LOCALS = 抹胸赤
	ELSEIF EQUIP:ARG:胸罩 && EQUIP:ARG:服 == 0
		IF CHECK_BRA("吊帯衫", ARG)
			SELECTCASE CLOCOLOR(CSTR:ARG:胸罩)
			CASE "真紅", "赤", "薄桃色"
				LOCALS = 情趣内衣赤
			CASE "緑色", "薄緑"
				LOCALS = 情趣内衣緑色
			CASE "黒", "紺色", "豹纹"
				LOCALS = 情趣内衣黒
			CASE "黄色", "橙色"
				LOCALS = 内衣橙
			CASE "紫", "青", "薄青色"
				LOCALS = 内衣青
			CASEELSE
				LOCALS = 内衣白
			ENDSELECT
		ELSEIF CHECK_BRA("吊帯背心", ARG)
			SELECTCASE CLOCOLOR(CSTR:ARG:胸罩)
			CASE "真紅", "赤", "薄桃色"
				LOCALS = 吊帯背心赤
			CASE "紫", "青", "薄青色", "紺色"
				LOCALS = 吊帯背心青
			CASE "白", "純白"
				LOCALS = 吊帯背心白
			CASEELSE
				LOCALS = 吊帯背心灰色
			ENDSELECT
		ELSE
			SELECTCASE CLOCOLOR(CSTR:ARG:胸罩)
			CASE "黒", "豹纹"
				LOCALS = 内衣黒
			CASE "真紅", "赤", "薄桃色"
				LOCALS = 内衣桃
			CASE "緑色", "薄緑"
				LOCALS = 内衣緑色
			CASE "黄色", "橙色"
				LOCALS = 内衣橙
			CASE "紫", "青", "薄青色", "紺色"
				LOCALS = 内衣青
			CASEELSE
				LOCALS = 内衣白
			ENDSELECT
		ENDIF
	ENDIF
;@CLO_ABOUTと近いが、こちらは可能な限り短く出力する
CASE "現在的格好"
	IF CHECK_OTHER("外套", ARG)
		LOCALS = %OTHER(CSTR:ARG:飾品, "外套")%姿
	ELSEIF CHECK_CLO("学生服姿", ARG)
		LOCALS = 制服姿
	ELSEIF CHECK_CLO("体操服姿", ARG)
		LOCALS = 体操服姿
	ELSEIF CHECK_CLO("比基尼姿", ARG)
		IF CHECK_CLO("Micro比基尼姿", ARG)
			LOCALS = Micro比基尼姿
		ELSE
			LOCALS = 比基尼姿
		ENDIF
	ELSEIF CHECK_CLO("泳装姿", ARG)
		LOCALS = 泳装姿
	ELSEIF CHECK_CLO("内衣姿", ARG)
		LOCALS = 内衣姿
	ELSEIF CHECK_CLO("裸体", ARG)
		LOCALS = 裸体
	ELSEIF CFLAG:ARG:Cosplay中
		IF EQUIP:ARG:服
			IF CHECK_SHIRT("の服", ARG)
				LOCALS = %EXTSTR(SHIRT(ARG), "", "の服")%姿
			ELSE
				LOCALS = %SHIRT(ARG)%姿
			ENDIF
		ELSEIF EQUIP:ARG:贴身制服
			LOCALS = %BODYS(ARG)%姿
		ENDIF
	ENDIF
CASE "現在的格好＆の"
	SIF CLO("現在的格好", ARG) != ""
		LOCALS = %CLO("現在的格好", ARG)%の
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;ARGの身につけている衣装全てを総合して、ARGSが可能かを判定する
;-------------------------------------------------
@CHECK_CLO(ARGS, ARG)
#FUNCTION

SIF ARG == 0 && TARGET
	ARG = TARGET
;下にはパンツなど、セットたりえるもの必須
SELECTCASE ARGS
CASE "萌え袖可能"
	;詰襟ジャージは無しにしておく
	SIF STRCOUNTS(STR:颜绘衣装, "詰襟运动衫") || CHECK_SHIRT("詰襟运动衫", ARG)
		RETURNF 0

	SIF STRCOUNTS(STR:颜绘衣装, "V领毛衣", "运动衫", "睡衣", "肩出し夹克")
		RETURNF 1
	SIF CHECK_SHIRT("V领毛衣", ARG) || CHECK_SHIRT("运动衫", ARG) || CHECK_SHIRT("睡衣", ARG)
		RETURNF 1
	SIF CHECK_SHIRT("无袖女式衬衫", ARG) && CHECK_OTHER("肩出し夹克", ARG)
		RETURNF 1

CASE "CIP汎用正面外套"
	SIF CHECK_OUTER("布鲁马", ARG)
		RETURNF 1
CASE "CIP汎用正面贴身制服"
	SIF CHECK_BODYS("兔", ARG) || CHECK_BODYS("競泳泳装", ARG)
		RETURNF 1
CASE "CIP汎用正面胖次"
	SELECTCASE PANTIES(ARG, "CIP正面")
	CASE "內褲", "三角胖次", "褶边", "Micro內褲", "高開衩", "低腰", "超低腰", "兜襠布", "四角内褲", "叶子胖次", "高開衩布鲁马", "襯褲"
		RETURNF 1
	ENDSELECT
CASE "CIPすじ"
	SELECTCASE PANTIES(ARG, "CIP正面")
	CASE "前貼", "すじ"
		RETURNF 1
	ENDSELECT
CASE "ＣＩＰ発生"
	SIF CFLAG:ARG:ＣＩＰ発生済
		RETURNF 0
	SIF EQUIP:ARG:裙
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CFLAG:ARG:Cosplay中 == 0
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("连褲襪", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("男物", ARG)
		RETURNF 0
	RETURNF 1
CASE "自分の服"
	SIF COND("便装に着替えた", ARG)
		RETURNF 1
CASE "没穿外衣"
	SIF EQUIP:ARG:胖次 + EQUIP:ARG:裙 + EQUIP:ARG:外套 && EQUIP:ARG:胸罩 + EQUIP:ARG:服 + EQUIP:ARG:贴身制服 == 0
		RETURNF 1
;上にはブラなど、セットたりえるもの必須
CASE "没穿下衣"
	SIF EQUIP:ARG:服 + EQUIP:ARG:胸罩 && EQUIP:ARG:胖次 + EQUIP:ARG:裙 + EQUIP:ARG:贴身制服 + EQUIP:ARG:外套 == 0
		RETURNF 1
;何かを脱いでトップレス状態になれるか
CASE "没穿外衣可能"
	;ワンピースの場合は脱いだ後にパンツかズボンが必要
	IF CHECK_SHIRT("連衣裙", ARG)
		SIF EQUIP:ARG:胖次 + EQUIP:ARG:外套
			RETURNF 1
	ELSE
		SIF EQUIP:ARG:胸罩 + EQUIP:ARG:服 == 0
			RETURNF 0
		SIF EQUIP:ARG:胖次 + EQUIP:ARG:裙 + EQUIP:ARG:外套
			RETURNF 1
	ENDIF
CASE "没穿下衣可能"
	;ワンピースの場合は脱いだ後にブラが必要
	IF CHECK_SHIRT("連衣裙", ARG)
		SIF EQUIP:ARG:胸罩
			RETURNF 1
	ELSE
		SIF EQUIP:ARG:胖次 + EQUIP:ARG:裙 + EQUIP:ARG:外套 == 0
			RETURNF 0
		SIF EQUIP:ARG:胸罩 + EQUIP:ARG:服
			RETURNF 1
	ENDIF
CASE "裙脱衣可能"
	SIF CHECK_SHIRT("連衣裙", ARG)
		RETURNF 0
	SIF EQUIP:ARG:裙
		RETURNF 1
;スカートなど、パンツが見えない状態必須。ノーパン＆パンストならOKとする
CASE "没穿内褲"
	SIF EQUIP:ARG:服 + EQUIP:ARG:裙 == 0
		RETURNF 0
	SIF EQUIP:ARG:胖次
		RETURNF 0
	IF EQUIP:ARG:外套
		;パンスト
		SIF CHECK_OUTER("连褲襪", ARG) || CHECK_OUTER("開洞", ARG) == 0
			RETURNF 1
	ELSE
		RETURNF 1
	ENDIF
	
CASE "全裸"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次 + EQUIP:ARG:胸罩 + EQUIP:ARG:服 + EQUIP:ARG:裙 + EQUIP:ARG:贴身制服 + EQUIP:ARG:外套
		RETURNF 0
	SIF EQUIP:ARG:襪子 + EQUIP:ARG:靴 + EQUIP:ARG:手袋
		RETURNF 0
	RETURNF 1
CASE "裸体"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次 + EQUIP:ARG:胸罩 + EQUIP:ARG:服 + EQUIP:ARG:裙 + EQUIP:ARG:贴身制服 + EQUIP:ARG:外套
		RETURNF 0
	RETURNF 1
CASE "内衣姿"
	SIF EQUIP:ARG:胖次 + EQUIP:ARG:胸罩 && EQUIP:ARG:服 + EQUIP:ARG:裙 + EQUIP:ARG:贴身制服 + EQUIP:ARG:外套 == 0
		RETURNF 1
CASE "ランジェリー姿"
	SIF CHECK_SHIRT("ランジェリー", ARG)
		RETURNF 1
	IF EQUIP:ARG:服 == 0
		SIF CHECK_BRA("ランジェリー", ARG) && CHECK_PANTIES("ランジェリー", ARG)
			RETURNF 1
	ENDIF
;スカートの下にズボン類を履いていて、それが周りからも分かる状態。ボトムレイヤード。
CASE "埴輪ルック"
	SIF CHECK_SKIRT("裙", ARG) == 0
		RETURNF 0
	SIF CHECK_OUTER("丈長長褲", ARG)
		RETURNF 1
;スカート履いてないとか色々です
CASE "胖次が見える格好"
	SIF EQUIP:ARG:胖次 == 0
		RETURNF 0
	SIF CHECK_PANTIES("泳装", ARG)
		RETURNF 0
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:裙
		RETURNF 0
	SIF CHECK_CLO("胖次露出", ARG) == 0
		RETURNF 0
	RETURNF 1
CASE "胸罩が見える格好"
	SIF EQUIP:ARG:胸罩 == 0
		RETURNF 0
	SIF CHECK_PANTIES("泳装", ARG)
		RETURNF 0
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:服
		RETURNF 0
	RETURNF 1
CASE "裤吊带", "褶边裙吊带"
	SIF CHECK_SHIRT(ARGS, ARG) || CHECK_OUTER(ARGS, ARG) || CHECK_OTHER(ARGS, ARG)
		RETURNF 1
CASE "学生服姿"
	SIF CHECK_SHIRT("学生服", ARG) || CHECK_SHIRT("水手", ARG) || CHECK_SHIRT("西装外衣", ARG)
		RETURNF 1
CASE "体操服姿"
	SIF EQUIP:ARG:裙 && CHECK_SKIRT("裙", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("泳装", ARG)
		RETURNF 0
	SIF CHECK_SHIRT("体操服", ARG) || CHECK_SHIRT("运动衫", ARG)
		RETURNF 1
CASE "泳装姿"
	SIF EQUIP:ARG:服 + EQUIP:ARG:裙 + EQUIP:ARG:外套
		RETURNF 0
	IF EQUIP:ARG:贴身制服 && CHECK_BODYS("泳装", ARG)
		RETURNF 1
	ELSEIF EQUIP:ARG:胖次
		SIF EQUIP:ARG:胸罩 && CHECK_BRA("泳装", ARG) == 0
			RETURNF 0
		SIF CHECK_PANTIES("泳装", ARG)
			RETURNF 1
	ELSEIF EQUIP:ARG:胸罩
		SIF CHECK_BRA("泳装", ARG)
			RETURNF 1
	ENDIF
CASE "比基尼姿"
	SIF EQUIP:ARG:服 + EQUIP:ARG:裙 + EQUIP:ARG:贴身制服 + EQUIP:ARG:外套
		RETURNF 0
	IF EQUIP:ARG:胖次
		SIF EQUIP:ARG:胸罩 && CHECK_BRA("泳装", ARG) == 0
			RETURNF 0
		SIF CHECK_PANTIES("泳装", ARG)
			RETURNF 1
	ELSEIF EQUIP:ARG:胸罩
		SIF CHECK_BRA("泳装", ARG)
			RETURNF 1
	ENDIF
CASE "Micro比基尼姿"
	SIF CHECK_CLO("比基尼姿", ARG) == 0
		RETURNF 0
	SIF CHECK_BRA("Micro", ARG) && CHECK_PANTIES("Micro", ARG)
		RETURNF 1
CASE "外套直穿き"
	SIF EQUIP:ARG:外套 && EQUIP:ARG:胖次 == 0
		RETURNF 1
CASE "上下が揃ってない"
	SIF CHECK_CLO("没穿下衣", ARG) || CHECK_CLO("没穿外衣", ARG)
		RETURNF 1
	SIF EQUIP:ARG:服 && CHECK_CLO("胖次が見える格好", ARG)
		RETURNF 1
	SIF EQUIP:ARG:服 == 0 && EQUIP:ARG:胸罩 && EQUIP:ARG:裙 + EQUIP:ARG:外套
		RETURNF 1

CASE "電源BOX保持可能"
	IF EQUIP:ARG:外套
		;クロッチありパンスト着用時は無理
 		SIF CHECK_OUTER("连褲襪", ARG) && CHECK_OUTER("開洞", ARG)
			RETURNF 0
		SIF CHECK_OUTER("長褲", ARG) && CHECK_OUTER("可从缝隙偷窥的", ARG) == 0
			RETURNF 0
	ENDIF
	SIF CHECK_SOCKS("直到大腿", ARG) || CHECK_OTHER("腿环", ARG)
		RETURNF 1

CASE "開洞見せ可能"
	SIF EQUIP:ARG:胖次 == 0 || TALENT:ARG:男性
		RETURNF 0
	SIF CHECK_PANTIES("男物", ARG) || CHECK_PANTIES("開洞", ARG) == 0 || CHECK_PANTIES("泳装", ARG) || CHECK_PANTIES("半脱ぎ時太もも保持可能", ARG) == 0
		RETURNF 0
	RETURNF 1
CASE "内褲冲", "胖次コキ"
	SIF EQUIP:ARG:贴身制服
		RETURNF 0
	IF EQUIP:ARG:外套
		SIF !CHECK_OUTER("不可手交", ARG)
			RETURNF 1
	ELSEIF EQUIP:ARG:胖次 && !CHECK_PANTIES("不可手交", ARG)
		RETURNF 1
	ENDIF
CASE "闷热的"
	IF EQUIP:ARG:贴身制服 && CHECK_BODYS("闷热的", ARG)
		RETURNF 1
	ELSEIF EQUIP:ARG:外套 && CHECK_OUTER("闷热的", ARG)
		RETURNF 1
	ELSEIF EQUIP:ARG:胖次 && CHECK_PANTIES("闷热的", ARG)
		RETURNF 1
	ENDIF
CASE "带污迹的", "湿的"
	IF EQUIP:ARG:贴身制服
		SIF CHECK_BODYS("带污迹的", ARG)
			RETURNF 1
	ELSEIF EQUIP:ARG:外套
		SIF CHECK_OUTER("带污迹的", ARG)
			RETURNF 1
	ELSEIF EQUIP:ARG:胖次
		SIF CHECK_PANTIES("带污迹的", ARG)
			RETURNF 1
	ENDIF
;ブラなど、胸を覆うものは"はだけ"る
CASE "半脱可"
	IF EQUIP:ARG:服 && CHECK_SHIRT("半脱可", ARG)
		RETURNF 1
	ELSEIF EQUIP:ARG:贴身制服 && CHECK_BODYS("半脱可", ARG)
		RETURNF 1
	ELSEIF EQUIP:ARG:胸罩 && CHECK_BRA("半脱可", ARG)
		SIF EQUIP:ARG:服 && CHECK_SHIRT("半脱不可", ARG)
			RETURNF 0
		SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("半脱不可", ARG)
			RETURNF 0
		RETURNF 1
	ENDIF
CASE "谷間露出"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:服 && !CHECK_SHIRT("半脱的", ARG) && !CHECK_SHIRT("谷間露出", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && !CHECK_BODYS("半脱的", ARG) && !CHECK_BODYS("谷間露出", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胸罩 && !CHECK_BRA("半脱的", ARG) && !CHECK_BRA("谷間露出", ARG)
		RETURNF 0
	RETURNF 1
CASE "乳輪露出"
	;乳首露出なら当然ＯＫ
	SIF CHECK_CLO("乳頭露出", ARG)
		RETURNF 1
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	;以下は巨乳輪でないと起きない
	SIF COND("巨乳輪", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:服 && !CHECK_SHIRT("半脱的", ARG)
		RETURNF 0
	IF EQUIP:ARG:贴身制服 && !CHECK_BODYS("半脱的", ARG)
		SIF CHECK_BODYS("半杯", ARG)
			RETURNF 1
	ENDIF
	IF EQUIP:ARG:胸罩 && !CHECK_BRA("半脱的", ARG)
		SIF CHECK_BRA("半杯", ARG) || CHECK_BRA("Micro", ARG)
			RETURNF 1
	ENDIF
	RETURNF 0
CASE "乳頭露出"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:服 && !CHECK_SHIRT("半脱的", ARG) && !CHECK_SHIRT("乳頭露出", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && !CHECK_BODYS("半脱的", ARG) && !CHECK_BODYS("乳頭露出", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胸罩 && !CHECK_BRA("半脱的", ARG) && !CHECK_BRA("乳頭露出", ARG)
		RETURNF 0
	RETURNF 1
CASE "ずらし可", "ずらせる"
	IF EQUIP:ARG:贴身制服
		SIF CHECK_BODYS("ずらせる", ARG)
			RETURNF 1
	ELSEIF EQUIP:ARG:外套
		SIF CHECK_OUTER("ずらせる", ARG)
			RETURNF 1
	ELSEIF EQUIP:ARG:胖次
		SIF CHECK_PANTIES("ずらせる", ARG)
			RETURNF 1
	ENDIF
CASE "布鲁马露出"
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 0
	IF EQUIP:ARG:外套
		SIF CHECK_OUTER("布鲁马", ARG) && CHECK_OUTER("開洞", ARG)
			RETURNF 1
	ELSE
		SIF CHECK_PANTIES("布鲁马", ARG) && CHECK_PANTIES("開洞", ARG)
			RETURNF 1
	ENDIF
CASE "布鲁马はみ胖次可能"
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 0
	IF EQUIP:ARG:外套 && CHECK_OUTER("布鲁马", ARG) && CHECK_OUTER("開洞", ARG)
		SIF CHECK_OUTER("高開衩布鲁马", ARG)
			RETURNF 0
		SIF EQUIP:ARG:胖次 && CHECK_PANTIES("はみ胖次可能", ARG)
			RETURNF 1
	ENDIF
CASE "外套露出"
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG)
		RETURNF 1
CASE "贴身制服露出"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 1
CASE "连褲襪露出"
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("连褲襪", ARG) && CHECK_OUTER("開洞", ARG)
		RETURNF 1
CASE "胸罩露出"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:服 && !CHECK_SHIRT("半脱的", ARG)
		RETURNF 0
	RETURNF 1
CASE "胖次露出"
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次
		RETURNF 1
CASE "胖次視認可能"
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG) && CHECK_OUTER("连褲襪", ARG) == 0
		RETURNF 0
	RETURNF 1
CASE "胖次を脱掉のが難儀"
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG)
		RETURNF 1
CASE "胖次を脱いだらＶ露出"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:裙
		RETURNF 0
	RETURNF 1

;例えば裾長めのシャツを着ていて下がズボン、など
CASE "掀起裙子の価値无"
	SIF CHECK_SKIRT("下摆", ARG) && CHECK_OUTER("長褲", ARG)
		RETURNF 1
	SIF CHECK_CLO("埴輪ルック", ARG)
		RETURNF 1
;未確認は調教中は使わない
CASE "外套未確認"
	SIF CFLAG:ARG:股間確認済
		RETURNF 0
	;パンストとズボンは例外
	SIF CHECK_OUTER("连褲襪", ARG) || CHECK_OUTER("打底褲", ARG) || CHECK_OUTER("長褲", ARG)
		RETURNF 0
	SIF EQUIP:ARG:裙
		RETURNF 1
CASE "胖次未確認"
	SIF COND("胖次形状確認", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 1
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG)
		RETURNF 1
	SIF EQUIP:ARG:裙 && CHECK_SKIRT("透明", ARG) == 0
		RETURNF 1
CASE "胸罩未確認"
	SIF COND("胸罩形状確認", ARG)
		RETURNF 0
	SIF EQUIP:ARG:服 && !CHECK_SHIRT("半脱的", ARG) && !CHECK_SHIRT("谷間露出", ARG) && CHECK_SHIRT("透明", ARG) == 0
		RETURNF 1
	SIF EQUIP:ARG:贴身制服 && !CHECK_BODYS("半脱的", ARG) && !CHECK_BODYS("谷間露出", ARG)
		RETURNF 1

;ボディスーツやパンツでは陰毛が隠しきれていません
CASE "ハミ毛", "ハミ毛：裙無視"
	SELECTCASE ARGS
	CASE "ハミ毛：裙無視"
	CASEELSE
		SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
			RETURNF 0
		SIF EQUIP:ARG:裙
			RETURNF 0
	ENDSELECT

	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG)
		RETURNF 0
	;ここでの陰毛はモサモサ度依存なので外見年齢が若い場合は若干考慮される
	IF EQUIP:ARG:贴身制服
		;ハイレグ＆(ナチュラルor生え放題)
		IF CHECK_BODYS("高開衩", ARG)
			SELECTCASE COND("陰毛", ARG)
			CASE IS >= 7
				RETURNF 1
			CASE 6
				;お姉さん以上でナチュラル
				SIF AGE(ARG) >= 3
					RETURNF 1
			ENDSELECT
		ENDIF
	ELSEIF EQUIP:ARG:胖次
		;生え放題は常時はみ毛
		SIF COND("陰毛", ARG) >= 7
			RETURNF 1

		;マイクロは生えてたら見える
		IF CHECK_PANTIES("Micro", ARG)
			RETURNF MAX(COND("陰毛", ARG) - 3, 0)
		;ローライズは整えている程度だと見える
		ELSEIF CHECK_PANTIES("低腰", ARG) || CHECK_PANTIES("Ｃ胖次", ARG)
			SELECTCASE COND("陰毛", ARG)
			CASE 5, 6
				;お姉さん以上なら見える
				SIF AGE(ARG) >= 3
					RETURNF 1
			ENDSELECT
		;ハイレグは剃ってないと見える
		ELSEIF CHECK_PANTIES("高開衩", ARG) || CHECK_PANTIES("淚珠泳衣", ARG)
			SELECTCASE COND("陰毛", ARG)
			CASE 6
				;お姉さん以上なら見える
				SIF AGE(ARG) >= 3
					RETURNF 1
			ENDSELECT
		ENDIF
	ENDIF
CASE "胖次ハミ毛"
	SIF EQUIP:ARG:胖次 == 0
		RETURNF 0
	;生え放題は常時はみ毛
	SIF COND("陰毛", ARG) >= 7
		RETURNF 1

	;マイクロは生えてたら見える
	IF CHECK_PANTIES("Micro", ARG)
		RETURNF MAX(COND("陰毛", ARG) - 3, 0)
	;ローライズは整えている程度だと見える
	ELSEIF CHECK_PANTIES("低腰", ARG) || CHECK_PANTIES("Ｃ胖次", ARG)
		SELECTCASE COND("陰毛", ARG)
		CASE 5, 6
			;お姉さん以上なら見える
			SIF AGE(ARG) >= 3
				RETURNF 1
		ENDSELECT
	;ハイレグは剃ってないと見える
	ELSEIF CHECK_PANTIES("高開衩", ARG) || CHECK_PANTIES("淚珠泳衣", ARG)
		SELECTCASE COND("陰毛", ARG)
		CASE 6
			;お姉さん以上なら見える
			SIF AGE(ARG) >= 3
				RETURNF 1
		ENDSELECT
	ENDIF
CASE "透け毛"
	;生えてない
	SIF COND("陰毛", ARG) <= 3
		RETURNF 0
	;ハミ毛ならそっち優先
	SIF CHECK_CLO("ハミ毛", ARG)
		RETURNF 0
	SIF EQUIP:ARG:裙
		RETURNF 0
	IF EQUIP:ARG:贴身制服
		SIF CHECK_BODYS("透明", ARG)
			RETURNF 1
	ELSEIF EQUIP:ARG:外套
		SIF CHECK_OUTER("连褲襪", ARG) && EQUIP:ARG:胖次 == 0
			RETURNF 1
	ELSEIF EQUIP:ARG:胖次
		SIF CHECK_PANTIES("透明", ARG)
			RETURNF 1
	ENDIF
	RETURNF 0
;ハミ毛や透け毛ではなく、全体が完全に見えている状態
CASE "陰毛露出"
	;生えてない
	SIF COND("陰毛", ARG) <= 3
		RETURNF 0
	;ハミ毛や透け毛状態ならそっち優先
	SIF CHECK_CLO("ハミ毛", ARG) || CHECK_CLO("透け毛", ARG)
		RETURNF 0
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:裙 || EQUIP:ARG:贴身制服 || EQUIP:ARG:胖次 || EQUIP:ARG:外套
		RETURNF 0
	RETURNF 1
CASE "勒紧的"
	;オトコだと食い込みは無理かなぁ
	SIF TALENT:ARG:男性
		RETURNF 0
	SIF EQUIP:ARG:胖次 + EQUIP:ARG:贴身制服 + EQUIP:ARG:外套 == 0
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("勒紧的", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("勒紧的", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("勒紧的", ARG) == 0
		RETURNF 0
	RETURNF 1
;Ｖ露出とは異なるのに注意
CASE "大陰唇露出"
	SIF TALENT:ARG:男性
		RETURNF 0
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次 + EQUIP:ARG:贴身制服 + EQUIP:ARG:外套 == 0
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("勒紧的", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("勒紧的", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("勒紧的", ARG) == 0
		RETURNF 0
	RETURNF 1
;ボディスーツやパンツでは鼠径部が隠しきれていません
CASE "鼠径部露出"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:裙
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG) && CHECK_OUTER("高開衩", ARG) == 0
		RETURNF 0
	IF EQUIP:ARG:贴身制服
		SIF CHECK_BODYS("高開衩", ARG)
			RETURNF 1
	ELSEIF EQUIP:ARG:胖次
		SIF CHECK_PANTIES("Micro", ARG) || CHECK_PANTIES("高開衩", ARG) || CHECK_PANTIES("Ｃ胖次", ARG)
			RETURNF 1
	ENDIF
;下腹部から下が全部見えていて、明らかに何も着けていないとみて分かる状態です
CASE "股間露出"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:裙
		RETURNF 0
	SIF EQUIP:ARG:贴身制服
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("開洞", ARG)
		RETURNF 0
	RETURNF 1
;ガーターやオープンパンスト、ニーソックスなどです
CASE "股間だけ露出"
	SIF CHECK_SOCKS("直到大腿", ARG)
		RETURNF 1
	SIF CHECK_OUTER("连褲襪", ARG) && CHECK_OUTER("Ｖ露出", ARG)
		RETURNF 1
;淫紋が見えている状態です
CASE "淫紋露出"
	;淫紋が無ければ勿論駄目
	SIF MARK:ARG:淫紋 == 0
		RETURNF 0
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 || EQUIP:ARG:裙
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("低腰", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:服 && CHECK_SHIRT("露脐", ARG) == 0
		RETURNF 0
	RETURNF 1
CASE "淫紋露出：裙無視"
	;淫紋が無ければ勿論駄目
	SIF MARK:ARG:淫紋 == 0
		RETURNF 0
	SIF EQUIP:ARG:贴身制服
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("低腰", ARG) == 0
		RETURNF 0
	RETURNF 1
CASE "露脐"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:服 && CHECK_SHIRT("露脐", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("露脐", ARG) == 0
		RETURNF 0
	RETURNF 1
;ローライズなアウター（一般にはズボン）を履いていて、お尻の上半分が見えている状態です。パンツは加味しない
CASE "低腰臀圍"
	SIF EQUIP:ARG:外套 == 0 || CHECK_OUTER("低腰", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:飾品 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	RETURNF 1
CASE "Ｔ字褲臀圍"
	IF EQUIP:ARG:外套
		SIF CHECK_OUTER("Ｔ字褲", ARG) == 0
			RETURNF 0
	ENDIF
	IF EQUIP:ARG:贴身制服
		SIF CHECK_BODYS("Ｔ字褲", ARG) == 0
			RETURNF 0
	ENDIF
	IF EQUIP:ARG:胖次
		SIF CHECK_PANTIES("Ｔ字褲", ARG) == 0
			RETURNF 0
	ENDIF
	SIF CHECK_OUTER("Ｔ字褲", ARG) || CHECK_BODYS("Ｔ字褲", ARG) || CHECK_PANTIES("Ｔ字褲", ARG)
		RETURNF 1
CASE "後開型臀圍"
	IF EQUIP:ARG:外套
		SIF CHECK_OUTER("後開型", ARG) == 0 && CHECK_OUTER("连褲襪", ARG) == 0
			RETURNF 0
	ENDIF
	IF EQUIP:ARG:贴身制服
		SIF CHECK_BODYS("後開型", ARG) == 0
			RETURNF 0
	ENDIF
	IF EQUIP:ARG:胖次
		SIF CHECK_PANTIES("後開型", ARG) == 0
			RETURNF 0
	ENDIF
	SIF CHECK_OUTER("後開型", ARG) || CHECK_BODYS("後開型", ARG) || CHECK_PANTIES("後開型", ARG)
		RETURNF 1
;主に"アソコのチラ見せ撮影"の判定で使う
CASE "めくり可能"
	SIF EQUIP:ARG:胖次 + EQUIP:ARG:外套 + EQUIP:ARG:贴身制服 == 0
		RETURNF 0
	SIF CHECK_CLO("Ｖ露出", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("めくり可能", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("めくり可能", ARG) == 0
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("めくり可能", ARG) == 0
		RETURNF 0
	RETURNF 1
CASE "带拉链的Ｖ露出可能"
	;すでに露出していたら駄目
	SIF CHECK_CLO("Ｖ露出", ARG)
		RETURNF 0
	SIF CHECK_BODYS("带拉链的", ARG) || CHECK_OUTER("带拉链的", ARG) || CHECK_PANTIES("带拉链的", ARG)
		RETURNF 1

CASE "Ｐ露出"
	SIF PENIS(ARG) == 0
		RETURNF 0
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("開洞", ARG)
		RETURNF 0
	RETURNF 1
CASE "Ｖ露出"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("開洞", ARG)
		RETURNF 0
	RETURNF 1
;これはＶ露出を含む
CASE "Ｖ露出可"
	;調教外ならＯＫとする
	SIF COND("調教中", ARG) == 0 && CHECK_PANTIES("脱衣不可", ARG) == 0
		RETURNF 1
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("開洞", ARG) && !CHECK_BODYS("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && !CHECK_OUTER("连褲襪", ARG) && CHECK_OUTER("開洞", ARG) && !CHECK_OUTER("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("開洞", ARG) && !CHECK_PANTIES("ずらせる", ARG)
		RETURNF 0
	RETURNF 1
CASE "Ａ露出"
	SIF COND("調教中", ARG) == 0 && CHECK_OTHER("外套", ARG)
		RETURNF 0
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("Ａ開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && CHECK_OUTER("Ａ開洞", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("Ａ開洞", ARG)
		RETURNF 0
	RETURNF 1
;これはＡ露出を含む
CASE "Ａ露出可"
	;調教外ならＯＫとする
	SIF COND("調教中", ARG) == 0 && CHECK_PANTIES("貞操帯", ARG) == 0
		RETURNF 1
	SIF EQUIP:ARG:贴身制服 && CHECK_BODYS("Ａ開洞", ARG) && !CHECK_BODYS("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:外套 && !CHECK_OUTER("连褲襪", ARG) && CHECK_OUTER("Ａ開洞", ARG) && !CHECK_OUTER("ずらせる", ARG)
		RETURNF 0
	SIF EQUIP:ARG:胖次 && CHECK_PANTIES("Ａ開洞", ARG) && !CHECK_PANTIES("ずらせる", ARG)
		RETURNF 0
	RETURNF 1

CASE "移開的"
	IF EQUIP:ARG:贴身制服 && CHECK_BODYS("移開的", ARG)
		RETURNF 1
	ELSEIF EQUIP:ARG:外套 && CHECK_OUTER("移開的", ARG)
		RETURNF 1
	ELSEIF EQUIP:ARG:胖次 && CHECK_PANTIES("移開的", ARG)
		RETURNF 1
	ENDIF
CASE "腋露出"
	SIF EQUIP:ARG:服 && CHECK_SHIRT("腋露出", ARG) == 0
		RETURNF 0
	SIF CHECK_OTHER("外套", ARG)
		RETURNF 0
	RETURNF 1
CASE "足露出"
	SIF EQUIP:ARG:靴
		RETURNF 0
	SIF EQUIP:ARG:襪子 && CHECK_SOCKS("足露出", ARG) == 0
		RETURNF 0
	RETURNF 1
ENDSELECT

RETURNF 0

;-------------------------------------------------
;キャラARGの衣装ARGSに色やゲーム的な制限など、それぞれでいちいち記述すると設定忘れが起きそうなフラグはまとめてここで設定
;パンツ,ブラ,服,ボディスーツ,アウターにのみ作用する。ここを通ったら "共通属性追加済/" が足される
;-------------------------------------------------
@SET_CLO_COMMON(ARGS, ARG)
#FUNCTIONS
#DIM LCOUNT
;追加で発生した属性は全部これに記述
#DIMS STR_ADD
;衣装の名称
#DIMS CLONAME
;結果的に作られた衣装の文字列
#DIMS CLOSTR
#DIMS COLOR_ABOUT

SIF ARG == 0 && TARGET > 0
	ARG = TARGET
SIF ARGS == "" || STRCOUNT(ARGS, "共通属性追加済")
	RETURNF ARGS

;ARGSの色を見る
COLOR_ABOUT = %CHECK_CLOCOLOR(ARGS)%
;色グループからの選択設定があるかを見る
SELECTCASE COLOR_ABOUT
CASE "條紋色", "水玉色", "豹纹", "儿童胖次颜色"
	COLOR_ABOUT = %SET_CLOCOLOR(COLOR_ABOUT, ARGS)%
CASE "淡色", "Ｈ的颜色", "隨機顏色", "辣妹", "男式色"
	IF CSTR:ARG:喜欢的颜色 != "" && RAND:3
		COLOR_ABOUT = %SET_CLOCOLOR(TEXTR(CSTR:ARG:喜欢的颜色), ARGS)%
	ELSE
		COLOR_ABOUT = %SET_CLOCOLOR(COLOR_ABOUT, ARGS)%
	ENDIF
ENDSELECT

;ARGSから色設定を消し去る
ARGS = %RESET_CLOCOLOR(ARGS)%

VARSET STR_ADD
VARSET LOCALS
;LOCALSを用いて属性ごとに分解
SPLIT ARGS, "/", LOCALS

CLONAME = %LOCALS%

;特殊な衣装の時の処理（長いので特別に分割）。これらは名前自体が変わる
SELECTCASE LOCALS
;夏服がある
CASE "水手服"
	SELECTCASE TEXTS("季節詳細")
	;夏服
	CASE "初夏", "夏", "初秋", "秋"
		CLONAME = 水手夏服
		STR_ADD = 汗水浸湿的/
	ENDSELECT
CASE "西装外衣"
	SELECTCASE TEXTS("季節詳細")
	CASE "初夏", "夏", "初秋", "秋"
		CLONAME = 領帯女式衬衫

		SELECTCASE COLOR_ABOUT
		CASE "青", "黒", "赤"
		CASEELSE
			COLOR_ABOUT = 赤
		ENDSELECT

		STR_ADD = 汗水浸湿的/
	ENDSELECT
CASE "男子西装外衣"
	SELECTCASE TEXTS("季節詳細")
	CASE "初夏", "夏", "初秋", "秋"
		CLONAME = 衬衫
		STR_ADD = 汗水浸湿的/
	ENDSELECT
CASE "校服"
	SELECTCASE TEXTS("季節詳細")
	CASE "初夏", "夏", "初秋", "秋"
		CLONAME = 恤夹克
	ENDSELECT
CASE "Ｈ的内衣"
	STR_ADD = Ｈ的内衣/
	;名称に影響あるので、これだけ先に判定する
	SIF RAND:3 == 0
		STR_ADD = %STR_ADD%紐/

	;ジャンル選択
	SELECTCASE RAND:11
	;CASE 8
	;	CLONAME = シルクの\@ STRCOUNT(STR_ADD, "紐") ? 紐 #  \@下着
	CASE IS >= 9
		IF PENIS(ARG)
			IF RAND:2
				CLONAME = 丁字褲
			ELSE
				CLONAME = Micro\@ STRCOUNT(STR_ADD, "紐") ? 繩系胖次 # 內褲 \@
			ENDIF
		ELSE
			CLONAME = 開放型開洞\@ STRCOUNT(STR_ADD, "紐") ? 繩系胖次 # 胖次 \@
		ENDIF
	CASE 8
		CLONAME = 後開型\@ STRCOUNT(STR_ADD, "紐") ? 繩系胖次 # \@
	CASE 7
		CLONAME = 淚珠泳衣
	CASE 6
		CLONAME = 丁字褲
	CASE 5
		CLONAME = 蕾丝的\@ STRCOUNT(STR_ADD, "紐") ? 繩系胖次 # 胖次 \@
	CASE 4
		CLONAME = Micro\@ STRCOUNT(STR_ADD, "紐") ? 繩系胖次 # 內褲 \@
	CASE 3
		CLONAME = 高開衩\@ STRCOUNT(STR_ADD, "紐") ? 繩系胖次 #  胖次 \@
	CASE 2
		CLONAME = Ｔ字褲\@ STRCOUNT(STR_ADD, "紐") ? 繩系胖次 #  \@
	CASE 1
		CLONAME = 超低腰\@ STRCOUNT(STR_ADD, "紐") ? 繩系胖次 #  \@
	CASEELSE
		CLONAME = 透明\@ STRCOUNT(STR_ADD, "紐") ? 繩系胖次 # 胖次 \@
	ENDSELECT

	;ランダムにエロい属性選択
	IF STRCOUNT(CLONAME, "開放型開洞")
		SIF RAND:3 == 0
			STR_ADD = %STR_ADD%%TEXTR("低腰/超低腰")%/
		SIF RAND:3 == 0
			STR_ADD = %STR_ADD%蕾丝/
		STR_ADD = %STR_ADD%Ｖ露出/Ａ露出/
	ELSEIF STRCOUNT(CLONAME, "後開型")
		SIF RAND:3 == 0
			STR_ADD = %STR_ADD%%TEXTR("低腰/超低腰")%/
		SIF RAND:3 == 0
			STR_ADD = %STR_ADD%蕾丝/
		STR_ADD = %STR_ADD%Ａ露出/
	ELSE
		SIF RAND:3 == 0 && STRCOUNT(CLONAME, "蕾丝") == 0
			STR_ADD = %STR_ADD%蕾丝/
		SIF RAND:3 == 0 && STRCOUNT(CLONAME, "透明") == 0
			STR_ADD = %STR_ADD%透明/
		SIF RAND:3 == 0 && STRCOUNT(CLONAME, "Ｔ字褲") == 0 && STRCOUNT(CLONAME, "高開衩") == 0
			STR_ADD = %STR_ADD%Ｔ字褲/
		;SIF RAND:3 == 0 || STR_ADD == ""
		;	STR_ADD = %STR_ADD%%TEXTR("ファスナー/スリット")%/
	ENDIF
CASE "Ｈ的胸罩"
	;ジャンル選択
	SELECTCASE RAND:6
	;CASE 5
	;	CLONAME = シルクブラ
	CASE 5
		CLONAME = 開放型胸罩
	CASE 4
		CLONAME = １／４杯胸罩
	CASE 3
		CLONAME = 蕾丝胸罩
	CASE 2
		CLONAME = 半杯胸罩
	CASE 1
		CLONAME = Micro胸罩
	CASEELSE
		CLONAME = 透明胸罩
	ENDSELECT

	STR_ADD = Ｈ的胸罩/
	;ランダムにエロい属性選択
	SIF RAND:3 == 0
		STR_ADD = %STR_ADD%紐/
	SIF RAND:3 == 0 && STRCOUNT(CLONAME, "蕾丝") == 0
		STR_ADD = %STR_ADD%蕾丝/
	SIF RAND:3 == 0 && STRCOUNT(CLONAME, "透明") == 0
		STR_ADD = %STR_ADD%透明/
ENDSELECT

;変更された名前を取得
LOCALS = %CLONAME%
CLOSTR = 
FOR LCOUNT, 0, 100
	;空文字列なら終了
	SIF LOCALS:LCOUNT == ""
		BREAK

	SIF STRCOUNT(LOCALS:LCOUNT, "皮革") || STRCOUNT(LOCALS:LCOUNT, "革")
		STR_ADD += "通気性没有/不可移開/不可手交/"
	SIF STRCOUNT(LOCALS:LCOUNT, "比基尼")
		STR_ADD += "泳装/"
	SIF STRCOUNT(LOCALS:LCOUNT, "兔")
		STR_ADD += "谷間露出/腋露出/"
	SIF STRCOUNT(LOCALS:LCOUNT, "无袖")
		STR_ADD += "腋露出/"
	SIF STRCOUNT(LOCALS:LCOUNT, "Micro") && STRCOUNT(LOCALS:LCOUNT, "胸罩") == 0
		STR_ADD += "高開衩/Ｔ字褲/超低腰/"
	;パンツの形状
	IF STRCOUNT(LOCALS:LCOUNT, "Ｃ胖次")
		STR_ADD += "Ｔ字褲/不可手交/不可移開/"
	ELSEIF STRCOUNT(LOCALS:LCOUNT, "前貼")
		STR_ADD += "Ａ露出/不可手交/不可移開/"
	ELSEIF STRCOUNT(LOCALS:LCOUNT, "丁字褲")
		STR_ADD += "高開衩/Ｔ字褲/"
	ELSEIF STRCOUNT(LOCALS:LCOUNT, "淚珠泳衣")
		STR_ADD += "Ｔ字褲/Micro/"
	ELSEIF STRCOUNT(LOCALS:LCOUNT, "貞操帯")
		STR_ADD += "通気性没有/不可移開/不可手交/脱衣不可/Ａ露出/"
	ENDIF

	SIF STRCOUNT(LOCALS:LCOUNT, "女式衬衫") == 0 && STRCOUNT(LOCALS:LCOUNT, "胸罩") && STRCOUNT(LOCALS:LCOUNT, "スポーツ") == 0
		STR_ADD += "谷間露出/"
	;SIF STRCOUNT(LOCALS:LCOUNT, "ハーフカップ")
	;	STR_ADD += "乳首露出/"
	SIF STRCOUNT(LOCALS:LCOUNT, "１／４杯胸罩") || STRCOUNT(LOCALS:LCOUNT, "開放型胸罩")
		STR_ADD += "乳頭露出/"

	SELECTCASE LOCALS:LCOUNT
	CASE "正面蝴蝶結"
		LOCALS:LCOUNT = 正面蝴蝶結%TEXTR("紐/三角")%%TEXTR("赤/黒/青/黄/白")%
	CASE "正面蝴蝶結紐"
		LOCALS:LCOUNT = 正面蝴蝶結紐%TEXTR("赤/黒/青/黄/白")%
	CASE "正面蝴蝶結三角"
		LOCALS:LCOUNT = 正面蝴蝶結三角%TEXTR("赤/黒/青/黄/白")%
	ENDSELECT

	;衣装を再構成
	CLOSTR += @"%LOCALS:LCOUNT%/"
NEXT

;最終的に出来た文字列に同じ属性が複数書かれないように整理
VARSET LOCALS
SPLIT @"%CLOSTR%%COLOR_ABOUT%/%STR_ADD%共通属性追加済/", "/", LOCALS
CLOSTR = 
FOR LCOUNT, 0, 100
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASEELSE
		;既に追加されている属性なら弾く
		SIF STRCOUNT(CLOSTR, @"/%LOCALS:LCOUNT%/")
			CONTINUE
		CLOSTR += @"%LOCALS:LCOUNT%/"
	ENDSELECT
NEXT

RETURNF CLOSTR


;-------------------------------------------------
;ARGの全衣装に@SET_CLO_COMMON を作用させて、EQUIPを再計算する
;-------------------------------------------------
@CHECK_ALLCLOFLAG, ARG
SIF ARG == 0 && TARGET > 0
	ARG = TARGET

;コモンフラグを設定する
CSTR:ARG:胖次 = %SET_CLO_COMMON(CSTR:ARG:胖次, ARG)%
CSTR:ARG:胸罩 = %SET_CLO_COMMON(CSTR:ARG:胸罩, ARG)%
CSTR:ARG:服 = %SET_CLO_COMMON(CSTR:ARG:服, ARG)%
CSTR:ARG:贴身制服 = %SET_CLO_COMMON(CSTR:ARG:贴身制服, ARG)%
CSTR:ARG:外套 = %SET_CLO_COMMON(CSTR:ARG:外套, ARG)%

;EQUIPを再計算する
EQUIP:ARG:胖次 = STRLENS(CSTR:ARG:胖次)
EQUIP:ARG:胸罩 = STRLENS(CSTR:ARG:胸罩)
EQUIP:ARG:服 = STRLENS(CSTR:ARG:服)
EQUIP:ARG:裙 = STRLENS(CSTR:ARG:裙)
EQUIP:ARG:贴身制服 = STRLENS(CSTR:ARG:贴身制服)
EQUIP:ARG:外套 = STRLENS(CSTR:ARG:外套)
EQUIP:ARG:襪子 = STRLENS(CSTR:ARG:襪子)
EQUIP:ARG:靴 = STRLENS(CSTR:ARG:靴)
EQUIP:ARG:手袋 = STRLENS(CSTR:ARG:手袋)
EQUIP:ARG:飾品 = STRLENS(CSTR:ARG:飾品)

SIF CONFIG("女仆帽") && CHECK_SHIRT("女仆装", ARG) && CHECK_OTHER("礼服头花", ARG)
	CALL SET_OTHER, "女仆帽", ARG

;-------------------------------------------------
;キャラARGの服装の汚れやずらしなどを直して、初期状態に戻す
;ARGSには"はだけた", "ずらした", "ファスナー開", "破いた"を入れることが出来、その場合にはARGSのみを除外する
;アクセサリには何もしなくてよい
;-------------------------------------------------
@RESETFLAG_CLO, ARG, ARGS
#DIM LCOUNT
#DIM LCOUNT2

SELECTCASE ARGS
CASE "移開的"
	FOR LCOUNT, 40, 49
		SIF EQUIP:ARG:LCOUNT == 0
			CONTINUE

		VARSET LOCALS
		SPLIT CSTR:ARG:LCOUNT, "/", LOCALS
		CSTR:ARG:LCOUNT = 

		FOR LCOUNT2, 0, 100
			;状態変更の文字列は除く
			SELECTCASE LOCALS:LCOUNT2
			CASE ""
				BREAK
			CASE ARGS
				CONTINUE
			ENDSELECT
			CSTR:ARG:LCOUNT = %CSTR:ARG:LCOUNT%%LOCALS:LCOUNT2%/
		NEXT
	NEXT
CASEELSE
	SELECTCASE ARGS
	CASE "汚れは就这様"
	CASEELSE
		FOR LCOUNT, 10, 19
			STAIN:ARG:LCOUNT = 0
		NEXT
	ENDSELECT

	FOR LCOUNT, 40, 49
		SIF EQUIP:ARG:LCOUNT == 0
			CONTINUE

		VARSET LOCALS
		SPLIT CSTR:ARG:LCOUNT, "/", LOCALS
		CSTR:ARG:LCOUNT = 

		FOR LCOUNT2, 0, 100
			;状態変更の文字列は除く
			SELECTCASE LOCALS:LCOUNT2
			CASE ""
				BREAK
			CASE "半脱的", "移開的", "带拉链的開", "破開的"
				CONTINUE
			ENDSELECT
			CSTR:ARG:LCOUNT = %CSTR:ARG:LCOUNT%%LOCALS:LCOUNT2%/
		NEXT
	NEXT
ENDSELECT
;-------------------------------------------------
;ARGの種族衣装の有無を判定(どれか一つでも満たしていると1を返す)
;-------------------------------------------------
@CLO_ORI(ARG, ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4)
#FUNCTION
#DIM LCOUNT
FOR LCOUNT, 0, 5
	SIF ARGS:LCOUNT == ""
		BREAK
	SIF CSVCSTR(NO:ARG, GETNUM(CSTR, ARGS:LCOUNT) ) != ""
		RETURNF 1
NEXT
RETURNF 0


;-------------------------------------------------
;ARGの現在の格好を簡略表示する
;ARGSに"姿"と入れてあると、違和感ない場合には"～姿"と表示する
;-------------------------------------------------
@CLO_ABOUT(ARG, ARGS)
#FUNCTIONS

VARSET LOCALS
IF CHECK_OTHER("外套", ARG)
	LOCALS = %OTHER(CSTR:ARG:飾品, "外套")%姿
ELSEIF EQUIP:ARG:服
	;特殊な名称になるセット
	IF CHECK_SHIRT("美式校服", ARG) && CHECK_SKIRT("裙", ARG)
		LOCALS = 美式校服
	ELSE
		;柄がある？
		IF SHIRT(ARG, "柄") != ""
			LOCALS = 『%SHIRT(ARG, "柄")%』と書かれた%SHIRT(ARG, "略")%
		ELSE
			LOCALS = %SHIRT(ARG)%
		ENDIF
		IF CHECK_SKIRT("裙", ARG)
			IF CHECK_SHIRT("連衣裙", ARG)
				IF CHECK_SKIRT("超短迷你", ARG)
					LOCALS = 超短迷你%LOCALS%
				ELSEIF CHECK_SKIRT("迷你", ARG)
					LOCALS = 露脐装超短裙%LOCALS%
				ENDIF
			ELSE
				LOCALS = %LOCALS%＆%SKIRT(ARG)%
			ENDIF
			SIF CHECK_CLO("埴輪ルック", ARG)
				LOCALS += "(はにわルック)"
		ELSEIF EQUIP:ARG:贴身制服
			LOCALS = %LOCALS%＆%BODYS(ARG)%
		ELSEIF EQUIP:ARG:外套
			SIF STRCOUNT(OUTER(ARG, "略"), SHIRT(ARG, "略") ) == 0
				LOCALS = %LOCALS%＆%OUTER(ARG)%
		ELSEIF CHECK_CLO("胖次が見える格好", ARG)
			LOCALS = %LOCALS%＆%PANTIES(ARG)%
		ENDIF
	ENDIF
ELSEIF EQUIP:ARG:贴身制服
	LOCALS = %BODYS(ARG)%
ELSEIF CHECK_OTHER("外套", ARG)
	LOCALS = %OTHER(CSTR:ARG:飾品, "外套")%
;以下は上半身が下着or裸
ELSEIF EQUIP:ARG:外套 && CHECK_OUTER("连褲襪", ARG) == 0
	IF EQUIP:ARG:胸罩
		LOCALS = %BRA(ARG)%＆%OUTER(ARG)%
	ELSE
		LOCALS = %OUTER(ARG)%
	ENDIF

ELSEIF CHECK_CLO("泳装姿", ARG)
	IF EQUIP:ARG:胖次
		LOCALS = %PANTIES(ARG)%
	ELSE
		LOCALS = %BRA(ARG)%
	ENDIF
ELSEIF CHECK_CLO("内衣姿", ARG)
	IF CHECK_PANTIES("肉球印花胖次", ARG) && CHECK_BRA("猫型胸罩", ARG)
		LOCALS = 猫ランジェリー
	ELSEIF EQUIP:ARG:胖次 && EQUIP:ARG:胸罩
		IF CHECK_PANTIES("蕾丝", ARG) && CHECK_BRA("蕾丝", ARG)
			LOCALS = 蕾丝的
		ELSEIF CHECK_PANTIES("褶边", ARG) && CHECK_BRA("褶边", ARG)
			LOCALS = 褶边の
		ELSEIF CHECK_PANTIES("透明", ARG) && CHECK_BRA("透明", ARG)
			LOCALS = 透明の
		ELSEIF CHECK_PANTIES("皮革", ARG) && CHECK_BRA("皮革", ARG)
			LOCALS = 拘束衣風の
		ENDIF

		IF CHECK_PANTIES("Ｖ露出", ARG)
			LOCALS += "穴開き内衣"
		ELSEIF CHECK_PANTIES("Ａ露出", ARG)
			LOCALS += "後開型内衣"
		ELSEIF CHECK_PANTIES("Ｃ胖次", ARG)|| CHECK_PANTIES("丁字褲", ARG)|| CHECK_PANTIES("淚珠泳衣", ARG) || CHECK_BRA("１／４杯胸罩", ARG)
			LOCALS += "エロ内衣"
		ELSEIF CHECK_PANTIES("Ｔ字褲", ARG)
			LOCALS += "Ｔ字褲内衣"
		ELSEIF CHECK_PANTIES("超低腰", ARG)
			LOCALS += "超低腰内衣"
		ELSEIF CHECK_PANTIES("性感", ARG) || CHECK_BRA("性感", ARG)
			LOCALS += "エロ内衣"
		ELSE
			LOCALS += "内衣姿"
		ENDIF

		;色描写
		IF CLOCOLOR(CSTR:ARG:胸罩) == CLOCOLOR(CSTR:ARG:胖次)
			IF CHECK_PANTIES("條紋", ARG)
				LOCALS = 條紋模様の%LOCALS%
			ELSEIF CHECK_PANTIES("水玉模様", ARG)
				LOCALS = 水玉模様の%LOCALS%
			ELSEIF CHECK_PANTIES("豹纹", ARG)
				LOCALS = 豹纹の%LOCALS%
			ELSE
				LOCALS = %CLOCOLOR(CSTR:ARG:胖次, "形容詞")%%LOCALS%
			ENDIF
		ENDIF
	ELSEIF EQUIP:ARG:胸罩
		LOCALS = %BRA(ARG)%
	ELSE
		LOCALS = %PANTIES(ARG)%
	ENDIF
ELSEIF CHECK_SKIRT("裙", ARG)
	IF EQUIP:ARG:胸罩
		LOCALS = %BRA(ARG)%＆%SKIRT(ARG)%
	ELSE
		LOCALS = %SKIRT(ARG)%
	ENDIF
	SIF CHECK_CLO("埴輪ルック", ARG)
		LOCALS += "(はにわルック)"
;以下は裸
ELSEIF CHECK_CLO("全裸", ARG)
	LOCALS = 全裸
ELSE
	IF CHECK_OUTER("连褲襪", ARG)
		LOCALS += "连褲襪"
		SIF EQUIP:ARG:襪子 || EQUIP:ARG:靴 || EQUIP:ARG:手袋
			LOCALS += "、"
	ENDIF
	IF EQUIP:ARG:襪子
		LOCALS += "襪子"
		SIF EQUIP:ARG:靴 || EQUIP:ARG:手袋
			LOCALS += "、"
	ENDIF
	IF EQUIP:ARG:靴
		LOCALS += "靴"
		SIF EQUIP:ARG:手袋
			LOCALS += "、"
	ENDIF
	SIF EQUIP:ARG:手袋
		LOCALS += "手袋"

	LOCALS = 全裸に%LOCALS%のみ
ENDIF

SELECTCASE ARGS
CASE "姿"
	;＆が含まれている場合は＆を"に"変える
	IF STRCOUNT(LOCALS, "＆")
		LOCALS = %EXTSTR(LOCALS, "", "＆")%に%EXTSTR(LOCALS, "＆")%
	ELSEIF STRCOUNTS(LOCALS, "姿", "全裸") == 0
		LOCALS += "姿"
	ENDIF
ENDSELECT

RETURNF LOCALS

