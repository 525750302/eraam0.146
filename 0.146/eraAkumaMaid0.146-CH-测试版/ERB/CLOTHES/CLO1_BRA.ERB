;-------------------------------------------------
;キャラARGにブラARGSを着せる。ARGS == ""なら脱がす
;-------------------------------------------------
@SET_BRA, ARGS, ARG
SIF ARG == 0 && TARGET > 0
	ARG = TARGET

CALL PALAM_BRA, ARGS, ARG

;コモンフラグを設定する
CSTR:ARG:パンツ = %SET_CLO_COMMON(CSTR:ARG:パンツ, ARG)%
CSTR:ARG:ブラ = %SET_CLO_COMMON(CSTR:ARG:ブラ, ARG)%

EQUIP:ARG:ブラ = STRLENS(CSTR:ARG:ブラ)
EQUIP:ARG:パンツ = STRLENS(CSTR:ARG:パンツ)

;パンツと色を揃える
CALL EQ_CLOCOLOR, ARG

;ボディスーツは脱がす
SIF EQUIP:ARG:ブラ && EQUIP:ARG:ボディスーツ
	CALL SET_BODYS, "", ARG
TFLAG:着替えた = 1

;-------------------------------------------------
;ARGの付けているブラの名前。ARGSに"略"と入れると略称になる
;-------------------------------------------------
@BRA(ARG, ARGS)
#FUNCTIONS
#DIMS COLOR_ABOUT

VARSET LOCALS
SELECTCASE ARGS
CASE "略"
	IF CHECK_BRA("貝殻", ARG)
		LOCALS = 貝殻ブラ
	ELSEIF CHECK_BRA("帷子", ARG)
		LOCALS = 帷子
	ELSEIF CHECK_BRA("前掛け", ARG)
		LOCALS = 前掛け
	ELSEIF CHECK_BRA("タンクトップ", ARG)
		LOCALS = タンクトップ
	;ELSEIF CHECK_BRA("マイクロ", ARG)
	;	LOCALS = マイクロブラ
	ELSEIF CHECK_BRA("水着", ARG)
		IF CHECK_BRA("マイクロ", ARG)
			LOCALS = マイクロビキニ
		ELSE
			LOCALS = ビキニ
		ENDIF
	ELSEIF CHECK_BRA("キャミソール", ARG)
		LOCALS = キャミソール
	ELSEIF CHECK_BRA("チューブトップ", ARG)
		LOCALS = チューブトップ
	ELSEIF CHECK_BRA("ヌーブラ", ARG)
		LOCALS = ヌーブラ
	ELSEIF CHECK_BRA("さらし", ARG)
		LOCALS = さらし
	ELSEIF CHECK_BRA("スポーツブラ", ARG)
		LOCALS = スポブラ
	ELSE
		LOCALS = ブラジャー
	ENDIF
CASE "種類"
	;LOCALSに名称が入る
	SPLIT CSTR:ARG:ブラ, "/", LOCALS
CASE "大体の形状"
	IF CHECK_BRA("３／４カップ", ARG)
		LOCALS = ３／４カップブラ
	ELSEIF CHECK_BRA("ハーフカップ", ARG)
		LOCALS = ハーフカップブラ
	ELSEIF CHECK_BRA("シェルフブラ", ARG)
		LOCALS = シェルフブラ
	ELSEIF CHECK_BRA("チューブトップ", ARG)
		LOCALS = チューブトップ
	ELSEIF CHECK_BRA("タンクトップ", ARG)
		LOCALS = タンクトップ
	ELSEIF CHECK_BRA("スポーツブラ", ARG)
		LOCALS = スポーツブラ
	ELSEIF CHECK_BRA("ヌーブラ", ARG)
		LOCALS = ヌーブラ
	ELSEIF CHECK_BRA("オープンブラ", ARG)
		LOCALS = オープンブラ
	ELSEIF CHECK_BRA("マイクロブラ", ARG)
		LOCALS = マイクロブラ
	ELSEIF CHECK_BRA("レース", ARG)
		LOCALS = レースのブラジャー
	ELSEIF CHECK_BRA("フリル", ARG)
		LOCALS = フリルのブラジャー
	ELSEIF CHECK_BRA("マイクロビキニ", ARG)
		LOCALS = マイクロビキニ
	ELSEIF CHECK_BRA("ビキニ", ARG)
		LOCALS = ビキニブラ
	ELSEIF CHECK_BRA("さらし", ARG)
		LOCALS = さらし
	ELSEIF CHECK_BRA("キャミソール", ARG)
		LOCALS = キャミソール
	ELSE
		LOCALS = ノーマルブラ
	ENDIF
CASEELSE
	;LOCALSに名称が入る
	SPLIT CSTR:ARG:ブラ, "/", LOCALS
	COLOR_ABOUT = %CHECK_CLOCOLOR(CSTR:ARG:ブラ)%

	;名称と色が被ってる場合は省略
	IF STRCOUNT(LOCALS, COLOR_ABOUT)
		RETURNF LOCALS
	ELSE
		LOCALS = %CLOCOLOR(CSTR:ARG:ブラ, "形容詞")%%LOCALS%
	ENDIF

ENDSELECT
RETURNF LOCALS


;-------------------------------------------------
;ARG番のブラの名称。着せ替えなどで表を作る際に使う
;-------------------------------------------------
@NAME_BRA(ARG)
#FUNCTIONS
VARSET LOCALS

SELECTCASE ARG
CASE 0
	LOCALS = シンプルブラ
CASE 1
	LOCALS = フルカップブラ
CASE 2
	LOCALS = ３／４カップブラ
CASE 3
	LOCALS = ハーフカップブラ
CASE 4
	LOCALS = シェルフブラ
CASE 5
	LOCALS = チューブトップ
CASE 6
	LOCALS = タンクトップ
CASE 7
	LOCALS = スポーツブラ
CASE 8
	LOCALS = ヌーブラ
CASE 9
	LOCALS = オープンブラ
CASE 10
	LOCALS = マイクロブラ
CASE 11
	LOCALS = キャミソール

CASE 15
	LOCALS = 縞々ブラ
CASE 16
	LOCALS = 水玉模様のブラ
CASE 17
	LOCALS = 豹柄ブラ

CASE 20
	LOCALS = レースのブラ
CASE 21
	LOCALS = シースルーブラ
CASE 22
	LOCALS = フリルのブラ
CASE 23
	LOCALS = レザーブラ
CASE 24
	LOCALS = 猫型ブラ
CASE 25
	LOCALS = さらし

CASE 30
	LOCALS = ビキニブラ
CASE 31
	LOCALS = マイクロビキニブラ

CASE 39
	LOCALS = Ｈなブラ
ENDSELECT

RETURNF LOCALS

;-------------------------------------------------
;キャラARGにブラARGSを着せた場合のCSTR処理
;名前/色/属性/…/属性/ というルールで記述される(一番左は名前、次は色で固定)
;後で属性を追加したい際には CSTR:ブラ += "水着/" のように単純に追加すればよい。同じ属性が複数あっても問題ない
;-------------------------------------------------
@PALAM_BRA, ARGS, ARG
;追加で記述された属性を一時的に保管する
#DIMS STR_ADD

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

STR_ADD = %EXTSTR(ARGS, "/")%
ARGS = %EXTSTR(ARGS, "", "/")%

SELECTCASE ARGS
CASE "", "ノーブラ"
	CSTR:ARG:ブラ = 
CASE "シンプルブラ"
	CSTR:ARG:ブラ = シンプルブラ/おとなしい色/
CASE "ブラジャー"
	CSTR:ARG:ブラ = ブラジャー/おとなしい色/
CASE "フルカップブラ"
	CSTR:ARG:ブラ = フルカップブラ/おとなしい色/
CASE "３／４カップブラ"
	CSTR:ARG:ブラ = ３／４カップブラ/おとなしい色/
CASE "ハーフカップブラ"
	CSTR:ARG:ブラ = ハーフカップブラ/Ｈな色/
CASE "シェルフブラ"
	CSTR:ARG:ブラ = シェルフブラ/Ｈな色/乳首露出/乳房露出/
CASE "チューブトップ"
	CSTR:ARG:ブラ = チューブトップ/おとなしい色/
CASE "タンクトップ"
	CSTR:ARG:ブラ = タンクトップ/おとなしい色/
CASE "スポーツブラ"
	CSTR:ARG:ブラ = スポーツブラ/おとなしい色/
CASE "ヌーブラ"
	CSTR:ARG:ブラ = ヌーブラ/ランダムカラー/
CASE "オープンブラ"
	CSTR:ARG:ブラ = オープンブラ/Ｈな色/乳首露出/
CASE "マイクロブラ"
	CSTR:ARG:ブラ = マイクロブラ/ランダムカラー/

CASE "縞々ブラ"
	CSTR:ARG:ブラ = 縞々ブラ/縞々色/
CASE "水玉模様のブラ"
	CSTR:ARG:ブラ = 水玉模様のブラ/水玉色/
CASE "豹柄ブラ"
	CSTR:ARG:ブラ = 豹柄ブラ/豹柄/

CASE "レースのブラ"
	CSTR:ARG:ブラ = レースのブラ/Ｈな色/シースルー/ランジェリー/
CASE "シースルーブラ"
	CSTR:ARG:ブラ = シースルーブラ/おとなしい色/ランジェリー/
CASE "フリルのブラ"
	CSTR:ARG:ブラ = フリルのブラ/おとなしい色/ランジェリー/
CASE "レザーブラ"
	CSTR:ARG:ブラ = レザーブラ/黒/

CASE "ビキニブラ"
	CSTR:ARG:ブラ = ビキニブラ/ランダムカラー/紐/
CASE "マイクロビキニブラ"
	CSTR:ARG:ブラ = マイクロビキニブラ/ランダムカラー/紐/

;特殊
CASE "さらし"
	CSTR:ARG:ブラ = さらし/白/
CASE "キャミソール"
	CSTR:ARG:ブラ = キャミソール/おとなしい色/ランジェリー/

CASE "Ｈなブラ"
	CSTR:ARG:ブラ = Ｈなブラ/Ｈな色/

CASE "白いビキニブラ"
	CSTR:ARG:ブラ = 白いビキニブラ/白/紐/
CASE "黒いビキニブラ"
	CSTR:ARG:ブラ = 黒いビキニブラ/黒/紐/
CASE "猫型ブラ"
	CSTR:ARG:ブラ = 猫型ブラ/黒/紐/ランジェリー/
CASEELSE
	CSTR:ARG:ブラ = %ARGS%/ランダムカラー/
ENDSELECT

CSTR:ARG:ブラ = %MERGE_STR(CSTR:ARG:ブラ, STR_ADD)%

;-------------------------------------------------
;ARGの着けているブラの詳細
;-------------------------------------------------
@DETAIL_BRA(ARG, ARGS)
#FUNCTIONS

SIF ARG == 0 && TARGET
	ARG = TARGET

VARSET LOCALS
SELECTCASE ARGS
;触っただけで類推する場合
CASE "感触"

CASEELSE
	;Ｈな下着
	IF CHECK_BRA("Ｈなブラ", ARG)
		LOCALS = エロチックな
		IF CHECK_BRA("シースルー", ARG)
			LOCALS += "シースルー素材の"
		ELSEIF CHECK_BRA("レース", ARG)
			LOCALS += "レースで彩られた"
		ELSEIF CHECK_BRA("シルク", ARG)
			LOCALS += "シルク製の"
		ENDIF
		IF CHECK_BRA("オープンブラ", ARG)
			LOCALS += "乳首周辺が全く隠されていないブラジャー(？)"
		ELSEIF CHECK_BRA("シェルフブラ", ARG)
			LOCALS += "乳房の下までしかなく乳首が丸見えのブラジャー(？)"
		ELSEIF CHECK_BRA("マイクロ", ARG)
			LOCALS += "布面積が異常に少ないブラジャー"
		ELSEIF CHECK_BRA("ハーフカップ", ARG)
			LOCALS += "乳房の上半分が半分見えているブラジャー"
		ELSE
			LOCALS += "ブラジャー"
		ENDIF

	ELSEIF CHECK_BRA("マイクロビキニ", ARG)
		IF RAND:2
			LOCALS = 小さな布と形容した方がいいような、物凄くきわどいビキニ
		ELSE
			LOCALS = 布地が小さいせいで乳輪が隠しきれていない極小生地の水着
		ENDIF

	ELSEIF CHECK_BRA("ビキニ", ARG)
		LOCALS = 大胆なビキニタイプのブラ
	ELSEIF CHECK_BRA("オープンブラ", ARG)
		LOCALS = 乳首周辺が全く隠されていないブラジャー
	ELSEIF CHECK_BRA("タンクトップ", ARG)
		LOCALS = 活動的なタンクトップ
	ELSEIF CHECK_BRA("スポーツブラ", ARG)
		LOCALS = 胸の揺れをカバーするスポーツブラ
	ELSEIF CHECK_BRA("さらし", ARG)
		LOCALS = 胸を押さえつけて盛り上がりを隠すさらし
	ELSEIF CHECK_BRA("キャミソール", ARG)
		LOCALS = 可愛らしいキャミソール

	ELSEIF CHECK_BRA("縞々", ARG)
		LOCALS = 胸のラインを控えめに主張させる縞々のブラ
	ELSEIF CHECK_BRA("水玉模様", ARG)
		LOCALS = 可愛らしい水玉模様の%TEXTR("ブラ/の下着")%
	ELSEIF CHECK_BRA("豹柄ブラ", ARG)
		SELECTCASE AGE(ARG)
		CASE IS >= 4
			LOCALS = %TEXTR("セクシーな/エロチックな/大人の香り漂う")%
		CASE 3
			LOCALS = %TEXTR("セクシーな/挑発的な/ギャルギャルしい")%
		CASE 2
			LOCALS = %TEXTR("大人びた/可愛らしい/挑発的な/")%
		CASE 1
			LOCALS = %TEXTR("見た目とのアンバランスさがたまらない/可愛らしい/挑発的な")%
		ENDSELECT
		LOCALS += "豹柄のブラ"

	ELSEIF CHECK_BRA("レザー", ARG)
		LOCALS = 倒錯的な雰囲気漂う、革製のブラ

	ELSEIF CHECK_BRA("レースのブラ", ARG)
		IF RAND:4 == 0
			LOCALS = セクシーな%CLOCOLOR(CSTR:ARG:ブラ, "形容詞")%レース生地のブラ
		ELSEIF RAND:3 == 0
			LOCALS = 大人の香り漂う%CLOCOLOR(CSTR:ARG:ブラ, "形容詞")%レースのブラジャー
		ELSEIF RAND:2
			LOCALS = 上品でお洒落なレース生地のブラ
		ELSE
			LOCALS = 大人っぽくも可愛らしいレースのブラジャー
		ENDIF
	ELSEIF CHECK_BRA("フリルのブラ", ARG)
		LOCALS = %TEXTR("可愛らしい/清楚な")%フリル%TEXTR("で飾られた/付きの")%ブラジャー
	ELSEIF CHECK_BRA("シースルーブラ", ARG)
		IF RAND:2 == 0
			LOCALS = 大胆に透けているブラジャー
		ELSE
			LOCALS = なんとも淫靡なシースルーのブラ
		ENDIF

	ELSEIF CHECK_BRA("ハーフカップ", ARG)
		LOCALS = おっぱいの上半分が露出した%CLOCOLOR(CSTR:ARG:ブラ, "形容詞")%ブラジャー
	ELSEIF CHECK_BRA("シェルフ", ARG)
		LOCALS = 布地がおっぱいの下までしか無く乳首が丸見えのブラジャー
	ELSEIF CHECK_BRA("ヌーブラ", ARG)
		LOCALS = 肩紐もベルトも無い、すっきりしたシルエットのブラ
	ELSEIF CHECK_BRA("チューブトップ", ARG)
		LOCALS = ぴったりと胸を覆うチューブトップ
	ELSEIF CHECK_BRA("シンプル", ARG)
		LOCALS = 飾り気の少ない%CLOCOLOR(CSTR:ARG:ブラ, "形容詞")%ブラジャー
	ELSE
		LOCALS = %BRA(ARG)%
	ENDIF
ENDSELECT

RETURNF LOCALS


;-------------------------------------------------
;ARGがブラARGSを好きor嫌いかチェックする
;-------------------------------------------------
@FAV_BRA(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
RETURNF STRCOUNT(CSTR:ARG:好きなブラ, ARGS)

@DISLIKE_BRA(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
RETURNF STRCOUNT(CSTR:ARG:嫌いなブラ, ARGS)

;-------------------------------------------------
;ARGのブラが条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_BRA(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:ブラ == 0
	RETURNF 0

SELECTCASE ARGS
CASE "はだけ可"
	;既に露出している場合
	SIF STRCOUNTS(CSTR:ARG:ブラ, "胸露出", "乳首露出", "はだけた", "はだけ不可")
		RETURNF 0
	RETURNF 1
CASE "乳首露出"
	SIF STRCOUNTS(CSTR:ARG:ブラ, "胸露出", "乳首露出")
		RETURNF 1
CASE "乳輪露出"
	SIF STRCOUNTS(CSTR:ARG:ブラ, "胸露出", "乳首露出", "乳輪露出")
		RETURNF 1
CASE "セクシー"
	SIF STRCOUNTS(CSTR:ARG:ブラ, "胸露出", "乳首露出", "乳輪露出", "マイクロ")
		RETURNF 1
CASE "見栄え良し"
	SIF STRCOUNTS(CSTR:ARG:ブラ, "レース", "シースルー", "フリル")
		RETURNF 1
CASEELSE
	SIF STRCOUNT(CSTR:ARG:ブラ, ARGS)
		RETURNF 1
ENDSELECT

RETURNF 0


;-------------------------------------------------
;キャラARGが今身に着けているパンツにふさわしいブラを（あれば）選ぶ
;-------------------------------------------------
@COORDINATE_BRA, ARG
;男物だと変更なし
IF CHECK_PANTIES("男物", ARG)
	;パンツと色を揃える
	CALL EQ_CLOCOLOR, ARG
ELSEIF CHECK_PANTIES("Ｈな下着", ARG)
	CALL SET_BRA, "Ｈなブラ", ARG
ELSEIF CHECK_PANTIES("オープンクロッチ", ARG)
	CALL SET_BRA, "オープンブラ", ARG
ELSEIF CHECK_PANTIES("褌", ARG)
	CALL SET_BRA, "さらし", ARG
ELSEIF CHECK_PANTIES("マイクロビキニ", ARG)
	CALL SET_BRA, "マイクロビキニブラ", ARG
ELSEIF CHECK_PANTIES("ビキニ", ARG)
	CALL SET_BRA, "ビキニブラ", ARG

ELSEIF CHECK_PANTIES("縞々", ARG)
	CALL SET_BRA, "縞々ブラ", ARG
ELSEIF CHECK_PANTIES("水玉", ARG)
	CALL SET_BRA, "水玉模様のブラ", ARG
ELSEIF CHECK_PANTIES("豹柄", ARG)
	CALL SET_BRA, "豹柄ブラ", ARG

ELSEIF CHECK_PANTIES("レース", ARG)
	CALL SET_BRA, "レースのブラ", ARG
ELSEIF CHECK_PANTIES("シースルー", ARG)
	CALL SET_BRA, "シースルーブラ", ARG
ELSEIF CHECK_PANTIES("フリル", ARG)
	CALL SET_BRA, "フリルのブラ", ARG
ELSEIF CHECK_PANTIES("レザー", ARG)
	CALL SET_BRA, "レザーブラ", ARG
ELSE
	;パンツと色を揃える
	CALL EQ_CLOCOLOR, ARG
ENDIF

