;SHOP画面で88224646でここに来ます
@DEBUG_MODE
#DIM LCOUNT
#DIMS CHOICES

PRINTL 
IF TARGET
	;実行可能コマンドの確認
	CALL CHECK_COM_ABLE, TARGET

	PRINTFORML  現在の編集対象：%CALLNAME:TARGET%
	SIF TARGET != MASTER
		CALL PRINT_MAIDFACE, TARGET
	PRINT  
	CALL PRINT_DRESSL, TARGET, "短縮表示"

	PRINTL  [ 0] CFLAG編集
	PRINTL  [ 1] ABL編集
	PRINTL  [ 2] EXP編集
	PRINTL  [ 3] MARK編集
	PRINTL  [ 4] TALENT編集
	PRINTL  [ 5] BASE編集
	PRINTL  [ 6] JUEL編集
	PRINTL  [ 7] 尿道の拡張度合い変更処理（仮）
	PRINTL  [ 8] CSTR確認・編集
	IF TARGET != MASTER
		PRINTL  [ 9] 制服に着替える
		PRINTL  [10] 体操服に着替える
		PRINTL  [11] 水着に着替える
		PRINTL  [12] 下着姿になる
		PRINTL  [13] 普段着になる
		PRINTL  [14] 部屋着になる
		PRINTL  [15] 外出着になる
		PRINTL  [16] デート衣装になる
		IF EQUIP:パンツ
			PRINTL  [17] ノーパンにする
			PRINTL  [18] 染みパンにする
		ELSE
			CALL PRINT_STRL, "暗灰色_ [×] ノーパンにする"
			CALL PRINT_STRL, "暗灰色_ [×] 染みパンにする"
		ENDIF
		IF CSTR:今年の水着 != ""
			PRINTL  [19] 今年の水着に着替える
		ELSE
			CALL PRINT_STRL, "暗灰色_ [×] 今年の水着に着替える"
		ENDIF
	ENDIF
	SIF BASE:胎児 == 0
		SETCOLOR DEF_COLOR("灰色")
	PRINTL  [30] TARGETの胎児の性別反転（当然妊婦のみ）
	RESETCOLOR
	IF TARGET != MASTER && CFLAG:初期仲間 == 0
		IF COND("現在不在")
			PRINTL  [40] このキャラを勧誘していない平行世界に飛ぶ(同じ部屋にいないキャラ限定)
		ELSE
			PRINTL  [×] このキャラを勧誘していない平行世界に飛ぶ(同じ部屋にいないキャラ限定)
		ENDIF
	ENDIF
	PRINTL 
ENDIF
PRINTL  [50] FLAG編集
PRINTL  [51] 所持金編集
PRINTL  [52] ITEM編集
PRINTL  [53] 日付変更
PRINTL  [54] 試験を受ける

IF TARGET > MASTER
	PRINTL  [60] 朝Ｈイベントを見る
	PRINTL  [61] 夜Ｈイベントを見る
ENDIF

PRINTFORML  [70] 発情スイッチを\@ CEVENT("発情スイッチ", MASTER) ? OFF # ON \@にする

PRINTL  [99] TARGET変更
PRINTL [100] 戻る

$INPUT_LOOP
INPUT

SELECTCASE RESULT
CASE 0 TO 6
	SIF TARGET == 0
		GOTO INPUT_LOOP
CASE 7
	SIF TARGET == 0
		GOTO INPUT_LOOP
	CALL DEBUG_U
	RESTART
CASE 8
	SIF TARGET == 0
		GOTO INPUT_LOOP
	CALL DEBUG_CSTR
	RESTART
CASE 9
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	CALL SET_COORDINATE, "制服"
	RESTART
CASE 10
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	CALL SET_COORDINATE, "体操服"
	RESTART
CASE 11
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	CALL SET_COORDINATE, "水着"
	RESTART
CASE 12
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	SIF EQUIP:パンツ == 0
		CALL SET_PANTIES, "自分の下着"
	CALL SETFLAG, "下着姿になる", TARGET
	RESTART
CASE 13
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	CALL SET_COORDINATE, "普段着"
	RESTART
CASE 14
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	CALL SET_COORDINATE, "部屋着"
	RESTART
CASE 15
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	CALL SET_COORDINATE, "外出着"
	RESTART
CASE 16
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	CALL SET_COORDINATE, "デート衣装"
	RESTART
CASE 17
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	SIF EQUIP:パンツ
		CALL SET_PANTIES, ""
	RESTART
CASE 18
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	SIF EQUIP:パンツ && CHECK_PANTIES("クロッチ染み") == 0
		CSTR:パンツ += "クロッチ染み/"
	RESTART
CASE 19
	SIF TARGET <= MASTER || CSTR:今年の水着 == ""
		GOTO INPUT_LOOP
	CALL SET_COORDINATE, "今年の水着"
	RESTART
CASE 30
	IF TARGET <= 0
		PRINTFORMW キャラを選んでください
	ELSEIF BASE:胎児 == 0
		PRINTFORMW 妊娠してないようです
	ELSEIF COND("妊娠中：息子")
		PRINTFORMW お腹の中の子供は、『女の子』になりました
		CLEARBIT BASE:胎児, 20
	ELSE
		PRINTFORMW お腹の中の子供は、『男の子』になりました
		SETBIT BASE:胎児, 20
	ENDIF
	RESTART
CASE 40
	SIF TARGET == MASTER || CFLAG:初期仲間 || COND("現在不在") == 0
		GOTO INPUT_LOOP
	PRINTFORMW %NAME:TARGET%を勧誘していない平行世界に移動しました
	CALL DELETE_CHARA, TARGET
	TARGET = FINDCHARA(CFLAG:優先順位, 1, 2)
	RESTART

CASE 50, 52
CASE 51
	CALL DEBUG_CHANGE_MONEY
	RESTART
CASE 53
	CALL DEBUG_CHANGE_DAY
	RESTART
CASE 54
	FOR LCOUNT, 1, CHARANUM
		LOCAL:LCOUNT = BASE:LCOUNT:学力
	NEXT
	CALL EVENT_ACADEMY, "試験終了"
	DRAWLINE
	CALL EVENT_ACADEMY, "試験結果発表"
	RESTART
;朝Ｈイベントを見る
CASE 60
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	STR:選択肢 = 朝キス/朝足コキ/朝クンニ/朝逆正常位/朝シックスナイン/朝フェラ/朝騎乗位/朝Ａ騎乗位/朝パイズリ/朝の新妻プレイ/朝添い寝/

	PRINTFORML どのイベントを見ますか？
	PRINTFORML （経験などを得てしまうため閲覧後のデータはセーブしないようにしてください）
	VARSET LOCALS
	STR:選択肢 += "戻る"
	SPLIT STR:選択肢, "/", LOCALS
	CALL PRINT_SELECT, STR:選択肢
	CHOICES = %LOCALS:RESULT%
	SELECTCASE CHOICES
	CASE "戻る"
		RESTART
	CASEELSE
		CALL RESET_PARTNER
		CALL EVENT_NEWDAY, CHOICES
		CALL SET_PARTNER
	ENDSELECT

	RESTART
;夜Ｈイベントを見る
CASE 61
	SIF TARGET <= MASTER
		GOTO INPUT_LOOP
	STR:選択肢 = 夜這い騎乗位/夜這いＡ騎乗位/添い寝/夜這いキス/

	PRINTFORML どのイベントを見ますか？
	PRINTFORML （経験などを得てしまうため閲覧後のデータはセーブしないようにしてください）
	VARSET LOCALS
	STR:選択肢 += "戻る"
	SPLIT STR:選択肢, "/", LOCALS
	CALL PRINT_SELECT, STR:選択肢
	CHOICES = %LOCALS:RESULT%
	SELECTCASE CHOICES
	CASE "戻る"
		RESTART
	CASEELSE
		CALL RESET_PARTNER
		CALL EVENT_YOBAI, CHOICES
		CALL SET_PARTNER
	ENDSELECT

	RESTART
CASE 70
	IF CEVENT("発情スイッチ", MASTER)
		CALL DEL_CEVENT, "発情スイッチ", MASTER
	ELSE
		CALL SET_CEVENT, "発情スイッチ", MASTER
		CVARSET BASE, GETNUM(BASE, "欲求不満"), 100
		PRINTFORML 発情スイッチＯＮ！
		PRINTFORMW 全員が発情中になります！！
	ENDIF
	RESTART
CASE 99
	CALL TARGET_CHANGE_DEBUG_MODE
	RESTART
CASE 100
	RETURN 0
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

CALL DEBUG_FLAG, RESULT

;TARGETの素質変動など
SIF TARGET
	CALL EVENTCHECK_T

RESTART

@DEBUG_CSTR
#DIM LCOUNT
;改行するかの判定に使う
#DIM NEXT_LINE
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT

PRINTL 

NEXT_LINE = 0

FOR LCOUNT, 0, VARSIZE("CSTR")

	SIF STRLENS(CSTRNAME:LCOUNT) == 0
		CONTINUE

	NEXT_LINE += 1

	PRINTFORM  [{LCOUNT, 3}] %TEXT_RJ(CSTRNAME:LCOUNT, 20)%

	SIF NEXT_LINE%3 == 0
		PRINTL 
NEXT

SIF NEXT_LINE%3
	PRINTL 
PRINTFORML [999] 戻る
PRINTFORML 表示したいCSTRを選択してください

CALL INPUT_SELECT, VARSIZE("CSTR"), 999

SELECTCASE RESULT
CASE 999
	RETURN 0
CASEELSE
	IF CSTRNAME:RESULT != ""
		PRINTFORML %CSTR:RESULT%
		PRINTFORML 編集しない場合には何も入力せずリターンしてください。空文字にしたい場合にはDELと入力してください
		INPUTS
		SELECTCASE RESULTS
		CASE ""
		CASE "DEL", "del", "ＤＥＬ"
			CSTR:RESULT = 
			PRINTFORMW 空文字にしました。
		CASEELSE
			CSTR:RESULT = %RESULTS%
		ENDSELECT
	ENDIF
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@DEBUG_U
#DIM MEMO_LINECOUNT

PRINTFORML [{TARGET, 2}]%CALLNAME:TARGET%の現在の尿道の太さは{CALCF("尿道径")}mmで、尿道拡張レベルは{ABL:尿道拡張}です
MEMO_LINECOUNT = LINECOUNT
PRINTFORML どうしますか？
PRINTFORML  [ 0]  +1mm
PRINTFORML  [ 1]  +5mm
PRINTFORML  [ 2] +10mm
PRINTFORML  [ 3]  -1mm
PRINTFORML  [ 4]  -5mm
PRINTFORML  [ 5] -10mm
PRINTFORML [100] 終了

CALL INPUT_SELECT, 6, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASE 0
	IF PENIS(TARGET)
		CSTR:Ｐ尿道拡張度 = %ADDNUM_STR(CSTR:Ｐ尿道拡張度, 1)%
	ELSE
		CSTR:Ｖ尿道拡張度 = %ADDNUM_STR(CSTR:Ｖ尿道拡張度, 1)%
	ENDIF
CASE 1
	IF PENIS(TARGET)
		CSTR:Ｐ尿道拡張度 = %ADDNUM_STR(CSTR:Ｐ尿道拡張度, 5)%
	ELSE
		CSTR:Ｖ尿道拡張度 = %ADDNUM_STR(CSTR:Ｖ尿道拡張度, 5)%
	ENDIF
CASE 2
	IF PENIS(TARGET)
		CSTR:Ｐ尿道拡張度 = %ADDNUM_STR(CSTR:Ｐ尿道拡張度, 10)%
	ELSE
		CSTR:Ｖ尿道拡張度 = %ADDNUM_STR(CSTR:Ｖ尿道拡張度, 10)%
	ENDIF
CASE 3
	IF PENIS(TARGET)
		CSTR:Ｐ尿道拡張度 = %ADDNUM_STR(CSTR:Ｐ尿道拡張度, -1)%
	ELSE
		CSTR:Ｖ尿道拡張度 = %ADDNUM_STR(CSTR:Ｖ尿道拡張度, -1)%
	ENDIF
CASE 4
	IF PENIS(TARGET)
		CSTR:Ｐ尿道拡張度 = %ADDNUM_STR(CSTR:Ｐ尿道拡張度, -5)%
	ELSE
		CSTR:Ｖ尿道拡張度 = %ADDNUM_STR(CSTR:Ｖ尿道拡張度, -5)%
	ENDIF
CASE 5
	IF PENIS(TARGET)
		CSTR:Ｐ尿道拡張度 = %ADDNUM_STR(CSTR:Ｐ尿道拡張度, -10)%
	ELSE
		CSTR:Ｖ尿道拡張度 = %ADDNUM_STR(CSTR:Ｖ尿道拡張度, -10)%
	ENDIF
ENDSELECT

CALL GET_EXABL_U, TARGET

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@TARGET_CHANGE_DEBUG_MODE
X = 1
CALL SHOW_CHARA_LIST, ""
PRINTFORML 対象の番号入力({MASTER}〜{CHARANUM - 1})。{CHARANUM}以上の値を入力すると前の画面に戻る。

$INPUT_LOOP
INPUT

SIF RESULT < MASTER
	GOTO INPUT_LOOP
SIF RESULT >= CHARANUM
	RETURN 0

TARGET = RESULT

@DEBUG_CHANGE_MONEY
PRINTL 所持金をいくらにしますか？

$INPUT_LOOP
INPUT

IF RESULT >= 0
	MONEY = RESULT
	PRINTFORMW 所持金を%MONEY_C(MONEY)%にしました。
ELSE
	GOTO INPUT_LOOP
ENDIF


@DEBUG_CHANGE_DAY
#DIM LCOUNT
#DIM MEMO_DAY
#DIMS CHOICES

MEMO_DAY = DAY

PRINTFORML 日付を入力してください。何も入力しないと変更しません。
PRINTFORML 現在は{DAY}日、%TEXTS("日付表示")%で、一年は{NUM("１年の長さ")}日です。
PRINTFORML 月〜日の曜日を入力すると直近のその曜日になります。

PRINTFORML （日付の近いイベント発生日は以下）
FOR LCOUNT, 0, NUM("１年の長さ")
	DAY += 1
	CALL SETFLAG, "年月日"
	LOCALS = %TEXTS("日付対応イベント")%
	SIF LOCALS == ""
		CONTINUE
	SIF DAYEV(LOCALS, "ゲーム内") == 0
		CONTINUE
	PRINTFORML [{DAY, 3}] %LOCALS% %TEXTS("曜日")%曜
NEXT
DAY = MEMO_DAY

$INPUT_LOOP
INPUTS

CHOICES = %RESULTS%

SELECTCASE CHOICES
CASE "月曜", "月"
	DAY += ABS(1 - (DAY - 1)%7)
CASE "火曜", "火"
	DAY += ABS(2 - (DAY - 1)%7)
CASE "水曜", "水"
	DAY += ABS(3 - (DAY - 1)%7)
CASE "木曜", "木"
	DAY += ABS(4 - (DAY - 1)%7)
CASE "金曜", "金"
	DAY += ABS(5 - (DAY - 1)%7)
CASE "土曜", "土"
	DAY += ABS(6 - (DAY - 1)%7)
CASE "日曜", "日"
	DAY += ABS(7 - (DAY - 1)%7)
CASEELSE
	SELECTCASE TOINT(CHOICES)
	CASE 0
		CALL SETFLAG, "年月日"
		PRINTFORMW 日付を{DAY}日、%TEXTS("日付表示")%のまま、変更しませんでした。
		RETURN 0
	CASE IS > 0
		DAY = TOINT(CHOICES)
	CASEELSE
		GOTO INPUT_LOOP
	ENDSELECT
ENDSELECT

CALL SETFLAG, "年月日"
PRINTFORMW 日付を{DAY}日、%TEXTS("日付表示")%にしました。
;未来に飛んだ際には各キャラのご無沙汰の数値を新しいDAYに合わせて変わらないようにする
IF DAY > MEMO_DAY
	FOR LCOUNT, 2, CHARANUM
		CFLAG:LCOUNT:前回Ｈ日 += DAY - MEMO_DAY
	NEXT
ENDIF

;ARGに選んだ選択肢番号が入っている
@DEBUG_FLAG, ARG
#DIM LCOUNT
;改行するかの判定に使う
#DIM NEXT_LINE
;変数ごとの最大値
#DIM MAX_VAR
;変更する変数
#DIMS VARS
;変更する変数名
#DIMS VAR_NAME, VARSIZE("CFLAG")
;変更する変数の値
#DIM VAR_VALUE, VARSIZE("CFLAG")
;選んだ番号
#DIM VAR_CHOSE

NEXT_LINE = 0

PRINTL 
SELECTCASE ARG
CASE 0
	VARS = CFLAG
	MAX_VAR = VARSIZE("CFLAG") - 1
	FOR LCOUNT, 0, MAX_VAR + 1
		VAR_NAME:LCOUNT = %CFLAGNAME:LCOUNT%
		VAR_VALUE:LCOUNT = CFLAG:LCOUNT

		SIF STRLENS(VAR_NAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM [{LCOUNT, 4}]%TEXT_RJ(VAR_NAME:LCOUNT, 18)%:{VAR_VALUE:LCOUNT, 4} 

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL 
CASE 1
	VARS = ABL
	MAX_VAR = VARSIZE("ABL") - 1
	FOR LCOUNT, 0, MAX_VAR + 1
		VAR_NAME:LCOUNT = %ABLNAME:LCOUNT%
		VAR_VALUE:LCOUNT = ABL:LCOUNT

		SELECTCASE LCOUNT
		CASE IS < 40, IS >= 50
			SIF STRLENS(ABLNAME:LCOUNT) == 0
				CONTINUE

			NEXT_LINE += 1

			IF ABL:LCOUNT >= 100
				PRINTFORM  [{LCOUNT, 2}]%TEXT_RJ(ABLNAME:LCOUNT, 10)%:{ABL:LCOUNT}
			ELSE
				PRINTFORM  [{LCOUNT, 2}]%TEXT_RJ(ABLNAME:LCOUNT, 10)%:{ABL:LCOUNT, 2} 
			ENDIF

			SIF NEXT_LINE%4 == 0
				PRINTL 
		ENDSELECT
	NEXT

	SIF NEXT_LINE%4
		PRINTL 
CASE 2
	VARS = EXP
	MAX_VAR = VARSIZE("EXP") - 1
	FOR LCOUNT, 0, MAX_VAR + 1
		VAR_NAME:LCOUNT = %EXPNAME:LCOUNT%
		VAR_VALUE:LCOUNT = EXP:LCOUNT

		SIF STRLENS(EXPNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM  [{LCOUNT, 2}]%TEXT_RJ(EXPNAME:LCOUNT, 14)%:{EXP:LCOUNT, 4}

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL 
CASE 3
	VARS = MARK
	MAX_VAR = VARSIZE("MARK") - 1
	FOR LCOUNT, 0, MAX_VAR + 1
		VAR_NAME:LCOUNT = %MARKNAME:LCOUNT%
		VAR_VALUE:LCOUNT = MARK:LCOUNT

		SIF STRLENS(MARKNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM   [{LCOUNT, 2}]%TEXT_RJ(MARKNAME:LCOUNT, 18)%:LV{MARK:LCOUNT, 2}

		SIF NEXT_LINE%2 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%2
		PRINTL 
CASE 4
	VARS = TALENT
	MAX_VAR = VARSIZE("TALENT") - 1
	FOR LCOUNT, 0, MAX_VAR + 1
		VAR_NAME:LCOUNT = %TALENTNAME:LCOUNT%
		VAR_VALUE:LCOUNT = TALENT:LCOUNT

		SIF STRLENS(TALENTNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM [{LCOUNT, 3}]%TEXT_RJ(VAR_NAME:LCOUNT, 16)%:{VAR_VALUE:LCOUNT, 4} 

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL 
CASE 5
	VARS = BASE
	MAX_VAR = VARSIZE("BASE") - 1
	FOR LCOUNT, 0, MAX_VAR + 1
		VAR_NAME:LCOUNT = %BASENAME:LCOUNT%
		VAR_VALUE:LCOUNT = BASE:LCOUNT

		SIF STRLENS(BASENAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		LOCALS = %BL(1)%[{LCOUNT, 3}]%TEXT_RJ(BASENAME:LCOUNT, 14)%:{BASE:LCOUNT, 5}

		SELECTCASE STRLENS(LOCALS)
		CASE IS > 27
			SELECTCASE NEXT_LINE%3
			CASE 0
				PRINTL 
			CASE 1
				NEXT_LINE += 2
			CASE 2
				NEXT_LINE += 1
			ENDSELECT
		ENDSELECT

		PRINTFORM %LOCALS%

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL 
CASE 6
	VARS = JUEL
	MAX_VAR = VARSIZE("PALAM") - 1
	FOR LCOUNT, 0, MAX_VAR + 1
		SIF PALAMNAME:LCOUNT == ""
			CONTINUE

		VAR_NAME:LCOUNT = %PALAMNAME:LCOUNT%の珠
		VAR_VALUE:LCOUNT = JUEL:LCOUNT

		NEXT_LINE += 1

		PRINTFORM   [{LCOUNT,3}] %PALAMNAME:LCOUNT%の珠:{JUEL:LCOUNT, 8}

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%3
		PRINTL 
CASE 50
	VARS = FLAG
	MAX_VAR = VARSIZE("FLAG") - 1
	FOR LCOUNT, 0, MAX_VAR + 1
		VAR_NAME:LCOUNT = %FLAGNAME:LCOUNT%
		VAR_VALUE:LCOUNT = FLAG:LCOUNT

		SIF STRLENS(VAR_NAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM [{LCOUNT, 4}]%TEXT_RJ(VAR_NAME:LCOUNT, 22)%:{VAR_VALUE:LCOUNT, 17} 

		SIF NEXT_LINE%2 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%2
		PRINTL 

CASE 52
	VARS = ITEM
	MAX_VAR = VARSIZE("ITEM") - 1
	FOR LCOUNT, 0, MAX_VAR + 1
		VAR_NAME:LCOUNT = %ITEMNAME:LCOUNT%
		VAR_VALUE:LCOUNT = ITEM:LCOUNT

		SIF STRLENS(ITEMNAME:LCOUNT) == 0
			CONTINUE

		NEXT_LINE += 1

		PRINTFORM  [{LCOUNT, 3}]%TEXT_RJ(ITEMNAME:LCOUNT, 18)%:{ITEM:LCOUNT, 3}　

		SIF NEXT_LINE%3 == 0
			PRINTL 
	NEXT

	SIF NEXT_LINE%2
		PRINTL 
ENDSELECT

PRINTFORML [99999] 戻る
PRINTFORML 変更したい%VARS%を入力してください（最大{MAX_VAR}）

SELECTCASE VARS
CASE "BASE"
	PRINTL なお、体力については、最大値も一緒に変更されます
ENDSELECT

$INPUT_LOOP_1
INPUT

SELECTCASE RESULT
CASE 99999
	RETURN 0
CASE IS < 0, IS > MAX_VAR
	GOTO INPUT_LOOP_1
ENDSELECT

VAR_CHOSE = RESULT

SELECTCASE ARG
CASE 0
	IF VAR_NAME:VAR_CHOSE == ""
		PRINTFORML CFLAG:{VAR_CHOSE} = {CFLAG:VAR_CHOSE} の変更
	ELSE
		PRINTFORML CFLAG:%VAR_NAME:VAR_CHOSE% = {CFLAG:VAR_CHOSE} の変更
	ENDIF
	PRINTL CFLAGに関しては負の値も入力可能です
CASE 50
	IF VAR_NAME:VAR_CHOSE == ""
		PRINTFORML CFLAG:{VAR_CHOSE} = {CFLAG:VAR_CHOSE} の変更
	ELSE
		PRINTFORML %VARS%:%VAR_NAME:VAR_CHOSE% = {VAR_VALUE:VAR_CHOSE} の変更
	ENDIF
CASEELSE
	SIF VAR_NAME:VAR_CHOSE == ""
		GOTO INPUT_LOOP_1
	PRINTFORML %VARS%:%VAR_NAME:VAR_CHOSE% = {VAR_VALUE:VAR_CHOSE} の変更
ENDSELECT


$INPUT_LOOP_2
INPUT

SELECTCASE RESULT
CASE IS < 0
	SIF VARS != "CFLAG"
		GOTO INPUT_LOOP_2
ENDSELECT

SELECTCASE ARG
CASE 0
	CFLAG:VAR_CHOSE = RESULT
CASE 1
	ABL:VAR_CHOSE = RESULT
	SELECTCASE VAR_CHOSE
	CASE GETNUM(ABL, "Ｃ感覚")
		ABL:Ｐ感覚 = ABL:Ｃ感覚
	CASE GETNUM(ABL, "Ｐ感覚")
		ABL:Ｃ感覚 = ABL:Ｐ感覚
	ENDSELECT
CASE 2
	EXP:VAR_CHOSE = RESULT
CASE 3
	MARK:VAR_CHOSE = RESULT
CASE 4
	TALENT:VAR_CHOSE = RESULT
CASE 5
	BASE:VAR_CHOSE = RESULT
	;体力は最大値も変更
	SIF VAR_CHOSE == 0
		MAXBASE:VAR_CHOSE = RESULT
CASE 6
	JUEL:VAR_CHOSE = RESULT
CASE 50
	FLAG:VAR_CHOSE = RESULT
CASE 52
	ITEM:VAR_CHOSE = RESULT
ENDSELECT

IF VAR_NAME:VAR_CHOSE == ""
	PRINTFORMW %VARS%:{VAR_CHOSE}を {RESULT} に変更しました
ELSE
	PRINTFORMW %VARS%:%VAR_NAME:VAR_CHOSE%を {RESULT} に変更しました
ENDIF


RESTART

