;-------------------------------------------------
;キャラクリ画面等でキャラARGの髪の色などを変更する（設定がされていないキャラ対策）
;ARG == 0 の場合はドッペルあなたを使ってのシミュレーションモード
;-------------------------------------------------
@SIMULATE_MAIDFACE, ARG
STR:颜绘固定 = 
STR:颜绘変化 = 
STR:颜绘衣装 = 
TFLAG:过滤器 = 0

SELECTCASE ARG
CASE 0
	ADDCHARA 99
	ARG = CHARANUM - 1
	CALL VARSET_MAIDFACE, ARG
	CFLAG:ARG:颜绘 = 1
	IF TARGET > MASTER && CFLAG:颜绘
		PRINTFORML 要以[{TARGET, 2}]%NAME:TARGET%的顔绘为基准吗？
		IF CFLAG:MASTER:颜绘
			CALL PRINT_SELECT, @"是/否/%CALLNAME:MASTER%の颜绘をベースにする"
		ELSE
			CALL PRINT_SELECT, "是/否"
		ENDIF
		IF RESULT == 0
			STR:颜绘衣装 = %CLO("颜绘衣装")%

			CSTR:ARG:髪色 = %CSTR:髪色%
			CSTR:ARG:髪長 = %CSTR:髪長%
			CSTR:ARG:前髪 = %CSTR:前髪%
			CSTR:ARG:後髪 = %CSTR:後髪%
			CSTR:ARG:改髪 = %CSTR:改髪%
			CSTR:ARG:呆毛 = %CSTR:呆毛%
			CSTR:ARG:蝴蝶結 = %CSTR:蝴蝶結%

			CSTR:ARG:瞳色 = %CSTR:瞳色%
			CSTR:ARG:眼形 = %CSTR:眼形%
			CSTR:ARG:白眼 = %CSTR:白眼%
			CSTR:ARG:眉形 = %CSTR:眉形%
			CSTR:ARG:嘴形 = %CSTR:嘴形%

			CSTR:ARG:種族 = %CSTR:種族%
			TALENT:ARG:肤色 = TALENT:肤色
			TALENT:ARG:精灵耳 = TALENT:精灵耳
			TALENT:ARG:動物耳 = TALENT:動物耳
			TALENT:ARG:角 = TALENT:角
		ELSEIF RESULT == 2
			CSTR:ARG:髪色 = %CSTR:MASTER:髪色%
			CSTR:ARG:髪長 = %CSTR:MASTER:髪長%
			CSTR:ARG:前髪 = %CSTR:MASTER:前髪%
			CSTR:ARG:後髪 = %CSTR:MASTER:後髪%
			CSTR:ARG:改髪 = %CSTR:MASTER:改髪%
			CSTR:ARG:呆毛 = %CSTR:MASTER:呆毛%
			CSTR:ARG:蝴蝶結 = %CSTR:MASTER:蝴蝶結%

			CSTR:ARG:瞳色 = %CSTR:MASTER:瞳色%
			CSTR:ARG:眼形 = %CSTR:MASTER:眼形%
			CSTR:ARG:白眼 = %CSTR:MASTER:白眼%
			CSTR:ARG:眉形 = %CSTR:MASTER:眉形%
			CSTR:ARG:嘴形 = %CSTR:MASTER:嘴形%

			CSTR:ARG:種族 = %CSTR:MASTER:種族%
			TALENT:ARG:肤色 = TALENT:MASTER:肤色
			TALENT:ARG:精灵耳 = TALENT:MASTER:精灵耳
			TALENT:ARG:動物耳 = TALENT:MASTER:動物耳
			TALENT:ARG:角 = TALENT:MASTER:角
		ENDIF
	ENDIF
	SIF	STR:颜绘衣装 == ""
		STR:颜绘衣装 = 水手紺色
	CALL SETFLAG, "MEMO_髪への記録", ARG
	CALL SETFLAG, "MEMO_表情への記録", ARG
	CFLAG:ARG:颜绘 = 1
	CALL SIMULATE_MAIDFACE_MAIN, ARG, "顔設定"
	CALL DELETE_CHARA, CHARANUM - 1
CASEELSE
	CALL SETFLAG, "MEMO_髪への記録", ARG
	CALL SETFLAG, "MEMO_表情への記録", ARG
	IF COND("髪型変更のお願い中", ARG)
		CALL SIMULATE_MAIDFACE_MAIN, ARG, "髪型変更"
	ELSE
		IF CFLAG:ARG:颜绘 == 0
			CALL PRESET_MAIDFACE, ARG
			CALL SETFLAG, "MEMO_髪への記録", ARG
			CALL SETFLAG, "MEMO_表情への記録", ARG
		ENDIF
		CFLAG:ARG:颜绘 = 1
		CALL SIMULATE_MAIDFACE_MAIN, ARG, "顔設定"
	ENDIF
ENDSELECT

STR:颜绘衣装 = 
STR:颜绘固定 = 
STR:颜绘変化 = 

;-------------------------------------------------
;ARGSで現在設定しているものの種類を管理する
;-------------------------------------------------
@SIMULATE_MAIDFACE_MAIN, ARG, ARGS
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIMS CHOICES
#DIMS PRINT_TABLE
#DIMS SHOW_TABLE
#DIMS EXAMPLE_EMOTE

MEMO_LINECOUNT = LINECOUNT

IF ARG == MASTER
	SELECTCASE ARGS
	CASE "制服", "体操服", "泳装", "自宅着", "外出着"
		IF SAVESTR:ARGS != ""
			STR:颜绘衣装 = %SAVESTR:ARGS%
		ELSE
			STR:颜绘衣装 = 
		ENDIF
	ENDSELECT

	IF TALENT:ARG:男性 && COND("伪娘", ARG) == 0
		SHOW_TABLE = 顔設定/制服/体操服/自宅着/外出着
	ELSE
		SHOW_TABLE = 顔設定/制服/体操服/泳装/自宅着/外出着
	ENDIF
	VARSET LOCALS
	SPLIT SHOW_TABLE, "/", LOCALS
	PRINT_TABLE = 　
	FOR LCOUNT, 0, 10
		SELECTCASE LOCALS:LCOUNT
		CASE ""
			BREAK
		CASE ARGS
			PRINT_TABLE += @"_黄色_BUTTON_[%LOCALS:LCOUNT%]"
		CASEELSE
			IF LOCALS:LCOUNT != "顔設定" && SAVESTR:(LOCALS:LCOUNT) != ""
				PRINT_TABLE += @"_緑_BUTTON_[%LOCALS:LCOUNT%]"
			ELSE
				PRINT_TABLE += @"_BUTTON_[%LOCALS:LCOUNT%]"
			ENDIF
		ENDSELECT
	NEXT
ENDIF

IF STR:颜绘変化 != ""
	CALL EMOTION_PLUS, STR:颜绘変化, ARG
ELSE
	CALL PRINT_MAIDFACE, ARG
ENDIF
SIF CFLAG:ARG:颜绘
	PRINTL 
SELECTCASE ARGS
CASE "表示衣装変更", "制服", "体操服", "泳装", "自宅着", "外出着"
	IF ARG == MASTER
		CALL PRINT_STRL, "已经设定完的服装会变成_緑_[緑色]_"
		CALL PRINT_STRL, PRINT_TABLE
		CALL PRINT_STRL, @"　現在的衣装:%CLO("颜绘衣装", ARG)%\@ SAVESTR:ARGS != "" ? _緑_(%ARGS%設定済) # \@"
	ELSE
		CALL PRINT_STRL, "　_BUTTON_[顔設定]_黄色_BUTTON_[表示衣装変更]"
		PRINTFORML 　現在的衣装:%CLO("颜绘衣装", ARG)%
		CALL PRINT_STRL, "以下のジャンル_緑_[0]～[4]_から選んで下さい。選択することで各種衣装が表示されるようになります"
	ENDIF
	PRINTL 
	CALL PRINTL_COLORTEXT, "[0]裸体・内衣・薄着・睡衣", DEF_COLOR("緑")
	IF TFLAG:过滤器 == 0
		CALL SELECTS, "裸体/浴衣", "", @"　%BL(20)%"
		CALL SELECTS, "黒/青/桃/白/緑色/橙", "内衣", @"　%TEXT_LJ("内衣", 20)%"
		CALL SELECTS, "黒/赤/緑色", "情趣内衣", @"　%TEXT_LJ("情趣内衣", 20)%"
		CALL SELECTS, "灰色/白/青/緑色", "吊帯背心", @"　%TEXT_LJ("吊帯背心", 20)%"
		CALL SELECTS, "黒/青/赤/白/緑色/橙", "吊帯衫", @"　%TEXT_LJ("吊帯衫", 20)%"
		PRINTL 
		CALL SELECTS, "白/黒/桃", "晨衣", @"　%TEXT_LJ("晨衣", 20)%"
		CALL SELECTS, "白/紺/桃/薄緑/水色", "睡衣", @"　%TEXT_LJ("睡衣", 20)%"
	ENDIF

	CALL PRINTL_COLORTEXT, "[1]制服・体操服・泳装", DEF_COLOR("緑")
	IF TFLAG:过滤器 == 1
		CALL SELECTS, "黒/灰色/紺色/青/赤/白", "水手", @"　%TEXT_LJ("水手", 20)%"
		IF STRCOUNT(CLO("颜绘衣装", ARG), "水手") && STRCOUNT(CLO("颜绘衣装", ARG), "无袖") == 0
			CALL SELECTS, "没有/黒/青/赤/緑色", "＆运动衫", @"　%TEXT_LJ("运动衫の衣领颜色", 20)%"
		ELSE
			CALL PRINT_STRL, @"暗灰色_　%TEXT_LJ("运动衫の衣领颜色", 20)%[没有][黒][青][赤][緑色](无袖でない水手着用時のみ)"
		ENDIF
		CALL SELECTS, "黒/灰色/紺色/青/赤/白", "无袖水手", @"　%TEXT_LJ("无袖水手", 20)%"
		PRINTL 
		CALL SELECTS, "灰色/青/赤/茶色/緑色/黒", "西装外衣", @"　%TEXT_LJ("西装外衣", 20)%"
		CALL SELECTS, "Ａ/Ｂ", "校服", @"　%TEXT_LJ("校服", 20)%"
		PRINTL 
		CALL SELECTS, "黒/青/赤/緑色", "运动衫", @"　%TEXT_LJ("运动衫", 20)%"
		CALL SELECTS, "緑色/橙/黄色/青/赤/白", "詰襟运动衫", @"　%TEXT_LJ("詰襟运动衫", 20)%"
		PRINTL 
		CALL SELECTS, "新死库水紺色/连体泳装黒/抹胸赤", "", @"　%BL(20)%"
		CALL SELECTS, "青/白", "死库水", @"　%TEXT_LJ("死库水", 20)%"
		CALL SELECTS, "黒/青/赤/緑色", "競泳", @"　%TEXT_LJ("競泳", 20)%"
	ENDIF

	CALL PRINTL_COLORTEXT, "[2]恤・女式衬衫・夹克・背心・紧身高袍", DEF_COLOR("緑")
	IF TFLAG:过滤器 == 2
		CALL SELECTS, "恤/宽大的Ｔ恤/女式衬衫白/宽松女式衬衫", "", @"　%BL(20)%"
		CALL SELECTS, "黄色/白/黒/赤/緑色/青/紫/桃/橙", "Ｔ恤", @"　%TEXT_LJ("Ｔ恤", 20)%"
		IF STRCOUNT(CLO("颜绘衣装", ARG), "Ｔ恤")
			CALL SELECTS, "没有/漢字１黒/英字１黒/英字２黒/ＡＡ１黒", "/変Ｔ", @"　%TEXT_LJ("変Ｔ文字", 20)%"
			CALL SELECTS, "漢字１白/英字１白/英字２白/ＡＡ１白", "/変Ｔ", @"　%BL(20)%　　　"
		ELSE
			CALL PRINT_STRL, @"暗灰色_　%TEXT_LJ("変Ｔ文字", 20)%[没有][漢字１黒][英字１黒][英字２黒][ＡＡ１黒]"
			CALL PRINT_STRL, @"暗灰色_　%BL(20)%　　　[漢字１白][英字１白][英字２白][ＡＡ１白](Ｔ恤着用時のみ)"
		ENDIF
		PRINTL 
		CALL SELECTS, "黒/青/赤", "領帯女式衬衫", @"　%TEXT_LJ("領帯女式衬衫", 20)%"
		CALL SELECTS, "黒/青/赤/緑色", "蝴蝶結女式衬衫", @"　%TEXT_LJ("蝴蝶結女式衬衫", 20)%"
		CALL SELECTS, "黒/白", "褶边女式衬衫", @"　%TEXT_LJ("褶边女式衬衫", 20)%"
		CALL SELECTS, "Ａ/Ｂ", "无袖女式衬衫", @"　%TEXT_LJ("无袖女式衬衫", 20)%"
		IF STRCOUNT(CLO("颜绘衣装", ARG), "无袖女式衬衫")
			CALL SELECTS, "没有/橙/灰色/赤/青/緑色/濃紫/桃/白/黒", "＆肩出し夹克", @"　%TEXT_LJ("肩出し夹克", 20)%"
		ELSE
			CALL PRINT_STRL, @"暗灰色_　%TEXT_LJ("肩出し夹克", 20)%[没有][橙][灰色][赤][青][緑色][濃紫][桃][白][黒](无袖着用時のみ)"
		ENDIF
		CALL SELECTS, "黒/米色/白/灰色", "露肩外衣", @"　%TEXT_LJ("露肩外衣", 20)%"
		CALL SELECTS, "白/黒/緑色/紺色", "恤夹克", @"　%TEXT_LJ("恤夹克", 20)%"
		CALL SELECTS, "白/黒/緑色/紺色", "前開的衬衫", @"　%TEXT_LJ("前開的衬衫", 20)%"
		CALL SELECTS, "灰色/赤/青/茶色/緑色/黒", "背心", @"　%TEXT_LJ("背心", 20)%"
		CALL SELECTS, "灰色/赤/青/橙/緑色/濃紫/桃/白/黒", "V领毛衣", @"　%TEXT_LJ("V领毛衣", 20)%"
		CALL SELECTS, "黄色/紺色", "紧身高袍", @"　%TEXT_LJ("紧身高袍", 20)%"
		CALL SELECTS, "赤/緑色/青", "中世紧身高袍", @"　%TEXT_LJ("中世紧身高袍", 20)%"
	ENDIF

	CALL PRINTL_COLORTEXT, "[3]連帽風衣・外套・毛衣・礼服・連衣裙", DEF_COLOR("緑")
	IF TFLAG:过滤器 == 3
		CALL SELECTS, "灰色/白/青/赤", "連帽風衣", @"　%TEXT_LJ("連帽風衣", 20)%"
		CALL SELECTS, "灰色/白/青/赤", "前開連帽風衣", @"　%TEXT_LJ("前開連帽風衣", 20)%"
		CALL SELECTS, "茶色/赤/白", "外套", @"　%TEXT_LJ("外套", 20)%"
		CALL SELECTS, "茶色/赤/白", "裸外套", @"　%TEXT_LJ("裸外套", 20)%"
		CALL SELECTS, "茶色/白/焦茶色/黒/青", "粗呢外套", @"　%TEXT_LJ("粗呢外套", 20)%"
		CALL SELECTS, "灰色/茶色/赤/緑色/紺色", "褞袍", @"　%TEXT_LJ("褞袍", 20)%"
		CALL SELECTS, "緑色/白/赤/紺色/水色/茶色", "针织毛衣", @"　%TEXT_LJ("针织毛衣", 20)%"
		CALL SELECTS, "紫/灰色/紺色/赤/茶色/緑色/黒", "縦毛衣", @"　%TEXT_LJ("縦毛衣", 20)%"
		CALL SELECTS, "高領毛衣/開胸高領毛衣", "", @"　%TEXT_LJ("", 20)%"
		CALL SELECTS, "针织露肩衣/无袖毛衣", "", @"　%TEXT_LJ("", 20)%"
		PRINTL 
		CALL SELECTS, "黒/赤/白", "哥特蘿莉礼服", @"　%TEXT_LJ("哥特蘿莉礼服", 20)%"
		CALL SELECTS, "黒/赤/白/緑色", "夏日礼服", @"　%TEXT_LJ("夏日礼服", 20)%"
		CALL SELECTS, "哥特礼服/哥特連衣裙/洛丽塔連衣裙/白色連衣裙", "", @"　%TEXT_LJ("", 20)%"
	ENDIF

	CALL PRINTL_COLORTEXT, "[4]Cosplay・和装", DEF_COLOR("緑")
	IF TFLAG:过滤器 == 4
		CALL SELECTS, "黒/青/赤/緑色/桃/水色", "女仆装", @"　%TEXT_LJ("女仆装", 20)%"
		CALL SELECTS, "黒/灰色/緑色", "執事服", @"　%TEXT_LJ("執事服", 20)%"
		CALL SELECTS, "黒/青/赤/緑色", "旗袍", @"　%TEXT_LJ("旗袍", 20)%"
		CALL SELECTS, "黒/青/赤/緑色/白", "胸開き旗袍", @"　%TEXT_LJ("胸開き旗袍", 20)%"
		CALL SELECTS, "黒/紫/青/赤/白", "兔女郎制服", @"　%TEXT_LJ("兔女郎制服", 20)%"
		CALL SELECTS, "黒/赤", "拘束衣", @"　%TEXT_LJ("拘束衣", 20)%"
		CALL SELECTS, "黒/赤", "紐拘束衣", @"　%TEXT_LJ("紐拘束衣", 20)%"
		PRINTL 
		CALL SELECTS, "黒/紫/青/赤/緑色", "振袖", @"　%TEXT_LJ("振袖", 20)%"
		CALL SELECTS, "黄色/黒/青/赤/緑色", "浴衣", @"　%TEXT_LJ("浴衣", 20)%"
		CALL SELECTS, "黒/青/赤/白/緑色", "和服", @"　%TEXT_LJ("和服", 20)%"
		PRINTL 
		CALL SELECTS, "圍裙/巫女服/修道服/护士服", "", @"　%TEXT_LJ("", 20)%"
		CALL SELECTS, "啦啦队制服/婚纱礼服", "", @"　%TEXT_LJ("", 20)%"
	ENDIF
CASEELSE
	IF ARGS != "髪型変更"
		CFLAG:ARG:髪型変更のお願い中 = 0
		IF ARG == MASTER
			CALL PRINT_STRL, "已经设定完的服装会变成_緑_[緑色]_"
			CALL PRINT_STRL, PRINT_TABLE
		ELSE
			;CALL PRINT_STRL, "髪の色～口の形の設定をダブルクリックすると_緑_緑色_に変わり、ランダムの際に固定するようになります"
			;CALL PRINT_STRL, "固定を解除する場合には、もう一度選択してください"
			CALL PRINT_STRL, "　_黄色_BUTTON_[顔設定]_BUTTON_[表示衣装変更]"
		ENDIF
		CALL PRINT_STRL, "※_緑_[0]～[2]_から表示する項目を選択すると、詳細が開きます"
		CALL PRINT_STRL, "※_黄色_同じものを２連続で選ぶと随机固定できます_。例）[巻髪]を２連続で選ぶと髪長が巻髪に固定"
		PRINTL 

		CALL PRINTL_COLORTEXT, "[0]髪の設定", DEF_COLOR("緑")
		IF TFLAG:过滤器 == 0
			CALL SELECTSR, "黒/金/銀/赤/青/緑/茶/紫/桃髪/紅/白/紺/焦茶/深緑/棕", "髪", @"　\@ STRCOUNT(@"/%STR:颜绘固定%", "/髪色/") ? _黄色_ # \@髪色:%TEXT_LJ(@"%CONDS("髪色の種類", ARG)%髪", 18)%"
			PRINTL 

			CALL PRINT_STRL, @"\@ STRCOUNT(@"/%STR:颜绘固定%", "/髪長/") ? _黄色_ # \@髪長:%TEXT_LJ(CONDS("髪長", ARG), 18)%_BUTTON_[短]_BUTTON_[巻髪]_BUTTON_[中等長度]_BUTTON_[披肩]_BUTTON_[長]"
			PRINTL 
		ENDIF
	ENDIF
	IF TFLAG:过滤器 == 0
		CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:颜绘固定%", "/後髪/") ? _黄色_ # \@後髪:%TEXT_LJ(CONDS("後髪", ARG), 18)%_BUTTON_[自然]_BUTTON_[直髪]_BUTTON_[外八]_BUTTON_[卷髪]"
		PRINTL 

		CALL PRINT_STRL, @"　　\@ STRCOUNT(@"/%STR:颜绘固定%", "/前髪/") ? _黄色_ # \@前髪:%TEXT_LJ(CONDS("前髪", ARG), 18)%_BUTTON_[自然刘海]_BUTTON_[自然刘海２]_BUTTON_[短刘海]_BUTTON_[短前发]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[Ｍ字刘海]_BUTTON_[斜刘海]_BUTTON_[斜刘海２]_BUTTON_[齐刘海]_BUTTON_[撩刘海]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[单眼遮盖]_BUTTON_[編髪]_BUTTON_[外八刘海]_BUTTON_[双眼遮盖]_BUTTON_[長鬓角]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[乱髪]_BUTTON_[乱髪２]_BUTTON_[乱髪３]_BUTTON_[乱髪４]_BUTTON_[乱髪５]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[露额头]_BUTTON_[不对称]_BUTTON_[不对称２]_BUTTON_[进气口发型]_BUTTON_[右斜]_BUTTON_[中分]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[大半遮盖]_BUTTON_[大半遮盖２]_BUTTON_[半遮眼]_BUTTON_[自然卷]_BUTTON_[交叉前发]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[透明刘海]_BUTTON_[透明刘海２]_BUTTON_[透明刘海３]"
		PRINTL 

		CALL PRINT_STRL, @"\@ STRCOUNT(@"/%STR:颜绘固定%", "/改髪/") ? _黄色_ # \@改髪:%TEXT_LJ(CONDS("改髪", ARG), 18)%_BUTTON_[无特殊髪型]_BUTTON_[马尾辫]_BUTTON_[三股辫]_BUTTON_[大三股辫]_BUTTON_[団子頭]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[双马尾]_BUTTON_[侧马尾]_BUTTON_[短双马尾]_BUTTON_[長双马尾]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[卷双马尾]_BUTTON_[钻头双马尾]_BUTTON_[团子头双马尾]_BUTTON_[编起来]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[麻花辫]_BUTTON_[单辫]_BUTTON_[双辫]_BUTTON_[盘头]_BUTTON_[侧盘头]_BUTTON_[简易扎发]"
		PRINTL 

		CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:颜绘固定%", "/呆毛/") ? _黄色_ # \@呆毛:%TEXT_LJ(CONDS("呆毛", ARG), 18)%_BUTTON_[无呆毛]_BUTTON_[呆毛]_BUTTON_[呆毛２]_BUTTON_[呆毛３]_BUTTON_[触角]"
		PRINTL 
		CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:颜绘固定%", "/蝴蝶結/") ? _黄色_ # \@蝴蝶結:%TEXT_LJ(CONDS("蝴蝶結", ARG), 18)%_BUTTON_[无蝴蝶結]_BUTTON_[両側蝴蝶結]_BUTTON_[单側蝴蝶結]_BUTTON_[頭後蝴蝶結]_BUTTON_[頭上蝴蝶結]"
		[IF_DEBUG]
			CALL PRINT_STRL, @"%BL(27)%_BUTTON_[单側蝴蝶結２]"
		[ENDIF]
	ENDIF

	IF ARGS != "髪型変更"
		CALL PRINTL_COLORTEXT, "[1]表情の設定", DEF_COLOR("緑")
		IF TFLAG:过滤器 == 1
			CALL SELECTS, "黒/青/紫/緑/赤/茶/金/紅/白/紺/焦茶/深緑/棕/銀", "", @"　\@ STRCOUNT(@"/%STR:颜绘固定%", "/瞳色/") ? _黄色_ # \@瞳色:%TEXT_LJ(CONDS("右瞳色", ARG), 18)%"
			CALL SELECTS, "黒/青/紫/緑/赤/茶/金/紅/白/紺/焦茶/深緑/棕/銀", "左", @"\@ STRCOUNT(@"/%STR:颜绘固定%", "/瞳色/") ? _黄色_ # \@左瞳色:%TEXT_LJ(CONDS("左瞳色", ARG), 18)%"
			PRINTL 

			CALL SELECTS, "普通眼/普通眼２/吊眼/吊眼２/垂眼/垂眼２", "", @"　\@ STRCOUNT(@"/%STR:颜绘固定%", "/眼形/") ? _黄色_ # \@眼形:%TEXT_LJ(CONDS("右眼形", ARG), 18)%"
			CALL SELECTS, "斜眼/斜眼２/水灵眼/眯眯眼/縦長目/三白眼/重度三白眼", "", BL(27)
			[IF_DEBUG]
				CALL SELECTS, "にっこり/瞑り/〇〇/＞＜", "", BL(27)
				PRINTL 

				CALL SELECTS, "普通眼/普通眼２/吊眼/吊眼２/垂眼/垂眼２", "左", @"\@ STRCOUNT(@"/%STR:颜绘固定%", "/眼形/") ? _黄色_ # \@左眼形:%TEXT_LJ(CONDS("左眼形", ARG), 18)%"
				CALL SELECTS, "斜眼/斜眼２/水灵眼/眯眯眼/縦長目/三白眼/重度三白眼", "左", BL(27)
				CALL SELECTS, "にっこり/瞑り/〇〇/＞＜", "左", BL(27)
			[ENDIF]
			PRINTL 

			;黒目の記載追加_2020_06_24_作者Ｉ
			CALL SELECTS, "无眼白/有眼白/黑眼白", "", @"　　\@ STRCOUNT(@"/%STR:颜绘固定%", "/白眼/") ? _黄色_ # \@白眼:%TEXT_LJ(CONDS("右白眼", ARG), 18)%"
			[IF_DEBUG]
				CALL SELECTS, "无眼白/有眼白/黑眼白", "左", @"　\@ STRCOUNT(@"/%STR:颜绘固定%", "/白眼/") ? _黄色_ # \@左白眼:%TEXT_LJ(CONDS("左白眼", ARG), 18)%"
			[ENDIF]
			PRINTL 

			CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:颜绘固定%", "/眉形/") ? _黄色_ # \@眉形:%TEXT_LJ(CONDS("眉形", ARG), 18)%_BUTTON_[通常眉]_BUTTON_[憤怒眉]_BUTTON_[斜眉]_BUTTON_[困扰眉]_BUTTON_[麻呂眉]"
			CALL PRINT_STRL, @"%BL(27)%_BUTTON_[短通常眉]_BUTTON_[短憤怒眉]_BUTTON_[短斜眉]_BUTTON_[短困扰眉]"
			PRINTL 

			CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:颜绘固定%", "/嘴形/") ? _黄色_ # \@嘴形:%TEXT_LJ(CONDS("嘴形", ARG), 18)%_BUTTON_[微笑]_BUTTON_[へ字]_BUTTON_[笑顔]_BUTTON_[△]_BUTTON_[大口]_BUTTON_[無口系]_BUTTON_[ω]_BUTTON_[ワ]_BUTTON_[半開]"
			CALL PRINT_STRL, @"%BL(27)%_BUTTON_[嘲笑]_BUTTON_[小口]_BUTTON_[嘟嘴]"
			PRINTL 

			IF NO:ARG  == 99 || ARG == MASTER
				CALL PRINT_STRL, @"　　\@ STRCOUNT(@"/%STR:颜绘固定%", "/眼鏡/") ? _黄色_ # \@眼鏡:%TEXT_LJ(CONDS("眼鏡", ARG), 18)%_BUTTON_[无眼鏡]_BUTTON_[眼鏡]_BUTTON_[专家眼鏡]_BUTTON_[黑框眼鏡]_BUTTON_[金丝眼鏡]_BUTTON_[玳瑁眼鏡]"
				CALL PRINT_STRL, @"%BL(27)%_BUTTON_[太阳鏡]_BUTTON_[眼罩白]_BUTTON_[眼罩黒]"
			ENDIF
		ENDIF
		SIF NO:ARG  == 99 || ARG == MASTER
			CALL PRINTL_COLORTEXT, "[2]其他の設定(ここの項目は全て随机固定です)", DEF_COLOR("緑")
		IF TFLAG:过滤器 == 2
			IF NO:ARG  == 99
				CALL PRINT_STRL, @"　肤色:%TEXT_LJ(CONDS("肤色", ARG), 18)%_BUTTON_[普通]_BUTTON_[褐色肌]_BUTTON_[色白]_BUTTON_[青肌]"
				CALL PRINT_STRL, @"　耳の形:%TEXT_LJ(CONDS("耳の形", ARG), 18)%_BUTTON_[人类耳]_BUTTON_[精灵耳]_BUTTON_[魔族耳]_BUTTON_[動物耳]_BUTTON_[動物耷拉耳]"
				CALL PRINT_STRL, @"　　種族:%TEXT_LJ(CONDS("種族", ARG), 18)%_BUTTON_[人类]_BUTTON_[獣人]_BUTTON_[兎獣人]_BUTTON_[妖狐]_BUTTON_[天使]_BUTTON_[悪魔]_BUTTON_[鬼]_BUTTON_[竜人]_BUTTON_[淫魔]_BUTTON_[吸血鬼]"
				PRINTL 

				CALL PRINT_STRL, "イエロー_　※頬傷と創口貼は複数選択できます(再選択でON/OFF切り替え)※"
				CALL PRINT_STRL, @"　頬化粧:%TEXT_LJ(CONDS("頬化粧", ARG), 18)%_BUTTON_[頬化粧没有]_BUTTON_[脸谱赤]_BUTTON_[脸谱青]_BUTTON_[战妆]"
				CALL PRINT_STRL, @"　　頬傷:%TEXT_LJ(CONDS("頬傷", ARG), 18)%_BUTTON_[頬傷没有]_BUTTON_[頬傷左]_BUTTON_[頬傷鼻]_BUTTON_[頬傷右]"
				CALL PRINT_STRL, @"頬創口貼:%TEXT_LJ(CONDS("頬創口貼", ARG), 18)%_BUTTON_[頬創口貼没有]_BUTTON_[頬創口貼左]_BUTTON_[頬創口貼鼻]_BUTTON_[頬創口貼右]"
				PRINTL 

				CALL SELECTS, "状態重置/睡眠/甜言蜜语/夸奖/心情差/躁动难忍/目陰", "", @"　　状態:%TEXT_LJ(CONDS("颜绘コンディション", ARG), 18)%"
				CALL SELECTS, "刚睡醒/憤怒/害羞/惊嚇/小悪魔/红潮恍惚/爱心眼", "", BL(27)
				PRINTL 

				CALL PRINT_STRL, @"　脸颊:%TEXT_LJ(FACES("脸颊の種類", ARG), 18)%_BUTTON_[脸颊没有]_BUTTON_[楕円]_BUTTON_[红晕]_BUTTON_[婴儿红晕]_BUTTON_[紅潮]_BUTTON_[赤面]_BUTTON_[血迹]"
				PRINTL 

				CALL SELECTS, "没有/憤怒/汗/爱心/兴奋/惊讶/睡眠/困倦", "对话框", @"对话框:%TEXT_LJ(FACES("对话框の種類", ARG), 18)%"
				CALL SELECTS, "開心/・・・/？/！/爱心大量", "对话框", BL(27)
				PRINTL 

				CALL SELECTS, "小物没有/烟/冰/pocky/面包/法棍", "", @"　　小物:%TEXT_LJ(FACES("小物", ARG), 18)%"
				CALL SELECTS, "挎包/茶/橘子/本/頭上橘子/頭上太阳鏡", "", BL(27)
				CALL SELECTS, "扇子赤/扇子富士山/扇子桜/扇子白/萌え袖/避孕套", "", BL(27)
				PRINTL 
			ENDIF
		ENDIF
		IF ARG == MASTER
			CALL PRINT_STRL, @"　这様就好？%BL(13)%_BUTTON_[ＯＫ！]_BUTTON_[回到初始状态]_BUTTON_[随机]_BUTTON_[不出现黒眼的随机]_BUTTON_[放弃显示顔绘]"
		ELSE
			CALL PRINT_STR, @"　这様就好？%BL(13)%_BUTTON_[ＯＫ！]_BUTTON_[回到初始状态]_BUTTON_[随机]_BUTTON_[不出现黒眼的随机]"
			[IF_DEBUG]
				SIF NO:ARG  == 99
					CALL PRINT_STR, @"BUTTON_[表情を出力]"
			[ENDIF]
			PRINTL
		ENDIF
	ELSE
		CALL PRINT_STRL, @"　这様就好？%BL(13)%_BUTTON_[ＯＫ！]_BUTTON_[回到初始状态]"
	ENDIF
ENDSELECT

INPUTS
CHOICES = %EXTSTR(RESULTS, "[", "]")%
CLEARLINE 1

SELECTCASE CHOICES
CASE "0", "1", "2", "3", "4"
	SIF ARGS != "髪型変更"
		TFLAG:过滤器 = TOINT(CHOICES)
CASE "状態重置", "睡眠", "甜言蜜语", "夸奖", "心情差", "躁动难忍", "刚睡醒", "憤怒", "害羞", "惊嚇", "小悪魔", "红潮恍惚", "爱心眼", "目陰"
	CFLAG:ARG:睡眠 = 0
	CFLAG:ARG:甜言蜜语 = 0
	CFLAG:ARG:夸奖 = 0
	CFLAG:ARG:心情差 = 0
	CFLAG:ARG:躁动难忍 = 0
	CFLAG:ARG:刚睡醒 = 0
	CFLAG:ARG:憤怒 = 0
	CFLAG:ARG:害羞 = 0

	SELECTCASE CHOICES
	CASE "状態重置"
		STR:颜绘変化 = 
	CASE "睡眠", "甜言蜜语", "夸奖", "心情差", "躁动难忍", "刚睡醒", "憤怒", "害羞"
		CFLAG:ARG:CHOICES = 1
		IF STRCOUNT(STR:颜绘変化, "爱心眼")
			STR:颜绘変化 = 爱心眼
		ELSE
			STR:颜绘変化 = 
		ENDIF
	CASE "惊嚇", "小悪魔", "红潮恍惚", "目陰"
		IF STRCOUNT(STR:颜绘変化, "爱心眼")
			STR:颜绘変化 = %CHOICES%/爱心眼
		ELSE
			STR:颜绘変化 = %CHOICES%/
		ENDIF
	CASE "爱心眼"
		SIF STRCOUNT(STR:颜绘変化, "爱心眼") == 0
			STR:颜绘変化 += "爱心眼/"
	ENDSELECT
CASE "随机", "不出现黒眼的随机"
	SIF STRCOUNT(@"/%STR:颜绘固定%", "/髪色/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("黒髪/金髪/銀髪/赤髪/青髪/緑髪/茶髪/紫髪/桃髪/紅髪/白髪/紺髪/焦茶髪/深緑髪/棕髪"), ARG
	SIF STRCOUNT(@"/%STR:颜绘固定%", "/髪長/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("短/巻髪/中等長度/披肩/長"), ARG
	SIF STRCOUNT(@"/%STR:颜绘固定%", "/前髪/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("自然刘海/自然バング２/短刘海/Ｍ字刘海/斜刘海/斜めバング２/齐刘海/撩刘海/单眼遮盖/編髪/外八刘海/双眼遮盖/長鬓角/露额头/乱髪/ボサボサ２/乱髪３/乱髪４/乱髪５/不对称/不对称２/进气口发型/透明刘海/透明バング２/透明刘海３/右斜/中分/大半遮盖/大半遮盖２/半遮眼/交叉前发/短前发/自然卷"), ARG
	SIF STRCOUNT(@"/%STR:颜绘固定%", "/後髪/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("自然/直髪/外八/卷髪"), ARG
	SIF STRCOUNT(@"/%STR:颜绘固定%", "/改髪/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("无特殊髪型/无特殊髪型/无特殊髪型/无特殊髪型/双马尾/侧马尾/马尾辫/三股辫/大三股辫/団子頭/短双马尾/長双马尾/卷双马尾/钻头双马尾/团子头双马尾/编起来/盘头/侧盘头/麻花辫/单辫/双辫/简易扎发"), ARG
	SIF STRCOUNT(@"/%STR:颜绘固定%", "/呆毛/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("无呆毛/呆毛无/无呆毛/呆毛无/呆毛/呆毛２/呆毛３/触角"), ARG
	SIF STRCOUNT(@"/%STR:颜绘固定%", "/蝴蝶結/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("无蝴蝶結/无蝴蝶結/无蝴蝶結/无蝴蝶結/両側蝴蝶結/单側蝴蝶結/单側蝴蝶結２/頭後蝴蝶結/頭上蝴蝶結"), ARG

	IF STRCOUNT(@"/%STR:颜绘固定%", "/瞳色/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("黒/青/紫/緑/赤/茶/金/紅/白/紺/焦茶/深緑/棕/銀"), ARG
		SIF RAND:10 == 0
			CALL CHANGE_MAIDFACE, TEXTR("左黒/左青/左紫/左緑/左赤/左茶/左金/左紅/左白/左紺/左焦茶/左深緑/左棕/左銀"), ARG
	ENDIF

	SIF STRCOUNT(@"/%STR:颜绘固定%", "/眼形/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("普通眼/普通眼２/吊眼/吊眼２/垂眼/垂眼２/斜眼/斜眼２/水灵眼/眯眯眼/縦長目/三白眼/重度三白眼"), ARG

	IF STRCOUNT(@"/%STR:颜绘固定%", "/白眼/") == 0
		IF CHOICES == "不出现黒眼的随机"
			CALL CHANGE_MAIDFACE, TEXTR("无眼白/有眼白"), ARG
		ELSE
			CALL CHANGE_MAIDFACE, TEXTR("无眼白/无眼白/无眼白/无眼白/有眼白/有眼白/有眼白/有眼白/黑眼白"), ARG
		ENDIF
	ENDIF
	SIF STRCOUNT(@"/%STR:颜绘固定%", "/眉形/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("通常眉/短通常眉/憤怒眉/短憤怒眉/斜眉/短斜眉/困扰眉/短困扰眉/麻呂眉"), ARG
	SIF STRCOUNT(@"/%STR:颜绘固定%", "/嘴形/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("微笑/へ字/笑顔/△/大口/無口系/ω/ワ/半開/嘲笑/小口/嘟嘴"), ARG
	IF STRCOUNT(@"/%STR:颜绘固定%", "/眼鏡/") == 0
		IF RAND:3 == 0
			CALL SET_OTHER, TEXTR("眼鏡/专家眼鏡/黑框眼鏡/金丝眼鏡/玳瑁眼鏡/太阳鏡"), ARG
		ELSE
			CALL RESET_OTHER, "眼鏡", ARG
		ENDIF
	ENDIF
CASE "回到初始状态"
	CALL SETFLAG, "MEMO_髪からの復元", ARG
	CALL SETFLAG, "MEMO_表情からの復元", ARG
	STR:颜绘変化 =
	CFLAG:ARG:睡眠 = 0
	CFLAG:ARG:甜言蜜语 = 0
	CFLAG:ARG:夸奖 = 0
	CFLAG:ARG:心情差 = 0
	CFLAG:ARG:躁动难忍 = 0
	CFLAG:ARG:刚睡醒 = 0
	CFLAG:ARG:憤怒 = 0
	CFLAG:ARG:害羞 = 0
CASE "放弃显示顔绘"
	CFLAG:ARG:颜绘 = 0
	RETURN 0
CASE "ＯＫ！"
	PRINTL 
	IF COND("髪型変更のお願い中", ARG)
		RETURN 1
	ELSEIF NO:ARG  == 99
		PRINTFORML 该顔绘対応的CSTR与素質如下所示。
		PRINTL 
		SIF TALENT:ARG:肤色
			PRINTFORML 素質,肤色,{TALENT:ARG:肤色},
		SIF TALENT:ARG:精灵耳
			PRINTFORML 素質,精灵耳,{TALENT:ARG:精灵耳},
		SIF TALENT:ARG:動物耳
			PRINTFORML 素質,動物耳,{TALENT:ARG:動物耳},
		SIF TALENT:ARG:角
			PRINTFORML 素質,角,1,
		PRINTFORML CSTR,種族,%CSTR:ARG:種族%,
		FOR LCOUNT, 90, 110
			SIF CSTR:ARG:LCOUNT == ""
				CONTINUE
			PRINTFORML CSTR,%CSTRNAME:LCOUNT%,%CSTR:ARG:LCOUNT%,
		NEXT
		IF CHECK_OTHER("眼鏡", ARG)
			PRINTL 
			PRINTFORML EVENT_KX1_CLOTHES.ERB 内の CASE "飾品"に
			PRINTFORML CALL SET_OTHER, "%SUBSTRING(CSTR:ARG:飾品, 0, STRLENS(CSTR:ARG:飾品) - 1)%"
		ENDIF
		PRINTL 
		PRINTFORMW CSV(眼鏡等飾品に関してはERB)にコピペする事で、角色の颜绘を只今設定したものにする事が出来ます。
		PRINTFORMW 依次点击窗口画面上的 ファイル→ログを阴蒂ップボードにコピー→向下滚动 可以这样获得。
	ELSEIF COND("角色メイク中", ARG) == 0
		PRINTFORMW [%CSTR:ARG:称号%]%NAME:ARG%的顔绘設定完毕。
		CALL SETFLAG, "普段の髪型記録", ARG
	ENDIF
	RETURN 1
CASE "表情を出力"
	;まずは表情から
	IF CONDS("颜绘コンディション", ARG) == ""
		EXAMPLE_EMOTE = リセット
	ELSE
		EXAMPLE_EMOTE = %CONDS("颜绘コンディション", ARG)%
	ENDIF

	;目や口が変わっている箇所だけ抜き出して入れる
	FOR LCOUNT, 100, 110
		SIF CSTR:ARG:LCOUNT == ""
			CONTINUE
		SIF CSTR:ARG:LCOUNT == CSTR:LCOUNT
			CONTINUE
		;100、102は変更の機能がない…念のため残しておく
		SELECTCASE LCOUNT
		CASE 100, 102
			CONTINUE
		CASEELSE
		ENDSELECT

		SIF EXAMPLE_EMOTE != ""
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/
		SELECTCASE LCOUNT
		CASE 100
			IF CONDS("左瞳色", ARG) == CONDS("右瞳色", ARG)
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右瞳色", ARG)%
			ELSEIF CONDS("右瞳色", ARG) != CONDS("右瞳色")
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右瞳色", ARG)%/左%CONDS("左瞳色", ARG)%
			ELSE
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%左%CONDS("左瞳色", ARG)%
			ENDIF
		CASE 101
			IF CONDS("左眼形", ARG) == CONDS("右眼形", ARG)
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右眼形", ARG)%
			ELSEIF CONDS("右眼形", ARG) != CONDS("右眼形")
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右眼形", ARG)%/左%CONDS("左眼形", ARG)%
			ELSE
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%左%CONDS("左眼形", ARG)%
			ENDIF
		CASE 102
			IF CONDS("左白眼", ARG) == CONDS("右白眼", ARG)
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右白眼", ARG)%
			ELSEIF CONDS("右白眼", ARG) != CONDS("右白眼")
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右白眼", ARG)%/左%CONDS("左白眼", ARG)%
			ELSE
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%左%CONDS("左白眼", ARG)%
			ENDIF
		CASEELSE
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CSTR:ARG:LCOUNT%
		ENDSELECT
	NEXT
	IF STR:颜绘変化 != ""
		SIF STRCOUNT(STR:颜绘変化, "爱心眼")
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/爱心眼
		SIF FACES("脸颊の種類", ARG) != "没有"
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/%FACES("脸颊の種類", ARG)%
		SIF FACES("对话框の種類", ARG) != "没有"
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/%FACES("对话框の種類", ARG)%
		SIF FACES("小物", ARG) != "没有"
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/%FACES("小物", ARG)%
	ENDIF
	PRINTL 
	DRAWLINE
	PRINTFORMW CALL EMOTION_PLUS, "%EXAMPLE_EMOTE%"

CASE "顔設定", "表示衣装変更"
	TFLAG:过滤器 = 0
	ARGS = %CHOICES%
CASE "制服", "体操服", "泳装", "自宅着", "外出着"
	ARGS = %CHOICES%
CASE "普通"
	TALENT:ARG:肤色 = 0
CASE "褐色肌"
	TALENT:ARG:肤色 = 1
CASE "色白"
	TALENT:ARG:肤色 = 2
CASE "青肌"
	TALENT:ARG:肤色 = 3
CASE "黒髪", "金髪", "銀髪", "赤髪", "青髪", "緑髪", "茶髪", "紫髪", "桃髪", "紅髪", "白髪", "紺髪", "焦茶髪", "深緑髪", "棕髪"
	IF CSTR:ARG:髪色 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "髪色")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "髪色")%
	ENDIF
	CSTR:ARG:髪色 = %CHOICES%
CASE "短", "巻髪", "中等長度", "披肩", "長"
	IF CSTR:ARG:髪長 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "髪長")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "髪長")%
	ENDIF
	CSTR:ARG:髪長 = %CHOICES%
CASE "自然刘海", "自然刘海２", "短刘海", "Ｍ字刘海", "斜刘海", "斜刘海２", "齐刘海", "撩刘海", "单眼遮盖", "編髪", "外八刘海", "双眼遮盖", "長鬓角", "露额头", "乱髪", "乱髪２", "乱髪３", "乱髪４", "乱髪５", "不对称", "不对称２", "进气口发型", "透明刘海", "透明刘海２", "透明刘海３", "右斜", "中分", "大半遮盖", "大半遮盖２", "半遮眼", "交叉前发", "短前发", "自然卷"
	IF CSTR:ARG:前髪 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "前髪")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "前髪")%
	ENDIF
	CSTR:ARG:前髪 = %CHOICES%
CASE "自然", "直髪", "外八", "卷髪"
	IF CSTR:ARG:後髪 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "後髪")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "後髪")%
	ENDIF
	CSTR:ARG:後髪 = %CHOICES%
CASE "无特殊髪型", "双马尾", "侧马尾", "马尾辫", "三股辫", "大三股辫", "団子頭", "短双马尾", "長双马尾", "卷双马尾", "钻头双马尾", "团子头双马尾", "编起来", "盘头", "侧盘头", "麻花辫", "单辫", "双辫", "简易扎发"
	IF CSTR:ARG:改髪 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "改髪")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "改髪")%
	ENDIF
	CSTR:ARG:改髪 = %CHOICES%
CASE "无呆毛", "呆毛", "呆毛２", "呆毛３", "触角"
	IF CSTR:ARG:呆毛 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "呆毛")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "呆毛")%
	ENDIF
	CSTR:ARG:呆毛 = %CHOICES%
CASE "无蝴蝶結", "両側蝴蝶結", "单側蝴蝶結", "单側蝴蝶結２", "頭後蝴蝶結", "頭上蝴蝶結"
	IF CSTR:ARG:蝴蝶結 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "蝴蝶結")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "蝴蝶結")%
	ENDIF
	CSTR:ARG:蝴蝶結 = %CHOICES%
CASE "黒", "青", "紫", "緑", "赤", "茶", "金", "紅", "白", "紺", "焦茶", "深緑", "棕", "銀"
	IF CSTR:ARG:瞳色 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "瞳色")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "瞳色")%
	ENDIF
	CSTR:ARG:瞳色 = %CHOICES%
CASE "左黒", "左青", "左紫", "左緑", "左赤", "左茶", "左金", "左紅", "左白", "左紺", "左焦茶", "左深緑", "左棕", "左銀"
	IF CONDS("左瞳色", ARG) == SUBSTRINGU(CHOICES, 1, -1)
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "瞳色")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "瞳色")%
	ENDIF
	CSTR:ARG:瞳色 = %CONDS("右瞳色", ARG)%_%SUBSTRINGU(CHOICES, 1, -1)%
	SIF CONDS("左瞳色", ARG) == CONDS("右瞳色", ARG)
		CSTR:ARG:瞳色 = %CONDS("右瞳色", ARG)%

CASE "普通眼", "普通眼２", "吊眼", "吊眼２", "垂眼", "垂眼２", "斜眼", "斜眼２", "水灵眼", "眯眯眼", "縦長目", "三白眼", "重度三白眼", "にっこり", "瞑り", "〇〇", "＞＜"
	IF CSTR:ARG:眼形 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "眼形")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "眼形")%
	ENDIF
	CSTR:ARG:眼形 = %CHOICES%
CASE "左普通眼", "左普通眼２", "左吊眼", "左吊眼２", "左垂眼", "左垂眼２", "左斜眼", "左斜眼２", "左水灵眼", "左眯眯眼", "左縦長目", "左三白眼", "左重度三白眼", "左にっこり", "左瞑り", "左〇〇", "左＞＜"
	IF CONDS("左眼形", ARG) == SUBSTRINGU(CHOICES, 1, -1)
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "眼形")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "眼形")%
	ENDIF
	CSTR:ARG:眼形 = %CONDS("右眼形", ARG)%_%SUBSTRINGU(CHOICES, 1, -1)%
	SIF CONDS("左眼形", ARG) == CONDS("右眼形", ARG)
		CSTR:ARG:眼形 = %CONDS("右眼形", ARG)%

;黒目の記載追加_2020_06_24_作者Ｉ
CASE "无眼白", "有眼白", "黑眼白"
	IF CSTR:ARG:白眼 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "白眼")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "白眼")%
	ENDIF
	CSTR:ARG:白眼 = %CHOICES%
CASE "左无眼白", "左有眼白", "左黑眼白"
	IF CONDS("左白眼", ARG) == SUBSTRINGU(CHOICES, 1, -1)
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "白眼")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "白眼")%
	ENDIF
	CSTR:ARG:白眼 = %CONDS("右白眼", ARG)%_%SUBSTRINGU(CHOICES, 1, -1)%
	SIF CONDS("左白眼", ARG) == CONDS("右白眼", ARG)
		CSTR:ARG:白眼 = %CONDS("右白眼", ARG)%

CASE "通常眉", "短通常眉", "憤怒眉", "短憤怒眉", "斜眉", "短斜眉", "困扰眉", "短困扰眉", "麻呂眉"
	IF CSTR:ARG:眉形 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "眉形")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "眉形")%
	ENDIF
	CSTR:ARG:眉形 = %CHOICES%
CASE "微笑", "へ字", "笑顔", "△", "大口", "無口系", "ω", "ワ", "半開", "嘲笑", "小口", "嘟嘴"
	IF CSTR:ARG:嘴形 == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "嘴形")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "嘴形")%
	ENDIF
	CSTR:ARG:嘴形 = %CHOICES%

CASE "頬化粧没有"
	CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, "脸谱赤", "脸谱青", "战妆")%
CASE "脸谱赤", "脸谱青", "战妆"
	CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, "脸谱赤", "脸谱青", "战妆")%
	CSTR:ARG:頬 = %SETSTR(CSTR:ARG:頬, CHOICES)%
CASE "頬傷没有"
	CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, "頬傷左", "頬傷鼻", "頬傷右")%
CASE "頬創口貼没有"
	CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, "頬創口貼左", "頬創口貼鼻", "頬創口貼右")%
CASE "頬傷左", "頬傷鼻", "頬傷右", "頬創口貼左", "頬創口貼鼻", "頬創口貼右"
	IF STRCOUNT(CSTR:ARG:頬, CHOICES)
		CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, CHOICES)%
	ELSE
		CSTR:ARG:頬 = %SETSTR(CSTR:ARG:頬, CHOICES)%
	ENDIF
CASE "人类耳"
	TALENT:ARG:精灵耳 = 0
	TALENT:ARG:動物耳 = 0
CASE "精灵耳"
	TALENT:ARG:精灵耳 = 1
	TALENT:ARG:動物耳 = 0
CASE "魔族耳"
	TALENT:ARG:精灵耳 = 2
	TALENT:ARG:動物耳 = 0
CASE "動物耳"
	TALENT:ARG:精灵耳 = 0
	TALENT:ARG:動物耳 = 1
CASE "動物耷拉耳"
	TALENT:ARG:精灵耳 = 0
	TALENT:ARG:動物耳 = 2
CASE "獣人"
	CSTR:ARG:種族 = %CHOICES%
	TALENT:ARG:精灵耳 = 0
	TALENT:ARG:動物耳 = 1
	CALL GET_EXTALENT, ARG
CASE "兎獣人"
	CSTR:ARG:種族 = 獣人_兎
	TALENT:ARG:精灵耳 = 0
	TALENT:ARG:動物耳 = 1
	CALL GET_EXTALENT, ARG
CASE "悪魔", "鬼", "竜人"
	CSTR:ARG:種族 = %CHOICES%
	TALENT:ARG:角 = 1
	TALENT:ARG:動物耳 = 0
	CALL GET_EXTALENT, ARG
CASE "人类", "獣人", "妖狐", "天使", "悪魔", "鬼", "竜人", "淫魔", "吸血鬼"
	CSTR:ARG:種族 = %CHOICES%
	TALENT:ARG:動物耳 = 0
	CALL GET_EXTALENT, ARG
CASE "无眼鏡"
	IF OTHER(CSTR:ARG:飾品, "眼鏡") == ""
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "眼鏡")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "眼鏡")%
	ENDIF
	CALL RESET_OTHER, "眼鏡", ARG
CASE "眼鏡", "专家眼鏡", "黑框眼鏡", "金丝眼鏡", "玳瑁眼鏡", "太阳鏡", "眼罩白", "眼罩黒"
	IF OTHER(CSTR:ARG:飾品, "眼鏡") == CHOICES
		STR:颜绘固定 = %SETSTR(STR:颜绘固定, "眼鏡")%
	ELSE
		STR:颜绘固定 = %CLEARSTR(STR:颜绘固定, "眼鏡")%
	ENDIF
	CALL SET_OTHER, CHOICES, ARG

;エモプラ関係
CASE "脸颊没有", "赤面", "楕円", "红晕", "婴儿红晕", "紅潮", "血迹"
	STR:颜绘変化 = %REPLACE(STR:颜绘変化, @"%FACES("脸颊の種類", ARG)%/", "")%
	SIF CHOICES != "脸颊没有"
		STR:颜绘変化 = %STR:颜绘変化%%CHOICES%/
CASE "对话框没有", "对话框憤怒", "对话框汗", "对话框爱心", "对话框兴奋", "对话框惊讶", "对话框睡眠", "对话框困倦", "对话框開心", "对话框・・・", "对话框！", "对话框？", "对话框爱心大量"
	STR:颜绘変化 = %REPLACE(STR:颜绘変化, @"%FACES("对话框の種類", ARG)%/", "")%
	SIF CHOICES != "对话框没有"
		STR:颜绘変化 = %STR:颜绘変化%%CHOICES%/
CASE "小物没有", "挎包", "茶", "橘子", "本", "頭上橘子", "頭上太阳鏡", "烟", "冰", "pocky", "面包", "法棍", "避孕套", "扇子赤", "扇子富士山", "扇子桜", "扇子白", "萌え袖"
	STR:颜绘変化 = %REPLACE(STR:颜绘変化, @"%FACES("小物", ARG)%/", "")%
	SELECTCASE CHOICES
	CASE "小物没有"
	CASE "萌え袖"
		STR:颜绘変化 = %STR:颜绘変化%%CHOICES%/
		IF CHECK_CLO("萌え袖可能", ARG) == 0
			PRINTL 
			CALL PRINT_STRW, "黄色_萌え袖は、V领毛衣or运动衫or睡衣or肩出し夹克着用時に起きます"
		ENDIF
	CASEELSE
		STR:颜绘変化 = %STR:颜绘変化%%CHOICES%/
	ENDSELECT

CASEELSE
	IF STRCOUNTS(CHOICES, "＆运动衫", "＆肩出し夹克")
		IF STRCOUNT(CHOICES, "没有")
			CHOICES = %EXTSTR(CLO("颜绘衣装", ARG), "", "＆")%
		ELSE
			CHOICES = %EXTSTR(CLO("颜绘衣装", ARG), "", "＆")%%CHOICES%
		ENDIF
	ELSEIF STRCOUNT(CHOICES, "/変Ｔ")
		IF STRCOUNT(CHOICES, "没有")
			CHOICES = %EXTSTR(CLO("颜绘衣装", ARG), "", "/")%
		ELSE
			CHOICES = %EXTSTR(CLO("颜绘衣装", ARG), "", "/")%%CHOICES%
		ENDIF
	ENDIF
	SELECTCASE ARGS
	CASE "表示衣装変更"
		STR:颜绘衣装 = %CHOICES%
	CASE "制服", "体操服", "泳装", "自宅着", "外出着"
		SAVESTR:ARGS = %CHOICES%
		STR:颜绘衣装 = %CHOICES%
	ENDSELECT
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@FACES(ARGS, ARG)
#FUNCTIONS

VARSET LOCALS

SIF ARG == 0 && TARGET
	ARG = TARGET

SELECTCASE ARGS
CASE "脸颊の種類"
	IF STRCOUNT(STR:颜绘変化, "战妆")
		LOCALS = 战妆
	ELSEIF STRCOUNT(STR:颜绘変化, "脸谱赤")
		LOCALS = 脸谱赤
	ELSEIF STRCOUNT(STR:颜绘変化, "脸谱青")
		LOCALS = 脸谱青
	ELSEIF STRCOUNT(STR:颜绘変化, "赤面")
		LOCALS = 赤面
	ELSEIF STRCOUNT(STR:颜绘変化, "楕円")
		LOCALS = 楕円
	ELSEIF STRCOUNT(STR:颜绘変化, "红晕")
		LOCALS = 红晕
	ELSEIF STRCOUNT(STR:颜绘変化, "婴儿红晕")
		LOCALS = 婴儿红晕
	ELSEIF STRCOUNT(STR:颜绘変化, "紅潮")
		LOCALS = 紅潮
	ELSEIF STRCOUNT(STR:颜绘変化, "血迹")
		LOCALS = 血迹
	ELSE
		LOCALS = 没有
	ENDIF

CASE "对话框の種類"
	IF STRCOUNT(STR:颜绘変化, "对话框憤怒")
		LOCALS =对话框憤怒
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框汗")
		LOCALS = 对话框汗
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框爱心大量")
		LOCALS = 对话框爱心大量
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框爱心")
		LOCALS = 对话框爱心
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框兴奋")
		LOCALS = 对话框兴奋
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框惊讶")
		LOCALS = 对话框惊讶
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框睡眠")
		LOCALS = 对话框睡眠
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框困倦")
		LOCALS = 对话框困倦
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框開心")
		LOCALS = 对话框開心
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框・・・")
		LOCALS = 对话框・・・
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框？")
		LOCALS = 对话框？
	ELSEIF STRCOUNT(STR:颜绘変化, "对话框！")
		LOCALS = 对话框！
	ELSE
		LOCALS = 没有
	ENDIF
CASE "小物"
	IF STRCOUNT(STR:颜绘変化, "挎包")
		LOCALS = 挎包
	ELSEIF STRCOUNT(STR:颜绘変化, "頭上橘子")
		LOCALS = 頭上橘子
	ELSEIF STRCOUNT(STR:颜绘変化, "頭上太阳鏡")
		LOCALS = 頭上太阳鏡
	ELSEIF STRCOUNT(STR:颜绘変化, "茶")
		LOCALS = 茶
	ELSEIF STRCOUNT(STR:颜绘変化, "橘子")
		LOCALS = 橘子
	ELSEIF STRCOUNT(STR:颜绘変化, "本")
		LOCALS = 本
	ELSEIF STRCOUNT(STR:颜绘変化, "烟")
		LOCALS = 烟
	ELSEIF STRCOUNT(STR:颜绘変化, "冰")
		LOCALS = 冰
	ELSEIF STRCOUNT(STR:颜绘変化, "pocky")
		LOCALS = pocky
	ELSEIF STRCOUNT(STR:颜绘変化, "面包")
		LOCALS = 面包
	ELSEIF STRCOUNT(STR:颜绘変化, "法棍")
		LOCALS = 法棍
	ELSEIF STRCOUNT(STR:颜绘変化, "避孕套")
		LOCALS = 避孕套
	ELSEIF STRCOUNT(STR:颜绘変化, "扇子赤")
		LOCALS = 扇子赤
	ELSEIF STRCOUNT(STR:颜绘変化, "扇子富士山")
		LOCALS = 扇子富士山
	ELSEIF STRCOUNT(STR:颜绘変化, "扇子桜")
		LOCALS = 扇子桜
	ELSEIF STRCOUNT(STR:颜绘変化, "扇子白")
		LOCALS = 扇子白
	ELSEIF STRCOUNT(STR:颜绘変化, "萌え袖")
		LOCALS = 萌え袖
	ELSE
		LOCALS = 没有
	ENDIF

ENDSELECT

RETURNF LOCALS


;-------------------------------------------------
;キャラARGの顔グラを弄るための関数
;使っても使わなくてもよい
;-------------------------------------------------
@CHANGE_MAIDFACE, ARGS, ARG

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

SELECTCASE ARGS
CASE "黒髪", "金髪", "銀髪", "赤髪", "青髪", "緑髪", "茶髪", "紫髪", "桃髪", "紅髪", "白髪", "紺髪", "焦茶髪", "深緑髪", "棕髪"
	CSTR:ARG:髪色 = %ARGS%
CASE "短", "巻髪", "中等長度", "披肩", "長"
	CSTR:ARG:髪長 = %ARGS%
CASE "自然刘海", "自然刘海２", "短刘海", "Ｍ字刘海", "斜刘海", "斜刘海２", "齐刘海", "撩刘海", "单眼遮盖", "編髪", "外八刘海", "双眼遮盖", "長鬓角", "露额头", "乱髪", "乱髪２", "乱髪３", "乱髪４", "乱髪５", "不对称", "不对称２", "进气口发型", "透明刘海", "透明刘海２", "透明刘海３", "右斜", "中分", "大半遮盖", "大半遮盖２", "半遮眼", "交叉前发", "短前发", "自然卷"
	CSTR:ARG:前髪 = %ARGS%
CASE "自然", "直髪", "外八", "卷髪"
	CSTR:ARG:後髪 = %ARGS%
CASE "无特殊髪型", "双马尾", "侧马尾", "马尾辫", "三股辫", "大三股辫", "団子頭", "短双马尾", "長双马尾", "卷双马尾", "钻头双马尾", "团子头双马尾", "编起来", "盘头", "侧盘头", "麻花辫", "单辫", "双辫", "简易扎发"
	CSTR:ARG:改髪 = %ARGS%
CASE "无呆毛", "呆毛", "呆毛２", "呆毛３", "触角"
	CSTR:ARG:呆毛 = %ARGS%
CASE "无蝴蝶結", "両側蝴蝶結", "单側蝴蝶結", "单側蝴蝶結２", "頭後蝴蝶結", "頭上蝴蝶結"
	CSTR:ARG:蝴蝶結 = %ARGS%
CASE "黒", "青", "紫", "緑", "赤", "茶", "金", "紅", "白", "紺", "焦茶", "深緑", "棕", "銀"
	CSTR:ARG:瞳色 = %ARGS%
CASE "左黒", "左青", "左紫", "左緑", "左赤", "左茶", "左金", "左紅", "左白", "左紺", "左焦茶", "左深緑", "左棕", "左銀"
	CSTR:ARG:瞳色 = %CONDS("右瞳色", ARG)%_%SUBSTRINGU(ARGS, 1, -1)%
CASE "右黒", "右青", "右紫", "右緑", "右赤", "右茶", "右金", "右紅", "右白", "右紺", "右焦茶", "右深緑", "右棕", "右銀"
	CSTR:ARG:瞳色 = %SUBSTRINGU(ARGS, 1, -1)%_%CONDS("左瞳色", ARG)%

CASE "普通眼", "普通眼２", "吊眼", "吊眼２", "垂眼", "垂眼２", "斜眼", "斜眼２", "水灵眼", "眯眯眼", "縦長目", "三白眼", "重度三白眼", "にっこり", "瞑り", "〇〇", "＞＜"
	CSTR:ARG:眼形 = %ARGS%
CASE "左普通眼", "左普通眼２", "左吊眼", "左吊眼２", "左垂眼", "左垂眼２", "左斜眼", "左斜眼２", "左水灵眼", "左眯眯眼", "左縦長目", "左三白眼", "左重度三白眼", "左にっこり", "左瞑り", "左〇〇", "左＞＜"
	CSTR:ARG:眼形 = %CONDS("右眼形", ARG)%_%SUBSTRINGU(ARGS, 1, -1)%
CASE "右普通眼", "右普通眼２", "右吊眼", "右吊眼２", "右垂眼", "右垂眼２", "右斜眼", "右斜眼２", "右水灵眼", "右眯眯眼", "右縦長目", "右三白眼", "右重度三白眼", "右にっこり", "右瞑り", "右〇〇", "右＞＜"
	CSTR:ARG:眼形 = %SUBSTRINGU(ARGS, 1, -1)%_%CONDS("左眼形", ARG)%

;黒目の記載追加_2020_06_24_作者Ｉ
CASE "无眼白", "有眼白", "黑眼白"
	CSTR:ARG:白眼 = %ARGS%
CASE "左无眼白", "左有眼白", "左黑眼白"
	CSTR:ARG:白眼 = %CONDS("右白眼", ARG)%_%SUBSTRINGU(ARGS, 1, -1)%
CASE "右无眼白", "右有眼白", "右黑眼白"
	CSTR:ARG:白眼 = %SUBSTRINGU(ARGS, 1, -1)%_%CONDS("左白眼", ARG)%_

CASE "通常眉", "短通常眉", "憤怒眉", "短憤怒眉", "斜眉", "短斜眉", "困扰眉", "短困扰眉", "麻呂眉"
	CSTR:ARG:眉形 = %ARGS%
CASE "微笑", "へ字", "笑顔", "△", "大口", "無口系", "ω", "ワ", "半開", "嘲笑", "小口", "嘟嘴"
	CSTR:ARG:嘴形 = %ARGS%
ENDSELECT

;目のCSTRが左右同じ場合の操作
SIF CONDS("左瞳色", ARG) == CONDS("右瞳色", ARG)
	CSTR:ARG:瞳色 = %CONDS("右瞳色", ARG)%
SIF CONDS("左眼形", ARG) == CONDS("右眼形", ARG)
	CSTR:ARG:眼形 = %CONDS("右眼形", ARG)%
SIF CONDS("左白眼", ARG) == CONDS("右白眼", ARG)
	CSTR:ARG:白眼 = %CONDS("右白眼", ARG)%
