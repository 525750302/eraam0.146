;-------------------------------------------------
;キャラクリ画面等でキャラARGの髪の色などを変更する（設定がされていないキャラ対策）
;ARG == 0 の場合はドッペルあなたを使ってのシミュレーションモード
;-------------------------------------------------
@SIMULATE_MAIDFACE, ARG
STR:顔グラ固定 = 
STR:顔グラ変化 = 
STR:顔グラ衣装 = 
TFLAG:フィルタ = 0

SELECTCASE ARG
CASE 0
	ADDCHARA 99
	ARG = CHARANUM - 1
	CALL VARSET_MAIDFACE, ARG
	CFLAG:ARG:顔グラ = 1
	IF TARGET > MASTER && CFLAG:顔グラ
		PRINTFORML [{TARGET, 2}]%NAME:TARGET%の顔グラをベースにしますか？
		IF CFLAG:MASTER:顔グラ
			CALL PRINT_SELECT, @"はい/いいえ/%CALLNAME:MASTER%の顔グラをベースにする"
		ELSE
			CALL PRINT_SELECT, "はい/いいえ"
		ENDIF
		IF RESULT == 0
			STR:顔グラ衣装 = %CLO("顔グラ衣装")%

			CSTR:ARG:髪の色 = %CSTR:髪の色%
			CSTR:ARG:髪の長さ = %CSTR:髪の長さ%
			CSTR:ARG:前髪 = %CSTR:前髪%
			CSTR:ARG:後ろ髪 = %CSTR:後ろ髪%
			CSTR:ARG:アレンジ = %CSTR:アレンジ%
			CSTR:ARG:アホ毛 = %CSTR:アホ毛%
			CSTR:ARG:リボン = %CSTR:リボン%

			CSTR:ARG:目の色 = %CSTR:目の色%
			CSTR:ARG:目の形 = %CSTR:目の形%
			CSTR:ARG:白目 = %CSTR:白目%
			CSTR:ARG:眉の形 = %CSTR:眉の形%
			CSTR:ARG:口の形 = %CSTR:口の形%

			CSTR:ARG:種族 = %CSTR:種族%
			TALENT:ARG:肌の色 = TALENT:肌の色
			TALENT:ARG:エルフ耳 = TALENT:エルフ耳
			TALENT:ARG:動物耳 = TALENT:動物耳
			TALENT:ARG:ツノ = TALENT:ツノ
		ELSEIF RESULT == 2
			CSTR:ARG:髪の色 = %CSTR:MASTER:髪の色%
			CSTR:ARG:髪の長さ = %CSTR:MASTER:髪の長さ%
			CSTR:ARG:前髪 = %CSTR:MASTER:前髪%
			CSTR:ARG:後ろ髪 = %CSTR:MASTER:後ろ髪%
			CSTR:ARG:アレンジ = %CSTR:MASTER:アレンジ%
			CSTR:ARG:アホ毛 = %CSTR:MASTER:アホ毛%
			CSTR:ARG:リボン = %CSTR:MASTER:リボン%

			CSTR:ARG:目の色 = %CSTR:MASTER:目の色%
			CSTR:ARG:目の形 = %CSTR:MASTER:目の形%
			CSTR:ARG:白目 = %CSTR:MASTER:白目%
			CSTR:ARG:眉の形 = %CSTR:MASTER:眉の形%
			CSTR:ARG:口の形 = %CSTR:MASTER:口の形%

			CSTR:ARG:種族 = %CSTR:MASTER:種族%
			TALENT:ARG:肌の色 = TALENT:MASTER:肌の色
			TALENT:ARG:エルフ耳 = TALENT:MASTER:エルフ耳
			TALENT:ARG:動物耳 = TALENT:MASTER:動物耳
			TALENT:ARG:ツノ = TALENT:MASTER:ツノ
		ENDIF
	ENDIF
	SIF	STR:顔グラ衣装 == ""
		STR:顔グラ衣装 = セーラー紺色
	CALL SETFLAG, "MEMO_髪への記録", ARG
	CALL SETFLAG, "MEMO_表情への記録", ARG
	CFLAG:ARG:顔グラ = 1
	CALL SIMULATE_MAIDFACE_MAIN, ARG, "顔設定"
	CALL DELETE_CHARA, CHARANUM - 1
CASEELSE
	CALL SETFLAG, "MEMO_髪への記録", ARG
	CALL SETFLAG, "MEMO_表情への記録", ARG
	IF COND("髪型変更のお願い中", ARG)
		CALL SIMULATE_MAIDFACE_MAIN, ARG, "髪型変更"
	ELSE
		IF CFLAG:ARG:顔グラ == 0
			CALL PRESET_MAIDFACE, ARG
			CALL SETFLAG, "MEMO_髪への記録", ARG
			CALL SETFLAG, "MEMO_表情への記録", ARG
		ENDIF
		CFLAG:ARG:顔グラ = 1
		CALL SIMULATE_MAIDFACE_MAIN, ARG, "顔設定"
	ENDIF
ENDSELECT

STR:顔グラ衣装 = 
STR:顔グラ固定 = 
STR:顔グラ変化 = 

;-------------------------------------------------
;ARGSで現在設定しているものの種類を管理する
;-------------------------------------------------
@SIMULATE_MAIDFACE_MAIN, ARG, ARGS
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIMS CHOICES
#DIMS PRINT_TABLE
#DIMS SHOW_TABLE
#DIMS EXAMPLE_EMOTE

MEMO_LINECOUNT = LINECOUNT

IF ARG == MASTER
	SELECTCASE ARGS
	CASE "制服", "体操服", "水着", "自宅着", "外出着"
		IF SAVESTR:ARGS != ""
			STR:顔グラ衣装 = %SAVESTR:ARGS%
		ELSE
			STR:顔グラ衣装 = 
		ENDIF
	ENDSELECT

	IF TALENT:ARG:オトコ && COND("男の娘", ARG) == 0
		SHOW_TABLE = 顔設定/制服/体操服/自宅着/外出着
	ELSE
		SHOW_TABLE = 顔設定/制服/体操服/水着/自宅着/外出着
	ENDIF
	VARSET LOCALS
	SPLIT SHOW_TABLE, "/", LOCALS
	PRINT_TABLE = 　
	FOR LCOUNT, 0, 10
		SELECTCASE LOCALS:LCOUNT
		CASE ""
			BREAK
		CASE ARGS
			PRINT_TABLE += @"_黄色_BUTTON_[%LOCALS:LCOUNT%]"
		CASEELSE
			IF LOCALS:LCOUNT != "顔設定" && SAVESTR:(LOCALS:LCOUNT) != ""
				PRINT_TABLE += @"_緑_BUTTON_[%LOCALS:LCOUNT%]"
			ELSE
				PRINT_TABLE += @"_BUTTON_[%LOCALS:LCOUNT%]"
			ENDIF
		ENDSELECT
	NEXT
ENDIF

IF STR:顔グラ変化 != ""
	CALL EMOTION_PLUS, STR:顔グラ変化, ARG
ELSE
	CALL PRINT_MAIDFACE, ARG
ENDIF
SIF CFLAG:ARG:顔グラ
	PRINTL 
SELECTCASE ARGS
CASE "表示衣装変更", "制服", "体操服", "水着", "自宅着", "外出着"
	IF ARG == MASTER
		CALL PRINT_STRL, "設定済みの衣装は_緑_[緑色]_に変わります"
		CALL PRINT_STRL, PRINT_TABLE
		CALL PRINT_STRL, @"　現在の衣装:%CLO("顔グラ衣装", ARG)%\@ SAVESTR:ARGS != "" ? _緑_(%ARGS%設定済) # \@"
	ELSE
		CALL PRINT_STRL, "　_BUTTON_[顔設定]_黄色_BUTTON_[表示衣装変更]"
		PRINTFORML 　現在の衣装:%CLO("顔グラ衣装", ARG)%
		CALL PRINT_STRL, "以下のジャンル_緑_[0]〜[4]_から選んで下さい。選択することで各種衣装が表示されるようになります"
	ENDIF
	PRINTL 
	CALL PRINTL_COLORTEXT, "[0]ハダカ・下着・薄着・パジャマ", DEF_COLOR("緑")
	IF TFLAG:フィルタ == 0
		CALL SELECTS, "ハダカ/バスタオル", "", @"　%BL(20)%"
		CALL SELECTS, "黒/青/桃/白/緑色/橙", "下着", @"　%TEXT_LJ("下着", 20)%"
		CALL SELECTS, "黒/赤/緑色", "ベビードール", @"　%TEXT_LJ("ベビードール", 20)%"
		CALL SELECTS, "灰色/白/青/緑色", "タンクトップ", @"　%TEXT_LJ("タンクトップ", 20)%"
		CALL SELECTS, "黒/青/赤/白/緑色/橙", "キャミソール", @"　%TEXT_LJ("キャミソール", 20)%"
		PRINTL 
		CALL SELECTS, "白/黒/桃", "ネグリジェ", @"　%TEXT_LJ("ネグリジェ", 20)%"
		CALL SELECTS, "白/紺/桃/薄緑/水色", "パジャマ", @"　%TEXT_LJ("パジャマ", 20)%"
	ENDIF

	CALL PRINTL_COLORTEXT, "[1]制服・体操服・水着", DEF_COLOR("緑")
	IF TFLAG:フィルタ == 1
		CALL SELECTS, "黒/灰色/紺色/青/赤/白", "セーラー", @"　%TEXT_LJ("セーラー", 20)%"
		IF STRCOUNT(CLO("顔グラ衣装", ARG), "セーラー") && STRCOUNT(CLO("顔グラ衣装", ARG), "ノースリーブ") == 0
			CALL SELECTS, "無し/黒/青/赤/緑色", "＆ジャージ", @"　%TEXT_LJ("ジャージの重ね着", 20)%"
		ELSE
			CALL PRINT_STRL, @"暗灰色_　%TEXT_LJ("ジャージの重ね着", 20)%[無し][黒][青][赤][緑色](ノースリーブでないセーラー着用時のみ)"
		ENDIF
		CALL SELECTS, "黒/灰色/紺色/青/赤/白", "ノースリーブセーラー", @"　%TEXT_LJ("ノースリーブセーラー", 20)%"
		PRINTL 
		CALL SELECTS, "灰色/青/赤/茶色/緑色/黒", "ブレザー", @"　%TEXT_LJ("ブレザー", 20)%"
		CALL SELECTS, "Ａ/Ｂ", "学ラン", @"　%TEXT_LJ("学ラン", 20)%"
		PRINTL 
		CALL SELECTS, "黒/青/赤/緑色", "ジャージ", @"　%TEXT_LJ("ジャージ", 20)%"
		CALL SELECTS, "緑色/橙/黄色/青/赤/白", "詰襟ジャージ", @"　%TEXT_LJ("詰襟ジャージ", 20)%"
		PRINTL 
		CALL SELECTS, "新スク紺色/ワンピ水着黒/チューブトップ赤", "", @"　%BL(20)%"
		CALL SELECTS, "青/白", "スク水", @"　%TEXT_LJ("スク水", 20)%"
		CALL SELECTS, "黒/青/赤/緑色", "競泳", @"　%TEXT_LJ("競泳", 20)%"
	ENDIF

	CALL PRINTL_COLORTEXT, "[2]シャツ・ブラウス・ジャケット・ベスト・チュニック", DEF_COLOR("緑")
	IF TFLAG:フィルタ == 2
		CALL SELECTS, "シャツ/だぼだぼＴシャツ/ブラウス白/ルーズブラウス", "", @"　%BL(20)%"
		CALL SELECTS, "黄色/白/黒/赤/緑色/青/紫/桃/橙", "Ｔシャツ", @"　%TEXT_LJ("Ｔシャツ", 20)%"
		IF STRCOUNT(CLO("顔グラ衣装", ARG), "Ｔシャツ")
			CALL SELECTS, "無し/漢字１黒/英字１黒/英字２黒/ＡＡ１黒", "/変Ｔ", @"　%TEXT_LJ("変Ｔ文字", 20)%"
			CALL SELECTS, "漢字１白/英字１白/英字２白/ＡＡ１白", "/変Ｔ", @"　%BL(20)%　　　"
		ELSE
			CALL PRINT_STRL, @"暗灰色_　%TEXT_LJ("変Ｔ文字", 20)%[無し][漢字１黒][英字１黒][英字２黒][ＡＡ１黒]"
			CALL PRINT_STRL, @"暗灰色_　%BL(20)%　　　[漢字１白][英字１白][英字２白][ＡＡ１白](Ｔシャツ着用時のみ)"
		ENDIF
		PRINTL 
		CALL SELECTS, "黒/青/赤", "ネクタイブラウス", @"　%TEXT_LJ("ネクタイブラウス", 20)%"
		CALL SELECTS, "黒/青/赤/緑色", "リボンブラウス", @"　%TEXT_LJ("リボンブラウス", 20)%"
		CALL SELECTS, "黒/白", "フリルブラウス", @"　%TEXT_LJ("フリルブラウス", 20)%"
		CALL SELECTS, "Ａ/Ｂ", "ノースリーブブラウス", @"　%TEXT_LJ("ノースリーブブラウス", 20)%"
		IF STRCOUNT(CLO("顔グラ衣装", ARG), "ノースリーブブラウス")
			CALL SELECTS, "無し/橙/灰色/赤/青/緑色/濃紫/桃/白/黒", "＆肩出しジャケット", @"　%TEXT_LJ("肩出しジャケット", 20)%"
		ELSE
			CALL PRINT_STRL, @"暗灰色_　%TEXT_LJ("肩出しジャケット", 20)%[無し][橙][灰色][赤][青][緑色][濃紫][桃][白][黒](ノースリーブ着用時のみ)"
		ENDIF
		CALL SELECTS, "黒/ベージュ/白/灰色", "オフショルダー", @"　%TEXT_LJ("オフショルダー", 20)%"
		CALL SELECTS, "白/黒/緑色/紺色", "シャツジャケット", @"　%TEXT_LJ("シャツジャケット", 20)%"
		CALL SELECTS, "白/黒/緑色/紺色", "前開きシャツ", @"　%TEXT_LJ("前開きシャツ", 20)%"
		CALL SELECTS, "灰色/赤/青/茶色/緑色/黒", "ベスト", @"　%TEXT_LJ("ベスト", 20)%"
		CALL SELECTS, "灰色/赤/青/橙/緑色/濃紫/桃/白/黒", "カーディガン", @"　%TEXT_LJ("カーディガン", 20)%"
		CALL SELECTS, "黄色/紺色", "チュニック", @"　%TEXT_LJ("チュニック", 20)%"
		CALL SELECTS, "赤/緑色/青", "中世チュニック", @"　%TEXT_LJ("中世チュニック", 20)%"
	ENDIF

	CALL PRINTL_COLORTEXT, "[3]パーカー・コート・セーター・ドレス・ワンピース", DEF_COLOR("緑")
	IF TFLAG:フィルタ == 3
		CALL SELECTS, "灰色/白/青/赤", "パーカー", @"　%TEXT_LJ("パーカー", 20)%"
		CALL SELECTS, "灰色/白/青/赤", "前開きパーカー", @"　%TEXT_LJ("前開きパーカー", 20)%"
		CALL SELECTS, "茶色/赤/白", "コート", @"　%TEXT_LJ("コート", 20)%"
		CALL SELECTS, "茶色/赤/白", "裸コート", @"　%TEXT_LJ("裸コート", 20)%"
		CALL SELECTS, "茶色/白/焦茶色/黒/青", "ダッフルコート", @"　%TEXT_LJ("ダッフルコート", 20)%"
		CALL SELECTS, "灰色/茶色/赤/緑色/紺色", "どてら", @"　%TEXT_LJ("どてら", 20)%"
		CALL SELECTS, "緑色/白/赤/紺色/水色/茶色", "ニットセーター", @"　%TEXT_LJ("ニットセーター", 20)%"
		CALL SELECTS, "紫/灰色/紺色/赤/茶色/緑色/黒", "縦セーター", @"　%TEXT_LJ("縦セーター", 20)%"
		CALL SELECTS, "タートルネック/胸開きタートルネック", "", @"　%TEXT_LJ("", 20)%"
		CALL SELECTS, "ニットオフショル/ノースリーブセーター", "", @"　%TEXT_LJ("", 20)%"
		PRINTL 
		CALL SELECTS, "黒/赤/白", "ゴスロリドレス", @"　%TEXT_LJ("ゴスロリドレス", 20)%"
		CALL SELECTS, "黒/赤/白/緑色", "サマードレス", @"　%TEXT_LJ("サマードレス", 20)%"
		CALL SELECTS, "ゴシックドレス/ゴシックワンピース/甘ロリワンピース/白いワンピース", "", @"　%TEXT_LJ("", 20)%"
	ENDIF

	CALL PRINTL_COLORTEXT, "[4]コスプレ・和装", DEF_COLOR("緑")
	IF TFLAG:フィルタ == 4
		CALL SELECTS, "黒/青/赤/緑色/桃/水色", "メイド服", @"　%TEXT_LJ("メイド服", 20)%"
		CALL SELECTS, "黒/灰色/緑色", "執事服", @"　%TEXT_LJ("執事服", 20)%"
		CALL SELECTS, "黒/青/赤/緑色", "チャイナドレス", @"　%TEXT_LJ("チャイナドレス", 20)%"
		CALL SELECTS, "黒/青/赤/緑色/白", "胸開きチャイナドレス", @"　%TEXT_LJ("胸開きチャイナドレス", 20)%"
		CALL SELECTS, "黒/紫/青/赤/白", "バニースーツ", @"　%TEXT_LJ("バニースーツ", 20)%"
		CALL SELECTS, "黒/赤", "ボンデージ", @"　%TEXT_LJ("ボンデージ", 20)%"
		CALL SELECTS, "黒/赤", "紐ボンデージ", @"　%TEXT_LJ("紐ボンデージ", 20)%"
		PRINTL 
		CALL SELECTS, "黒/紫/青/赤/緑色", "振袖", @"　%TEXT_LJ("振袖", 20)%"
		CALL SELECTS, "黄色/黒/青/赤/緑色", "浴衣", @"　%TEXT_LJ("浴衣", 20)%"
		CALL SELECTS, "黒/青/赤/白/緑色", "和服", @"　%TEXT_LJ("和服", 20)%"
		PRINTL 
		CALL SELECTS, "エプロン/巫女服/修道服/ナース服", "", @"　%TEXT_LJ("", 20)%"
		CALL SELECTS, "チアユニフォーム/ウェディングドレス", "", @"　%TEXT_LJ("", 20)%"
	ENDIF
CASEELSE
	IF ARGS != "髪型変更"
		CFLAG:ARG:髪型変更のお願い中 = 0
		IF ARG == MASTER
			CALL PRINT_STRL, "設定済みの衣装は_緑_[緑色]_に変わります"
			CALL PRINT_STRL, PRINT_TABLE
		ELSE
			;CALL PRINT_STRL, "髪の色〜口の形の設定をダブルクリックすると_緑_緑色_に変わり、ランダムの際に固定するようになります"
			;CALL PRINT_STRL, "固定を解除する場合には、もう一度選択してください"
			CALL PRINT_STRL, "　_黄色_BUTTON_[顔設定]_BUTTON_[表示衣装変更]"
		ENDIF
		CALL PRINT_STRL, "※_緑_[0]〜[2]_から表示する項目を選択すると、詳細が開きます"
		CALL PRINT_STRL, "※_黄色_同じものを２連続で選ぶとランダム固定できます_。例）[ボブ]を２連続で選ぶと髪の長さがボブに固定"
		PRINTL 

		CALL PRINTL_COLORTEXT, "[0]髪の設定", DEF_COLOR("緑")
		IF TFLAG:フィルタ == 0
			CALL SELECTSR, "黒/金/銀/赤/青/緑/茶/紫/桃髪/紅/白/紺/焦茶/深緑/真鍮", "髪", @"　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/髪の色/") ? _黄色_ # \@髪の色:%TEXT_LJ(@"%CONDS("髪色の種類", ARG)%髪", 18)%"
			PRINTL 

			CALL PRINT_STRL, @"\@ STRCOUNT(@"/%STR:顔グラ固定%", "/髪の長さ/") ? _黄色_ # \@髪の長さ:%TEXT_LJ(CONDS("髪の長さ", ARG), 18)%_BUTTON_[ショート]_BUTTON_[ボブ]_BUTTON_[ミディアム]_BUTTON_[セミロング]_BUTTON_[ロング]"
			PRINTL 
		ENDIF
	ENDIF
	IF TFLAG:フィルタ == 0
		CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/後ろ髪/") ? _黄色_ # \@後ろ髪:%TEXT_LJ(CONDS("後ろ髪", ARG), 18)%_BUTTON_[ナチュラル]_BUTTON_[ストレート]_BUTTON_[外ハネ]_BUTTON_[ウェーブ]"
		PRINTL 

		CALL PRINT_STRL, @"　　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/前髪/") ? _黄色_ # \@前髪:%TEXT_LJ(CONDS("前髪", ARG), 18)%_BUTTON_[ナチュラルバング]_BUTTON_[ナチュラルバング２]_BUTTON_[ショートバング]_BUTTON_[ベリーショート]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[Ｍ字バング]_BUTTON_[斜めバング]_BUTTON_[斜めバング２]_BUTTON_[ぱっつん]_BUTTON_[かきあげ]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[片目隠れ]_BUTTON_[編み込み]_BUTTON_[外ハネバング]_BUTTON_[両目隠れ]_BUTTON_[もみあげロング]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[ボサボサ]_BUTTON_[ボサボサ２]_BUTTON_[ボサボサ３]_BUTTON_[ボサボサ４]_BUTTON_[ボサボサ５]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[デコ出し]_BUTTON_[アシメ]_BUTTON_[アシメ２]_BUTTON_[インテーク]_BUTTON_[右分け]_BUTTON_[真ん中分け]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[弱目隠れ]_BUTTON_[弱目隠れ２]_BUTTON_[かむろ風]_BUTTON_[うね毛バング]_BUTTON_[前髪クロス]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[シースルーバング]_BUTTON_[シースルーバング２]_BUTTON_[シースルーバング３]"
		PRINTL 

		CALL PRINT_STRL, @"\@ STRCOUNT(@"/%STR:顔グラ固定%", "/アレンジ/") ? _黄色_ # \@アレンジ:%TEXT_LJ(CONDS("アレンジ", ARG), 18)%_BUTTON_[ノーアレンジ]_BUTTON_[ポニーテール]_BUTTON_[三つ編み]_BUTTON_[でか三つ編み]_BUTTON_[お団子]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[ツインテール]_BUTTON_[サイドテール]_BUTTON_[ショートツインテ]_BUTTON_[ロングツインテ]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[ウェーブツインテ]_BUTTON_[ドリルツインテ]_BUTTON_[お団子ツインテ]_BUTTON_[編み上げ]"
		CALL PRINT_STRL, @"%BL(27)%_BUTTON_[おさげ]_BUTTON_[一つ結び]_BUTTON_[二つ結び]_BUTTON_[シニヨン]_BUTTON_[サイドシニヨン]_BUTTON_[バレッタ留め]"
		PRINTL 

		CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/アホ毛/") ? _黄色_ # \@アホ毛:%TEXT_LJ(CONDS("アホ毛", ARG), 18)%_BUTTON_[アホ毛なし]_BUTTON_[アホ毛]_BUTTON_[アホ毛２]_BUTTON_[アホ毛３]_BUTTON_[触角]"
		PRINTL 
		CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/リボン/") ? _黄色_ # \@リボン:%TEXT_LJ(CONDS("リボン", ARG), 18)%_BUTTON_[リボンなし]_BUTTON_[両側リボン]_BUTTON_[片側リボン]_BUTTON_[後ろリボン]_BUTTON_[頭上リボン]"
		[IF_DEBUG]
			CALL PRINT_STRL, @"%BL(27)%_BUTTON_[片側リボン２]"
		[ENDIF]
	ENDIF

	IF ARGS != "髪型変更"
		CALL PRINTL_COLORTEXT, "[1]表情の設定", DEF_COLOR("緑")
		IF TFLAG:フィルタ == 1
			CALL SELECTS, "黒/青/紫/緑/赤/茶/金/紅/白/紺/焦茶/深緑/真鍮/銀", "", @"　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/目の色/") ? _黄色_ # \@目の色:%TEXT_LJ(CONDS("右目の色", ARG), 18)%"
			CALL SELECTS, "黒/青/紫/緑/赤/茶/金/紅/白/紺/焦茶/深緑/真鍮/銀", "左", @"\@ STRCOUNT(@"/%STR:顔グラ固定%", "/目の色/") ? _黄色_ # \@左目の色:%TEXT_LJ(CONDS("左目の色", ARG), 18)%"
			PRINTL 

			CALL SELECTS, "通常目/通常目２/ツリ目/ツリ目２/たれ目/たれ目２", "", @"　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/目の形/") ? _黄色_ # \@目の形:%TEXT_LJ(CONDS("右目の形", ARG), 18)%"
			CALL SELECTS, "ジト目/ジト目２/ぱっちり/糸目/縦長目/三白眼/ジト三白眼", "", BL(27)
			[IF_DEBUG]
				CALL SELECTS, "にっこり/瞑り/〇〇/＞＜", "", BL(27)
				PRINTL 

				CALL SELECTS, "通常目/通常目２/ツリ目/ツリ目２/たれ目/たれ目２", "左", @"\@ STRCOUNT(@"/%STR:顔グラ固定%", "/目の形/") ? _黄色_ # \@左目の形:%TEXT_LJ(CONDS("左目の形", ARG), 18)%"
				CALL SELECTS, "ジト目/ジト目２/ぱっちり/糸目/縦長目/三白眼/ジト三白眼", "左", BL(27)
				CALL SELECTS, "にっこり/瞑り/〇〇/＞＜", "左", BL(27)
			[ENDIF]
			PRINTL 

			;黒目の記載追加_2020_06_24_作者Ｉ
			CALL SELECTS, "白目なし/白目あり/黒目あり", "", @"　　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/白目/") ? _黄色_ # \@白目:%TEXT_LJ(CONDS("右白目", ARG), 18)%"
			[IF_DEBUG]
				CALL SELECTS, "白目なし/白目あり/黒目あり", "左", @"　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/白目/") ? _黄色_ # \@左白目:%TEXT_LJ(CONDS("左白目", ARG), 18)%"
			[ENDIF]
			PRINTL 

			CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/眉の形/") ? _黄色_ # \@眉の形:%TEXT_LJ(CONDS("眉の形", ARG), 18)%_BUTTON_[通常眉]_BUTTON_[怒り眉]_BUTTON_[たれ眉]_BUTTON_[困り眉]_BUTTON_[麻呂眉]"
			CALL PRINT_STRL, @"%BL(27)%_BUTTON_[通常眉短]_BUTTON_[怒り眉短]_BUTTON_[たれ眉短]_BUTTON_[困り眉短]"
			PRINTL 

			CALL PRINT_STRL, @"　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/口の形/") ? _黄色_ # \@口の形:%TEXT_LJ(CONDS("口の形", ARG), 18)%_BUTTON_[微笑]_BUTTON_[への字]_BUTTON_[笑顔]_BUTTON_[△]_BUTTON_[大口]_BUTTON_[無口系]_BUTTON_[ω]_BUTTON_[ワ]_BUTTON_[半開き]"
			CALL PRINT_STRL, @"%BL(27)%_BUTTON_[嘲笑]_BUTTON_[小口]_BUTTON_[ふくれっ面]"
			PRINTL 

			IF NO:ARG  == 99 || ARG == MASTER
				CALL PRINT_STRL, @"　　\@ STRCOUNT(@"/%STR:顔グラ固定%", "/眼鏡/") ? _黄色_ # \@眼鏡:%TEXT_LJ(CONDS("眼鏡", ARG), 18)%_BUTTON_[眼鏡なし]_BUTTON_[眼鏡]_BUTTON_[ブロー眼鏡]_BUTTON_[フルリム眼鏡]_BUTTON_[フィンチ眼鏡]_BUTTON_[ロイド眼鏡]"
				CALL PRINT_STRL, @"%BL(27)%_BUTTON_[サングラス]_BUTTON_[目隠し白]_BUTTON_[目隠し黒]"
			ENDIF
		ENDIF
		SIF NO:ARG  == 99 || ARG == MASTER
			CALL PRINTL_COLORTEXT, "[2]その他の設定(ここの項目は全てランダム固定です)", DEF_COLOR("緑")
		IF TFLAG:フィルタ == 2
			IF NO:ARG  == 99
				CALL PRINT_STRL, @"　肌の色:%TEXT_LJ(CONDS("肌の色", ARG), 18)%_BUTTON_[普通]_BUTTON_[褐色肌]_BUTTON_[色白]_BUTTON_[青肌]"
				CALL PRINT_STRL, @"　耳の形:%TEXT_LJ(CONDS("耳の形", ARG), 18)%_BUTTON_[人間耳]_BUTTON_[エルフ耳]_BUTTON_[魔族耳]_BUTTON_[動物耳]_BUTTON_[動物タレ耳]"
				CALL PRINT_STRL, @"　　種族:%TEXT_LJ(CONDS("種族", ARG), 18)%_BUTTON_[人間]_BUTTON_[獣人]_BUTTON_[兎獣人]_BUTTON_[妖狐]_BUTTON_[天使]_BUTTON_[悪魔]_BUTTON_[鬼]_BUTTON_[竜人]_BUTTON_[淫魔]_BUTTON_[吸血鬼]"
				PRINTL 

				CALL PRINT_STRL, "イエロー_　※頬傷と絆創膏は複数選択できます(再選択でON/OFF切り替え)※"
				CALL PRINT_STRL, @"　頬化粧:%TEXT_LJ(CONDS("頬化粧", ARG), 18)%_BUTTON_[頬化粧無し]_BUTTON_[隈取赤]_BUTTON_[隈取青]_BUTTON_[戦化粧]"
				CALL PRINT_STRL, @"　　頬傷:%TEXT_LJ(CONDS("頬傷", ARG), 18)%_BUTTON_[頬傷無し]_BUTTON_[頬傷左]_BUTTON_[頬傷鼻]_BUTTON_[頬傷右]"
				CALL PRINT_STRL, @"頬絆創膏:%TEXT_LJ(CONDS("頬絆創膏", ARG), 18)%_BUTTON_[頬絆創膏無し]_BUTTON_[頬絆創膏左]_BUTTON_[頬絆創膏鼻]_BUTTON_[頬絆創膏右]"
				PRINTL 

				CALL SELECTS, "状態リセット/睡眠/甘い言葉/褒める/不機嫌/ムラムラ/目陰", "", @"　　状態:%TEXT_LJ(CONDS("顔グラコンディション", ARG), 18)%"
				CALL SELECTS, "寝起き/怒り/恥じらい/びっくり/小悪魔/頬染め呆れ/ハート目", "", BL(27)
				PRINTL 

				CALL PRINT_STRL, @"　チーク:%TEXT_LJ(FACES("チークの種類", ARG), 18)%_BUTTON_[チーク無し]_BUTTON_[楕円]_BUTTON_[頬染]_BUTTON_[丸ほっぺ]_BUTTON_[紅潮]_BUTTON_[赤面]_BUTTON_[返り血]"
				PRINTL 

				CALL SELECTS, "無し/怒り/汗/ハート/ワイワイ/驚き/眠り/寝ぼけ", "ふきだし", @"ふきだし:%TEXT_LJ(FACES("ふきだしの種類", ARG), 18)%"
				CALL SELECTS, "満開/・・・/？/！/ハート大量", "ふきだし", BL(27)
				PRINTL 

				CALL SELECTS, "小物無し/タバコ/アイス/ココアシガレット/食パン/フランスパン", "", @"　　小物:%TEXT_LJ(FACES("小物", ARG), 18)%"
				CALL SELECTS, "スクールバッグ/お茶/みかん/本/頭上みかん/頭上サングラス", "", BL(27)
				CALL SELECTS, "扇子赤/扇子富士山/扇子桜/扇子白/萌え袖/ゴム", "", BL(27)
				PRINTL 
			ENDIF
		ENDIF
		IF ARG == MASTER
			CALL PRINT_STRL, @"　これでよい？%BL(13)%_BUTTON_[ＯＫ！]_BUTTON_[最初に戻す]_BUTTON_[ランダム]_BUTTON_[not黒目ランダム]_BUTTON_[顔グラ表示をやめる]"
		ELSE
			CALL PRINT_STR, @"　これでよい？%BL(13)%_BUTTON_[ＯＫ！]_BUTTON_[最初に戻す]_BUTTON_[ランダム]_BUTTON_[not黒目ランダム]"
			[IF_DEBUG]
				SIF NO:ARG  == 99
					CALL PRINT_STR, @"BUTTON_[表情を出力]"
			[ENDIF]
			PRINTL
		ENDIF
	ELSE
		CALL PRINT_STRL, @"　これでよい？%BL(13)%_BUTTON_[ＯＫ！]_BUTTON_[最初に戻す]"
	ENDIF
ENDSELECT

INPUTS
CHOICES = %EXTSTR(RESULTS, "[", "]")%
CLEARLINE 1

SELECTCASE CHOICES
CASE "0", "1", "2", "3", "4"
	SIF ARGS != "髪型変更"
		TFLAG:フィルタ = TOINT(CHOICES)
CASE "状態リセット", "睡眠", "甘い言葉", "褒める", "不機嫌", "ムラムラ", "寝起き", "怒り", "恥じらい", "びっくり", "小悪魔", "頬染め呆れ", "ハート目", "目陰"
	CFLAG:ARG:睡眠 = 0
	CFLAG:ARG:甘い言葉 = 0
	CFLAG:ARG:褒める = 0
	CFLAG:ARG:不機嫌 = 0
	CFLAG:ARG:ムラムラ = 0
	CFLAG:ARG:寝起き = 0
	CFLAG:ARG:怒り = 0
	CFLAG:ARG:恥じらい = 0

	SELECTCASE CHOICES
	CASE "状態リセット"
		STR:顔グラ変化 = 
	CASE "睡眠", "甘い言葉", "褒める", "不機嫌", "ムラムラ", "寝起き", "怒り", "恥じらい"
		CFLAG:ARG:CHOICES = 1
		IF STRCOUNT(STR:顔グラ変化, "ハート目")
			STR:顔グラ変化 = ハート目
		ELSE
			STR:顔グラ変化 = 
		ENDIF
	CASE "びっくり", "小悪魔", "頬染め呆れ", "目陰"
		IF STRCOUNT(STR:顔グラ変化, "ハート目")
			STR:顔グラ変化 = %CHOICES%/ハート目
		ELSE
			STR:顔グラ変化 = %CHOICES%/
		ENDIF
	CASE "ハート目"
		SIF STRCOUNT(STR:顔グラ変化, "ハート目") == 0
			STR:顔グラ変化 += "ハート目/"
	ENDSELECT
CASE "ランダム", "not黒目ランダム"
	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/髪の色/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("黒髪/金髪/銀髪/赤髪/青髪/緑髪/茶髪/紫髪/桃髪/紅髪/白髪/紺髪/焦茶髪/深緑髪/真鍮髪"), ARG
	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/髪の長さ/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("ショート/ボブ/ミディアム/セミロング/ロング"), ARG
	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/前髪/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("ナチュラルバング/ナチュラルバング２/ショートバング/Ｍ字バング/斜めバング/斜めバング２/ぱっつん/かきあげ/片目隠れ/編み込み/外ハネバング/両目隠れ/もみあげロング/デコ出し/ボサボサ/ボサボサ２/ボサボサ３/ボサボサ４/ボサボサ５/アシメ/アシメ２/インテーク/シースルーバング/シースルーバング２/シースルーバング３/右分け/真ん中分け/弱目隠れ/弱目隠れ２/かむろ風/前髪クロス/ベリーショート/うね毛バング"), ARG
	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/後ろ髪/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("ナチュラル/ストレート/外ハネ/ウェーブ"), ARG
	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/アレンジ/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("ノーアレンジ/ノーアレンジ/ノーアレンジ/ノーアレンジ/ツインテール/サイドテール/ポニーテール/三つ編み/でか三つ編み/お団子/ショートツインテ/ロングツインテ/ウェーブツインテ/ドリルツインテ/お団子ツインテ/編み上げ/シニヨン/サイドシニヨン/おさげ/一つ結び/二つ結び/バレッタ留め"), ARG
	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/アホ毛/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("アホ毛なし/アホ毛なし/アホ毛なし/アホ毛なし/アホ毛/アホ毛２/アホ毛３/触角"), ARG
	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/リボン/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("リボンなし/リボンなし/リボンなし/リボンなし/両側リボン/片側リボン/片側リボン２/後ろリボン/頭上リボン"), ARG

	IF STRCOUNT(@"/%STR:顔グラ固定%", "/目の色/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("黒/青/紫/緑/赤/茶/金/紅/白/紺/焦茶/深緑/真鍮/銀"), ARG
		SIF RAND:10 == 0
			CALL CHANGE_MAIDFACE, TEXTR("左黒/左青/左紫/左緑/左赤/左茶/左金/左紅/左白/左紺/左焦茶/左深緑/左真鍮/左銀"), ARG
	ENDIF

	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/目の形/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("通常目/通常目２/ツリ目/ツリ目２/たれ目/たれ目２/ジト目/ジト目２/ぱっちり/糸目/縦長目/三白眼/ジト三白眼"), ARG

	IF STRCOUNT(@"/%STR:顔グラ固定%", "/白目/") == 0
		IF CHOICES == "not黒目ランダム"
			CALL CHANGE_MAIDFACE, TEXTR("白目なし/白目あり"), ARG
		ELSE
			CALL CHANGE_MAIDFACE, TEXTR("白目なし/白目なし/白目なし/白目なし/白目あり/白目あり/白目あり/白目あり/黒目あり"), ARG
		ENDIF
	ENDIF
	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/眉の形/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("通常眉/通常眉短/怒り眉/怒り眉短/たれ眉/たれ眉短/困り眉/困り眉短/麻呂眉"), ARG
	SIF STRCOUNT(@"/%STR:顔グラ固定%", "/口の形/") == 0
		CALL CHANGE_MAIDFACE, TEXTR("微笑/への字/笑顔/△/大口/無口系/ω/ワ/半開き/嘲笑/小口/ふくれっ面"), ARG
	IF STRCOUNT(@"/%STR:顔グラ固定%", "/眼鏡/") == 0
		IF RAND:3 == 0
			CALL SET_OTHER, TEXTR("眼鏡/ブロー眼鏡/フルリム眼鏡/フィンチ眼鏡/ロイド眼鏡/サングラス"), ARG
		ELSE
			CALL RESET_OTHER, "眼鏡", ARG
		ENDIF
	ENDIF
CASE "最初に戻す"
	CALL SETFLAG, "MEMO_髪からの復元", ARG
	CALL SETFLAG, "MEMO_表情からの復元", ARG
	STR:顔グラ変化 =
	CFLAG:ARG:睡眠 = 0
	CFLAG:ARG:甘い言葉 = 0
	CFLAG:ARG:褒める = 0
	CFLAG:ARG:不機嫌 = 0
	CFLAG:ARG:ムラムラ = 0
	CFLAG:ARG:寝起き = 0
	CFLAG:ARG:怒り = 0
	CFLAG:ARG:恥じらい = 0
CASE "顔グラ表示をやめる"
	CFLAG:ARG:顔グラ = 0
	RETURN 0
CASE "ＯＫ！"
	PRINTL 
	IF COND("髪型変更のお願い中", ARG)
		RETURN 1
	ELSEIF NO:ARG  == 99
		PRINTFORML この顔グラに対応するCSTRと素質は以下です。
		PRINTL 
		SIF TALENT:ARG:肌の色
			PRINTFORML 素質,肌の色,{TALENT:ARG:肌の色},
		SIF TALENT:ARG:エルフ耳
			PRINTFORML 素質,エルフ耳,{TALENT:ARG:エルフ耳},
		SIF TALENT:ARG:動物耳
			PRINTFORML 素質,動物耳,{TALENT:ARG:動物耳},
		SIF TALENT:ARG:ツノ
			PRINTFORML 素質,ツノ,1,
		PRINTFORML CSTR,種族,%CSTR:ARG:種族%,
		FOR LCOUNT, 90, 110
			SIF CSTR:ARG:LCOUNT == ""
				CONTINUE
			PRINTFORML CSTR,%CSTRNAME:LCOUNT%,%CSTR:ARG:LCOUNT%,
		NEXT
		IF CHECK_OTHER("眼鏡", ARG)
			PRINTL 
			PRINTFORML EVENT_KX1_CLOTHES.ERB 内の CASE "アクセサリ"に
			PRINTFORML CALL SET_OTHER, "%SUBSTRING(CSTR:ARG:アクセサリ, 0, STRLENS(CSTR:ARG:アクセサリ) - 1)%"
		ENDIF
		PRINTL 
		PRINTFORMW CSV(眼鏡等アクセサリに関してはERB)にコピペする事で、キャラの顔グラを只今設定したものにする事が出来ます。
		PRINTFORMW 画面上の ファイル→ログをクリップボードにコピー→下にスクロール でGetするのが良いでしょう。
	ELSEIF COND("キャラメイク中", ARG) == 0
		PRINTFORMW [%CSTR:ARG:肩書%]%NAME:ARG%の顔グラを設定しました。
		CALL SETFLAG, "普段の髪型記録", ARG
	ENDIF
	RETURN 1
CASE "表情を出力"
	;まずは表情から
	IF CONDS("顔グラコンディション", ARG) == ""
		EXAMPLE_EMOTE = リセット
	ELSE
		EXAMPLE_EMOTE = %CONDS("顔グラコンディション", ARG)%
	ENDIF

	;目や口が変わっている箇所だけ抜き出して入れる
	FOR LCOUNT, 100, 110
		SIF CSTR:ARG:LCOUNT == ""
			CONTINUE
		SIF CSTR:ARG:LCOUNT == CSTR:LCOUNT
			CONTINUE
		;100、102は変更の機能がない…念のため残しておく
		SELECTCASE LCOUNT
		CASE 100, 102
			CONTINUE
		CASEELSE
		ENDSELECT

		SIF EXAMPLE_EMOTE != ""
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/
		SELECTCASE LCOUNT
		CASE 100
			IF CONDS("左目の色", ARG) == CONDS("右目の色", ARG)
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右目の色", ARG)%
			ELSEIF CONDS("右目の色", ARG) != CONDS("右目の色")
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右目の色", ARG)%/左%CONDS("左目の色", ARG)%
			ELSE
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%左%CONDS("左目の色", ARG)%
			ENDIF
		CASE 101
			IF CONDS("左目の形", ARG) == CONDS("右目の形", ARG)
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右目の形", ARG)%
			ELSEIF CONDS("右目の形", ARG) != CONDS("右目の形")
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右目の形", ARG)%/左%CONDS("左目の形", ARG)%
			ELSE
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%左%CONDS("左目の形", ARG)%
			ENDIF
		CASE 102
			IF CONDS("左白目", ARG) == CONDS("右白目", ARG)
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右白目", ARG)%
			ELSEIF CONDS("右白目", ARG) != CONDS("右白目")
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CONDS("右白目", ARG)%/左%CONDS("左白目", ARG)%
			ELSE
				EXAMPLE_EMOTE = %EXAMPLE_EMOTE%左%CONDS("左白目", ARG)%
			ENDIF
		CASEELSE
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%%CSTR:ARG:LCOUNT%
		ENDSELECT
	NEXT
	IF STR:顔グラ変化 != ""
		SIF STRCOUNT(STR:顔グラ変化, "ハート目")
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/ハート目
		SIF FACES("チークの種類", ARG) != "無し"
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/%FACES("チークの種類", ARG)%
		SIF FACES("ふきだしの種類", ARG) != "無し"
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/%FACES("ふきだしの種類", ARG)%
		SIF FACES("小物", ARG) != "無し"
			EXAMPLE_EMOTE = %EXAMPLE_EMOTE%/%FACES("小物", ARG)%
	ENDIF
	PRINTL 
	DRAWLINE
	PRINTFORMW CALL EMOTION_PLUS, "%EXAMPLE_EMOTE%"

CASE "顔設定", "表示衣装変更"
	TFLAG:フィルタ = 0
	ARGS = %CHOICES%
CASE "制服", "体操服", "水着", "自宅着", "外出着"
	ARGS = %CHOICES%
CASE "普通"
	TALENT:ARG:肌の色 = 0
CASE "褐色肌"
	TALENT:ARG:肌の色 = 1
CASE "色白"
	TALENT:ARG:肌の色 = 2
CASE "青肌"
	TALENT:ARG:肌の色 = 3
CASE "黒髪", "金髪", "銀髪", "赤髪", "青髪", "緑髪", "茶髪", "紫髪", "桃髪", "紅髪", "白髪", "紺髪", "焦茶髪", "深緑髪", "真鍮髪"
	IF CSTR:ARG:髪の色 == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "髪の色")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "髪の色")%
	ENDIF
	CSTR:ARG:髪の色 = %CHOICES%
CASE "ショート", "ボブ", "ミディアム", "セミロング", "ロング"
	IF CSTR:ARG:髪の長さ == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "髪の長さ")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "髪の長さ")%
	ENDIF
	CSTR:ARG:髪の長さ = %CHOICES%
CASE "ナチュラルバング", "ナチュラルバング２", "ショートバング", "Ｍ字バング", "斜めバング", "斜めバング２", "ぱっつん", "かきあげ", "片目隠れ", "編み込み", "外ハネバング", "両目隠れ", "もみあげロング", "デコ出し", "ボサボサ", "ボサボサ２", "ボサボサ３", "ボサボサ４", "ボサボサ５", "アシメ", "アシメ２", "インテーク", "シースルーバング", "シースルーバング２", "シースルーバング３", "右分け", "真ん中分け", "弱目隠れ", "弱目隠れ２", "かむろ風", "前髪クロス", "ベリーショート", "うね毛バング"
	IF CSTR:ARG:前髪 == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "前髪")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "前髪")%
	ENDIF
	CSTR:ARG:前髪 = %CHOICES%
CASE "ナチュラル", "ストレート", "外ハネ", "ウェーブ"
	IF CSTR:ARG:後ろ髪 == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "後ろ髪")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "後ろ髪")%
	ENDIF
	CSTR:ARG:後ろ髪 = %CHOICES%
CASE "ノーアレンジ", "ツインテール", "サイドテール", "ポニーテール", "三つ編み", "でか三つ編み", "お団子", "ショートツインテ", "ロングツインテ", "ウェーブツインテ", "ドリルツインテ", "お団子ツインテ", "編み上げ", "シニヨン", "サイドシニヨン", "おさげ", "一つ結び", "二つ結び", "バレッタ留め"
	IF CSTR:ARG:アレンジ == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "アレンジ")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "アレンジ")%
	ENDIF
	CSTR:ARG:アレンジ = %CHOICES%
CASE "アホ毛なし", "アホ毛", "アホ毛２", "アホ毛３", "触角"
	IF CSTR:ARG:アホ毛 == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "アホ毛")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "アホ毛")%
	ENDIF
	CSTR:ARG:アホ毛 = %CHOICES%
CASE "リボンなし", "両側リボン", "片側リボン", "片側リボン２", "後ろリボン", "頭上リボン"
	IF CSTR:ARG:リボン == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "リボン")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "リボン")%
	ENDIF
	CSTR:ARG:リボン = %CHOICES%
CASE "黒", "青", "紫", "緑", "赤", "茶", "金", "紅", "白", "紺", "焦茶", "深緑", "真鍮", "銀"
	IF CSTR:ARG:目の色 == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "目の色")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "目の色")%
	ENDIF
	CSTR:ARG:目の色 = %CHOICES%
CASE "左黒", "左青", "左紫", "左緑", "左赤", "左茶", "左金", "左紅", "左白", "左紺", "左焦茶", "左深緑", "左真鍮", "左銀"
	IF CONDS("左目の色", ARG) == SUBSTRINGU(CHOICES, 1, -1)
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "目の色")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "目の色")%
	ENDIF
	CSTR:ARG:目の色 = %CONDS("右目の色", ARG)%_%SUBSTRINGU(CHOICES, 1, -1)%
	SIF CONDS("左目の色", ARG) == CONDS("右目の色", ARG)
		CSTR:ARG:目の色 = %CONDS("右目の色", ARG)%

CASE "通常目", "通常目２", "ツリ目", "ツリ目２", "たれ目", "たれ目２", "ジト目", "ジト目２", "ぱっちり", "糸目", "縦長目", "三白眼", "ジト三白眼", "にっこり", "瞑り", "〇〇", "＞＜"
	IF CSTR:ARG:目の形 == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "目の形")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "目の形")%
	ENDIF
	CSTR:ARG:目の形 = %CHOICES%
CASE "左通常目", "左通常目２", "左ツリ目", "左ツリ目２", "左たれ目", "左たれ目２", "左ジト目", "左ジト目２", "左ぱっちり", "左糸目", "左縦長目", "左三白眼", "左ジト三白眼", "左にっこり", "左瞑り", "左〇〇", "左＞＜"
	IF CONDS("左目の形", ARG) == SUBSTRINGU(CHOICES, 1, -1)
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "目の形")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "目の形")%
	ENDIF
	CSTR:ARG:目の形 = %CONDS("右目の形", ARG)%_%SUBSTRINGU(CHOICES, 1, -1)%
	SIF CONDS("左目の形", ARG) == CONDS("右目の形", ARG)
		CSTR:ARG:目の形 = %CONDS("右目の形", ARG)%

;黒目の記載追加_2020_06_24_作者Ｉ
CASE "白目なし", "白目あり", "黒目あり"
	IF CSTR:ARG:白目 == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "白目")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "白目")%
	ENDIF
	CSTR:ARG:白目 = %CHOICES%
CASE "左白目なし", "左白目あり", "左黒目あり"
	IF CONDS("左白目", ARG) == SUBSTRINGU(CHOICES, 1, -1)
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "白目")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "白目")%
	ENDIF
	CSTR:ARG:白目 = %CONDS("右白目", ARG)%_%SUBSTRINGU(CHOICES, 1, -1)%
	SIF CONDS("左白目", ARG) == CONDS("右白目", ARG)
		CSTR:ARG:白目 = %CONDS("右白目", ARG)%

CASE "通常眉", "通常眉短", "怒り眉", "怒り眉短", "たれ眉", "たれ眉短", "困り眉", "困り眉短", "麻呂眉"
	IF CSTR:ARG:眉の形 == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "眉の形")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "眉の形")%
	ENDIF
	CSTR:ARG:眉の形 = %CHOICES%
CASE "微笑", "への字", "笑顔", "△", "大口", "無口系", "ω", "ワ", "半開き", "嘲笑", "小口", "ふくれっ面"
	IF CSTR:ARG:口の形 == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "口の形")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "口の形")%
	ENDIF
	CSTR:ARG:口の形 = %CHOICES%

CASE "頬化粧無し"
	CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, "隈取赤", "隈取青", "戦化粧")%
CASE "隈取赤", "隈取青", "戦化粧"
	CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, "隈取赤", "隈取青", "戦化粧")%
	CSTR:ARG:頬 = %SETSTR(CSTR:ARG:頬, CHOICES)%
CASE "頬傷無し"
	CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, "頬傷左", "頬傷鼻", "頬傷右")%
CASE "頬絆創膏無し"
	CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, "頬絆創膏左", "頬絆創膏鼻", "頬絆創膏右")%
CASE "頬傷左", "頬傷鼻", "頬傷右", "頬絆創膏左", "頬絆創膏鼻", "頬絆創膏右"
	IF STRCOUNT(CSTR:ARG:頬, CHOICES)
		CSTR:ARG:頬 = %CLEARSTR(CSTR:ARG:頬, CHOICES)%
	ELSE
		CSTR:ARG:頬 = %SETSTR(CSTR:ARG:頬, CHOICES)%
	ENDIF
CASE "人間耳"
	TALENT:ARG:エルフ耳 = 0
	TALENT:ARG:動物耳 = 0
CASE "エルフ耳"
	TALENT:ARG:エルフ耳 = 1
	TALENT:ARG:動物耳 = 0
CASE "魔族耳"
	TALENT:ARG:エルフ耳 = 2
	TALENT:ARG:動物耳 = 0
CASE "動物耳"
	TALENT:ARG:エルフ耳 = 0
	TALENT:ARG:動物耳 = 1
CASE "動物タレ耳"
	TALENT:ARG:エルフ耳 = 0
	TALENT:ARG:動物耳 = 2
CASE "獣人"
	CSTR:ARG:種族 = %CHOICES%
	TALENT:ARG:エルフ耳 = 0
	TALENT:ARG:動物耳 = 1
	CALL GET_EXTALENT, ARG
CASE "兎獣人"
	CSTR:ARG:種族 = 獣人_兎
	TALENT:ARG:エルフ耳 = 0
	TALENT:ARG:動物耳 = 1
	CALL GET_EXTALENT, ARG
CASE "悪魔", "鬼", "竜人"
	CSTR:ARG:種族 = %CHOICES%
	TALENT:ARG:ツノ = 1
	TALENT:ARG:動物耳 = 0
	CALL GET_EXTALENT, ARG
CASE "人間", "獣人", "妖狐", "天使", "悪魔", "鬼", "竜人", "淫魔", "吸血鬼"
	CSTR:ARG:種族 = %CHOICES%
	TALENT:ARG:動物耳 = 0
	CALL GET_EXTALENT, ARG
CASE "眼鏡なし"
	IF OTHER(CSTR:ARG:アクセサリ, "眼鏡") == ""
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "眼鏡")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "眼鏡")%
	ENDIF
	CALL RESET_OTHER, "眼鏡", ARG
CASE "眼鏡", "ブロー眼鏡", "フルリム眼鏡", "フィンチ眼鏡", "ロイド眼鏡", "サングラス", "目隠し白", "目隠し黒"
	IF OTHER(CSTR:ARG:アクセサリ, "眼鏡") == CHOICES
		STR:顔グラ固定 = %SETSTR(STR:顔グラ固定, "眼鏡")%
	ELSE
		STR:顔グラ固定 = %CLEARSTR(STR:顔グラ固定, "眼鏡")%
	ENDIF
	CALL SET_OTHER, CHOICES, ARG

;エモプラ関係
CASE "チーク無し", "赤面", "楕円", "頬染", "丸ほっぺ", "紅潮", "返り血"
	STR:顔グラ変化 = %REPLACE(STR:顔グラ変化, @"%FACES("チークの種類", ARG)%/", "")%
	SIF CHOICES != "チーク無し"
		STR:顔グラ変化 = %STR:顔グラ変化%%CHOICES%/
CASE "ふきだし無し", "ふきだし怒り", "ふきだし汗", "ふきだしハート", "ふきだしワイワイ", "ふきだし驚き", "ふきだし眠り", "ふきだし寝ぼけ", "ふきだし満開", "ふきだし・・・", "ふきだし！", "ふきだし？", "ふきだしハート大量"
	STR:顔グラ変化 = %REPLACE(STR:顔グラ変化, @"%FACES("ふきだしの種類", ARG)%/", "")%
	SIF CHOICES != "ふきだし無し"
		STR:顔グラ変化 = %STR:顔グラ変化%%CHOICES%/
CASE "小物無し", "スクールバッグ", "お茶", "みかん", "本", "頭上みかん", "頭上サングラス", "タバコ", "アイス", "ココアシガレット", "食パン", "フランスパン", "ゴム", "扇子赤", "扇子富士山", "扇子桜", "扇子白", "萌え袖"
	STR:顔グラ変化 = %REPLACE(STR:顔グラ変化, @"%FACES("小物", ARG)%/", "")%
	SELECTCASE CHOICES
	CASE "小物無し"
	CASE "萌え袖"
		STR:顔グラ変化 = %STR:顔グラ変化%%CHOICES%/
		IF CHECK_CLO("萌え袖可能", ARG) == 0
			PRINTL 
			CALL PRINT_STRW, "黄色_萌え袖は、カーディガンorジャージorパジャマor肩出しジャケット着用時に起きます"
		ENDIF
	CASEELSE
		STR:顔グラ変化 = %STR:顔グラ変化%%CHOICES%/
	ENDSELECT

CASEELSE
	IF STRCOUNTS(CHOICES, "＆ジャージ", "＆肩出しジャケット")
		IF STRCOUNT(CHOICES, "無し")
			CHOICES = %EXTSTR(CLO("顔グラ衣装", ARG), "", "＆")%
		ELSE
			CHOICES = %EXTSTR(CLO("顔グラ衣装", ARG), "", "＆")%%CHOICES%
		ENDIF
	ELSEIF STRCOUNT(CHOICES, "/変Ｔ")
		IF STRCOUNT(CHOICES, "無し")
			CHOICES = %EXTSTR(CLO("顔グラ衣装", ARG), "", "/")%
		ELSE
			CHOICES = %EXTSTR(CLO("顔グラ衣装", ARG), "", "/")%%CHOICES%
		ENDIF
	ENDIF
	SELECTCASE ARGS
	CASE "表示衣装変更"
		STR:顔グラ衣装 = %CHOICES%
	CASE "制服", "体操服", "水着", "自宅着", "外出着"
		SAVESTR:ARGS = %CHOICES%
		STR:顔グラ衣装 = %CHOICES%
	ENDSELECT
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@FACES(ARGS, ARG)
#FUNCTIONS

VARSET LOCALS

SIF ARG == 0 && TARGET
	ARG = TARGET

SELECTCASE ARGS
CASE "チークの種類"
	IF STRCOUNT(STR:顔グラ変化, "戦化粧")
		LOCALS = 戦化粧
	ELSEIF STRCOUNT(STR:顔グラ変化, "隈取赤")
		LOCALS = 隈取赤
	ELSEIF STRCOUNT(STR:顔グラ変化, "隈取青")
		LOCALS = 隈取青
	ELSEIF STRCOUNT(STR:顔グラ変化, "赤面")
		LOCALS = 赤面
	ELSEIF STRCOUNT(STR:顔グラ変化, "楕円")
		LOCALS = 楕円
	ELSEIF STRCOUNT(STR:顔グラ変化, "頬染")
		LOCALS = 頬染
	ELSEIF STRCOUNT(STR:顔グラ変化, "丸ほっぺ")
		LOCALS = 丸ほっぺ
	ELSEIF STRCOUNT(STR:顔グラ変化, "紅潮")
		LOCALS = 紅潮
	ELSEIF STRCOUNT(STR:顔グラ変化, "返り血")
		LOCALS = 返り血
	ELSE
		LOCALS = 無し
	ENDIF

CASE "ふきだしの種類"
	IF STRCOUNT(STR:顔グラ変化, "ふきだし怒り")
		LOCALS =ふきだし怒り
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだし汗")
		LOCALS = ふきだし汗
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだしハート大量")
		LOCALS = ふきだしハート大量
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだしハート")
		LOCALS = ふきだしハート
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだしワイワイ")
		LOCALS = ふきだしワイワイ
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだし驚き")
		LOCALS = ふきだし驚き
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだし眠り")
		LOCALS = ふきだし眠り
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだし寝ぼけ")
		LOCALS = ふきだし寝ぼけ
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだし満開")
		LOCALS = ふきだし満開
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだし・・・")
		LOCALS = ふきだし・・・
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだし？")
		LOCALS = ふきだし？
	ELSEIF STRCOUNT(STR:顔グラ変化, "ふきだし！")
		LOCALS = ふきだし！
	ELSE
		LOCALS = 無し
	ENDIF
CASE "小物"
	IF STRCOUNT(STR:顔グラ変化, "スクールバッグ")
		LOCALS = スクールバッグ
	ELSEIF STRCOUNT(STR:顔グラ変化, "頭上みかん")
		LOCALS = 頭上みかん
	ELSEIF STRCOUNT(STR:顔グラ変化, "頭上サングラス")
		LOCALS = 頭上サングラス
	ELSEIF STRCOUNT(STR:顔グラ変化, "お茶")
		LOCALS = お茶
	ELSEIF STRCOUNT(STR:顔グラ変化, "みかん")
		LOCALS = みかん
	ELSEIF STRCOUNT(STR:顔グラ変化, "本")
		LOCALS = 本
	ELSEIF STRCOUNT(STR:顔グラ変化, "タバコ")
		LOCALS = タバコ
	ELSEIF STRCOUNT(STR:顔グラ変化, "アイス")
		LOCALS = アイス
	ELSEIF STRCOUNT(STR:顔グラ変化, "ココアシガレット")
		LOCALS = ココアシガレット
	ELSEIF STRCOUNT(STR:顔グラ変化, "食パン")
		LOCALS = 食パン
	ELSEIF STRCOUNT(STR:顔グラ変化, "フランスパン")
		LOCALS = フランスパン
	ELSEIF STRCOUNT(STR:顔グラ変化, "ゴム")
		LOCALS = ゴム
	ELSEIF STRCOUNT(STR:顔グラ変化, "扇子赤")
		LOCALS = 扇子赤
	ELSEIF STRCOUNT(STR:顔グラ変化, "扇子富士山")
		LOCALS = 扇子富士山
	ELSEIF STRCOUNT(STR:顔グラ変化, "扇子桜")
		LOCALS = 扇子桜
	ELSEIF STRCOUNT(STR:顔グラ変化, "扇子白")
		LOCALS = 扇子白
	ELSEIF STRCOUNT(STR:顔グラ変化, "萌え袖")
		LOCALS = 萌え袖
	ELSE
		LOCALS = 無し
	ENDIF

ENDSELECT

RETURNF LOCALS


;-------------------------------------------------
;キャラARGの顔グラを弄るための関数
;使っても使わなくてもよい
;-------------------------------------------------
@CHANGE_MAIDFACE, ARGS, ARG

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

SELECTCASE ARGS
CASE "黒髪", "金髪", "銀髪", "赤髪", "青髪", "緑髪", "茶髪", "紫髪", "桃髪", "紅髪", "白髪", "紺髪", "焦茶髪", "深緑髪", "真鍮髪"
	CSTR:ARG:髪の色 = %ARGS%
CASE "ショート", "ボブ", "ミディアム", "セミロング", "ロング"
	CSTR:ARG:髪の長さ = %ARGS%
CASE "ナチュラルバング", "ナチュラルバング２", "ショートバング", "Ｍ字バング", "斜めバング", "斜めバング２", "ぱっつん", "かきあげ", "片目隠れ", "編み込み", "外ハネバング", "両目隠れ", "もみあげロング", "デコ出し", "ボサボサ", "ボサボサ２", "ボサボサ３", "ボサボサ４", "ボサボサ５", "アシメ", "アシメ２", "インテーク", "シースルーバング", "シースルーバング２", "シースルーバング３", "右分け", "真ん中分け", "弱目隠れ", "弱目隠れ２", "かむろ風", "前髪クロス", "ベリーショート", "うね毛バング"
	CSTR:ARG:前髪 = %ARGS%
CASE "ナチュラル", "ストレート", "外ハネ", "ウェーブ"
	CSTR:ARG:後ろ髪 = %ARGS%
CASE "ノーアレンジ", "ツインテール", "サイドテール", "ポニーテール", "三つ編み", "でか三つ編み", "お団子", "ショートツインテ", "ロングツインテ", "ウェーブツインテ", "ドリルツインテ", "お団子ツインテ", "編み上げ", "シニヨン", "サイドシニヨン", "おさげ", "一つ結び", "二つ結び", "バレッタ留め"
	CSTR:ARG:アレンジ = %ARGS%
CASE "アホ毛なし", "アホ毛", "アホ毛２", "アホ毛３", "触角"
	CSTR:ARG:アホ毛 = %ARGS%
CASE "リボンなし", "両側リボン", "片側リボン", "片側リボン２", "後ろリボン", "頭上リボン"
	CSTR:ARG:リボン = %ARGS%
CASE "黒", "青", "紫", "緑", "赤", "茶", "金", "紅", "白", "紺", "焦茶", "深緑", "真鍮", "銀"
	CSTR:ARG:目の色 = %ARGS%
CASE "左黒", "左青", "左紫", "左緑", "左赤", "左茶", "左金", "左紅", "左白", "左紺", "左焦茶", "左深緑", "左真鍮", "左銀"
	CSTR:ARG:目の色 = %CONDS("右目の色", ARG)%_%SUBSTRINGU(ARGS, 1, -1)%
CASE "右黒", "右青", "右紫", "右緑", "右赤", "右茶", "右金", "右紅", "右白", "右紺", "右焦茶", "右深緑", "右真鍮", "右銀"
	CSTR:ARG:目の色 = %SUBSTRINGU(ARGS, 1, -1)%_%CONDS("左目の色", ARG)%

CASE "通常目", "通常目２", "ツリ目", "ツリ目２", "たれ目", "たれ目２", "ジト目", "ジト目２", "ぱっちり", "糸目", "縦長目", "三白眼", "ジト三白眼", "にっこり", "瞑り", "〇〇", "＞＜"
	CSTR:ARG:目の形 = %ARGS%
CASE "左通常目", "左通常目２", "左ツリ目", "左ツリ目２", "左たれ目", "左たれ目２", "左ジト目", "左ジト目２", "左ぱっちり", "左糸目", "左縦長目", "左三白眼", "左ジト三白眼", "左にっこり", "左瞑り", "左〇〇", "左＞＜"
	CSTR:ARG:目の形 = %CONDS("右目の形", ARG)%_%SUBSTRINGU(ARGS, 1, -1)%
CASE "右通常目", "右通常目２", "右ツリ目", "右ツリ目２", "右たれ目", "右たれ目２", "右ジト目", "右ジト目２", "右ぱっちり", "右糸目", "右縦長目", "右三白眼", "右ジト三白眼", "右にっこり", "右瞑り", "右〇〇", "右＞＜"
	CSTR:ARG:目の形 = %SUBSTRINGU(ARGS, 1, -1)%_%CONDS("左目の形", ARG)%

;黒目の記載追加_2020_06_24_作者Ｉ
CASE "白目なし", "白目あり", "黒目あり"
	CSTR:ARG:白目 = %ARGS%
CASE "左白目なし", "左白目あり", "左黒目あり"
	CSTR:ARG:白目 = %CONDS("右白目", ARG)%_%SUBSTRINGU(ARGS, 1, -1)%
CASE "右白目なし", "右白目あり", "右黒目あり"
	CSTR:ARG:白目 = %SUBSTRINGU(ARGS, 1, -1)%_%CONDS("左白目", ARG)%_

CASE "通常眉", "通常眉短", "怒り眉", "怒り眉短", "たれ眉", "たれ眉短", "困り眉", "困り眉短", "麻呂眉"
	CSTR:ARG:眉の形 = %ARGS%
CASE "微笑", "への字", "笑顔", "△", "大口", "無口系", "ω", "ワ", "半開き", "嘲笑", "小口", "ふくれっ面"
	CSTR:ARG:口の形 = %ARGS%
ENDSELECT

;目のCSTRが左右同じ場合の操作
SIF CONDS("左目の色", ARG) == CONDS("右目の色", ARG)
	CSTR:ARG:目の色 = %CONDS("右目の色", ARG)%
SIF CONDS("左目の形", ARG) == CONDS("右目の形", ARG)
	CSTR:ARG:目の形 = %CONDS("右目の形", ARG)%
SIF CONDS("左白目", ARG) == CONDS("右白目", ARG)
	CSTR:ARG:白目 = %CONDS("右白目", ARG)%
