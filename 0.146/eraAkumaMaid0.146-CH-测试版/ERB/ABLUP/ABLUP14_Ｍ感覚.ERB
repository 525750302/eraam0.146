;-------------------------------------------------
;ABL:Ｍ感覚 のLVUP処理
;-------------------------------------------------
@ABLUP14
#DIM LCOUNT
#DIM CHOICE
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP14
;何もできない場合
IF RESULT == 0 && ABL:Ｍ感覚 >= 99
	PRINTFORMW 既にMAXです。
	RETURN 0
ENDIF

DRAWLINE
VARSET NUM_JUEL
NUM_CHOICES = 0

IF ABL:Ｍ感覚 < 99
	;珠以外で必要な条件や軽減の内訳の記載
	SETCOLOR DEF_COLOR("黄色")
	SELECTCASE EXP:キス経験
	CASE IS >= EXP_LV(ABL:Ｍ感覚 + 3)
		PRINTFORML ※キス経験が非常に多いので、要求される珠が80％減少しています※
	CASE IS >= EXP_LV(ABL:Ｍ感覚 + 2)
		PRINTFORML ※キス経験がとても多いので、要求される珠が50％減少しています※
	CASE IS >= EXP_LV(ABL:Ｍ感覚 + 1)
		PRINTFORML ※キス経験が多いので、要求される珠が20％減少しています※
	;著しく足りていない
	CASE IS < MAX(EXP_LV(ABL:Ｍ感覚), 1)
		PRINTFORML ※キス経験が少なすぎるので、要求される珠が３倍に増加しています※
	ENDSELECT
	RESETCOLOR

	FOR LCOUNT, 0, 16
		SIF JUEL:0:LCOUNT == 0
			CONTINUE

		IF JUEL:LCOUNT >= JUEL:0:LCOUNT
			PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
		ELSE
			SETCOLOR DEF_COLOR("灰色")
			PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
			RESETCOLOR
		ENDIF
		NUM_JUEL:NUM_CHOICES = LCOUNT
		NUM_CHOICES += 1
	NEXT
ENDIF

;特殊な選択肢
SET_CHOICE = 0
;蕩唇を得る
IF TALENT:蕩唇 == 0
	IF COND("蕩唇取得可能")
		PRINTFORML  [10] [蕩唇]を獲得する
		SET_CHOICE = 10
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] [蕩唇]を獲得する(要:快Ｍの珠{NUM("宝珠：蕩唇取得")}、Ｍ感覚4LV、今回のＭ絶頂数20)
		RESETCOLOR
	ENDIF
ENDIF

PRINTL [100] 戻る

CALL INPUT_SELECT, NUM_CHOICES, SET_CHOICE, 100

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE 10
	IF JUEL:快Ｍ >= NUM("宝珠：蕩唇取得") && TALENT:蕩唇 == 0
		JUEL:快Ｍ -= NUM("宝珠：蕩唇取得")
		TALENT:蕩唇 = 1
		PRINTFORMW %CALLNAME:TARGET%のこちらを見る目がおかしい…
		PRINTFORMW %CALLNAME:TARGET%は行為が終わり立ち去ろうとした%CALLNAME:MASTER%を抱き寄せ、幾度も口付けを繰り返してきた…
		PRINTL 
		SETCOLOR DEF_COLOR("ハートピンク")
		PRINTFORMW %CALLNAME:TARGET%は[蕩唇]を得た
		RESETCOLOR
	ENDIF
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:CHOICE) >= JUEL:0:(NUM_JUEL:CHOICE)
		JUEL:(NUM_JUEL:CHOICE) -= JUEL:0:(NUM_JUEL:CHOICE)
		ABL:Ｍ感覚 += 1
		PRINTFORMW Ｍ感覚が{ABL:Ｍ感覚}LVになりました。
	ENDIF
ENDSELECT

;喉性感の獲得
IF TALENT:喉性感 == 0 && ABL:Ｍ感覚 + TALENT:Ｍ性向 >= 5
	PRINTFORM %CALLNAME:TARGET%は
	CALL PRINT_COLORTEXT, "[喉性感]", DEF_COLOR("黄色")
	PRINTFORML を得た
	TALENT:喉性感 = 1
	FORCEWAIT
ENDIF

;まだレベルが上げられたりする場合には最初に戻る
CALL IS_ABLUP14
SIF RESULT
	RESTART



;ABL:Ｍ感覚のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP14, ARGS
CALL VARSET_ABLUP
SELECTCASE ABL:Ｍ感覚
CASE 0
	JUEL:0:快Ｍ = 500
CASE 1
	JUEL:0:快Ｍ = 1000
CASE 2
	JUEL:0:快Ｍ = 3000
CASE 3 TO 4
	JUEL:0:快Ｍ = (ABL:Ｍ感覚 - 2) * 5000
CASE 5 TO 9
	JUEL:0:快Ｍ = 10000 + (ABL:Ｍ感覚 - 4) * 10000
CASEELSE
	JUEL:0:快Ｍ = 70000 + (ABL:Ｍ感覚 - 9) * 30000
ENDSELECT

;キス経験による軽減
SELECTCASE EXP:キス経験
CASE IS >= EXP_LV(ABL:Ｍ感覚 + 3)
	TIMES JUEL:0:快Ｍ, 0.20
CASE IS >= EXP_LV(ABL:Ｍ感覚 + 2)
	TIMES JUEL:0:快Ｍ, 0.50
CASE IS >= EXP_LV(ABL:Ｍ感覚 + 1)
	TIMES JUEL:0:快Ｍ, 0.80
;著しく足りていない
CASE IS < MAX(EXP_LV(ABL:Ｍ感覚), 1)
	TIMES JUEL:0:快Ｍ, 3.00
ENDSELECT


;蕩唇が獲得できる？
SIF COND("蕩唇取得可能") && ARGS != "ＬＶ限定"
	RETURN 1
;最大LV未満か？
SIF ABL:Ｍ感覚 >= 99
	RETURN 0

;条件のどれかをクリアしてる？
SIF JUEL:0:快Ｍ && JUEL:快Ｍ >= JUEL:0:快Ｍ
	RETURN 1
RETURN 0
