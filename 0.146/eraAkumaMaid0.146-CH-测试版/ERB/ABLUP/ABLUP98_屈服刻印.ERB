;-------------------------------------------------
;MARK:屈服刻印 のLVUP処理。最大LVは3
;-------------------------------------------------
@ABLUP98
#DIM LCOUNT
#DIM NUM_CHOICES
#DIM NUM_JUEL, 10

;必要な珠などの値を取得
CALL IS_ABLUP98
;何もできない場合
IF RESULT == 0 && MARK:屈服刻印 >= 3
	PRINTFORMW 既にMAXです。
	RETURN 0
ENDIF

DRAWLINE
;珠以外で必要な条件や軽減の内訳の記載
;SETCOLOR DEF_COLOR("黄色")

;RESETCOLOR

VARSET NUM_JUEL
NUM_CHOICES = 0
FOR LCOUNT, 0, 16
	SIF JUEL:0:LCOUNT == 0
		CONTINUE

	IF JUEL:LCOUNT >= JUEL:0:LCOUNT
		PRINTFORML  [{NUM_CHOICES, 2}] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT} ……ＯＫ
	ELSE
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML  [×] %PALAMNAME:LCOUNT%の珠 {JUEL:LCOUNT, 8}/{JUEL:0:LCOUNT}
		RESETCOLOR
	ENDIF
	NUM_JUEL:NUM_CHOICES = LCOUNT
	NUM_CHOICES += 1
NEXT

PRINTL [100] 戻る

CALL INPUT_SELECT, NUM_CHOICES, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASEELSE
	IF NUM_CHOICES && JUEL:(NUM_JUEL:RESULT) >= JUEL:0:(NUM_JUEL:RESULT)
		JUEL:(NUM_JUEL:RESULT) -= JUEL:0:(NUM_JUEL:RESULT)
		MARK:屈服刻印 += 1
		PRINTFORMW 屈服刻印が{MARK:屈服刻印}LVになりました。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に戻る
CALL IS_ABLUP98
SIF RESULT
	RESTART



;MARK:屈服刻印のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP98, ARGS
CALL VARSET_ABLUP

;屈服or恐怖でLVUP
SELECTCASE MARK:屈服刻印
CASE 0
	JUEL:0:屈服 = 3000
CASE 1
	JUEL:0:屈服 = 10000
CASE 2
	JUEL:0:屈服 = 30000
ENDSELECT

;最大LV未満か？
SIF MARK:屈服刻印 >= 3
	RETURN 0

;条件のどれかをクリアしてる？
SIF JUEL:0:屈服 && JUEL:屈服 >= JUEL:0:屈服
	RETURN 1
RETURN 0

