;-------------------------------------------------
;日常イベントでの用途別の汎用地の文置き場
;-------------------------------------------------
@DAILY_MESSAGE, ARGS
#DIM MEMO_LINECOUNT

SELECTCASE ARGS
CASE "帰り道"
	;SAVESTR:現在位置 と SAVESTR:帰り道の行動 を決定する
	CALL SETFLAG, "遭遇地点"

	;雨に降られた
	IF STRCOUNT(SAVESTR:現在位置, "にわか雨")
		PRINTFORML %TEXTR("天気予報が外れて/急に/さっきまで晴れていたというのに/さぁ帰ろうというところで")%雨が降ってきた……

		PRINTFORM %TEXTR("鞄に忍ばせていた/折り畳み/部室に忘れられていた")%傘を差した%CALLNAME:MASTER%は、
		PRINTFORMW %CALLNAME:TARGET%を誘って相合傘で家路についた。

		RETURN 1
	ENDIF

	PRINTFORM 午後の講義を受け終えた%CALLNAME:MASTER%
	;寄り道した場所がパートナーの行動圏内
	SELECTCASE SAVESTR:現在位置
	CASE "水着屋", "下着屋", "ランジェリーショップ"
		PRINTFORM は、%CALLNAME:TARGET%に誘われて
	CASE "ラブホテル"
		PRINTFORM が繁華街をブラついていると
	CASEELSE
		IF COND("放課後デート中")
			PRINTFORM は%CALLNAME:TARGET%を誘って、
		ELSEIF STRCOUNT(CSTR:出現場所, SAVESTR:現在位置)
			PRINTFORM は、%CALLNAME:TARGET%に誘われて
		ELSE
			PRINTFORM は、%CALLNAME:TARGET%と一緒に
		ENDIF
	ENDSELECT

	SELECTCASE SAVESTR:帰り道の行動
	CASE "もうすぐクリスマス"
		PRINTFORMW 煌びやかに飾り付けられた街並みを手を繋いで肩を寄せながら散策した。
	CASE "ラブホ前で遭遇"
		PRINTFORMW ……ラブホから%SAVESTR:帰り道のゲスト%と共に出てきた%CALLNAME:TARGET%に偶然出会った。
	CASE "お参り"
		PRINTFORMW %SAVESTR:現在位置%に%SAVESTR:帰り道の行動%をしに行った。
		IF COND("パンツ形状確認") == 0 && CHECK_SKIRT("ミニ") && CHECK_SKIRT("スカート")
			PRINTFORM 長い石段を上るついでに先行く%CALLNAME:TARGET%のスカートの中も拝んだところ、
			IF EQUIP:パンツ == 0 || RAND:2
				CALL TEXT_PANTU_CHECK, SKIRT(TARGET)
			ELSE
				CALL TEXT_PANTU_CHECK_SHORT
			ENDIF
			CALL SETFLAG_DAILY, "パンツ形状確認"
		ENDIF
	CASE "直帰"
		PRINTFORMW %TEXTR("だらだら散策/学園での出来事を話/適当に雑談")%しながら帰った。
	CASE "買い出し"
		PRINTFORM %SAVESTR:現在位置%
		SELECTCASE SAVESTR:現在位置
		CASE "スーパー"
			PRINTFORMW で晩御飯のおかず等を物色した。
		CASE "茶葉の量り売り店"
			SELECTCASE TEXTS("季節")
			CASE "春"
				PRINTFORMW で%TEXTR("春茶を物色した/切れている茶葉の補充をした")%。
			CASE "夏"
				PRINTFORMW で%TEXTR("夏摘みのお茶を物色した/切れている茶葉の補充をした")%。
			CASEELSE
				PRINTFORMW で%TEXTR("面白い茶葉を求めて見て回った/ハーブティーを物色した/切れている茶葉の補充をした")%。
			ENDSELECT
		ENDSELECT
	CASE "下着選び"
		PRINTFORMW %SAVESTR:現在位置%で%TEXTR("下着を物色した/下着選びに付き合った")%。
	CASE "水着選び"
		PRINTFORMW %SAVESTR:現在位置%で%TEXTR("水着を物色した/今年の水着選びに付き合った")%。
	CASE "準新作を物色"
		PRINTFORMW %SAVESTR:現在位置%の準新作コーナーで好きな作品について語り合った。
	CASE "イチャイチャ", "立ち読み", "買い食い", "ゲームを物色"
		PRINTFORMW %SAVESTR:現在位置%で%SAVESTR:帰り道の行動%した。
	CASE "新刊のチェック", "音ゲー", "メダルゲーム", "新作の物色"
		PRINTFORMW %SAVESTR:現在位置%で%SAVESTR:帰り道の行動%をした。
	CASE "クレーンゲーム"
		PRINTFORMW %SAVESTR:現在位置%で%SAVESTR:帰り道の行動%%TEXTR("にお小遣いを吸い取られた…/でワイワイ楽しんだ。/で欲しいプライズがあるか物色した。")%
	CASE "プリクラ"
		PRINTFORMW %SAVESTR:現在位置%で一緒に%SAVESTR:帰り道の行動%を撮った。
	CASE "対戦ゲーム"
		PRINTFORMW %SAVESTR:現在位置%で%SAVESTR:帰り道の行動%をして%TEXTR("鎬を削った/楽しんだ")%。
	CASE "寄り道"
		IF SAVESTR:現在位置 == "寄り道"
			PRINTFORMW %TEXTR("学園での出来事を話/適当に雑談")%しながら寄り道をした。
		ELSE
			PRINTFORMW %SAVESTR:現在位置%に%TEXTR("寄り道をした/赴いた")%。
		ENDIF
	ENDSELECT

CASE "寝言"
	IF INSERT("インサート")
		IF COND("起きそう")
			CALL PRINT_STR, @"%CALLNAME:MASTER%に犯される%CALLNAME:TARGET%は少し寝苦しそうだ……_黄色_すぐにでも目を覚ますかもしれない_L"
		ELSEIF RAND:3 == 0 && INSERT("正常位")
			PRINTFORML %CALLNAME:TARGET%は無意識に%CALLNAME:MASTER%の腰を足で抱きかかえてきた……
		ELSEIF RAND:2
			PRINTFORM %CALLNAME:TARGET%は%CALLNAME:MASTER%の%TEXTR("腰の動きで揺らされ/ペニスで膣を掻き回され/性処理に使われ")%ながら
			IF COND("熟睡")
				PRINTFORML も、規則正しい寝息を立てている……
			ELSE
				PRINTFORML 寝息を立てている……
			ENDIF
		ELSE
			PRINTFORML %CALLNAME:TARGET%は寝返りを打とうとしたが、%CALLNAME:MASTER%に打ち込まれているペニスのせいで出来なかった……
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:TARGET%は
		IF CFLAG:スカートめくり
			IF CHECK_CLO("ノーパン")
				PRINTFORM %TEXTR("大股を開かれた体勢で、/スカートが捲れ、アソコが丸見えの状態で/恥ずかしい場所全てを白日の下に晒されたまま、")%
			ELSEIF EQUIP:パンツ
				PRINTFORM %TEXTR("大股を開かれた体勢で、/パンツ丸見えの状態で/下着を白日の下に晒されたまま、")%
			ELSE
				PRINTFORM 大股を開かれた体勢で、
			ENDIF
		ELSE
			IF COND("夏")
				PRINTFORM %TEXTR("ベッドで/タオルケットにくるまり、/寝返りを打ちつつ、")%
			ELSE
				PRINTFORM %TEXTR("布団にもぐり込んで、/ベッドで/毛布にくるまり、/寝返りを打ちつつ、")%
			ENDIF
		ENDIF
		IF COND("起きそう")
			PRINTFORML 寝息を立てている……が、見るからに眠りが浅い
			CALL PRINT_STR, "黄色_そろそろ目を覚ましそうだ……_L"
		ELSE
			PRINTFORML %TEXTR("ぐっすりと眠っ/穏やかに寝息を立て/気持ちよさそうに眠りこけ/幸せそうに眠りに落ち")%ている……
		ENDIF
	ENDIF
CASE "パンツを脱いで手渡し"
	SIF TFLAG:取引承諾 == 0
		RETURN 0
	PRINTFORM %CALLNAME:TARGET%は
	IF CHARA_NUM("室内の邪魔者") >= 1
		PRINTFORM 他に人が居るにも拘らず\@ CHECK_OUTER("ブルマ") ? ブルマを下ろし # \@、その場で%PANTIES(TARGET, "略")%を
		SELECTCASE PANTIES(TARGET, "略")
		CASE "前貼り"
			PRINTFORMW 剥がして%CALLNAME:MASTER%に手渡ししてきた。
		CASE "Ｃストリング"
			PRINTFORMW 外して%CALLNAME:MASTER%に手渡ししてきた。
		CASEELSE
			PRINTFORMW 脱いで%CALLNAME:MASTER%に手渡ししてきた。
		ENDSELECT
	ELSEIF CHARA_NUM("集合予定") <= 2 || PLACE("個室")
		PRINTFORM 二人きりと言う事で、%CALLNAME:MASTER%に
		SELECTCASE PANTIES(TARGET, "略")
		CASE "前貼り"
			PRINTFORMW 剥がすところを見せつけながら%PANTIES(TARGET, "略")%を手渡してきた。
		CASE "Ｃストリング"
			PRINTFORMW 外すところを見せつけながら%PANTIES(TARGET, "略")%を手渡してきた。
		CASEELSE
			PRINTFORMW 脱ぐところを見せつけながら%PANTIES(TARGET, "略")%を手渡してきた。
		ENDSELECT
	ELSE
		PRINTFORM 他の人達が居ない今がチャンスと素早く
		SELECTCASE PANTIES(TARGET, "略")
		CASE "前貼り"
			PRINTFORMW 前貼りを剥がし、%CALLNAME:MASTER%に手渡ししてきた。
		CASE "Ｃストリング"
			PRINTFORMW Ｃストリングを外し、%CALLNAME:MASTER%に手渡ししてきた。
		CASEELSE
			PRINTFORMW 下着を下ろし、%CALLNAME:MASTER%に手渡ししてきた。
		ENDSELECT
	ENDIF
CASE "汎用一言イベント"

CASE "性欲解消"
	;あなたが攻めっ気を出すルート
	IF STR:プレイ内容 != STR:プレイ要望 || RAND:2
		PRINTFORM %CALLNAME:PLAYER%は%TEXTS("挿入準備脱衣")%%NAMES("ソファ")%に座り直した%CALLNAME:TARGET%の
		SELECTCASE NAMES("性処理")
		CASE "クンニ"
			PRINTFORM 股間に顔を埋め、%TEXTS("割れ目")%を%TEXTR("指で寛げて/指先で押し広げて")%
			PRINTFORMW %TEXTR(@"%TEXTS("匂い")%の強い粘膜/内部の粘膜/鮮やかな色の粘膜")%%TEXTR("に舌を這わせ/を舌で舐め/を舌先でくすぐり")%始めた。
		CASE "フェラする"
			PRINTFORM 股間に顔を埋め、
			IF SIZE("ペニス", TARGET) >= 3 && RAND:3
				PRINTFORM %TEXTS("ペニス：大きさ表現付")%
			ELSE
				PRINTFORM %TEXTS("ペニス：視覚", TARGET)%
				IF COND("真性包茎")
					PRINTFORM 包茎ペニス
				ELSEIF COND("ズル剥け")
					PRINTFORM ズル剥けペニス
				ELSE
					PRINTFORM ペニス
				ENDIF
			ENDIF
			PRINTFORMW %TEXTR("に一度舌を這わせた後に咥えて舐めしゃぶり/を咥えて激しく扱きながら味わい/を口に迎え入れて舌先で磨き上げながら刺激し")%始めた。
		CASE "Ｖセックス", "Ａセックス", "尿道セックス"
			PRINTFORM %TEXTR("膝/足首")%を掴んで股を押し開き、腰を突き出して
			SELECTCASE NAMES("性処理")
			CASE "Ｖセックス"
				PRINTFORM 涎をこぼす膣
			CASE "Ａセックス"
				PRINTFORM 犯され慣れたアナル
			CASE "尿道セックス"
				PRINTFORM すっかり性器と化した尿道
			ENDSELECT
			IF RAND:2 && CHECK_COM("完全挿入可能", SELECTCOM)
				PRINTFORMW に%TEXTS("ペニス：大きさ表現付", PLAYER)%を%TEXTR("根元まで埋め込んだ/余すことなく咥え込ませた/挿入して恥骨を擦り付けた")%。
			ELSEIF SIZE("ペニス", PLAYER) >= 2
				PRINTFORMW を%TEXTS("ペニス：大きさ表現付", PLAYER)%で%TEXTR("深々と貫いた/奥深くまで犯し始めた/勢いよく犯し始めた")%。
			ELSE
				PRINTFORMW を%TEXTS("ペニス：大きさ表現付", PLAYER)%で%TEXTR("貫いた/勢いよく犯し始めた")%。
			ENDIF
		ENDSELECT
	ELSE
		PRINTFORM %TEXTS("挿入準備脱衣")%%NAMES("ソファ")%に座り直した%CALLNAME:TARGET%は
		IF TALENT:恥じらい == 0 && (COND("発情期") || COND("欲求不満") >= 3) && RAND:3 == 0
			PRINTFORM %CALLNAME:PLAYER%に向けて%TEXTR("待ちきれないといった様子で/余裕の無さを滲ませながら")%股を開き、
		ELSEIF TALENT:気品 + TALENT:小悪魔
			PRINTFORM %CALLNAME:PLAYER%に向けて%TEXTR("ゆっくりと/挑発的な仕草で/品のある仕草で")%股を開き、
		ELSEIF TALENT:恥薄い && TALENT:粗野 + TALENT:愚か
			PRINTFORM %TEXTR(@"お行儀悪く大股を開き\/%CALLNAME:PLAYER%に大股を開いて股間を見せつけながら")%、
		ELSE
			PRINTFORM %CALLNAME:PLAYER%に向けて%TEXTR("軽く/慣れた様子で/僅かな恥じらいと共に")%股を開き、
		ENDIF
		SELECTCASE NAMES("性処理")
		CASE "クンニ"
			IF TALENT:ラビアピアス && RAND:3
				PRINTFORM %TEXTS("割れ目：表現短め")%から%TEXTR("食み出る/顔を出す")%ピアスを抓んで粘膜を%TEXTR("寛げ/押し広げ/左右に引き伸ばし")%つつ
			ELSE
				PRINTFORM %TEXTS("割れ目")%を%TEXTR("指で寛げて/指先で押し広げて/グッと広げて")%
			ENDIF
			PRINTFORMW 舌での奉仕を促してきた。
		CASE "フェラする"
			IF SIZE("ペニス", TARGET) >= 3 && RAND:3
				PRINTFORM %TEXTS("ペニス：大きさ表現付")%
			ELSE
				PRINTFORM %TEXTS("ペニス：視覚", TARGET)%
				IF COND("真性包茎")
					PRINTFORM 包茎ペニス
				ELSEIF COND("ズル剥け")
					PRINTFORM ズル剥けペニス
				ELSE
					PRINTFORM ペニス
				ENDIF
			ENDIF
			PRINTFORMW を見せつけて舌での奉仕を促してきた。
		CASE "Ｖセックス", "Ａセックス", "尿道セックス"
			PRINTFORM 突き出された%TEXTS("ペニス：大きさ表現付", PLAYER)%を
			SELECTCASE NAMES("性処理")
			CASE "Ｖセックス"
				PRINTFORM 膣
			CASE "Ａセックス"
				PRINTFORM アナル
			CASE "尿道セックス"
				PRINTFORM 尿道
			ENDSELECT
			IF RAND:2 && CHECK_COM("完全挿入可能", SELECTCOM)
				PRINTFORMW で%TEXTR("根元まで/余すことなく")%受け入れた。
			ELSEIF SIZE("ペニス", PLAYER) >= 2
				PRINTFORMW で深々と受け入れた。
			ELSE
				PRINTFORMW で受け入れた。
			ENDIF
		ENDSELECT
	ENDIF

	SELECTCASE NAMES("性処理")
	CASE "クンニ"
		SIF TEXTS("Ｖコンディション", TARGET) != ""
			CALL PRINT_STRW, @"(%CALLNAME:TARGET%のＶコンディションは_黄色_%TEXTS("Ｖコンディション", TARGET)%_です)"
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の%TEXTR("頑張りに応じて優しく頭を撫でたりし/舌の動きに応じて鼻にかかったような声を時折漏らし/クンニの激しさに鼻息を少し荒くしたりし")%ながら、
		PRINTFORMW 敏感な場所への奉仕に夢中になっている……
	CASE "フェラする"
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の%TEXTR("頑張りに応じて優しく頭を撫でたりし/舌の動きに応じて気持ちよさそうな表情を時折見せ/口淫の激しさに鼻息を少し荒くしたりし")%ながら、
		PRINTFORMW 敏感な場所への奉仕に夢中になっている……
	CASE "Ｖセックス", "Ａセックス", "尿道セックス"
		MEMO_LINECOUNT = LINECOUNT
		IF RAND:2
			SELECTCASE NAMES("性処理")
			CASE "Ｖセックス"
				CALL TEXT, "追加地の文：Ｖ挿入後"
			CASE "Ａセックス"
				CALL TEXT, "追加地の文：Ａ挿入後"
			CASE "尿道セックス"
				CALL TEXT, "追加地の文：尿道挿入後"
			ENDSELECT
		ENDIF
		SIF MEMO_LINECOUNT != LINECOUNT
			RETURN 1

		CALL TEXT, "追加地の文：騎乗位以外のインサート"
	ENDSELECT

CASE "性欲解消：逆挿入"
	;相手が攻めっ気を出すルート
	IF TALENT:意地悪 + TALENT:粗野 + TALENT:愚か + TALENT:小悪魔 + TALENT:情熱的 && COND("ガチオトコ", PLAYER) == 0 && RAND:2
		PRINTFORM %CALLNAME:TARGET%は%TEXTR(@"%NAMES("ソファ")%に座っ/%NAMES("ソファ")%に腰かけ")%ている%CALLNAME:PLAYER%の
		IF RAND:2
			PRINTFORM パンツを引きずり下ろした後に
		ELSE
			PRINTFORM %TEXTR("膝/足首")%を掴んで
		ENDIF
		PRINTFORM %TEXTR("グッと/勢いよく/容赦なく")%股を押し開き、腰を突き出して
		SELECTCASE NAMES("性処理")
		CASE "Ｖセックスする"
			PRINTFORM 期待で濡れた膣
		CASE "Ａセックスする"
			PRINTFORM 経験豊富なアヌス
		ENDSELECT
		IF RAND:2 && CHECK_COM("完全挿入可能", SELECTCOM)
			PRINTFORMW に%TEXTS("ペニス：大きさ表現付", TARGET)%を%TEXTR("根元まで埋め込んだ/余すことなく咥え込ませた/挿入して恥骨を擦り付けた")%。
		ELSEIF SIZE("ペニス", TARGET) >= 2
			PRINTFORMW を%TEXTS("ペニス：大きさ表現付", TARGET)%で%TEXTR("深々と貫いた/奥深くまで犯し始めた/勢いよく犯し始めた")%。
		ELSE
			PRINTFORMW を%TEXTS("ペニス：大きさ表現付", TARGET)%で%TEXTR("貫いた/勢いよく犯し始めた")%。
		ENDIF
	ELSE
		IF COND("ガチオトコ", PLAYER) == 0
			PRINTFORM %TEXTR(@"%NAMES("ソファ")%/%NAMES("ソファ")%の端")%に座り直した%CALLNAME:PLAYER%は%CALLNAME:TARGET%に向けて股を開き、突き出された
		ELSE
			PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%に向けて尻を突きだし、宛がわれた
		ENDIF
		PRINTFORM %TEXTS("ペニス：大きさ表現付", TARGET)%を
		SELECTCASE NAMES("性処理")
		CASE "Ｖセックスする"
			PRINTFORM 膣
		CASE "Ａセックスする"
			PRINTFORM アヌス
		ENDSELECT
		IF RAND:2 && CHECK_COM("完全挿入可能", SELECTCOM)
			PRINTFORMW で%TEXTR("根元まで/余すことなく")%受け入れた。
		ELSEIF SIZE("ペニス", TARGET) >= 2
			PRINTFORMW で深々と受け入れた。
		ELSE
			PRINTFORMW で受け入れた。
		ENDIF
	ENDIF

	CALL TEXT, "挿入具合の描写"

	SELECTCASE NAMES("性処理")
	CASE "Ｖセックスする"
		PRINTFORM %CALLNAME:TARGET%は
		SIF TALENT:恋慕 && RAND:3
			PRINTFORM 首筋やうなじに舌を這わせながら
		;「TARGETが真性のサドか」「サドっ気持ちか」で分岐
		IF ABL:技巧 >= 4 && EXP:腰使い経験 + ABL:腰使い*10 >= 50 && RAND:4 == 0
			PRINTFORM 子宮口を押し込むように
		ELSEIF ABL:技巧 >= 3 && EXP:腰使い経験 + ABL:腰使い*10 >= 30 && RAND:3 == 0
			PRINTFORM Ｇスポットを擦りあげるように
		ELSEIF RAND:2 == 0
			PRINTFORM 焦らすようにゆっくりと
		ELSE
			PRINTFORM 打ちつけるように激しく
		ENDIF
		PRINTFORMW 腰を動かして、%CALLNAME:PLAYER%の反応を楽しんでいる…
	CASE "Ａセックスする"
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の腰を掴んで、
		;恋慕で追加
		SIF TALENT:恋慕 && RAND:3
			PRINTFORM 首筋やうなじに舌を這わせながら
		IF ABL:腰使い >= 5 && RAND:3 == 0
			PRINTFORMW 立体的に腰をグラインドさせながら直腸粘膜を盛んに擦りあげている…
		ELSEIF ABL:サドっ気 >= 3 && RAND:2 == 0
			PRINTFORMW 獣のように激しく腰を振りたてている…
		ELSE
			PRINTFORMW 腰を大きく前後に動かしている…
		ENDIF
	ENDSELECT
CASE "性欲解消：射精"
	SIF RAND:3 && COND("性欲解消を手伝う可能")
		LOCALS = 欲望に任せて
	SELECTCASE NOWEX:射精
	CASE IS >= 3
		LOCALS:1 = 物凄い量の
	CASE 2
		LOCALS:1 = 大量の
	CASE 1
		LOCALS:1 = 熱い
	ENDSELECT

	PRINTFORM %CALLNAME:TARGET%は腰を%TEXTR("密着させて/突き出して")%
	SELECTCASE NAMES("性処理")
	CASE "Ｖセックスする"
		IF RAND:2 && SIZE("ペニス", TARGET) >= 2
			PRINTFORM 膣%TEXTR("を深くまで犯し/の奥の壁を叩き/奥を押し上げ/の深い場所を擦りあげ")%ながら、
		ELSE
			PRINTFORM 膣%TEXTR("を激しく犯し/壁を擦り上げ/を激しくかき混ぜ")%ながら、
		ENDIF
		IF TEQUIP:コンドーム
			PRINTFORMW %LOCALS:1%精液をコンドームの中に吐き出した。
		ELSEIF RAND:3 == 0
			PRINTFORMW %LOCALS:1%精液を%CALLNAME:PLAYER%の最奥めがけて流し込んだ。
		ELSEIF EXP_GAIN("膣内精液経験", PLAYER) == 0 && COND("危険日", PLAYER)
			PRINTFORMW %LOCALS:1%精液を危険日の子宮に%LOCALS%注ぎ込んだ。
		ELSE
			PRINTFORMW %LOCALS%%LOCALS:1%精%TEXTR(@"液で%CALLNAME:PLAYER%の中を染め上げた/を放った/液を注ぎ込んだ")%。
		ENDIF
	CASE "Ａセックスする"
		PRINTFORM %TEXTR("直腸/アナル")%
		IF RAND:2 && SIZE("ペニス", PLAYER) >= 2
			PRINTFORM %TEXTR("の深い場所を擦りあげ/を深くまで犯し")%ながら、
		ELSE
			PRINTFORM %TEXTR("に根元まで咥え込ませ/を激しくかき混ぜ/を激しく犯し")%ながら、
		ENDIF
		IF TEQUIP:コンドーム
			PRINTFORMW %LOCALS:1%精液をコンドームの中に吐き出した。
		ELSEIF RAND:3 == 0
			PRINTFORMW %LOCALS:1%精液を%CALLNAME:PLAYER%の腸内の奥深くめがけて流し込んだ。
		ELSE
			PRINTFORMW %LOCALS%%LOCALS:1%精%TEXTR(@"液で%CALLNAME:PLAYER%の中を染め上げた/を放った/液を注ぎ込んだ")%。
		ENDIF
	ENDSELECT

	PRINTFORM %CALLNAME:TARGET%は
	IF TALENT:恋慕 && RAND:3
		PRINTFORM %CALLNAME:PLAYER%%TEXTR("の顔を見つめ/に抱きつき/の乱れた髪を手櫛で整え")%ながら、
	ELSE
		PRINTFORM %TEXTR("乱れた呼吸を整え/腰を緩々と動かして最後の一滴まで吐き出し/フーッと深く息を吐き")%ながら、
	ENDIF
	SELECTCASE NAMES("性処理")
	CASE "Ｖセックスする"
		IF TEQUIP:コンドーム == 0
			PRINTFORMW %TEXTR("気持ちよさそうな表情で/満足げな様子で")%中出しセックスの余韻に浸っている…
		ELSE
			PRINTFORMW %TEXTR("気持ちよさそうな表情で/満足げな様子で")%性行為の余韻に浸っている…
		ENDIF
	CASE "Ａセックスする"
		PRINTFORMW %TEXTR("気持ちよさそうな表情で/満足げな様子で")%アナルセックスの余韻に浸っている…
	ENDSELECT

CASE "性処理"
	;まずはプレイごとの衣装変化の処理(相手が主導するプレイのみ)
	SELECTCASE NAMES("性処理")
	CASE "手淫", "フェラチオ"
		;PRINTFORM なぜか服を全部脱いで靴下と靴のみの格好になった
	CASE "パイズリ"

	CASE "ニプルファック"

	ENDSELECT

	;プレイ描写
	SELECTCASE NAMES("性処理")
	CASE "奉仕クンニ"
		;相手が攻めっ気を出すルート
		IF TALENT:意地悪 + TALENT:粗野 + TALENT:愚か + TALENT:小悪魔 + TALENT:情熱的 && RAND:2
			PRINTFORM %CALLNAME:TARGET%は腰かけている%CALLNAME:PLAYER%の
			IF RAND:2
				PRINTFORM パンツを引きずり下ろした後に
			ELSE
				PRINTFORM %TEXTR("膝/足首")%を掴んで
			ENDIF
			PRINTFORM %TEXTR("グッと/勢いよく/容赦なく")%股を押し開いて%TEXTR("顔を突っ込み/顔を埋め")%、
			PRINTFORMW 露わになった%TEXTS("大陰唇：視覚", PLAYER)%の%TEXTR("内側/割れ目/隙間")%へと舌を%TEXTR("這わせてきた/伸ばして奉仕し始めた/伸ばして粘膜を刺激してきた")%…
			SIF TEXTS("Ｖコンディション", PLAYER) != ""
				CALL PRINT_STRW, @"(%CALLNAME:PLAYER%のＶコンディションは_黄色_%TEXTS("Ｖコンディション", PLAYER)%_です)"
		ELSE
			PRINTFORM パンツを下して足から抜きながら手頃な場所に腰かけた%CALLNAME:PLAYER%は%CALLNAME:TARGET%に向けて股を開き、
			PRINTFORMW %TEXTS("割れ目", PLAYER)%を指で%TEXTR("寛げ/押し広げ/開帳し")%ながら舌での奉仕を促した。
			SIF TEXTS("Ｖコンディション", PLAYER) != ""
				CALL PRINT_STRW, @"(%CALLNAME:PLAYER%のＶコンディションは_黄色_%TEXTS("Ｖコンディション", PLAYER)%_です)"
			PRINTFORM %CLO("現在の格好＆の")%%CALLNAME:TARGET%は%TEXTR("目の前の光景を眺め/僅かに鼻をヒクつかせ/開かれた膝を手で更に左右に広げ")%ながら%CALLNAME:PLAYER%の股間に顔を埋め、
			PRINTFORMW %TEXTR("うっすらついた汚れも余さず掃除するように執拗な舌使いでの/クリトリスだけでなく膣にも舌を這わせての熱心な/上目遣いで様子を伺いながら献身的な")%クンニを始めた…
		ENDIF
	CASE "手淫"
		PRINTFORM %CLO("現在の格好＆の")%%CALLNAME:TARGET%は
		;他の人との性処理直後
		IF COND("他の人の性処理直後")
			IF COND("Ｐ汚れの相手", MASTER)
				PRINTFORMW %CONDS("Ｐ汚れの相手", MASTER)%で楽しんだばかりの%CALLNAME:MASTER%のペニスを手に取り、指先で軽く弄んだ後に本腰を入れて扱き始めた…
			ELSE
				PRINTFORMW 射精直後の%CALLNAME:MASTER%のペニスを手に取り、指先で軽く弄んだ後に本腰を入れて扱き始めた…
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:MASTER%のズボンからペニスを取り出し、指先で軽く弄んだ後に本腰を入れて手で扱き始めた…
		ENDIF
		;PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%のズボンからペニスを取り出し、先端を舌でペロペロ舐めながら利き手で扱き始めた。
		;PRINTFORMW 余った方の手は自らの股間に延び、グチュグチュと粘っこい水音を立てつつ気持ちのいい部分を弄り続けている…
	CASE "フェラチオ"
		PRINTFORM %CLO("現在の格好＆の")%%CALLNAME:TARGET%は
		;他の人との性処理直後
		IF COND("他の人の性処理直後")
			IF COND("Ｐ汚れの相手", MASTER)
				SELECTCASE CONDS("Ｐ汚れ", MASTER)
				CASE "Ｖ"
					PRINTFORM %CONDS("Ｐ汚れの相手", MASTER)%%TEXTR("との中出しセックスで汚れたままの/とのセックス直後で愛液臭が漂う")%
				CASE "Ａ"
					PRINTFORM %CONDS("Ｐ汚れの相手", MASTER)%%TEXTR("とのアナルセックスで汚れたままの/とのアナルセックス直後で酷い臭いをさせている")%
				CASE "尿"
					PRINTFORM %CONDS("Ｐ汚れの相手", MASTER)%%TEXTR("との尿道セックスで汚れたままの/の尿道を使ったために染み付いたおしっこの臭いをさせている")%
				ENDSELECT
				PRINTFORMW %CALLNAME:MASTER%のペニスを口に迎え入れ、舌を絡めながら頭を動かして溜まった精液を搾り取り始めた…
			ELSE
				PRINTFORMW 射精直後の%CALLNAME:MASTER%のペニスを口に迎え入れ、舌を絡めながら頭を動かして溜まった精液を搾り取り始めた…
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:MASTER%のペニスを口に迎え入れ、舌を絡めながら頭を動かして溜まった精液を搾り取り始めた…
		ENDIF
	CASE "パイズリ"
		PRINTFORMW %CLO("現在の格好＆の")%%CALLNAME:TARGET%は%CALLNAME:MASTER%のペニスを\@ TALENT:巨乳 ? 自慢の # \@胸で挟み込み、顔を出した亀頭に舌を這わせながらパイズリを開始した…
	CASE "ニプルファック"
		PRINTFORMW %CLO("現在の格好＆の")%%CALLNAME:TARGET%は%CALLNAME:MASTER%のペニスを胸で受け入れ、激しい突き込みに合わせるかのように緩急をつけた締め付けで愉しませてきた…
	CASE "Ｖセックス", "Ａセックス", "尿道セックス"
		SELECTCASE CONDS("体位")
		CASE "騎乗位"
			PRINTFORM %TEXTS("挿入準備脱衣")%%CALLNAME:TARGET%は、
			IF COND("常時拡張：引き抜き発生") && RAND:3 == 0
				PRINTFORM %TEXTR("拡張されっぱなしで綻んでいる/異物を引き抜かれたばかりで口を開けたままの/いつでも出来るようにと準備されている")%
				SELECTCASE NAMES("性処理")
				CASE "Ｖセックス"
					PRINTFORM アソコで
				CASE "Ａセックス"
					PRINTFORM アナルで
				CASE "尿道セックス"
					PRINTFORM 尿道で
				ENDSELECT
				PRINTFORM %CALLNAME:MASTER%の
			ELSE
				SIF CFLAG:絆創膏装着 && (NAMES("性処理") == "Ｖセックス" || NAMES("性処理") == "尿道セックス")
					PRINTFORM 絆創膏をめくりながら
				SELECTCASE SAVESTR:現在位置
				CASE "教官室", "研究室", "司書室"
					PRINTFORM 仮眠用のベッドに寝そべる%CALLNAME:MASTER%に跨って
				CASE "体育倉庫"
					PRINTFORM マットレスに寝そべる%CALLNAME:MASTER%に跨って
				CASE "部室"
					PRINTFORM 備え付けのベッドに寝そべる%CALLNAME:MASTER%に跨って
				CASE "旧宿直室"
					PRINTFORM 敷かれていた布団に寝そべる%CALLNAME:MASTER%に跨って
				CASEELSE
					IF PLACE("個室")
						PRINTFORM 寝そべる%CALLNAME:MASTER%に跨って
					ELSE
						PRINTFORM %NAMES("ソファ")%に寝そべる%CALLNAME:MASTER%に跨って
					ENDIF
				ENDSELECT
			ENDIF
			IF COND("他の人の性処理直後")
				IF COND("Ｐ汚れの相手", MASTER)
					SELECTCASE CONDS("Ｐ汚れ", MASTER)
					CASE "Ｖ"
						PRINTFORM %CONDS("Ｐ汚れの相手", MASTER)%%TEXTR("との中出しセックスで汚れたままの/とのセックス直後で愛液臭が漂う")%
					CASE "Ａ"
						PRINTFORM %CONDS("Ｐ汚れの相手", MASTER)%%TEXTR("とのアナルセックスで汚れたままの/とのアナルセックス直後で酷い臭いをさせている")%
					CASE "尿"
						PRINTFORM %CONDS("Ｐ汚れの相手", MASTER)%%TEXTR("との尿道セックスで汚れたままの/の尿道を使ったために染み付いたおしっこの臭いをさせている")%
					ENDSELECT
					PRINTFORM ペニスを
				ELSE
					PRINTFORM 射精直後の\@ SIZE("ペニス", MASTER) >= 2 ? 巨根 # ペニス \@を
				ENDIF
			ELSEIF NAMES("性処理") == "Ｖセックス" && COND("挿入余裕度") && SIZE("ペニス", MASTER) >= 2 && RAND:3
				IF SIZE("ペニス", MASTER) + TALENT:小柄 - TALENT:長身 >= 3
					PRINTFORM 自らの臍上まで届くような%TEXTR("凶悪な/長大な")%ペニスを
				ELSE
					PRINTFORM 臍下まで到達する%TEXTR("凶悪な/長大な")%ペニスを
				ENDIF
			ELSEIF RAND:2
				PRINTFORM %TEXTR("天を仰ぐように隆起している/天井向けてそそり立つ/凄い角度で勃起している")%ペニスを
			ELSE
				PRINTFORM %TEXTS("ペニス：視覚", PLAYER)%ペニスを
			ENDIF
			IF TALENT:好色 + TALENT:淫乱 + TALENT:恋慕 && COND("視力") && RAND:3 == 0
				PRINTFORM %TEXTR("顔を見つめながら/愛おしそうに微笑みながら")%
			ELSEIF COND("挿入余裕度") >= 2 && RAND:2
				PRINTFORM %TEXTR("優越感を滲ませながら/余裕の表情で/気持ち良さそうな顔を見せながら")%
			ELSEIF COND("挿入余裕度") && ABL:サドっ気 >= 2 && RAND:2
				IF TALENT:高慢 + TALENT:強気 && RANDIF(2, TALENT:生意気 + TALENT:小悪魔)
					PRINT 不敵な笑みを浮かべながら
				ELSEIF TALENT:生意気 + TALENT:小悪魔 && RAND:2
					PRINT ニヤニヤしながら
				ELSE
					PRINTFORM %TEXTR("優越感/嗜虐的な悦び")%を滲ませながら
				ENDIF
			ELSE
				PRINTFORM %TEXTR("甘い吐息を漏らしながら/熱っぽい息を吐きながら")%
			ENDIF

			SELECTCASE COND("挿入余裕度")
			;よゆー
			CASE IS >= 2
				PRINTFORMW %TEXTR("根元まで/残さず")%%TEXTR("収めた/咥え込んだ/呑み込んだ")%。
			;ぴったり
			CASE 1
				PRINTFORMW %TEXTR("収めた/咥え込んだ/呑み込んだ")%。
			;全部は無理
			CASEELSE
				PRINTFORMW 限界まで%TEXTR("収めた/咥え込んだ/呑み込んだ")%。
			ENDSELECT
		CASEELSE
			IF RAND:3 && (ABL:欲望 >= 5 || TALENT:快感に素直)
				PRINTFORM %CALLNAME:TARGET%は
				IF COND("常時拡張：引き抜き発生") && RAND:3
					;場所
					SELECTCASE NAMES("性処理")
					CASE "Ｖセックス"
						PRINTFORM %TEXTR("前の穴/膣")%
					CASE "Ａセックス"
						PRINTFORM %TEXTR("お尻の穴/直腸")%
					CASE "尿道セックス"
						PRINTFORM %TEXTR("おしっこの穴/排泄のための穴")%
					ENDSELECT
					;サイズで分岐
					SELECTCASE COND("常時拡張：引き抜き発生")
					CASE IS >= 4
						PRINTFORM %TEXTR("から引きずり出した/をみっちりと広げていた/を埋めていた")%%TEXTR("規格外の/有り得ない大きさの/バカでかい")%
					CASE 3
						PRINTFORM %TEXTR("から引きずり出した/をみっちりと広げていた/を塞いでいた")%%TEXTR("極太の/野太い/大きな")%
					CASEELSE
						PRINTFORM %TEXTR("から引き抜いた/で咥え込んでいた/を押し広げていた")%
					ENDSELECT
					SELECTCASE NAMES("性処理")
					CASE "Ｖセックス"
						PRINTFORM バイブ
					CASE "Ａセックス"
						PRINTFORM プラグ
					CASE "尿道セックス"
						PRINTFORM プラグ
					ENDSELECT
					;抜いたものの扱い
					IF COND("自発お掃除フェラ可能") && RAND:3
						IF COND("Ａお掃除フェラ可能") && RAND:3
							PRINTFORM の%TEXTR("汚れ/粘液/恥ずかしい汚れ")%を%TEXTR("尖らせた舌先で/美味しそうに/うっとりと")%舐め取りつつ、
						ELSE
							PRINTFORM の%TEXTR("先端/根本から先端まで/凹凸")%を興奮した面持ちで%TEXTR("ペロペロと/ねっとりと/楽しそうに")%舐めつつ、
						ENDIF
					ELSEIF TALENT:恥じらい
						PRINTFORM を%TEXTR("恥ずかしそうに/幾ばくかの恥じらいと共に/さりげなく死角を意識しつつ")%脇に置くと、
					ELSE
						PRINTFORM を%TEXTR("汚れに気を付けつつ脇に/よく見える場所に/解放感から溜息を出しつつ側に")%置くと、
					ENDIF
				ELSE
					PRINTFORM %TEXTS("挿入準備脱衣")%
					SELECTCASE SAVESTR:現在位置
					CASE "体育倉庫"
						PRINTFORM %TEXTR("跳び箱に腰を下ろすと/マットに腰を下ろすと")%、
					CASE "更衣室"
						PRINTFORM %TEXTR("ソファベッドに腰を下ろすと/壁にもたれかかると")%、
					CASE "男子トイレ", "女子トイレ"
						PRINTFORM %TEXTR("便座/洋式便座")%に腰かけると、
					CASE "屋上"
						PRINTFORM %TEXTR("フェンスを掴んで体重を預けると/壁にもたれかかると")%、
					;ベッドがある
					CASE "部室"
						PRINTFORM 備え付けのベッド%TEXTR("に腰を下ろすと/に身を横たえると")%、
					;机がある
					CASE "研究室", "教官室", "司書室"
						PRINTFORM %TEXTR("机に腰を下ろすと/デスクチェアに深く腰掛けると")%、
					CASEELSE
						PRINTFORM %TEXTR("壁にもたれかかると")%、
					ENDSELECT
				ENDIF
				SIF CFLAG:絆創膏装着 && (NAMES("性処理") == "Ｖセックス" || NAMES("性処理") == "尿道セックス")
					PRINTFORM 絆創膏をめくりながら
				PRINTFORM %CALLNAME:MASTER%に
				SELECTCASE NAMES("性処理")
				CASE "Ｖセックス"
					PRINTFORM 向けて%TEXTR("準備の出来ている/涎を垂らす/発情した")%アソコを
				CASE "Ａセックス"
					PRINTFORM 向けて%TEXTR("準備万端な/ヒクつく/発情した")%アナルを
				CASE "尿道セックス"
					PRINTFORM 向けて%TEXTR("すっかり緩くなった/快感に飢えた/待ちきれない様子の")%尿道を
				ENDSELECT
				PRINTFORMW %TEXTR("押し広げ/見せつけ/露出させ")%て%TEXTR("催促/挑発/誘惑")%してきた…
				SIF NAMES("性処理") == "Ｖセックス" && TEXTS("Ｖコンディション", TARGET) != ""
					CALL PRINT_STRW, @"(%CALLNAME:TARGET%のＶコンディションは_黄色_%TEXTS("Ｖコンディション", TARGET)%_です)"
			ELSE
				SELECTCASE SAVESTR:現在位置
				CASE "体育倉庫"
					PRINTFORM %TEXTR("跳び箱に腰を下ろした/マットに腰を下ろした")%
				CASE "更衣室"
					PRINTFORM %TEXTR("ソファベッドに腰を下ろした/壁にもたれかかった/壁に手を付いてお尻を向けてきた")%
				CASE "男子トイレ", "女子トイレ"
					PRINTFORM %TEXTR("便座/洋式便座")%に腰かけた
				CASE "屋上"
					PRINTFORM %TEXTR("フェンスを掴んで体重を預けた/壁にもたれかかった/壁に手を付いてお尻を向けてきた")%
				CASE "旧宿直室"
					PRINTFORM 敷かれていた布団%TEXTR("に腰を下ろした/に身を横たえた")%
				;ベッドがある
				CASE "部室"
					PRINTFORM 備え付けのベッド%TEXTR("に腰を下ろした/に身を横たえた")%
				;机がある
				CASE "研究室", "教官室", "司書室"
					PRINTFORM %TEXTR("机に腰を下ろした/デスクチェアに深く腰掛けた")%
				CASEELSE
					PRINTFORM %TEXTR("壁にもたれかかった/壁に手を付いてお尻を向けてきた")%
				ENDSELECT
				PRINTFORM %CALLNAME:TARGET%は、
				SIF CFLAG:絆創膏装着 && (NAMES("性処理") == "Ｖセックス" || NAMES("性処理") == "尿道セックス")
					PRINTFORM 絆創膏をめくりながら
				IF ABL:欲望 >= 5 || TALENT:快感に素直 || TALENT:献身
					PRINTFORM %TEXTR("率先して/大きく/腰を突き出すように", \@ EXP:性処理経験 >= 5 ? 慣れた様子で # \@)%股を開いて
				ELSE
					PRINTFORM %TEXTR("おずおずと/控えめに/周囲を気にした様子で")%股を開いて
				ENDIF
				PRINTFORM %CALLNAME:MASTER%の\@ SIZE("ペニス", MASTER) >= 2 ? 巨根 # ペニス \@を
				SELECTCASE NAMES("性処理")
				CASE "Ｖセックス"
					PRINTFORM 膣
				CASE "Ａセックス"
					PRINTFORM アナル
				CASE "尿道セックス"
					PRINTFORM 尿道
				ENDSELECT

				IF SIZE("ペニス", MASTER) >= 2 && RANDIF(1, (ABL:欲望 >= 5 || TALENT:快感に素直) )
					SELECTCASE NAMES("性処理")
					CASE "Ｖセックス"
						IF ABL:Ｖ拡張 >= SIZE("ペニス", MASTER)
							PRINTFORMW で%TEXTR("深々と/根元まで/余すことなく")%受け入れた…
						ELSE
							PRINTFORMW を引き延ばされながらも、なんとか受け入れた…
						ENDIF
					CASE "Ａセックス"
						IF ABL:Ａ拡張 >= SIZE("ペニス", MASTER)
							PRINTFORMW で%TEXTR("深々と/根元まで/余すことなく")%受け入れた…
						ELSE
							PRINTFORMW を引き延ばされながらも、なんとか受け入れた…
						ENDIF
					CASE "尿道セックス"
						IF ABL:尿道拡張 >= SIZE("ペニス", MASTER)
							PRINTFORMW で%TEXTR("深々と/根元まで/余すことなく")%受け入れた…
						ELSE
							PRINTFORMW を引き延ばされながらも、なんとか受け入れた…
						ENDIF
					ENDSELECT
				ELSEIF ABL:欲望 >= 5 || TALENT:快感に素直
					PRINTFORMW で嬉々として受け入れた…
				ELSEIF TALENT:反抗的
					PRINTFORMW で渋々受け入れた…
				ELSE
					PRINTFORMW でしっかりと受け入れた…
				ENDIF
			ENDIF
		ENDSELECT
	ENDSELECT
	CALL TEXT, "挿入具合の描写"
	;ゴムを使ったならその解説
	SIF TEQUIP:MASTER:コンドーム
		CALL PRINT_STRW, @"黄色_（%CALLNAME:TARGET%の希望に従い\@ COND("中出し嫌い") ? # 、今日は \@コンドームを使います）"
	SIF CONDS("体位") == "騎乗位"
		CALL TEXT, "追加地の文：騎乗位"

CASE "ながら性処理"
	SELECTCASE NAMES("性処理")
	CASE "手淫"
		PRINTFORM %CALLNAME:TARGET%は%STR:ながら性処理%、
		PRINTFORMW 空いた方の手で%CALLNAME:PLAYER%のペニスを%TEXTR("欠片のやる気も見せずに/雑に/ぞんざいに")%扱き始めた……
	CASEELSE
		PRINTFORM %TEXTS("挿入準備脱衣")%%NAMES("ソファ")%に座り直した%CALLNAME:TARGET%は
		IF TALENT:気品 + TALENT:小悪魔
			PRINTFORM %CALLNAME:PLAYER%に向けて%TEXTR("ゆっくりと/挑発的な仕草で/品のある仕草で")%股を開き、
		ELSEIF TALENT:恥薄い && TALENT:粗野 + TALENT:愚か
			PRINTFORM %TEXTR(@"お行儀悪く大股を開き\/%CALLNAME:PLAYER%に大股を開いて股間を見せつけながら")%、
		ELSE
			PRINTFORM %CALLNAME:PLAYER%に向けて%TEXTR("軽く/慣れた様子で/僅かな恥じらいと共に")%股を開き、
		ENDIF
		SIF CFLAG:絆創膏装着 && (NAMES("性処理") == "Ｖセックス" || NAMES("性処理") == "尿道セックス")
			PRINTFORM 絆創膏を剥がして
		PRINTFORM 突き出された%TEXTS("ペニス：大きさ表現付", PLAYER)%を
		SELECTCASE NAMES("性処理")
		CASE "Ｖセックス"
			PRINTFORM 膣
		CASE "Ａセックス"
			PRINTFORM アナル
		CASE "尿道セックス"
			PRINTFORM 尿道
		ENDSELECT
		PRINTFORMW で%TEXTR("深々と/根元まで/余すことなく")%受け入れた。

		CALL TEXT, "挿入具合の描写"
		;ゴムを使ったならその解説
		SIF TEQUIP:PLAYER:コンドーム
			CALL PRINT_STRW, @"黄色_（%CALLNAME:TARGET%の希望に従い\@ COND("中出し嫌い") ? # 、今回は \@コンドームを使います）"

		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の腰の動き%TEXTR("に応じて鼻にかかったような声を時折漏らし/の無遠慮さに文句を言ったりし/の激しさに鼻息を少し荒くしたりし")%ながら、
		PRINTFORMW %TEXTS("夢中")%に夢中になっている……
	ENDSELECT

;デートイベント
CASE "デートのお誘い"
	PRINTFORMW ……%CALLNAME:TARGET%と話をしているうちに、次の休みにデートしないかという流れになってきました。
CASE "待ち合わせ"
	PRINTFORM \@ COND("同棲中") ? 玄関の前 # 待ち合わせ場所 \@で待っていると、
	IF COND("デート衣装に着替えた")
		PRINTFORM %TEXTR("普段よりお洒落をした/いつもとは異なる/今日のデートに備えておめかししてきた")%%CLO_ABOUT(TARGET, "姿")%の%CALLNAME:TARGET%が
	ELSEIF CHECK_CLO("学生服姿")
		PRINTFORM 制服姿の%CALLNAME:TARGET%が
	ELSE
		PRINTFORM 見慣れた姿の%CALLNAME:TARGET%が
	ENDIF
	;今日楽しみ！
	IF TALENT:素直 + TALENT:陽気 && TALENT:力持ち + TALENT:粗野 + TALENT:愚か && RAND:3
		PRINTFORMW %TEXTR("ぶんぶんと手を振って待ちきれない様子で/瞳をキラキラ輝かせながら/タッタッタッと弾むように駆けながら/息を切らせながら小走りで")%現れた。
	;ちょっと恥ずい
	ELSEIF TALENT:恥薄い == 0 && (TALENT:恋慕 || EXP:デート経験 == 0) && (TALENT:相思相愛 == 0 || EXP:デート経験 < 5) && RAND:4
		PRINTFORMW %TEXTR("照れ臭さからか視線を外して/少し恥ずかしそうに/少しだけ赤い顔で/僅かに頬を染めつつ")%挨拶しながら現れた。
	;まった？
	ELSEIF TALENT:素直 + TALENT:陽気
		PRINTFORMW %CALLNAME:MASTER%%TEXTR("と目が合うや否や笑顔で手を振って/が気付くよりも早く大きな声で名前を呼びながら")%近づいてきた。
	ELSE
		PRINTFORMW %TEXTR("笑顔で/普段通りの様子で/片手を軽く上げて")%挨拶しながら現れた。
	ENDIF

	TFLAG:衣装解説済み = 1

CASE "デート本番"
	;まずはデート内容
	PRINTFORM %CALLNAME:MASTER%と%CALLNAME:TARGET%
	SELECTCASE SAVESTR:デート
	CASE "文化祭"
		PRINTFORMW 年に一度の文化祭に参加するため、しおりをパラパラ眺めつつ普段通っている学園へと向かった。
	CASE "散歩"
		PRINTFORM は%TEXTR("お互いが最近ハマってる事を紹介しあい/学園での出来事を話し/適当に雑談し")%ながら、
		PRINTFORM %TEXTR("普段は一人で歩く近所を/あえて通った事のない道を選んで/出会う野良猫をからかいつつ/１つのイヤホンを片方づつ分け合って同じ音楽を聴きながら")%
		PRINTFORMW 散歩した。
	CASE "食べ歩き"
		PRINTFORM は%TEXTR("前日の夕ご飯の話なんかをし/学園でのお昼ご飯について話し/食べ物についての蘊蓄を披露しあい/なんとなく始めたしりとりを楽しみ/適当に雑談し")%ながら、
		PRINTFORM %TEXTR("近頃話題になっているお店/目についた良さそうな店/予め目星をつけておいたお店")%を
		PRINTFORMW 何件か梯子して食べ歩いた。
	CASE "街でお買い物"
		PRINTFORM は%TEXTR("欲しいけど自分では買いたくない物ＴＯＰ３を言い合い/最近仕入れた雑学を披露しあい/道すがら出会った知り合いからの追及をかわし/もしも今サンタから貰うなら何が良いかなどを駄弁り/適当に雑談し")%ながら、
		PRINTFORM %TEXTR("各々が行く予定だったお店に順番に足を運んで/街の商店街で/近場は勿論のこと普段一人では行かない少し遠めの場所にも足を延ばして")%
		PRINTFORMW お買い物した。
	CASE "水族館に行く"
		PRINTFORM は%TEXTR("擦れ違う人のカップル率をカウントし/親に連れられて来てはしゃいでる子供の様子を横目で眺め/あえて順路に逆らって回り/こんな所に一緒に来るなんてもう完全にカップルだなと軽口を叩き/適当に雑談し")%ながら、
		PRINTFORM %TEXTR("普段肉眼で見る事はまず無い巨大な海洋生物の雄大さを/最近食べた魚が生きて泳いでいる勇姿を眺めて/分厚いガラス越しにこっちに興味を持って寄ってきた魚と意思疎通をはかったりして")%
		PRINTFORMW 楽しんだ。
	CASE "遊園地に行く"
		PRINTFORM は%TEXTR("人込みではぐれない様に注意し/入り口でもらった１枚のパンフレットを顔を寄せて睨み/遊園地の最後には何をするかを話し合い/周りの熱気に引きずられて普段よりも感情を出し合い")%ながら、
		PRINTFORM %TEXTR("アトラクションを次々に回って/束の間の非日常の世界を/頭を空っぽにして直感任せで")%
		PRINTFORMW 楽しんだ。
	CASE "図書館に行く"
		PRINTFORM は%TEXTR("場所が場所だけにひそひそと小声で雑談し/思ったよりも大量にいる学園生たちを横目で眺め/目が合った互いの知り合いと挨拶しあい")%ながら、
		PRINTFORMW %TEXTR("学生らしく勉強をして過ごす/向かい合った席で課題をこなす/お目当ての本を探したり勉強を教え合ったりする")%事にした。
	;2019/9 hokaguru 追記
	CASE "映画館に行く"
		PRINTFORM が%TEXTR("路地の寂れた/駅前の大きな/アヤシゲな")%映画館へと足を運ぶと
		PRINTFORM %TEXTR("サイコスリラー/サスペンス/ホラー/ラブロマンス/アクション/SF/ポルノ/サイコサスペンスホラーロマンスSFアクションポルノ")%映画のポスターが張り出されており、
		PRINTFORMW 丁度良いとばかりにそれを観賞することにした。
	CASE "水着を買いに行く"
		PRINTFORMW は%TEXTR("学園近くのスポーツショップに/デパートの水着売り場に/少し遠いが品揃えの良い水着屋に")%足を延ばして、今年の水着を物色する事にした。
	CASE "泳ぎに行く"
		IF COND("今年の水着選択済")
			PRINTFORM は一緒に水着を選んで用意した事だしと、%TEXTR("少し遠場のバカでかいウォーターパーク/一人ではまず行く機会のないスパリゾート/前もってチェックしておいたアミューズメントプール")%に
		ELSE
			PRINTFORM はあまりの暑さに耐えかねて、%TEXTR("近場の多目的プール/学園の近くにある大きめのプール施設")%に
		ENDIF
		PRINTFORMW 泳ぎに行くことにした。
	;CASE "お花見"
	;CASE "山に行く"
	;CASE "紅葉狩りに行く"
	;CASE "スケート"

	CASEELSE
		IF STRCOUNT(SAVESTR:デート, "行く")
			PRINTFORMW は%SAVESTR:デート%ことにした。
		ELSE
			PRINTFORMW は%SAVESTR:デート%に行くことにした。
		ENDIF
	ENDSELECT

	;ついで相手の反応
	IF TALENT:陽気 + TALENT:素直 && (TALENT:恥じらい == 0 || TALENT:相思相愛) && STRCOUNTS(SAVESTR:デート, "遊園地") && RAND:3
		PRINTFORM %CALLNAME:TARGET%は着ぐるみのキャラクターを見かけるたびに%CALLNAME:MASTER%の%TEXTR("手を引っぱり/腕を抱き寄せて駆け出し")%、
		PRINTFORMW %TEXTR("満面の笑顔で着ぐるみにのしかかったり/嬉しそうに抱きついたり/写真撮影をねだったり")%してはしゃいでいる……
		TFLAG:手繋ぎ = TARGET
	ELSEIF TALENT:陽気 + TALENT:素直 && (TALENT:恥じらい == 0 || TALENT:相思相愛) && STRCOUNTS(SAVESTR:デート, "水族館") && RAND:3
		PRINTFORM %CALLNAME:TARGET%は%TEXTR("メートル級の海洋生物を発見する/グロテスクな割に愛嬌のある深海生物を見つける/クラゲや見てて和むものが現れる")%度に
		PRINTFORM %CALLNAME:MASTER%の%TEXTR("手を引っぱり/腕を抱き寄せて駆け出し")%、
		PRINTFORMW %TEXTR("満面の笑顔で水槽のガラスに張り付いて/説明書きを音読し始めたりして/写真撮影をねだったりして")%はしゃいでいる……
		TFLAG:手繋ぎ = TARGET
	ELSEIF STRCOUNTS(SAVESTR:デート, "文化祭")
		PRINTFORMW 学園は文化祭の影響で文字通りお祭り騒ぎの様相で、兄姉と遊びにきたと思わしき初等部の子どもや、どこに行くか真剣に話し合っているカップルなど、さまざまな人でごった返している……
	;2019/9 hokaguru 追記
	ELSEIF STRCOUNTS(SAVESTR:デート, "映画館")
		IF MONEY >= 150
			PRINTL 
			PRINTFORMW ……席に着く前に何か買って持ち込みますか？
			PRINTFORML お値段はどれでも150＄！　(所持金：%MONEY_C(MONEY)%)

			STR:選択肢 = ポップコーン/チュロス/ホットドッグ/やめておく
			SPLIT STR:選択肢, "/", LOCALS
			CALL PRINT_SELECT, STR:選択肢
			STR:選択したもの = %LOCALS:RESULT%

			SELECTCASE STR:選択したもの
			CASE "ポップコーン"
				PRINTFORM %CALLNAME:MASTER%達は%TEXTR("バケツみたいな容器に入った/バカでかいカップに入った/そう簡単には食べきれなさそうなデカさの")%
			CASE "チュロス"
				PRINTFORM %CALLNAME:MASTER%達は%TEXTR("紙容器に何本か入れられてチョコソースがセットで付けられた/紙袋入りのやたら長い/フライドポテトのような容器に何本か入れられ砂糖をまぶされた")%
			CASE "ホットドッグ"
				PRINTFORM %CALLNAME:MASTER%達は%TEXTR("おやつにするには少々デカい紙容器入りの/色々とトッピングされた大きめの")%
			CASEELSE
				GOTO SCREENING_START
			ENDSELECT
			PRINTFORMW %STR:選択したもの%とジュースを二人分買って席に着いた。
			CALL KOJO_DATE, "デート中の買い食い"
			MONEY -= 150
			PRINTFORML 所持金-%MONEY_C(150)%
			FLAG:ムード += 15
			CALL TEXT, "ムード変化＆ハート個数表示", 15
			WAIT
			DRAWLINE
		ENDIF

		$SCREENING_START

		PRINTFORM 上映が始まりシンと静まり返った%TEXTR("ガラガラの/そこそこの入りの/立ち見も居る")%館内に映画館ならではの爆音が響くと
		PRINTFORMW %CALLNAME:TARGET%は%TEXTR("目を輝かせて/楽しげに口元を緩ませて/吟味するように頬杖を突いて")%鑑賞の姿勢をとった……
		PRINTFORMW ちなみに映画の出来は……%TEXTR("実質死霊の盆踊りだった/学生が撮った方がマシだろう/見れないわけでは無いものだった/無難だ/どこか引き込まれそうだった/思わず熱中してしまう程だ/エロい")%……
	;あとは会話
	ELSE
		IF RAND:10 == 0
			PRINTFORM %CALLNAME:TARGET%は何故か擦れ違いざまに手を振って来た子供に%TEXTR("少し恥ずかしそうに/ブンブンと/笑顔で")%手を振り返しつつ、
		ELSEIF TALENT:好奇心 && STRCOUNTS(SAVESTR:デート, "遊園地", "水族館") && RAND:3
			IF TALENT:陽気 + TALENT:強気 + TALENT:優しい && RAND:2
				PRINTFORM テンションが上がっている様子の%CALLNAME:TARGET%は%TEXTR(@"行きたいところを指差して%CALLNAME:MASTER%の手を引きつつ/%CALLNAME:MASTER%の手をギュッと握りしめ")%、
				TFLAG:手繋ぎ = TARGET
			ELSE
				PRINTFORM %CALLNAME:TARGET%は出店やアトラクションに目移りして若干上の空の様子だが、
			ENDIF
		ELSEIF TALENT:強気 && TALENT:一線越えない == 0 && RAND:2
			PRINTFORM %CALLNAME:TARGET%は%TEXTR("こうするのが当然といった様子/さりげない素振り")%で%CALLNAME:MASTER%の手を取ってリードしつつ、
			TFLAG:手繋ぎ = TARGET
		ELSE
			PRINTFORM %CALLNAME:TARGET%は%CALLNAME:MASTER%の%TEXTR("服の裾をちょこんと掴んで着いて行き/身体をつんつん突いたりしてじゃれ/袖をつまんで歩調を合わせ")%つつ、
		ENDIF
		;会話が弾む
		PRINTFORM %TEXTR("お互いのくだらない会話/なんとなしに続くとりとめのない話/なんて事の無い話題の応酬")%
		IF TALENT:素直 + TALENT:好奇心 && RAND:3
			PRINTFORMW に目を輝かせながら話を合わせている……
		ELSEIF TALENT:強気 + TALENT:高慢 && RAND:3
			PRINTFORMW に満更でもなさそうに調子を合わせている……
		ELSE
			PRINTFORMW に笑顔を見せながら相槌を打っている……
		ENDIF
	ENDIF
CASE "水着選び開始"
CASE "水着決定"
	SELECTCASE SWIM(STR:選んだ水着, "種類")
	CASE "マイクロビキニ"
	CASE "ビキニ"
	CASE "競泳水着"
	CASE "ハイレグワンピ"
	CASE "ワンピース水着"
	CASE "ティアドロップ"
	CASE "スリングショット"
	CASEELSE
	ENDSELECT

CASE "お弁当"
	PRINTFORM %TEXTR(@"一息ついた\/楽しいお弁当タイムを終えた\/%CALLNAME:TARGET%の手料理を堪能した\/腹の虫を満足させた")%
	PRINTFORM %CALLNAME:MASTER%達は、
	SELECTCASE SAVESTR:デート
	CASE "文化祭"
		PRINTFORM %TEXTR("次に何を見に行くかを冗談交じりで話し合い/出し物の内容について感想を言い合い/変わりばんこに膝枕をしたりし/一緒に後片付けをし/僅かな気怠さと共にダラ〜ッとし/何するでもなく互いの体に寄りかかったりし")%ながら
	CASE "映画館に行く"
		PRINTFORM %TEXTR("映画の内容について感想を言い合い/お弁当の内容について感想を言い合い/変わりばんこに膝枕をしたりし/一緒に後片付けをし/僅かな気怠さと共にダラ〜ッとし/何するでもなく互いの体に寄りかかったりし")%ながら
	CASEELSE
		PRINTFORM %TEXTR("午後何やるかを冗談交じりで話し合い/内容について感想を言い合い/変わりばんこに膝枕をしたりし/一緒に後片付けをし/僅かな気怠さと共にダラ〜ッとし/何するでもなく互いの体に寄りかかったりし")%ながら
	ENDSELECT
	IF TEXTS("季節詳細") == "初夏"
		PRINTFORM 初夏の空気を木陰で
	ELSEIF TEXTS("季節") == "春" && RAND:2
		PRINTFORM 普段は意識しなかった春の景色を
	ELSEIF TEXTS("季節") == "秋"
		PRINTFORM %TEXTR("秋めいてきた風景/心地良い秋の空気/穏やかな昼の秋風")%を
	ELSE
		PRINTFORM 食後のまったりとした空気を
	ENDIF
	PRINTFORMW 楽しんだ……

CASE "手をつなぐ"
	;恥ずかしい
	IF TALENT:内気 + TALENT:保守的 && TALENT:感情乏しい + TALENT:好色 + TALENT:小悪魔 == 0
		IF RAND:2
		;	PRINTFORML 「！」
			PRINTFORMW %CALLNAME:TARGET%は緊張していたのか、%CALLNAME:MASTER%に手を握られた瞬間に身体全体をビクッとさせた。
			PRINTFORMW ……が、すぐに硬直を解いて優しく\@ TALENT:強気 + TALENT:力持ち ? 、そして力強く # \@握り返してきた。
			PRINTFORMW %CALLNAME:TARGET%が顔を伏せてしまったため表情を伺うことは出来ないが、嫌がられてはいない様だ……
		ELSE
		;	PRINTFORMW 「……」
			PRINTFORMW %CALLNAME:MASTER%に手を握られた%CALLNAME:TARGET%は軽くうつむき、黙ってしまった。
			PRINTFORMW と思いきや、ぎこちない動きで%CALLNAME:MASTER%の手にしっとりと汗ばんだ指を、そっと絡ませてきた……
		ENDIF
	;嬉しい
	ELSEIF TALENT:素直 + TALENT:陽気
		IF TALENT:恥じらい && TALENT:オトコ == 0 && RAND:2
		;	CALL PRINT_STRW, "「……_H_」"
			PRINTFORMW %CALLNAME:TARGET%は手を握られたのに乗じて身を寄せ、%CALLNAME:MASTER%の腕に抱きついてきた！
			PRINTFORMW 袖越しに伝わる%CALLNAME:TARGET%の柔らかさと心臓の鼓動が、なんとも心地いい……
		ELSE
		;	CALL PRINT_STRW, "「……_／／／_」"
			PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の手を、ぎゅっと笑顔で握り返してきた。
			PRINTFORMW 恥ずかしさを振り払うように大股で歩きだした%CALLNAME:TARGET%は、赤い顔でにやけながら繋がれた手を見つめている……
		ENDIF
	;余裕
	ELSE
		IF RAND:2
		;	PRINTFORMW 「…………♪」
			PRINTFORMW %CALLNAME:TARGET%は手を握られても特に動揺する事なく、%CALLNAME:MASTER%に自然に微笑みかけて来た。
			PRINTFORMW そして普段通りの態度で話しつつ、歩きながらさりげなく指を絡めて恋人つなぎにしてきた……
		ELSE
		;	PRINTFORMW 「…………」
			PRINTFORMW %CALLNAME:MASTER%の指が触れるかどうかという所で、%CALLNAME:TARGET%が先に手を握ってきた。
			PRINTFORMW %CALLNAME:TARGET%は不意を打たれた%CALLNAME:MASTER%の様子を、目を細めて嬉しそうに眺めている……
		ENDIF
	ENDIF
CASE "ちょっと物陰へ……", "こっそり口で抜いてもらう"
	SELECTCASE SAVESTR:デート
	CASE "水着を買いに行く"
		PRINTFORM 水着姿の%CALLNAME:TARGET%は%TEXTR("場所が場所/カーテン一枚隔てれば他のお客がいる状況な")%だけに
	;2020/02/24　マリーちゃんの中の人　追記　更に雑に分岐を増やす
	CASE "図書館に行く"
		PRINTFORM %TEXTR("書架の物陰に連れ込まれた/他の人が来なさそうな奥まった一角にやって来た/図書館の机の下に潜り込んだ")%%CALLNAME:TARGET%は
	CASE "文化祭"
		PRINTFORM %TEXTR("普段使われていない教室/屋上へと続く、誰も来ない階段の踊り場/人目に付かない場所")%へ連れ込まれた%CALLNAME:TARGET%は
	CASEELSE
		PRINTFORM %TEXTR("奥まった/人目に付かない/見るからに人が立ち入らなさそうな")%場所へ連れ込まれた%CALLNAME:TARGET%は
	ENDSELECT
	SELECTCASE SAVESTR:デート
	CASE "文化祭"
		PRINTFORM 学生たちの%TEXTR("出す物/足")%音%TEXTR("を気にしながら/に耳を澄ませながら/に神経質になりつつ")%も、
	CASEELSE
		PRINTFORM %TEXTR("周りの物/足")%音%TEXTR("を気にしながら/に耳を澄ませながら/に神経質になりつつ")%も、
	ENDSELECT
	;かなり乗り気
	IF COND("欲求不満") || TALENT:淫乱 || TALENT:好色
		PRINTFORM %TEXTR("して欲しい事はお見通しと言った態度で/これからする事自体には乗り気な様子で/嫌がるどころかむしろ率先して")%
	ELSE
		PRINTFORM %TEXTR("強く抵抗する事もなく/声高に文句を言う事もなく/望まれるままに")%
	ENDIF
	PRINTFORMW %CALLNAME:MASTER%の股間の前%TEXTR("で蹲踞の姿勢を取った/に腰を下ろした/で片膝をついた")%。

	;慣れている
	IF EXP:性処理経験 >= 5 && EXP:フェラ経験 >= 20 && ABL:舌使い >= 3
		PRINTFORM %TEXTR("慣れた手つきで/敢えて指ではなく口を使って/ズボンがテントを張っているのを見て取るや否や")%
	ELSE
		PRINTFORM %TEXTR("ぎこちない手つきで/慣れない手つきで/緊張のせいか少し手間取りつつも")%
	ENDIF
	IF RAND:2 && COND("欲求不満")
		PRINTFORM ファスナーを下ろして%TEXTR("露茎させ/ペニスを取り出し/お目当ての物を探り当て")%た%CALLNAME:TARGET%は、飛び出してきたそれ
	ELSE
		PRINTFORM ファスナーを下ろした%CALLNAME:TARGET%は、飛び出してきた%TEXTR("汗ばむ/臭いの強い/先走りを垂らす")%ペニス
	ENDIF
	IF COND("自発フェラ可能")
		PRINTFORM に%TEXTR("キスをする/挨拶する/舌なめずりする/舌を這わせる")%と、
	ELSE
		PRINTFORM を
	ENDIF
	;かなり乗り気
	IF (COND("欲求不満") || TALENT:淫乱 || TALENT:好色) && RAND:3
		;IF COND("自発フェラ可能") && TALENT:オトコ == 0 && ABL:自慰中毒 > RAND:4 && COND("欲求不満") > RAND:3
		;	PRINTFORM %TEXTR("自らのアソコを指で弄りつつ")%
		;ELSE
			PRINTFORM %TEXTR("待ちきれないといった態度で/うっとりした様子で/喉を伝う唾液を気にする風もなく/目を閉じて意識を集中させつつ")%
		;ENDIF
	ELSE
		PRINTFORM %CALLNAME:PLAYER%%TEXTR("の顔を見つめつつ/の様子を伺いつつ/を上目遣いで見あげて楽しませつつ")%、
	ENDIF
	IF TALENT:恋慕 || COND("欲求不満") || TALENT:淫乱 || TALENT:好色
		PRINTFORMW %TEXTR("熱心に/美味しそうに/愛おしそうに/深く咥えて鼻で息継ぎしながら")%しゃぶりだした……
	ELSE
		PRINTFORMW %TEXTR("いつもするように/周囲を気にしながら/恥ずかしそうに")%しゃぶりだした……
	ENDIF

CASE "こっそり手コキしてもらう"
	PRINTFORM %CALLNAME:PLAYER%のペニスへ手を誘導された%CALLNAME:TARGET%は、
	SELECTCASE SAVESTR:デート
	CASE "映画館に行く"
		IF COND("欲求不満") || TALENT:好色 || TALENT:淫乱
			PRINTFORM 映画の内容そっちのけで%CALLNAME:MASTER%の目を見ながら、
		ELSEIF TALENT:恥じらい
			PRINTFORM 頬を赤らめてなるべくスクリーンの方に目を向けながら、
		ELSE
			PRINTFORM %CALLNAME:MASTER%の意図を察して、
		ENDIF
	;いずれどっかでも手コキさせて貰えると良いね（
	CASEELSE
		IF COND("欲求不満") || TALENT:好色 || TALENT:淫乱
			PRINTFORM 待っていましたとばかりに笑顔を見せると、
		ELSEIF TALENT:恥じらい
			PRINTFORM 察して頬を染め、おずおずと
		ELSE
			PRINTFORM %CALLNAME:MASTER%の意図を察して、
		ENDIF
	ENDSELECT
	PRINTFORMW 隆起する%CALLNAME:MASTER%のモノが満足するまで扱き上げた。
CASE "キスする"
	;PRINTFORMW %CALLNAME:TARGET%は楽しそうに微笑みながら、%CALLNAME:MASTER%にキスをし返してきた。
CASE "お尻を触る"
	PRINTFORM %CALLNAME:TARGET%は%TEXTR("前触れの無い悪戯/唐突なセクハラ/突然お尻を撫でまわされた事")%に
	IF TALENT:強気 + TALENT:冷静 + TALENT:感情乏しい && RAND:4
		PRINTFORM %TEXTR("生暖かい笑みを浮かべたが/も大して動じることなく")%、
	ELSEIF TALENT:素直 + TALENT:陽気 + TALENT:内気 && RAND:4
		PRINTFORM %TEXTR("驚いた様子を見せた/一瞬身体を強張らせた/")%が、
	ELSE
		PRINTFORM %TEXTR("頬を紅潮させた/顔を赤くした/変な声を出してしまった")%が、
	ENDIF
	PRINTFORM %TEXTR("強く抗議をするでもなく/特に振り払う事もせず/それほど嫌な素振りも見せずに")%
	IF TFLAG:水着デート
		PRINTFORMW %CALLNAME:MASTER%にされるがままの状態で水着でのデートを楽しんでいるようだ……
	ELSE
		PRINTFORMW %CALLNAME:MASTER%に合わせて歩き続けている……
	ENDIF
;文化祭イベント
CASE "バンド演奏", "演劇", "占いの館", "お化け屋敷", "お悩み相談室", "ぱふぱふ屋", "メイド喫茶", "執事喫茶", "レストラン"
	CALL DAILY_MESSAGE_BUNKASAI, ARGS
ENDSELECT
