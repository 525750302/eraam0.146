;-------------------------------------------------
;日常イベントで特定の行動をした際に、ここで新たな行動が派生する。
;ちなみに、ここに来るまでにSTR:日常イベント名はリセット済み
;主に選択肢を出して色々する事となるだろう
;ちなみにここでの行動はあなたの行動履歴に残す程ではないのでSAVESTR:PREV_日常イベント名 は変化しない
;-------------------------------------------------
@EVENT_DAILY_EX, ARG, ARGS
#DIMS MEMO_SAVESTR
#DIMS MEMO_SAVESTR2

MEMO_SAVESTR = %SAVESTR:PREV_日常イベント名%
MEMO_SAVESTR2 = %SAVESTR:PREV_直前行動%

CALL EVENT_DAILY_EX_MAIN, ARG, ARGS
;EVENT_DAILY_EXでの変化を記録
CALL SET_PREV_DAILYCOM

SAVESTR:PREV_日常イベント名 = %MEMO_SAVESTR%
;直前行動が変化していたら両方記録する
SIF SAVESTR:PREV_直前行動 != MEMO_SAVESTR2
	SAVESTR:PREV_直前行動 = %MEMO_SAVESTR2%\/%SAVESTR:PREV_直前行動%

SELECTCASE CFLAG:PREV_DAILYCATE
CASE 6, 7
CASEELSE
	;ちんこを汚れたままにしておくのはあまり良くないので綺麗にする
	IF PENIS(MASTER) && COND("Ｐ汚れ", MASTER) && COND("Ｐ汚れの相手", MASTER) && FIND_COND("性処理直後") == 0
		IF CFLAG:睡眠 == 0
			DRAWLINE
			PRINTFORMW （休息后冷静下来的%CALLNAME:MASTER%、坐下后把和%CONDS("Ｐ汚れの相手", MASTER)%的Ｈ中弄脏的阴茎擦干净了）
		ENDIF
		CALL RESET_STAIN_EX, "Ｐ", MASTER
	ENDIF
ENDSELECT
;ここで顔グラ変化を一旦リセット
CVARSET CSTR, GETNUM(CSTR, "颜绘変化")
CVARSET CFLAG, GETNUM(CFLAG, "KOJO_DAILY済")

@EVENT_DAILY_EX_MAIN, ARG, ARGS
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT_SUB
#DIM MEMO_TARGET
#DIM CHOICE
#DIM IS_SELECT
;ペニスルートorアソコルート
#DIM IS_PENISROUTE
#DIMS EVENT_TABLE

TFLAG:地の文スキップ = 0
IS_PENISROUTE = 0
MEMO_TARGET = TARGET

;キャラ独自のイベントは、ここに書いても良い
MEMO_LINECOUNT = LINECOUNT
CALL KOJO_DAILYCOM, "特殊事件", "改行没有"
;何か起きていたらここまで
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1


CALL BLANK_DRAWLINE

MEMO_LINECOUNT = LINECOUNT
;現在進行しているイベントの補足となるような物（コマンド区分で分岐）
SELECTCASE ARG
;トーク
CASE 1
	SELECTCASE SAVESTR:PREV_日常イベント名
	;雑談中ならちょっかいかけやすい
	CASE "Talk", "甜言蜜语", "夸奖"
		;レアイベントを上に置く
		IF SAVESTR:PREV_日常イベント名 == "Talk" && COND("ながら胖次穿き替え可能") && RAND:3 == 0
			CALL EVENT_ACADEMY, "ながら胖次穿き替え"
		;週末が近づくとデートのお誘いをしてくれるかも
		ELSEIF COND("约会のお誘い可能") && FLAG:対複数 == 0 && (DAY - 1)%7 - 3 > RAND:10
			CALL EVENT_ACADEMY, "约会のお誘い"
		;試験前は特殊なイベントが起きるかも
		ELSEIF COND("Ｈ的約定可能")
			CALL EVENT_ACADEMY, "Ｈ的約定"
		ENDIF
	ENDSELECT
;もてなす
CASE 2
	SELECTCASE CSTR:PREV_DAILYCOM
	CASE "睡眠姦正常位", "睡眠姦寝后背位"
		CALL EVENT_ACADEMY, "睡眠姦"
	CASE "睡眠姦口交", "睡眠姦逆口交"
		CALL EVENT_ACADEMY, CSTR:PREV_DAILYCOM
	ENDSELECT
;隙を見せる
CASE 3

;パンツチェック
CASE 4

;性処理
CASE 6

CASE 7
	IF NOWEX:絶頂漏尿
		PRINTFORMW （一息ついて落ち着いた%CALLNAME:MASTER%は、%CALLNAME:TARGET%とのPLAYで弄脏的周囲や衣服を率先して綺麗に掃除した）
		;口も漱ぐ
		SIF CHECK_STAIN(STAIN:MASTER:Ｍ, "尿")
			CALL RESET_STAIN_EX, "Ｍ", MASTER
	ENDIF
ENDSELECT

;CFLAG:睡眠 == 1なら眠りから覚ます処理
IF CFLAG:睡眠 == 1
	DRAWLINE
	;汚した？
	IF COND("着衣の乱れ")
		PRINTFORMW ……再不停下来的话就有暴露的风险了。
		PRINTFORMW 在%TEXTR("把衣服整理好/快速清理了污迹/处理好现场情况")%销毁完证据的%CALLNAME:MASTER%的身旁、轻轻摇晃着肩膀的%CALLNAME:TARGET%从睡梦中慢慢醒来。
	ELSEIF EXP_GAIN("睡眠悪戯経験") + EXP_GAIN("睡眠姦経験")
		PRINTFORMW ……再不停下来的话就有暴露的风险了。
		PRINTFORMW 已经充分享受完了的%CALLNAME:MASTER%的身旁、轻轻摇晃着肩膀的%CALLNAME:TARGET%从睡梦中慢慢醒来。
	ELSE
		PRINTFORMW %CALLNAME:MASTER%的身旁、轻轻摇晃着肩膀的%CALLNAME:TARGET%从睡梦中慢慢醒来。
	ENDIF
	CALL KOJO_DAILYCOM, "叫醒"
	CALL SETFLAG_DAILY, "叫醒"
ENDIF

;何か起きていたらここまで
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

;ここより下はパートナー以外も対象のイベント。TARGETが変更された場合にはイベントの記録を終えた後で戻される
;まずはパートナー以外のみ起きるイベント
TARGET = FIND_COND("現在存在", "起床中")
IF TARGET > 0
	CALL CHECK_COM_ABLE, TARGET
	CALL SETFLAG, "性処理でのPLAY内容：計算のみ", TARGET

	MEMO_LINECOUNT_SUB = LINECOUNT
	;全裸終了
	IF COND("全裸状態終了可能")
		CALL KOJO_DAILYCOM, "服を着た", "改行没有"
		SIF LINECOUNT == MEMO_LINECOUNT_SUB
			PRINTFORMW 「…………」
		SIF TFLAG:地の文スキップ == 0
			PRINTFORMW 察觉到%CALLNAME:MASTER%的興趣从自己身上移開的%CALLNAME:TARGET%、把之前脱掉的衣服穿了回去。
		CALL SET_MEMO_DRESS, "脱掉的衣服一览", TARGET
	;酔い→眠気→ベッドイン
	ELSEIF STATE("困倦") && PLACE("自宅") && PLACE("对象的房間") == 0 && RAND:3 == 0
		CALL KOJO_DAILYCOM, "眠い", "改行没有"
		SIF LINECOUNT == MEMO_LINECOUNT_SUB
			PRINTFORMW 「…………」
		SIF TFLAG:地の文スキップ == 0
			PRINTFORMW 喝醉了酒很困的%CALLNAME:TARGET%、慢吞吞地回到了自己的房间……
		CFLAG:睡眠 = 8 + RAND:5 + RAND:5
		CALL REMOVE_STATE, TARGET, "困倦"
		CALL MOVE_PLACE, @"%CSVCALLNAME(NO:TARGET)%的房間", TARGET
	ELSEIF FIND_COND("性処理直後", "性処理：挿入直後") && COND("Ｐ汚れ", MASTER) && COND("干劲性処理") + CFLAG:躁动难忍 + TALENT:气味弱点 + TALENT:汚臭敏感 + COND("発情期") && COND("自発清洁口交可能") && (RAND:3 == 0 || CFLAG:躁动难忍)
		CALL EVENT_ACADEMY, "自発清洁口交"
	ENDIF
ENDIF
;何か起きていたらここまで
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

;パートナー含め起きるイベント
TARGET = 0
TARGET = FIND_COND("現在存在", "起床中")
IF TARGET <= 0
	;イベントが起きないようならDRAWLINEを消して終了
	CLEARLINE 1
	RETURN 0
ENDIF

;パンツイベント中は誰かがいつの間にかトイレに行くことのみ起きる
IF ARG == 4
	;トイレ
	IF BASE:尿意 >= 5000 && CFLAG:行動参加中 == 0
		CALL SETFLAG_DAILY, "廁所"
		PRINTFORMW （%TEXTR(@"在%CALLNAME:MASTER%不注意时/%CALLNAME:MASTER%不知道的时候")%离開的%CALLNAME:TARGET%回来了）
	ENDIF
;汗が引いたら服を着る
ELSEIF TEQUIP:出浴 && ARG != 5
	IF CHECK_CLO("内衣姿") || CHECK_CLO("泳装姿") || CHECK_CLO("裸体")
		IF CHECK_CLO("泳装姿")
			PRINTFORM ……穿着泳装的
		ELSEIF CHECK_CLO("内衣姿")
			PRINTFORM ……只穿着内衣的
		ELSE
			PRINTFORM ……露出了不是很雅观的様子
		ENDIF
		PRINTFORMW %CALLNAME:TARGET%的汗水流了下来。

		IF CFLAG:Cosplay中 == 0
		 	IF PLACE("自宅")
				PRINTFORMW %CALLNAME:TARGET%换上了平常的衣服。
				CALL SET_COORDINATE, "便装"
			ELSE
				PRINTFORMW %CALLNAME:TARGET%换上了制服。
				CALL SET_COORDINATE, "制服"
			ENDIF
		ENDIF
	ENDIF
	TEQUIP:出浴 = 0
;薄着終了
ELSEIF COND("薄着状態終了可能")
	IF CHECK_CLO("泳装姿")
		PRINTFORM ……穿着泳装的
	ELSEIF CHECK_CLO("内衣姿")
		PRINTFORM ……只穿着内衣的
	ELSE
		PRINTFORM ……露出了不是很雅观的様子
	ENDIF
	PRINTFORMW %CALLNAME:TARGET%的汗水流了下来。
	PRINTFORMW %CALLNAME:TARGET%发出衣物摩擦皮肤的声音、穿上了之前脱下的衣服。
	CALL SET_MEMO_DRESS, "脱掉的衣服一览", TARGET
	CFLAG:薄着变成了 = 0

;暑いので扇風機独占して下半身に風を当てちゃう
ELSEIF TARGET != MEMO_TARGET && COND("股間に扇風機可能") && (COND("浑身是汗") || RAND:3 == 0)
	CALL EVENT_ACADEMY, "扇風機独占"
;薄着になる
ELSEIF COND("薄着化可能") && (STATE("酔酒") || RAND:3 == 0)
	CALL EVENT_ACADEMY, "薄着变成了"
;トイレ
ELSEIF BASE:尿意 >= 9000 || (BASE:尿意 >= 5000 && CFLAG:行動参加中 == 0)
	IS_SELECT = 0
	;雑談中と隙を見せている時以外は選択肢を出さない
	SELECTCASE SAVESTR:PREV_日常イベント名
	CASE "Talk"
		IS_SELECT = 1
	CASEELSE
		SELECTCASE ARG
		;隙を見せる
		CASE 3
			IS_SELECT = 1
		ENDSELECT
	ENDSELECT
	SIF CFLAG:行動参加中
		IS_SELECT = 1

	IF IS_SELECT
		CALL KOJO_DAILYCOM, "廁所", "改行没有"
		PRINTFORMW ……看起来%CALLNAME:TARGET%似乎是要去一下洗手间的様子。
		PRINTFORML [0] 安静地等着
		PRINTFORML [1] 等肯定要等、不过回来了之后会问一问去干什么了
		IF BASE:PLAYER:尿意 >= 5000
			IF TALENT:MASTER:男性 && TALENT:男性
				PRINTFORML [2] 正好我也要去
			ELSEIF TALENT:MASTER:男性 == 0 && TALENT:男性 == 0
				PRINTFORML [2] 正好我也一起去
			ELSE
				PRINTFORML [2] 正好我也要去一下(当然是有些别的目的了)
			ENDIF
			CALL INPUT_SELECT, 3
		ELSE
			CALL INPUT_SELECT, 2
		ENDIF
		;PRINTFORML [3] してる最中に鍵を開けて覗く(これはアンロックを覚えてから)

		CHOICE = RESULT
		CALL SETFLAG_DAILY, "廁所"

		SELECTCASE CHOICE
		CASE 0
			PRINTFORMW …过了一会之后、%CALLNAME:TARGET%回来了。
			CALL KOJO_DAILYCOM, "廁所"
		CASE 1
			PRINTFORMW …对着数分後回来了的%CALLNAME:TARGET%、询问了刚才去做什么了。
			CALL KOJO_DAILYCOM, "廁所詮索"
		CASE 2
			CALL SETFLAG_DAILY, "廁所", MASTER
			IF TALENT:MASTER:男性 && TALENT:男性
				PRINTFORMW %CALLNAME:MASTER%追着%CALLNAME:TARGET%过去、并排着解决了问题。
				CALL KOJO_DAILYCOM, "連れション"
			ELSEIF TALENT:MASTER:男性 == 0 && TALENT:男性 == 0 
				PRINTFORMW %CALLNAME:MASTER%追着%CALLNAME:TARGET%过去、在洗手间里进行了井戸端会議。
				CALL KOJO_DAILYCOM, "廁所で井戸端会議"
			ELSE
				PRINTFORMW …在%CALLNAME:MASTER%去完洗手间回来之后、又过了一会%CALLNAME:TARGET%才回来。
				CALL KOJO_DAILYCOM, "廁所"
			ENDIF
		CASE 3
			PRINTFORMW 吱嘎。
			PRINTFORMW %CALLNAME:MASTER%打開了洗手间的门、和正坐在马桶上解小手的%CALLNAME:TARGET%对上了眼睛。
			CALL KOJO_DAILYCOM, "廁所を覗く"
		ENDSELECT
	ELSE
		CALL SETFLAG_DAILY, "廁所"
		PRINTFORMW （%TEXTR(@"在%CALLNAME:MASTER%不注意时/%CALLNAME:MASTER%不知道的时候")%离開的%CALLNAME:TARGET%回来了）
	ENDIF
;以下のイベントはシチュを選ぶ
ELSEIF CFLAG:刚睡醒 || ARG == 6

;その他
ELSE
	;トイレハプニングイベント
	CALL EVENT_TOILET_HAPPENING
ENDIF

;何か起きていたらここまで
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

SELECTCASE ARG
CASE 1 TO 3
	;ふいにパンツが見えたりするイベント総合
	CALL EVENT_ACADEMY, "チラ見え"
ENDSELECT

;何か起きていたらここまで
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

TARGET = MEMO_TARGET
;パートナー以外の人が自撮りを送ってくるイベント
CALL SEND_SELFY_DAILY_EX

;何か起きていたらここまで
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

;33%で何も起きない
IF RAND:3 == 0
	CLEARLINE 1
	RETURN 0
ENDIF

;暇してる人たちだけで遊び始める
CALL EVENT_ACADEMY, "レ阴蒂エーション開始"

;何か起きていたらここまで
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

;ここより下はパートナー以外の、まだ相手していないキャラが対象のイベント。TARGETが変更された場合にはイベントの記録を終えた後で戻される
TARGET = MEMO_TARGET
TARGET = FIND_COND("暇を持て余すイベント可能")
;相手していない人が居ないなら、複数行動でないなら全員の中から選ぶ
IF TARGET <= 0 && FLAG:対複数 == 0
	TARGET = MEMO_TARGET
	TARGET = FIND_COND("現在存在", "起床中")
ENDIF
;それでも候補がいない場合はDRAWLINEを消して終了
IF TARGET <= 0
	CLEARLINE 1
	RETURN 0
ENDIF

;本来のパートナーとやってた事で大まかに分岐
SELECTCASE SAVESTR:PREV_日常イベント名
;雑談中や性処理中ならちょっかいかけやすい
CASE "Talk", "甜言蜜语", "夸奖", "拜托処理性欲", "进行性処理"
	;CALL PRINT_MAIDFACE, TARGET
	SELECTCASE SAVESTR:PREV_日常イベント名
	CASE "Talk"
		PRINTFORM 和%CALLNAME:MEMO_TARGET%%TEXTR("雑談闲聊/说些无関紧要的话/滔滔不绝")%的时候、
	CASE "甜言蜜语", "夸奖"
		PRINTFORM 和%CALLNAME:MEMO_TARGET%说话的時候、
	CASE "拜托処理性欲"
		PRINTFORM 和%CALLNAME:MEMO_TARGET%一起做事後処理的时候、
	CASE "进行性処理"
		PRINTFORM %CALLNAME:MEMO_TARGET%一个人処理善后工作的时候、
	ENDSELECT
	IF COND("無自覚腋チラ可能") && RAND:3 == 0
		CALL EVENT_ACADEMY, "無自覚腋チラ"
	ELSEIF BASE:欲求不満 >= 50 && TALENT:自制心 == 0 && (COND("発情期") || ABL:自慰中毒 >= 5) && RAND:3
		PRINTFORMW 眼睛的余光看到了%CALLNAME:TARGET%正一边%TEXTR("看着雑誌/玩着手机/看着漫画/想着什么")%一边%TEXTR("玩弄着自己的股間/用桌角摩擦着股間/用椅子的扶手摩擦着股間")%。
		IF RAND:3
			PRINTFORMW 看起来好像是、%TEXTR("身子自己擅自動了起来/無意識地開始自慰行為/并没有注意到自己在干什么")%的様子……好好陪一陪她释放一下比较好吧。
		ELSE
			PRINTFORMW ……注意到了%CALLNAME:MASTER%的視線看向自己股間的%CALLNAME:TARGET%、%TEXTR("一瞬间脑袋上冒出了『？』/眨了眨眼/不解地歪了歪头")%、之後便猛然察觉了自己正在干什么、%TEXTR("身体瞬间僵硬了/脸一下子变得通红/慌忙地停止了動作")%！
			STR:日常イベント名 = オナバレ
		ENDIF
		EXP:自慰経験 += 1
		PRINTFORML 
		PRINTFORMW 自慰経験＋１(%CALLNAME:TARGET%)
	ELSEIF CFLAG:MASTER:茶 == 0 && (TALENT:献身 || ABL:奉仕精神 >= 5) && RAND:3 == 0
		SAVESTR:茶 = %TEXTR(CSTR:茶)%
		SIF SAVESTR:茶 == ""
			SAVESTR:茶 = 茶

		IF TALENT:献身
			PRINTFORMW %CALLNAME:TARGET%端着%SAVESTR:茶%和点心走了过来。
		ELSE
			PRINTFORMW %CALLNAME:TARGET%准备了%SAVESTR:茶%。
		ENDIF
		STR:日常イベント名 = 沏茶
		CALL KOJO_DAILYCOM, "卖个破绽"
		CALL SETFLAG_DAILY, "茶"
	ELSEIF (TALENT:開朗 || TALENT:粗野) && RAND:3 == 0
		PRINTFORMW %CALLNAME:TARGET%%TEXTR("把手搭在了你的肩膀上/从後面抱了上来")%。
		CALL KOJO_DAILYCOM, "構って欲しい！", "改行没有"
		SIF TFLAG:地の文スキップ == 0
			PRINTFORMW ……看起来好像是、%TEXTR(@"无聊得受不了了/闲得发慌/想让%CALLNAME:MASTER%来陪")%的様子。
	ELSEIF EQUIP:服 && COND("易出汗") && RAND:3 == 0
		PRINTFORMW %CALLNAME:TARGET%把领口\@ CHECK_SKIRT("裙子") ? 和裙子 # \@的扣子打開、露出了正在冷却汗液的身体。
		PRINTFORMW 不知不觉间、%TEXTR(@"被%CALLNAME:TARGET%新鲜汗水的气味吸引了注意/对%CALLNAME:TARGET%新鲜汗水的气味莫名的在意/闻到%CALLNAME:TARGET%新鲜汗水的气味兴奋了起来")%……
	ELSE
		IF CFLAG:MASTER:茶 == 0 && ABL:奉仕精神 && RAND:2
			PRINTFORMW 和%TEXTR("斜望着这边/瞥看着这边/若无其事地看着这边")%的%CALLNAME:TARGET%对上了视线。
			PRINTFORMW ……稍微%TEXTR("有点害臊地/不好意思地/有些尴尬地")%微笑着的%CALLNAME:TARGET%、像是遮羞一样从椅子上站了起来给大家倒茶。
			STR:日常イベント名 = 沏茶
			CALL SETFLAG_DAILY, "茶を用意"
			CALL KOJO_DAILYCOM, "卖个破绽"
		ELSE
			IF RAND:2
				;道端のメロンパンさん加筆
				EVENT_TABLE = %TEXTR("悠闲看着動画的/一副清闲的様子読着雑誌的/呆呆地望着外面的")%/
				SIF STRCOUNTS(CSTR:学年,  "司書")
					EVENT_TABLE += "正在读预订进货书籍目录的/"
				SIF COND("学生") || COND("教員")
					EVENT_TABLE += "正在预习明天的课程的/"
				SIF COND("種族：魔法使")
					EVENT_TABLE += "一个人開始塔罗牌占卜的/"
				SIF TALENT:尾巴 && COND("種族：猫")
					EVENT_TABLE += "開始梳理尾巴上的毛的/"
				SIF SAVESTR:茶 != ""
					EVENT_TABLE += @"默默喝着%SAVESTR:茶%的/"
		        PRINTFORMW %TEXTR(EVENT_TABLE)%%CALLNAME:TARGET%进入了视线。
			ELSE
				PRINTFORMW 注意到%CALLNAME:TARGET%%TEXTR("正在斜眼看着我/往这边瞥了一眼/正在偷瞄这边")%。
			ENDIF
			IF TALENT:恋慕 && RAND:2
				PRINTFORMW ……%TEXTR("肯能是因为心情原因/总觉得")%坐的很近。
				PRINTFORMW 感觉%TEXTR(@"刚才/和%CALLNAME:MEMO_TARGET%開始聊天之前")%座位距离比起现在要更远一些。
			ELSEIF COND("同居可能") || ABL:信頼 >= 3
				PRINTFORMW %CALLNAME:TARGET%%TEXTR("不自觉地探出了身子/后背离開椅背稍微向前倾斜着")%……说着混在一起的话。
			ELSE
				PRINTFORMW ……虽然没说出口但似乎很想和%CALLNAME:MASTER%交往试试。
			ENDIF
		ENDIF
	ENDIF

;性器観察系
CASE "检查阴部"
	IF COND("合意：检查阴部", MEMO_TARGET) && (COND("性処理：挿入直後") || CHOSE_DEVENT("检查阴部", MEMO_TARGET) == 1)
		PRINTFORM %CALLNAME:MEMO_TARGET%的
		IF TALENT:MEMO_TARGET:男性 || (TALENT:MEMO_TARGET:扶她 && RAND:2)
			PRINTFORM %TEXTR("阴茎/股間")%被欣赏着的同时、
			IS_PENISROUTE = 1
		ELSE
			PRINTFORM %TEXTR("阴部/股間/秘密場所")%被欣赏着的同时、
		ENDIF

		;アソコの状態比較
		LOCALS = 
		IF IS_PENISROUTE
			IF COND("真性包茎", MEMO_TARGET) && RAND:2
				LOCALS = %TEXTR("完全被包皮覆盖着的/一点都没露出亀頭的/真性包茎的")%
			ELSEIF COND("假性包茎", MEMO_TARGET) && RAND:2
				LOCALS = %TEXTR("能看到粉紅亀頭的/大部分被皮包着的/假性包茎的")%
			ELSE
				LOCALS = %TEXTS("阴茎：視覚", MEMO_TARGET)%
			ENDIF
			IF LOCALS == "" || RAND:2
				IF TALENT:MEMO_TARGET:陰蒂環 && TALENT:陰蒂環 + TALENT:陰唇環 == 0
					LOCALS = %TEXTR("將被反射著光芒的穿環穿刺著的/被穿環装飾的")%
				ENDIF
			ENDIF
		ELSE
			;中出し直後
			IF CHOSE_DEVENT("性処理Ｖ中出し", MEMO_TARGET)
				LOCALS = %TEXTR("中出直後的/精液混合愛液滴下的/精液垂落的")%
			ELSEIF COND("陰毛") >= 5
				SELECTCASE CONDS("陰毛", MEMO_TARGET)
				CASE "白虎", "無毛"
					LOCALS = %TEXTR("连长了毛的痕迹都没有的/光洁的/一点毛都没有的")%
				CASE "脱毛処理"
					LOCALS = 被漂亮脱毛处理过的
				CASE "光滑无毛"
					LOCALS = 光滑地被刮干净的
				ENDSELECT
			ENDIF
			IF LOCALS == "" || RAND:2
				IF TALENT:MEMO_TARGET:陰蒂環 + TALENT:MEMO_TARGET:陰唇環 && TALENT:陰蒂環 + TALENT:陰唇環 == 0
					LOCALS = %TEXTR("將被反射著光芒的穿環穿刺著的/被穿環装飾的")%
				ENDIF
			ENDIF
		ENDIF

		IF (IS_PENISROUTE == 0 && TALENT:男性) || (IS_PENISROUTE && TALENT:男性 == 0) || TALENT:好奇心 || ABL:欲望 >= 5 || (ABL:欲望 >= 3 && TALENT:開朗) || STRCOUNT(LOCALS, "穿環")
			PRINTFORMW %CALLNAME:TARGET%%TEXTR("探出身子/好奇地/用热腾腾的眼神/饶有兴趣地")%\@ (TALENT:害羞 == 0 || TALENT:冷静) ? # 一边脸红一边 \@%TEXTR("看着这边的情况/偷偷看向这边/凝视着这边")%。
			IF (ABL:欲望 < 3 && TALENT:好奇心 == 0) || RAND:2
				PRINTFORM ……看来、虽然认为%TEXTR("这很不好/这是不对的/不应该看")%但还是在
				IF IS_PENISROUTE == 0 && (COND("教員", MEMO_TARGET) || AGE(MEMO_TARGET) == 4) && AGE(TARGET) < 4 && RAND:2
					PRINTFORMW 目不转睛地盯着%LOCALS%成熟的阴部。
				ELSEIF IS_PENISROUTE == 0 && (RAND:3 == 0 || LOCALS == "")
					PRINTFORMW 目不转睛地盯着%CALLNAME:MEMO_TARGET%的%TEXTS("割れ目", MEMO_TARGET)%。
				ELSE
					PRINTFORM %CALLNAME:MEMO_TARGET%的%LOCALS%
					PRINTFORMW \@ IS_PENISROUTE ? %TEXTS("阴茎：呼び方",MEMO_TARGET)% # 阴部 \@上无法移開目光。
				ENDIF
			ELSE
				PRINTFORM ……似乎对%CALLNAME:MEMO_TARGET%的\@ IS_PENISROUTE ? %TEXTS("阴茎：呼び方", MEMO_TARGET)% # 阴部 \@
				IF (IS_PENISROUTE && PENIS(TARGET) ) || (IS_PENISROUTE == 0 && TALENT:男性 == 0)
					PRINTFORMW %TEXTR("的形状之类很在意/无法移開目光/在脑中和自己的东西来对比")%的样子。
				ELSE
					PRINTFORMW %TEXTR("的形状之类很在意/无法移開目光")%的样子。
				ENDIF
			ENDIF
			IF (TALENT:MEMO_TARGET:小悪魔 || TALENT:MEMO_TARGET:好色) && ( (ABL:欲望 < 3 && TALENT:好奇心 == 0) || (IS_PENISROUTE == 0 && TALENT:男性) || RAND:3 == 0)
				PRINTFORMW 注意到了的%CALLNAME:MEMO_TARGET%、为了能让%CALLNAME:TARGET%看得更清楚%TEXTR("嬉笑着/流露着優越感地/忍不住笑")%更加%TEXTR("打開双腿/伸出腰部")%地展示出来……
				CALL CHANGE_RELATION, 1, TARGET, MEMO_TARGET
				PRINTFORMW 二人的相性度+1
			ENDIF
		ELSE
			PRINTFORMW 注意到了的%CALLNAME:TARGET%\@ (TALENT:害羞 == 0 || TALENT:冷静) ? # 红着脸 \@%TEXTR("看着这边的情况/看向这边")%。
			PRINTFORMW ……虽然没有说出口、%CALLNAME:TARGET%的%TEXTR("眼神/气势/様子")%像是在高声主张着『注意場所』之类的样子。
		ENDIF
	ENDIF
;股間観察系
CASE "检查布鲁马", "股間観察"

;パンツ観察系
CASE "检查内褲", "開放型開洞", "高開衩", "Ｔ字褲", "Ｃ胖次"

;スカート系
CASE "掀起裙子", "撩起裙子"

ENDSELECT

IF LINECOUNT == MEMO_LINECOUNT
	;イベントが起きないようならDRAWLINEを消して終了
	CLEARLINE 1
ELSE
	CFLAG:暇を持て余す = 1
ENDIF

