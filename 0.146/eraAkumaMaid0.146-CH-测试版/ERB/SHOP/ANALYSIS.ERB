@SHOW_CHARA_ANALYSIS
#DIM MEMO_TARGET
CALL SHOW_CHARA_LIST_PRE, "アナライズ"
CALL SHOW_CHARA_LIST, "アナライズ"

$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT < MASTER || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

MEMO_TARGET = TARGET
TARGET = RESULT

CALL ANALYSIS

TARGET = MEMO_TARGET

RESTART


@ANALYSIS
#DIM LCOUNT
#DIM MEMO_SOURCE, 30
#DIM MEMO_CUP, 30
#DIM MEMO_PALAM, 30
#DIM MEMO_MOOD

MEMO_MOOD = FLAG:ムード
FOR LCOUNT, 0, 30
	MEMO_SOURCE:LCOUNT = SOURCE:LCOUNT
	MEMO_CUP:LCOUNT = CUP:LCOUNT
	MEMO_PALAM:LCOUNT = PALAM:LCOUNT
NEXT

CALL ANALYSIS_CALC
PRINTFORML 〜%CALLNAME:TARGET%の識別結果〜

;-------------------------------------------------
;結果表示
;-------------------------------------------------
CALL SHOW_UP

TCVAR:基本実行値 = 0
CALL COM_ORDER, 1

LOCAL = RESULT

PRINTFORML 調教実行値補正:\@ LOCAL ? {LOCAL} # なし \@

CALL ANALYSIS_CALC_SB

FLAG:ムード = MEMO_MOOD
FOR LCOUNT, 0, 30
	SOURCE:LCOUNT = MEMO_SOURCE:LCOUNT
	CUP:LCOUNT = MEMO_CUP:LCOUNT
	PALAM:LCOUNT = MEMO_PALAM:LCOUNT
NEXT

WAIT

;パワーバランス
@ANALYSIS_CALC_SB, ARG
#DIM LCOUNT
#DIM MEMO_SELECTCOM
#DIM MEMO_BASE

VARSET LOCAL
MEMO_BASE = BASE:PLAYER:絶頂
MEMO_SELECTCOM = SELECTCOM

;ハダカにするので、衣装を記録
FOR LCOUNT, 40, 50
	LOCAL:LCOUNT = EQUIP:LCOUNT
	LOCALS:LCOUNT = %CSTR:LCOUNT%
NEXT

CALL SETFLAG, "ハダカにする", TARGET

CALL ANALYSIS_CALC_SB_MAIN, ARG

BASE:PLAYER:絶頂 = MEMO_BASE
SELECTCOM = MEMO_SELECTCOM
;服装を戻す
FOR LCOUNT, 40, 50
	EQUIP:LCOUNT = LOCAL:LCOUNT
	CSTR:LCOUNT = %LOCALS:LCOUNT%
NEXT

@ANALYSIS_CALC_SB_MAIN, ARG
#DIM LCOUNT
#DIM VAL_ATK
#DIM VAL_DEF
#DIM VAL_POW
;PALAMとSOURCEとCUPとTEQUIPとFLAG:ムードを記録
#DIM MEMO_SOURCE, 30
#DIM MEMO_CUP, 30
#DIM MEMO_PALAM, 30
#DIM MEMO_TEQUIP, 100
#DIM MEMO_MOOD

MEMO_MOOD = FLAG:ムード
FOR LCOUNT, 0, 30
	MEMO_SOURCE:LCOUNT = SOURCE:LCOUNT
	MEMO_CUP:LCOUNT = CUP:LCOUNT
	MEMO_PALAM:LCOUNT = PALAM:LCOUNT
NEXT
FOR LCOUNT, 0, 100
	MEMO_TEQUIP:LCOUNT = TEQUIP:LCOUNT
NEXT

VARSET VAL_ATK
VARSET VAL_DEF
VARSET VAL_POW
;愛撫から奉仕までを見る
FOR LCOUNT, 0, 300
	;ＣＶＡＢＭ快感系などのコマンド抜粋
	SELECTCASE TRAINNAME:LCOUNT
	CASE "クンニ", "アナル愛撫", "フェラする", "胸愛撫", "キスする", "アソコ愛撫", "尿道指挿入れ"
	CASE "正常位", "アナル後背位", "貝合わせ", "ニプルファック", "尿道正常位"
	CASE "手淫", "フェラチオ", "パイズリ", "足コキ", "アナル奉仕", "奉仕クンニ"
	CASE "正常位奉仕", "Ａ後背位奉仕"
	CASEELSE
		CONTINUE
	ENDSELECT

	SELECTCOM = LCOUNT

	TRYCALLFORM COM_ABLE{SELECTCOM}
	SIF RESULT == 0
		CONTINUE

	VARSET PALAM
	VARSET SOURCE
	VARSET CUP
	VARSET TEQUIP

	BASE:PLAYER:絶頂 = 0
	;条件はかなり良いもので試算
	PALAM:Ｖ潤 = 30000
	PALAM:Ａ潤 = 30000
	PALAM:恭順 = 30000
	PALAM:欲情 = 30000
	FLAG:ムード = 160

	TRYCALLFORM SOURCE_COM{SELECTCOM}, 100
	CALL SAMEN_CHECK_PLAYER

	CALL CALC_SOURCE

	;VAL_DEFが感度の100以下切捨て、VAL_ATKが攻撃力の100以下切捨て
	VAL_DEF = MAX(VAL_DEF, CUP:快Ｃ, CUP:快Ｖ, CUP:快Ａ, CUP:快Ｂ, CUP:快Ｍ) / 100 * 100
	VAL_ATK = MAX(VAL_ATK, BASE:PLAYER:絶頂) / 100 * 100
NEXT

VARSET TFLAG
VARSET PALAM

IF ARG
	;敏感度
	SELECTCASE VAL_DEF
	;CASE IS >= 120000
	;	SETCOLOR DEF_COLOR("ショッキングピンク")
	CASE IS >= 80000
		SETCOLOR DEF_COLOR("ピンク")
	CASE IS >= 40000
		SETCOLOR DEF_COLOR("ハートピンク")
	CASE IS >= 20000
		SETCOLOR DEF_COLOR("黄色")
	ENDSELECT
	PRINTFORM {VAL_DEF, 8} 
	RESETCOLOR

	;攻撃力
	SELECTCASE VAL_ATK
	;CASE IS >= MAXBASE:MASTER:絶頂 * 4
	;	SETCOLOR DEF_COLOR("ショッキングピンク")
	CASE IS >= MAXBASE:MASTER:絶頂 * 3
		SETCOLOR DEF_COLOR("ピンク")
	CASE IS >= MAXBASE:MASTER:絶頂 * 2
		SETCOLOR DEF_COLOR("ハートピンク")
	CASE IS >= MAXBASE:MASTER:絶頂
		SETCOLOR DEF_COLOR("黄色")
	ENDSELECT
	PRINTFORM {VAL_ATK, 8} 
	RESETCOLOR
ELSE
	PRINTFORML 敏感度:{VAL_DEF, 7}
	PRINTFORML 攻撃力:{VAL_ATK, 7}
ENDIF

;VAL_POWは優位度。※優位度は、MASTERとTARGETの技量比。50未満ならばMASTERが優勢
;お互いが弱すぎる時
IF VAL_DEF < 1000 && VAL_ATK < 1000
	VAL_POW = 50
ELSE
	VAL_POW = 100 - (VAL_DEF * 100 / (VAL_DEF + VAL_ATK * 10000 / MAXBASE:PLAYER:絶頂) )
ENDIF

SIF ARG == 0
	PRINTFORM 手強さ:
PRINTFORM {VAL_POW, 3}/100 … 
SELECTCASE VAL_POW
CASE IS <= 20
	SETCOLOR DEF_COLOR("ハートピンク")
	PRINTFORM 楽勝
CASE IS <= 40
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORM 優勢
CASE IS <= 60
	PRINTFORM 互角
CASE IS <= 80
	SETCOLOR DEF_COLOR("水色")
	PRINTFORM 劣勢
CASEELSE
	SETCOLOR DEF_COLOR("青")
	PRINTFORM 無理
ENDSELECT
RESETCOLOR
PRINTL 

FLAG:ムード = MEMO_MOOD
FOR LCOUNT, 0, 30
	SOURCE:LCOUNT = MEMO_SOURCE:LCOUNT
	CUP:LCOUNT = MEMO_CUP:LCOUNT
	PALAM:LCOUNT = MEMO_PALAM:LCOUNT
NEXT
FOR LCOUNT, 0, 100
	TEQUIP:LCOUNT = MEMO_TEQUIP:LCOUNT
NEXT


@ANALYSIS_CALC
#DIM LCOUNT, 2
#DIM MEMO_TARGET
MEMO_TARGET = TARGET

;雛形キャラで数値の基本値算出
ADDCHARA 99
TARGET = CHARANUM - 1
BASE:精力 = 10
ABL:Ｃ感覚 = 1
ABL:Ｖ感覚 = 1
ABL:Ａ感覚 = 1
ABL:Ｂ感覚 = 1
ABL:Ｍ感覚 = 1
TALENT:処女 = 0
TALENT:オンナ = 1

VARSET LOCAL

CALL ANALYSIS_CALC_MAIN, "技巧補正有り"
FOR LCOUNT, 0, 30
	LOCAL:LCOUNT = MAX(CUP:LCOUNT, 1)
	;PRINTFORML %PALAMNAME:LCOUNT% {LOCAL:LCOUNT}
NEXT
;表示されていたキャラを白紙に戻す
DELCHARA CHARANUM-1

TARGET = MEMO_TARGET
CALL ANALYSIS_CALC_MAIN, "技巧補正有り"
;-------------------------------------------------
;結果の計算
;-------------------------------------------------
FOR LCOUNT, 0, 30
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	;PRINTFORM %PALAMNAME:LCOUNT% {CUP:LCOUNT}
	CUP:LCOUNT = CUP:LCOUNT * 1000 / LOCAL:LCOUNT
	;PRINTFORML →{CUP:LCOUNT}
NEXT


@ANALYSIS_CALC_MAIN, ARGS
#DIM MEMO_PLAYER_ABL
SELECTCOM = -1
PREVCOM = -1

;ARGS == "技巧補正有り"のときのみ機能
MEMO_PLAYER_ABL = ABL:PLAYER:技巧

SELECTCASE ARGS
CASE "技巧補正有り"
	ABL:PLAYER:技巧 = ABL:技巧
ENDSELECT

FLAG:ムード = 160
VARSET PALAM
CALL SETFLAG, "調教外潤滑追加", 30000
PALAM:恭順 = 30000
PALAM:欲情 = 30000
VARSET SOURCE, 1000
;ＣＶＡＢＭ感覚等はSOURCEの基本値が多いという仕組みなので、ここで代入しておく
SOURCE:Ｃ快楽 = CALCF("Ｃ刺激", 0)
SOURCE:Ｖ快楽 = CALCF("Ｖ刺激", 0)
SOURCE:Ａ快楽 = CALCF("Ａ刺激", 0)
SOURCE:Ｂ快楽 = CALCF("Ｂ刺激", 0)
SOURCE:Ｍ快楽 = CALCF("Ｍ刺激", 0)
VARSET CUP

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
CALL CALC_SOURCE

FLAG:ムード = 0
VARSET PALAM

ABL:PLAYER:技巧 = MEMO_PLAYER_ABL



@SHOW_UP
#DIM LCOUNT

;獲得PALAMの倍率表示
FOR LCOUNT, 0, 30
	SIF PALAMNAME:LCOUNT == ""
		CONTINUE
	SIF CUP:LCOUNT / 100 == 10
		CONTINUE

	SELECTCASE LCOUNT
	CASE 10 TO 19
		SELECTCASE CUP:LCOUNT/1000
		CASE IS >= 50
			SETCOLOR DEF_COLOR("ショッキングピンク")
		CASE IS >= 20
			SETCOLOR DEF_COLOR("ピンク")
		CASE IS >= 10
			SETCOLOR DEF_COLOR("ハートピンク")
		CASE IS >= 5
			SETCOLOR DEF_COLOR("黄色")
		CASE 0
			SETCOLOR DEF_COLOR("青")
		ENDSELECT
	CASE 0 TO 4, IS >= 20
		SELECTCASE CUP:LCOUNT/1000
		CASE IS >= 6
			SETCOLOR DEF_COLOR("ピンク")
		CASE IS >= 4
			SETCOLOR DEF_COLOR("ハートピンク")
		CASE IS >= 2
			SETCOLOR DEF_COLOR("黄色")
		CASE 0
			SETCOLOR DEF_COLOR("青")
		ENDSELECT
	CASEELSE
		SELECTCASE CUP:LCOUNT/1000
		CASE IS >= 2
			SETCOLOR DEF_COLOR("青")
		CASE 0
			SETCOLOR DEF_COLOR("ハートピンク")
		ENDSELECT
	ENDSELECT

	PRINTFORML %PALAMNAME:LCOUNT%…%TEXTS("0.001", CUP:LCOUNT)%倍

	RESETCOLOR
NEXT
