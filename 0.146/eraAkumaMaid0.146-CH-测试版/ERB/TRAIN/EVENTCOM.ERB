@RESET_TFLAGS
;NEXTCOMを使用している時には改行を入れる
;なお、NEXTCOMの時には@COM_ABLEを通らないので、実行可能かどうかを判定してから設定すること。
SIF NEXTCOM >= 0
	CALL BLANKLINE
;次の調教は空白とする
NEXTCOM = -1
;不要な文字列を初期化
VARSET STR, "", 100, 110
;助手の選んだコマンドリセット
SELECTCOM:1 = -1
SELECTCOM:2 = -1
SELECTCOM:3 = -1

;派生コマンドなどで変更される前のSELECTCOMを保存
SELECTCOM:4 = SELECTCOM

;TFLAG:0～TFLAG:29はコマンドごとにリセット
VARSET TFLAG, 0, 0, 30

TFLAG:地の文スキップ = 0

;NOWEX、TCVAR:0～TCVAR:29は毎ターン初期化＆ペニバンはＰ汚れなし
CALL SETFLAG, "ターン開始", TARGET
CALL SETFLAG, "ターン開始", MASTER
SIF ASSI
	CALL SETFLAG, "ターン開始", ASSI


@EVENTCOM
#PRI
REDRAW 1
CALL RESET_TFLAGS

;コマンド選択時のTARGETを記録
CALL SET_PARTNER

SIF FLAG:調教開始時の対象 != TARGET
	FLAG:助手を調教した = 1

CALL SETFLAG, "助手相手の恋慕外し開始"

;以下コマンドジャンプなど
;酔っ払い
IF PREVCOM != 507 && (TEQUIP:推倒 || (SELECTCOM >= 90 && SELECTCOM <= 119) ) && STATE("酔酒", TARGET) && RAND:3 == 0
	SELECTCOM = 507
;ELSEIF TEQUIP:押し倒し == 0 && STATE("酔い", PLAYER) && RAND:3 == 0
;	SELECTCOM = 506
;以下はSELECTCOM依存
ELSE
	SELECTCASE SELECTCOM
	;胸愛撫
	CASE 5
		;ぱふぱふ
		SIF TEQUIP:推倒
			SELECTCOM = 104
	;セックス系
	CASE 60 TO 79, 83 TO 88
		;ひょっとしたら挿入れる前に暴発しちゃうかも
		IF CHECK_COM("挿入前暴発")
			CALL SETFLAG, "挿入前暴発フラグ", PLAYER
			SELECTCOM = 511
		ENDIF

		;うっかり子宮挿入フラグ。挿入子宮口責めを連続実行＆Ｖインサート中で無いと発生しない
		IF CHECK_COM("うっかり子宮挿入") && (RAND:5 == 0 || MARK:淫紋 >= 4 || EX:Ｖ絶頂 >= 5)
			CALL SETFLAG, "うっかり子宮挿入フラグ", TARGET
			SELECTCOM = 68
		ENDIF

		SELECTCASE SELECTCOM
		CASE 68
		CASEELSE
			;騎乗位連続＆腰使い＆強気などでワイルドスウィング(連続ではしてこない)
			CALL SETFLAG, "狂野摇摆", TARGET
			;前のプレイがセックス系かつ調教者の技巧3以上の場合、挿入Ｇスポ責めまたは挿入子宮口責めへ派生
			;Ｖ感覚が高く欲情しているほど子宮口へ行きやすい
			SIF CHECK_COM("两根挿入Ｖ中") == 0 && CHECK_COM("二穴挿入") == 0
				SELECTCOM = CHECK_COM("刺激挿入部位", SELECTCOM)
		ENDSELECT
	;スパンキング
	CASE 130
		;SIF TEQUIP:女王様プレイ
		;	SELECTCOM = 509
	CASE 141 TO 143
		;二本フィストフラグ解除
		SIF SELECTCOM != PREVCOM
			CLEARBIT TEQUIP:PLAYER:特殊行動, 1
	;逆剃毛
	CASE 145
		SIF TEQUIP:推倒
			SELECTCOM = 504
	ENDSELECT
ENDIF


;助手参加(もがく、なすがままの時にはそれぞれのCOMF中で決定)
IF TEQUIP:Ｗ推倒
	IF SELECTCOM == 280 || SELECTCOM == 281
		SELECTCOM:1 = PREVCOM:1
	ELSE
		CALL SET_SELECTCOM_MULTI, "Ｗ推倒"
	ENDIF
ELSE
	;シックスナイン判定
	CALL SET_PLAY69

	;３Ｐによる協力コマンド管理
	SIF TARGET:1 && STATE("撃沈", TARGET:1) == 0 && (PREVCOM:1 >= 0 || PREVCOM:2 >= 0)
		CALL SET_SELECTCOM_MULTI, "３Ｐ"
;意地悪い人は思ったとおりに動いてくれない、とかどうだろう
;ELSEIF TEQUIP:押し倒し == 0 && TALENT:意地悪 && ABL:信頼 + ABL:奉仕精神 <= 5
;	IF SELECTCOM == 91 && NO:TARGET == 12
;		CALL COM_ABLE95
;		IF RESULT == 1
;			SELECTCOM = 95
;			PRINTFORMW 「アタシに口でして欲しいですって？　ふんっ、あんたなんか足で十分でしょ」
;		ENDIF
;	ENDIF
ENDIF

;PLAYERが交代しているならば参加していない人のインサートなど解除
IF SELECTCOM:2 == -1
	CALL RESET_INSERT, PLAYER:1
	;調教者のものを咥えているので無いなら解除
	SIF TEQUIP:深喉 != PLAYER
		TEQUIP:深喉 = 0
ENDIF

;キス継続…ここではフラグOFFのみ
;フラグONはCHECK_EX_EQUIPで行う
;インサート中はモード継続してみる
IF INSERT("挿入") == 0
	SELECTCASE SELECTCOM
	;キス、挿入局所責めなどは値を変更させない。アイテムとアルカナも変更しないことに
	CASE 6, 65 TO 68, 230 TO 279, 282, 301, 311, TNUM("甜言蜜语"), TNUM("夸奖"), TNUM("抱上去")
	;同じ調教者で連続選択時のみキス継続
	CASE 0, 2, 5, 7, 8, 10, 15, 16, 60 TO 79, 83 TO 88, 90, 93, 94, 97, 105, 120 TO 129, 130, 176, 280, 281, 283
		;調教者が変わるとダメ
		SIF COND("前Ｔ調教者") != PLAYER
			TEQUIP:接吻 = 0

		;前のターンのコマンド次第でダメ
		SELECTCASE PREVCOM
		CASE TNUM("接吻")
		;連続実行ならOK
		CASE SELECTCOM
		CASEELSE
			SELECTCASE SELECTCOM
			;インサート中はモード継続してみる
			CASE 60 TO 79
			CASEELSE
				SIF PREVPLAY("挿入") == 0
					TEQUIP:接吻 = 0
			ENDSELECT
		ENDSELECT
	CASEELSE
		TEQUIP:接吻 = 0
	ENDSELECT
ENDIF

;占有不可な場合
IF TEQUIP:Ｗ推倒 || COND("３Ｐ")
	TEQUIP:接吻 = 0
	TEQUIP:ASSI:接吻 = 0
ENDIF


;自慰するのフラグ消滅判定
;特定コマンドの次のターン、ないし特定の状況化じゃないとダメということにしておく
SELECTCASE SELECTCOM
;クンニ, 自慰, フェラする, キスする, アナル舐め, くぱぁさせる, Ａくぱぁさせる, アナル奉仕, ぱふぱふ
CASE 1, 3, 4, 6, 9, 13, 14, 96, 104, 282

CASEELSE
	IF TEQUIP:面部騎乗
	ELSE
		TEQUIP:PLAYER:自慰 = 0
	ENDIF
ENDSELECT

;胸愛撫のフラグ消滅判定
SIF SELECTCOM != 5
	TEQUIP:胸愛撫 = 0
;アソコ愛撫のフラグ消滅判定
SIF SELECTCOM != 7
	TEQUIP:阴部愛撫 = 0
;足コキのフラグ消滅判定
SIF SELECTCOM != 95
	TEQUIP:足交 = 0

;調教コマンド実行前のパラメータの記録
CALL SAVE_PALAM_BEFORECOM

;-------------------------------------------------
;嗜虐or奉仕快楽経験発生フラグ
;-------------------------------------------------
CALL SETFLAG, "快楽経験判定", SELECTCOM


@EVENTCOMEND
#LATER
#DIM LCOUNT

;-------------------------------------------------
;効果終了のお知らせなど、ターンの最後に出力するテキスト
;-------------------------------------------------
IF TEQUIP:甜言蜜语 == 1
	TEQUIP:甜言蜜语 = 0
	CALL BLANKLINE
	PRINTL 高涨的情緒回到了原状…
ENDIF
;ビデオ撮影中
SIF TEQUIP:攝像機
	CALL EQUIP_COM170_EVENTCOMEND
;女王様プレイ中
;IF TEQUIP:女王様プレイ
;	IF TEQUIP:女王様プレイ == 1 && EXP_GAIN("嗜虐快楽経験") >= 30
;		TEQUIP:女王様プレイ = 2
;	ELSEIF TEQUIP:女王様プレイ == 2 && EXP_GAIN("嗜虐快楽経験") >= 100
;		TEQUIP:女王様プレイ = 3
;	ENDIF
;ENDIF

;-------------------------------------------------
;PLAYERのTEQUIPの管理
;-------------------------------------------------
IF SELECTCOM == 106
	TEQUIP:PLAYER:振動棒 = 0
ELSEIF SELECTCOM == 107
	TEQUIP:PLAYER:肛用振動棒 = 0
ENDIF

CALL SETFLAG, "助手相手の恋慕外し終了"

;-------------------------------------------------
;特別経験獲得_5
;-------------------------------------------------
CALL RECORD_SP_EXP_ALLACTOR


;押し倒されると起きるイベント
IF TEQUIP:推倒
	;マスかき止め！ぱんつはそのまま
	TEQUIP:PLAYER:自慰 = 0

	IF TEQUIP:手下留情
		CALL BLANKLINE
		PRINTFORMW 主导权被%CALLNAME:TARGET%夺走、%CALLNAME:PLAYER%不能从其手下逃脱…！
	ENDIF
	TEQUIP:手下留情 = 0

	;３ＰはＷ押し倒しに移行
	SIF TEQUIP:推倒 && SELECTCOM:1 >= 90 && SELECTCOM:1 <= 97
		CALL SETFLAG, "Ｗ推倒"
ELSE
	;押し倒しの解除→二人で押し倒しも解除
	SIF TARGET:1 && TEQUIP:(TARGET:1):Ｗ推倒
		CALL SETFLAG, "Ｗ推倒解除"
ENDIF

;シックスナインにおける道具コマンド等の処理
CALL SET_PLAY69_TURNEND

CALL TARGET_DOWN

;衣装の汚れなど
CALL STAIN_CALC_COSPLAY

;PLAYERが恍惚状態
IF STATE("恍惚", PLAYER)
	SIF TEQUIP:推倒 == 0
		TEQUIP:推倒 = 1
	;相手が乗り気じゃない
	IF TALENT:叛逆
		CALL BLANKLINE
		PRINTFORM ……%CALLNAME:TARGET%似乎不想自己主动做的様子、
		IF RAND:2
			PRINTFORMW 摇着%CALLNAME:PLAYER%的肩膀直到%CALLNAME:PLAYER%清醒过来为止。
		ELSE
			PRINTFORMW 轻轻拍打着%CALLNAME:PLAYER%的脸頬直到%CALLNAME:PLAYER%清醒过来为止。
		ENDIF
		CALL REMOVE_STATE, PLAYER, "恍惚"
		TEQUIP:推倒 = 0
	;恍惚状態で相手の好きにさせるなら
	ELSEIF COND("疲労") == 0 && FLAG:恍惚状態
		NEXTCOM = 281
	ELSEIF COND("疲労")
		CALL BLANKLINE
		PRINTFORM %CALLNAME:TARGET%感觉像是累了的样子、
		IF TALENT:恋慕 && PLAYER == MASTER
			IF RAND:2
				IF INSERT("挿入")
					PRINTFORMW 用骑着的姿势看着%CALLNAME:PLAYER%的脸、等待%CALLNAME:PLAYER%清醒过来…
				ELSE
					PRINTFORMW 撫摸着%CALLNAME:PLAYER%的头髪用膝枕等待着%CALLNAME:PLAYER%清醒过来…
				ENDIF
			ELSE
				PRINTFORMW 就这样抱着等待%CALLNAME:PLAYER%恢复正常…
			ENDIF
		ELSEIF RAND:2
			PRINTFORMW 摇着%CALLNAME:PLAYER%的肩膀直到%CALLNAME:PLAYER%清醒过来为止…
		ELSE
			PRINTFORMW 轻轻拍打着%CALLNAME:PLAYER%的脸頬直到%CALLNAME:PLAYER%清醒过来为止…
		ENDIF
		CALL REMOVE_STATE, PLAYER, "恍惚"
	ENDIF
ENDIF

;撃沈フラグ操作
FOR LCOUNT, 2, CHARANUM
	SIF STATE("撃沈", LCOUNT)
		CALL SETFLAG, "撃沈", LCOUNT
NEXT

;-------------------------------------------------
;各種フラグのクリンナップ
;-------------------------------------------------
DRAWLINE
CALL SETFLAG, "ターン終了", TARGET
CALL SETFLAG, "ターン終了", MASTER
IF ASSI
	CALL SETFLAG, "ターン終了", ASSI
	;助手経験
	IF TEQUIP:推倒 == 0 && PLAYER != ASSI && RAND:2
		EXP:ASSI:助手経験 += 1
		SIF TALENT:ASSI:聰慧 && RAND:2
			EXP:ASSI:助手経験 += 1
		SIF TALENT:ASSI:笨蛋 && RAND:2
			EXP:ASSI:助手経験 -= 1
	ENDIF

	;相性の変動(とりあえずは上昇のみ)。絶頂していなくても上昇します。
	IF (NOWEX:絶頂数合計 + NOWEX:射精 + NOWEX:噴乳 + NOWEX:絶頂漏尿 >= RAND:10 && ASSIPLAY) || (COND("絶頂", PLAYER) >= RAND:4 && TEQUIP:Ｗ推倒 && TARGET:1 == ASSI)
		LOCAL = 0
		SELECTCASE RELATION:(NO:ASSI)
		CASE IS >= 200
			SIF RAND:12 == 0
				LOCAL = 1
		CASE IS >= 150
			SIF RAND:8 == 0
				LOCAL = 1
		CASE IS >= 120
			SIF RAND:6 == 0
				LOCAL = 1
		CASEELSE
			SIF RAND:4 == 0
				LOCAL = 1
		ENDSELECT
		IF LOCAL && NO:ASSI != 99
			CALL CHANGE_RELATION, 5, TARGET, ASSI
			;CALL BLANKLINE
			;PRINTFORMW %CALLNAME:TARGET%と%CALLNAME:ASSI%の仲が良くなったようだ…
		ENDIF
	ENDIF
ENDIF
;-------------------------------------------------
;助手の行動や起こった事を総合して今回のコマンド行動を記録
;-------------------------------------------------
CALL SETFLAG, "今回指令"

;前回のTARGETが誰か
TFLAG:前Ｔ対象 = TARGET
;前回の調教者が助手か主人か
TFLAG:前Ｔ調教者 = PLAYER
