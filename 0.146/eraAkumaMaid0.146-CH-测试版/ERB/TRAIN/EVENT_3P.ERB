;-------------------------------------------------
;多人数プレイで用いる共通処理はここで行う
;-------------------------------------------------
;-------------------------------------------------
;３Ｐにおけるコマンド口上の汎用部分
;-------------------------------------------------
@TRAIN_MESSAGE_3P
;同じコマンドの組み合わせで連続実行
IF SELECTCOM:2 == PREVCOM:2 && SELECTCOM == PREVCOM
	IF RAND:3 == 0
		PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:(PLAYER:1)%は%CALLNAME:TARGET%で思う存分快楽を貪っている…
		RETURN 1
	ENDIF
	SELECTCASE SELECTCOM
	;インサート
	CASE 60 TO 79, 83
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:(PLAYER:1)%
		SELECTCASE PREVCOM:2
		CASE 90, 97
			PRINTFORM のペニスを\@ PREVCOM:2 == 97 ? %CLO("ぱんつコキ")% # 手 \@で扱いている
		CASE 91
			PRINTFORM のペニスを鼻息荒くしゃぶっている
		CASE 92
			PRINTFORM にパイズリをしている
		CASE 96
			PRINTFORM のお尻の穴を責めている
		CASE 98
			PRINTFORM のアソコを舌で味わっている
		ENDSELECT
		PRINTFORMW %CALLNAME:TARGET%を激しく犯し続けた…
	;イラマチオ
	CASE 140
		PRINTFORM %CALLNAME:PLAYER%と%CALLNAME:(PLAYER:1)%は、腰の動きを合わせて%CALLNAME:TARGET%の
		SELECTCASE PREVCOM:2
		CASE 60 TO 69
			PRINTFORM 膣
		CASE 70 TO 79
			PRINTFORM アナル
		CASE 83 TO 88
			PRINTFORM 尿道
		CASEELSE
			IF INSERT("Ｖインサート", TARGET, PLAYER:1)
				PRINTFORM 膣
			ELSEIF INSERT("Ａインサート", TARGET, PLAYER:1)
				PRINTFORM アナル
			ELSEIF INSERT("尿道インサート", TARGET, PLAYER:1)
				PRINTFORM 尿道
			ENDIF
		ENDSELECT
		PRINTFORMW と喉を犯し続けている…
	CASEELSE
		;この値は助手側の行動での分岐(PREVCOM:2での分岐と思って問題ない)
		SELECTCASE CHECK_COM("３Ｐ奉仕")
		;ニプルファック
		CASE 82
			SELECTCASE SELECTCOM
			;左右ニプルファック
			CASE 82
				PRINTFORMW %CALLNAME:PLAYER%と%CALLNAME:(PLAYER:1)%は、腰の動きを合わせて%CALLNAME:TARGET%の左右の乳首を犯し続けている…
			CASE 90
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:(PLAYER:1)%のペニスを胸で受け入れながら、%CALLNAME:PLAYER%のモノを自分に向けさせて手で扱き続けている…
			CASE 91
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:(PLAYER:1)%のペニスを胸で受け入れながら、%CALLNAME:PLAYER%のペニスに舌を絡めている…
			CASE 97
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:(PLAYER:1)%のペニスを胸で受け入れながら、%CALLNAME:PLAYER%のモノを%CLO("ぱんつコキ")%で扱き続けている…
			ENDSELECT
		CASE 90
			SELECTCASE SELECTCOM
			CASE 82
				PRINTFORMW %CALLNAME:PLAYER%は、%CALLNAME:(PLAYER:1)%のペニスを手で扱いている%CALLNAME:TARGET%の乳首を犯し続けた…
			CASE 90, 97
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%達二人のペニスを自分の顔に向けさせて、器用に両手で手コキを続けている…
			CASEELSE
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:(PLAYER:1)%のペニスを手で弄りながら、%CALLNAME:PLAYER%のペニスに舌を絡めている…
			ENDSELECT
		CASE 91
			SELECTCASE SELECTCOM
			CASE 82
				PRINTFORMW %CALLNAME:PLAYER%は、%CALLNAME:(PLAYER:1)%のペニスに口で奉仕している%CALLNAME:TARGET%の乳首を犯し続けた…
			CASE 90
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:(PLAYER:1)%のペニスをねっとりと舐め回しながら、%CALLNAME:PLAYER%のモノを自分に向けさせて手で扱き続けている…
			CASE 97
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:(PLAYER:1)%のペニスをねっとりと舐め回しながら、%CALLNAME:PLAYER%のモノを%CLO("ぱんつコキ")%で扱き続けている…
			CASEELSE
				PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%達二人のペニスをせわしなく口でお世話している…
			ENDSELECT
		CASEELSE
			PRINTFORM %CALLNAME:TARGET%は%CALLNAME:(PLAYER:1)%に跨って腰を振りながら、
			SELECTCASE SELECTCOM
			;現在はこれは死に分岐
			CASE 82
				PRINTFORMW %CALLNAME:PLAYER%のモノを乳腺で受け入れている…
			CASE 90
				PRINTFORMW %CALLNAME:PLAYER%のモノを自分に向けさせて手で扱き続けている…
			CASE 97
				PRINTFORMW %CALLNAME:PLAYER%のモノを%CLO("ぱんつコキ")%で扱き続けている…
			CASE 91
				PRINTFORMW %CALLNAME:PLAYER%のモノを鼻息荒くしゃぶり続けている…
			CASE 92
				PRINTFORMW %CALLNAME:PLAYER%のモノをおっぱいで挟んで擦り上げ続けている…
			CASE 96
				PRINTFORMW %CALLNAME:PLAYER%のアナルに舌を這わせ続けている…
			CASE 98
				PRINTFORMW %CALLNAME:PLAYER%の愛液を舐めとりつつアソコを舌で刺激し続けている…
			ENDSELECT
		ENDSELECT
	ENDSELECT
	RETURN 1
ENDIF

SELECTCASE SELECTCOM
;セックス系やイラマチオなどPLAYER主導コマンド
CASE 60 TO 79, 82, 83, 140
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:(PLAYER:1)%
	SELECTCASE PREVCOM:2
	CASE 60 TO 69
		PRINTFORM に激しく犯されている
	CASE 70 TO 79
		PRINTFORM に激しくアナルを犯されている
	CASE 82
		PRINTFORM %TEXTR("に乳首を犯され/のモノを胸で受け止め")%ている
	CASE 83 TO 88
		PRINTFORM に激しく尿道を犯されている
	CASE 90, 97
		PRINTFORM のモノを\@ PREVCOM:2 == 97 ? %CLO("ぱんつコキ")% # 手 \@で扱いている
	CASE 91
		PRINTFORM のモノを口でしゃぶっている
	CASE 92
		PRINTFORM のモノを胸で擦りあげている
	CASE 96
		PRINTFORM のお尻の穴に舌で奉仕している
	CASE 98
		PRINTFORM のアソコに舌で奉仕している
	CASE 140
		PRINTFORM に喉の奥まで犯されている
	ENDSELECT

	PRINTFORM %CALLNAME:TARGET%

	SELECTCASE SELECTCOM
	CASE 60 TO 69
		PRINTFORM の%TEXTS("Ｖ挿入前")%
		SELECTCASE PREVCOM
		CASE 70 TO 79
			PRINTFORMW にアナルから引き抜いた%NAMES("ペニス", PLAYER)%をぶち込んで犯しはじめた…
		CASE 83 TO 88
			PRINTFORMW に尿道から引き抜いた%NAMES("ペニス", PLAYER)%をぶち込んで犯しはじめた…
		CASEELSE
			PRINTFORMW を犯しはじめた…
		ENDSELECT
	CASE 70 TO 79
		PRINTFORM の%TEXTS("アナル")%アナル
		SELECTCASE PREVCOM
		CASE 60 TO 69
			PRINTFORMW を愛液まみれの%NAMES("ペニス", PLAYER)%で犯しはじめた…
		CASE 83 TO 88
			PRINTFORMW をアンモニア臭い%NAMES("ペニス", PLAYER)%で犯しはじめた…
		CASEELSE
			PRINTFORMW を犯しはじめた…
		ENDSELECT
	CASE 82
		PRINTFORM の、使われていない方の乳首
		SELECTCASE PREVCOM
		CASE 60 TO 69
			PRINTFORMW を愛液まみれの%NAMES("ペニス", PLAYER)%で犯しはじめた…
		CASE 70 TO 79
			PRINTFORMW にアナルから引き抜いた%NAMES("ペニス", PLAYER)%をぶち込んで犯しはじめた…
		CASE 83 TO 88
			PRINTFORMW を小便まみれの%NAMES("ペニス", PLAYER)%で犯しはじめた…
		CASEELSE
			PRINTFORMW を犯しはじめた…
		ENDSELECT
	CASE 83
		PRINTFORMW 
	CASE 140
		PRINTFORMW の頭を掴んで、荒々しく%NAMES("ペニス", PLAYER)%で口を犯しはじめた…
	ENDSELECT
CASEELSE
	PRINTFORM %CALLNAME:TARGET%は%CALLNAME:(PLAYER:1)%

	SELECTCASE CHECK_COM("３Ｐ奉仕")
	;ニプルファック
	CASE 82
		PRINTFORM %TEXTR("に乳首を犯され/のモノを胸で受け止め")%ながら、
	CASE 90
		PRINTFORM のモノを\@ PREVCOM:2 == 97 ? %CLO("ぱんつコキ")% # 手 \@で扱きながら、
	CASE 91
		SELECTCASE SELECTCOM
		CASE 91
			PRINTFORM のモノと同時に、
		CASEELSE
			PRINTFORM のモノを口でしゃぶりながら、
		ENDSELECT
	CASEELSE
		SELECTCASE PREVCOM:2
		CASE 60 TO 69
			PRINTFORM のモノを膣で締め付けて緩やかに腰を動かしながら、
		CASE 70 TO 79
			PRINTFORM のモノを直腸で呑み込んだまま、
		CASE 83 TO 88
			PRINTFORM のモノを尿道できつく締め付けながら、
		CASEELSE
			IF INSERT("Ｖインサート", TARGET, PLAYER:1)
				PRINTFORM のモノを膣で締め付けて緩やかに腰を動かしながら、
			ELSEIF INSERT("Ａインサート", TARGET, PLAYER:1)
				PRINTFORM のモノを直腸で呑み込んだまま、
			ELSEIF INSERT("尿道インサート", TARGET, PLAYER:1)
				PRINTFORM のモノを尿道できつく締め付けながら、
			ENDIF
		ENDSELECT
	ENDSELECT

	SELECTCASE SELECTCOM
	CASE 90 TO 92, 97
		SELECTCASE PREVCOM
		CASE 60 TO 69
			PRINTFORM さっきまで自分を犯していた
		CASE 70 TO 79
			PRINTFORM アナルから引き抜かれたばかりの
		CASE 83 TO 88
			PRINTFORM オシッコの穴から引き抜かれたばかりの
		ENDSELECT
	ENDSELECT

	PRINTFORM %CALLNAME:PLAYER%の
	SELECTCASE SELECTCOM
	CASE 90
		PRINTFORM ペニスを余った方の手で
		IF RAND:3 == 0
			PRINTW しゅっしゅっと擦りはじめた…
		ELSEIF RAND:2
			PRINTW 扱きはじめた…
		ELSE
			PRINTW 弄りはじめた…
		ENDIF
	CASE 97
		PRINTFORM ペニスを%CLO("ぱんつコキ")%で
		IF RAND:3 == 0
			PRINTW 擦りはじめた…
		ELSEIF RAND:2
			PRINTW 扱きはじめた…
		ELSE
			PRINTW 包んで弄りはじめた…
		ENDIF
	CASE 91
		PRINTFORM ペニスに口をつけて
		;奉仕精神3以上か欲情4以上
		IF (ABL:奉仕精神 >= 3  || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
			PRINTFORMW 激しくしゃぶりはじめた…
		ELSEIF RAND:2 == 0 && ABL:技巧 >= 2
			PRINTFORMW 吸い込むようにしゃぶりはじめた…
		ELSE
			PRINTFORMW 舐めまわしてきた…
		ENDIF
	CASE 92
		PRINTFORM ペニスを
		IF TALENT:貧乳
			PRINTFORM 薄い胸で懸命に
		ELSEIF TALENT:巨乳
			PRINTFORM 豊満な胸で包み込んで
		ELSE
			PRINTFORM 胸ではさんで
		ENDIF
		PRINTW 擦りあげた…
	CASE 96
		PRINTFORM お尻の穴を
		IF ABL:奉仕精神 >= 3 && ABL:サドっ気 >= 3 && RAND:2 == 0
			PRINTFORMW 尖らせた舌で中まで舐めてきた…
		ELSEIF ABL:奉仕精神 >= 3
			PRINTFORMW しっかりと舌で舐め清めはじめた…
			STAIN:PLAYER:Ａ = SET_STAIN(0, "Ａ")
		ELSEIF ABL:サドっ気 >= 3 || TEQUIP:押し倒し
			PRINTFORMW 舌で責めてきた…
		ELSE
			PRINTFORMW おそるおそる舐めてきた…
		ENDIF
	CASE 98
		PRINTFORM アソコを
		IF ABL:奉仕精神 >= 3 && ABL:サドっ気 >= 3 && RAND:2 == 0
			PRINTFORMW 尖らせた舌で中の襞まで舐めてきた…
		ELSEIF ABL:奉仕精神 >= 3
			PRINTFORMW 押し広げて、隅々まで舌で舐めはじめた…
			STAIN:PLAYER:Ａ = SET_STAIN(0, "Ａ")
		ELSE
			PRINTFORMW 舐めてきた…
		ENDIF
	ENDSELECT
ENDSELECT

;-------------------------------------------------
;３Ｐにおける射精口上の汎用部分
;-------------------------------------------------
@SAMEN_SHOOT_MESSAGE_3P, ARGS
PRINTFORM %CALLNAME:(PLAYER:1)%
SELECTCASE SELECTCOM:2
CASE 60 TO 79, 83 TO 88
	IF COND("絶頂", PLAYER:1)
		PRINTFORM に中出しされてゾクゾクしている
	ELSEIF INSERT("騎乗位")
		PRINTFORM に跨って腰を振っている
	ELSE
		PRINTFORM に犯されている
	ENDIF
CASE 82
	IF COND("絶頂", PLAYER:1)
		PRINTFORM に精液を胸の中で出されて身体を震わせている
	ELSE
		PRINTFORM のペニスで胸を犯されている
	ENDIF
CASE 90, 97
	IF COND("絶頂", PLAYER:1)
		IF ARGS == "手"
			PRINTFORM と同時に
		ELSE
			PRINTFORM にぶっかけられている
		ENDIF
	ELSE
		IF ARGS == "手"
			PRINTFORM 二本のペニスを夢中で扱いている
		ELSE
			PRINTFORM のペニスを扱いている
		ENDIF
	ENDIF
CASE 91
	IF COND("絶頂", PLAYER:1)
		IF ARGS == "口"
			PRINTFORM と同時に
		ELSE
			PRINTFORM に精液を口に出されている
		ENDIF
	ELSE
		IF ARGS == "口"
			PRINTFORM のペニスから口を離した
		ELSE
			PRINTFORM のペニスをしゃぶっている
		ENDIF
	ENDIF
CASE 92
	IF COND("絶頂", PLAYER:1)
		PRINTFORM に精液で胸を汚されている
	ELSE
		PRINTFORM のペニスを胸で擦りあげている
	ENDIF
CASE 96
	IF COND("絶頂", PLAYER:1)
		PRINTFORM をお尻の快楽でイかせている
	ELSE
		PRINTFORM のアヌスに舌を差し込んでいる
	ENDIF
CASE 98
	IF COND("絶頂", PLAYER:1)
		PRINTFORM をクンニで絶頂させている
	ELSE
		PRINTFORM のアソコに舌で奉仕している
	ENDIF
CASE 140
	IF COND("絶頂", PLAYER:1)
		PRINTFORM に精液を飲まされている最中の
	ELSE
		PRINTFORM に荒々しく喉を犯されている
	ENDIF
ENDSELECT


;-------------------------------------------------
;３Ｐにおける補佐役（通常は助手）のコマンド決定（自由行動は今のところ無し）
;-------------------------------------------------
@SET_SELECTCOM_3P
SELECTCASE SELECTCOM
CASE 82, 90 TO 104, 140
	SIF CHECK_COM("Ｗ奉仕")
		SELECTCOM:1 = SELECTCOM
	SIF CHECK_COM("３Ｐ奉仕")
		SELECTCOM:2 = PREVCOM:2
CASE 60 TO 69
	IF CHECK_COM("Ｖ二本挿入")
		SELECTCOM:2 = 64
	ELSEIF CHECK_COM("二穴挿入")
		SELECTCOM:2 = 74
	ELSEIF CHECK_COM("３Ｐ奉仕")
		SELECTCOM:2 = PREVCOM:2
	ENDIF
CASE 70 TO 79
	IF CHECK_COM("Ａ二本挿入")
		SELECTCOM:2 = 75
	ELSEIF CHECK_COM("二穴挿入")
		SELECTCOM:2 = 65
	ELSEIF CHECK_COM("３Ｐ奉仕")
		SELECTCOM:2 = PREVCOM:2
	ENDIF
CASE 83 TO 88
	SIF CHECK_COM("３Ｐ奉仕")
		SELECTCOM:2 = PREVCOM:2
ENDSELECT

;-------------------------------------------------
;ARGS:1〜5で指定された禁止ワードに応じたビットを設定する
;-------------------------------------------------
@BIT_FORBIDCOM(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5)
#FUNCTION
#DIM LCOUNT

LOCAL = 0
FOR LCOUNT, 1, 6
	ARGS = %ARGS:LCOUNT%
	;補佐役がプレイヤーの動き（封印中）
	IF COND("調教側", PLAYER:1)
		SELECTCASE ARGS
		;TARGETに対するコマンド管理
		CASE "Ｔ顔"
			SETBIT LOCAL, 0
		CASE "Ｔ下半身"
			SETBIT LOCAL, 1
			SETBIT LOCAL, 2
			SETBIT LOCAL, 3
			SETBIT LOCAL, 4
		CASE "Ｔペニス"
			SETBIT LOCAL, 2
		CASE "Ｔアソコ"
			SETBIT LOCAL, 3
		CASE "Ｔアナル"
			SETBIT LOCAL, 4
		CASE "Ｔ胸"
			SETBIT LOCAL, 5
		CASE "Ｔ指"
			SETBIT LOCAL, 6
		CASE "Ｔ前面"
			SETBIT LOCAL, 7
			SETBIT LOCAL, 5
			SETBIT LOCAL, 2
		CASE "Ｔ後面"
			SETBIT LOCAL, 8
			SETBIT LOCAL, 4
		ENDSELECT
	ELSE
		SELECTCASE ARGS
		;PLAYERに対するコマンド管理
		CASE "Ｐ顔"
			SETBIT LOCAL, 20
		CASE "Ｐ下半身"
			SETBIT LOCAL, 21
			SETBIT LOCAL, 22
			SETBIT LOCAL, 23
			SETBIT LOCAL, 24
		CASE "Ｐペニス"
			SETBIT LOCAL, 22
		CASE "Ｐアソコ"
			SETBIT LOCAL, 23
		CASE "Ｐアナル"
			SETBIT LOCAL, 24
		CASE "Ｐ胸"
			SETBIT LOCAL, 25
		CASE "Ｐ指"
			SETBIT LOCAL, 26
		CASE "Ｐ前面"
			SETBIT LOCAL, 27
			SETBIT LOCAL, 25
			SETBIT LOCAL, 22
		CASE "Ｐ後面"
			SETBIT LOCAL, 28
			SETBIT LOCAL, 24
		;PLAYERに対する奉仕コマンド
		CASE "手コキ"
			SETBIT LOCAL, 30
		CASE "手コキ以外"
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "フェラチオ"
			SETBIT LOCAL, 31
		CASE "フェラチオ以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "パイズリ"
			SETBIT LOCAL, 32
		CASE "パイズリ以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 31
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "足コキ"
			SETBIT LOCAL, 33
		CASE "足コキ以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "素股"
			SETBIT LOCAL, 34
		CASE "素股以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 35
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "泡踊り"
			SETBIT LOCAL, 35
		CASE "泡踊り以外"
			SETBIT LOCAL, 30
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 36
			SETBIT LOCAL, 39
		CASE "ぱんつコキ"
			SETBIT LOCAL, 36
		CASE "ぱんつコキ以外"
			SETBIT LOCAL, 31
			SETBIT LOCAL, 32
			SETBIT LOCAL, 33
			SETBIT LOCAL, 34
			SETBIT LOCAL, 35
			SETBIT LOCAL, 39
		CASE "セックス"
			SETBIT LOCAL, 39
		ENDSELECT
	ENDIF
	SIF STRLENS(ARGS:(LCOUNT + 1) ) == 0
		RETURNF LOCAL
NEXT
RETURNF LOCAL

;-------------------------------------------------
;PLAYERでもTARGETでも無いARG(主に助手)に、他のキャラが選んだコマンドARG:1に応じた禁止コマンドを設定する
;-------------------------------------------------
@SET_FORBIDCOM, ARG, ARG:1
VARSET LOCAL

SELECTCASE ARG:1
;顔面騎乗などPLAYERの口を責める
CASE 1
	LOCAL = BIT_FORBIDCOM("Ｐ顔", "Ｔアソコ")
CASE 4
	LOCAL = BIT_FORBIDCOM("Ｐ顔", "Ｔペニス")
CASE 5
	LOCAL = BIT_FORBIDCOM("Ｐ指", "Ｔ胸")
CASE 6
	LOCAL = BIT_FORBIDCOM("Ｐ顔", "Ｔ顔")
CASE 9
	LOCAL = BIT_FORBIDCOM("Ｐ顔", "Ｔアナル")
;騎乗位などPLAYERのペニスをアソコやアナルで責める
CASE 60, 62, 63
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ前面")
CASE 61
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ後面")
CASE 64
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｐ前面", "Ｔ後面")
CASE 65
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔアソコ")
CASE 66 TO 69
	IF TEQUIP:押し倒し
		LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔアソコ")
	ELSE
		LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ下半身")
	ENDIF
CASE 70, 72, 73
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ前面")
CASE 71
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ後面")
CASE 74
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｐ前面", "Ｔ後面")
CASE 75
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔアソコ", "Ｔ後面")
CASE 76 TO 79
	IF TEQUIP:押し倒し
		LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔアソコ", "Ｔ後面")
	ELSE
		LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ下半身")
	ENDIF
;貝合わせ
CASE 80
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔ下半身")
;尿道挿入
CASE 83, 85, 86
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ前面")
CASE 84
	LOCAL = BIT_FORBIDCOM("Ｐ前面", "Ｔ下半身", "Ｔ後面")
CASE 87
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｐ前面", "Ｔ後面")
CASE 88
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｔアソコ", "Ｔ後面")
;手コキなどPLAYERのペニスを手とかで責める
CASE 90
	LOCAL = BIT_FORBIDCOM("手コキ以外", "Ｔ指")
CASE 91
	LOCAL = BIT_FORBIDCOM("フェラチオ以外", "Ｔ顔")
CASE 92
	LOCAL = BIT_FORBIDCOM("パイズリ以外", "Ｔ胸")
CASE 93, 101
	LOCAL = BIT_FORBIDCOM("素股以外", "Ｔ下半身")
CASE 94
	LOCAL = BIT_FORBIDCOM("泡踊り以外", "Ｔ前面")
CASE 95
	IF PENIS(MASTER)
		LOCAL = BIT_FORBIDCOM("足コキ以外", "Ｔ下半身")
	ELSE
		LOCAL = BIT_FORBIDCOM("足コキ以外", "Ｔ下半身")
	ENDIF
CASE 96
	LOCAL = BIT_FORBIDCOM("Ｐアナル", "Ｔ顔")
CASE 97
	LOCAL = BIT_FORBIDCOM("ぱんつコキ以外", "Ｔ指")
CASE 98
	LOCAL = BIT_FORBIDCOM("Ｐアソコ", "Ｔ顔")
CASE 104
	LOCAL = BIT_FORBIDCOM("Ｐ顔", "Ｔ胸", "Ｔ前面")
;逆アナル
CASE 125 TO 129
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｐ後面", "Ｔペニス", "Ｔ前面")
;逆レイプなどPLAYERのアソコを責める
CASE 120 TO 124
	LOCAL = BIT_FORBIDCOM("Ｐ下半身", "Ｐ前面", "Ｔペニス", "Ｔ前面")
CASE 140
	LOCAL = BIT_FORBIDCOM("Ｐペニス", "Ｐ前面", "Ｔ顔")
CASE 141
	LOCAL = BIT_FORBIDCOM("Ｐ指", "Ｔアソコ")
CASE 142
	LOCAL = BIT_FORBIDCOM("Ｐ指", "Ｔアナル")
CASE 143
	LOCAL = BIT_FORBIDCOM("Ｐ指", "Ｐ前面", "Ｔ下半身")
ENDSELECT
TCVAR:ARG:コマンド制限 = LOCAL

;-------------------------------------------------
;ARGS:1〜5が実際に禁止されているかを見る
;-------------------------------------------------
@FORBIDCOM(ARG, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5)
#FUNCTION
SIF TCVAR:ARG:コマンド制限 & BIT_FORBIDCOM(ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5)
	RETURNF 1
RETURNF 0


;-------------------------------------------------
;３ＰやＷ押し倒しなどでの補佐役（通常は助手）の行動を決定する
;-------------------------------------------------
@SET_SELECTCOM_MULTI, ARGS
SELECTCASE ARGS
CASE "Ｗ押し倒し"
	CALL SET_SELECTCOM_W
CASE "３Ｐ"
	CALL SET_SELECTCOM_3P
ENDSELECT

;-------------------------------------------------
;Ｗ押し倒しの時には助手のコマンド口上とSOURCE等を管理する
;補佐役の取得経験は邪魔だから隠すことにした
;-------------------------------------------------
@EQUIP_COM_3P
#DIM MEMO_LINECOUNT
#DIM ACTOR

ACTOR = TARGET:1
VARSET LOCAL

;表示があったかを判定
MEMO_LINECOUNT = LINECOUNT

;最初のターンは何もしてこない
IF SELECTCOM:1 == -1
	IF PREVCOM:1 >= 0
		CALL BLANKLINE
		SIF TEQUIP:押し倒し
			PRINTL ＜押し倒され中＞
		PRINTFORMW %CALLNAME:ACTOR%は%CALLNAME:PLAYER%たちのプレイを楽しそうに眺めている…
	ENDIF
	RETURN 0
;二人して同じことをしているor二穴挿入
ELSEIF SELECTCOM:1 == SELECTCOM || (SELECTCOM == 120 && SELECTCOM:1 == 126) || (SELECTCOM == 126 && SELECTCOM:1 == 120)
	CALL GET_PALAM_MULTI, 1
	SIF LINECOUNT > MEMO_LINECOUNT
		CALL BLANKLINE
	SIF TEQUIP:押し倒し
		PRINTL ＜押し倒され中＞
;前回の助手、今回のTARGETとも異なる行動or眺めている
ELSEIF SELECTCOM:1 != SELECTCOM && SELECTCOM:1 != PREVCOM:1
	CALL BLANKLINE
	SIF TEQUIP:押し倒し
		PRINTL ＜押し倒され中＞
	PRINTFORM %CALLNAME:ACTOR%は
	SELECTCASE SELECTCOM:1
	CASE 1
		PRINTFORMW %CALLNAME:PLAYER%の顔に跨ってアソコを押し付けてきた…
		SIF INSERT("騎乗位") && COND("性別嫌悪", ACTOR) == 0 && RAND:3 == 0
			PRINTFORMW そして、目の前で腰を振っている%CALLNAME:TARGET%とキスをし始めた…
	CASE 9
		PRINTFORMW %CALLNAME:PLAYER%の顔に跨ってアナルを舐めさせてきた…
		SIF INSERT("騎乗位") && COND("性別嫌悪", ACTOR) == 0 && RAND:3 == 0
			PRINTFORMW そして、目の前で腰を振っている%CALLNAME:TARGET%とキスをし始めた…
	CASE 4
		PRINTFORMW %CALLNAME:PLAYER%の口をペニスで犯し始めた…
	CASE 6
		PRINTFORMW %CALLNAME:PLAYER%に優しくキスしてきた…
	CASE 60 TO 79, 83
		PRINTFORMW %CALLNAME:PLAYER%のペニスに狙いを定めて腰を下ろしてきた…
		PRINTFORM そして、
		SELECTCASE SELECTCOM:1
		CASE 60 TO 69
			PRINTFORM 膣
		CASE 70 TO 79
			PRINTFORM アナル
		CASE 83 TO 88
			PRINTFORM 尿道
		ENDSELECT
		IF RAND:3 == 0
			PRINTFORM を巧みに蠢かせながら
		ELSEIF RAND:2 == 0
			PRINTFORM をきつく締め付けながら
		ELSE
			SELECTCASE SELECTCOM:1
			CASE 60 TO 69
				PRINTFORM から愛液を溢しながら
			CASE 70 TO 79
				PRINTFORM から粘液を溢しながら
			CASE 83 TO 88
				PRINTFORM の隙間からオシッコを溢しながら
			ENDSELECT
		ENDIF
		IF ABL:ACTOR:技巧 >= 3 && RAND:3 == 0
			PRINTFORMW 激しく腰をくねらせ始めた…
		ELSEIF RAND:2 == 0
			PRINTFORMW 腰を振り始めた…
		ELSE
			PRINTFORMW 長いストロークで腰を動かしはじめた…
		ENDIF
	CASE 90
		IF TALENT:ACTOR:恋慕 && RAND:4 == 0
			PRINTFORMW %CALLNAME:PLAYER%のペニスを愛しそうに手で扱いてきた…
		ELSEIF ABL:ACTOR:技巧 >= 3 && ABL:ACTOR:奉仕精神 >= 3 && RAND:3 == 0
			PRINTFORMW %CALLNAME:PLAYER%の亀頭を手の平でこね回してきた…
		ELSEIF ABL:ACTOR:奉仕精神 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%のペニスをゆっくりと扱いてきた…
		ELSEIF ABL:ACTOR:技巧 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%のペニスを絶妙な力加減で刺激してきた…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%のペニスを手で扱き始めた…
		ENDIF
	CASE 91
		IF TALENT:ACTOR:恋慕 && RAND:4 == 0
			PRINTFORMW %CALLNAME:PLAYER%のペニスを愛しそうに口に咥えてきた…
		ELSEIF ABL:ACTOR:技巧 >= 4 && ABL:ACTOR:奉仕精神 >= 3 && RAND:3 == 0
			PRINTFORMW %CALLNAME:PLAYER%のペニスを喉奥まで咥えて、長いストロークで扱いてきた…
		ELSEIF ABL:ACTOR:奉仕精神 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%のペニスを美味しそうに口で味わってきた…
		ELSEIF ABL:ACTOR:技巧 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%のペニスに舌を絡ませながら、激しくしゃぶってきた…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%のペニスを舌で舐め始めた…
		ENDIF
	CASE 92
		IF TALENT:ACTOR:巨乳
			PRINTFORM 大きな
		ELSEIF TALENT:ACTOR:貧乳
			PRINTFORM 小さな
		ENDIF
		PRINTFORM 胸で%CALLNAME:PLAYER%のペニスを
		IF TALENT:ACTOR:巨乳
			PRINTFORMW 扱きながら、胸の谷間から顔を出した亀頭を舐め回してきた…
		ELSE
			PRINTFORMW 扱いてきた…
		ENDIF
	CASE 93
		PRINTFORMW %CALLNAME:PLAYER%のペニスにアソコを擦り付けて来た…
	CASE 94
		PRINTFORMW %CALLNAME:PLAYER%に泡まみれの肢体を擦り付けて来た…
	CASE 95
		PRINTFORM %CALLNAME:PLAYER%の
		IF TALENT:MASTER:ふたなり + TALENT:MASTER:オトコ
			PRINTFORMW ペニスを足で扱いてきた…
		ELSE
			PRINTFORMW 股間を足で弄ってきた…
		ENDIF
	CASE 96
		PRINTFORMW %CALLNAME:PLAYER%のアナルを舐めてきた…
	CASE 97
		PRINTFORMW パンツを脱ぐと、%CALLNAME:PLAYER%のペニスをそれで扱き始めた…
	CASE 98
		PRINTFORMW %CALLNAME:PLAYER%のアソコを舐めてきた…
	CASE 101
		PRINTFORMW %CALLNAME:PLAYER%のペニスにお尻を擦り付けて来た…
	CASE 104
		PRINTFORMW %CALLNAME:PLAYER%の顔を胸で押し潰してきた…
	CASE 120
		PRINTFORM %CALLNAME:PLAYER%の
		IF TALENT:MASTER:処女 == 1
			PRINTFORM 男を知らない
		ELSE
			PRINTFORM 濡れそぼった
		ENDIF
		IF TALENT:小柄
			PRINTFORM 幼いワレメを
		ELSE
			PRINTFORM 膣を
		ENDIF
		IF PENIS(ACTOR)
			PRINTFORM ペニス
		ELSE
			PRINTFORM ペニスバンド
		ENDIF
		PRINTFORMW で犯してきた…
	CASE 126
		PRINTFORM %CALLNAME:PLAYER%のアナルを
		IF PENIS(ACTOR)
			PRINTFORM ペニス
		ELSE
			PRINTFORM ペニスバンド
		ENDIF
		PRINTFORMW で犯してきた…
	;CASE 997
	;	PRINTFORMW 絶頂の余韻を味わいながら、%CALLNAME:PLAYER%たちのプレイを眺めている…
	CASE 0
		IF SELECTCOM:1 == PREVCOM:1
			PRINTFORMW %CALLNAME:PLAYER%に身体をすりつけている…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%に身体をすり寄せて、指をしゃぶったりしてきた…
		ENDIF
	ENDSELECT
	SIF SELECTCOM:1 >= 0
		CALL GET_PALAM_MULTI, 1
;口上は発言させない
ELSE
	CALL BLANKLINE
	SIF TEQUIP:押し倒し
		PRINTL ＜押し倒され中＞
	CALL GET_PALAM_MULTI
ENDIF

;何かテキストを表示したならもう一個改行
SIF LINECOUNT > MEMO_LINECOUNT
	CALL BLANKLINE

;-------------------------------------------------
;3Pの行動による補佐役のPALAM、EXP変化
;Ｗ押し倒しの場合にはSELECTCOM:1が助手が選択したコマンドなので、それに応じて計算する
;-------------------------------------------------
@GET_PALAM_MULTI, ARG
#DIM MEMO_PLAYER
#DIM ACTOR

;何もやっていない場合
SIF SELECTCOM:1 == -1
	RETURN 0

;助手のBEGIN ABLUPをＯＮにする
SIF TARGET:1 == ASSI
	FLAG:助手を調教した = 1

SWAP SELECTCOM, SELECTCOM:1
SWAP PREVCOM, PREVCOM:1

IF COND("調教側", PLAYER:1)
	ACTOR = PLAYER:1

	MEMO_PLAYER = PLAYER
	PLAYER = PLAYER:1

	CALL SOURCE_CHECK_SIMPLE, SELECTCOM, ARG, 100, 1

	PLAYER = MEMO_PLAYER
ELSE
	ACTOR = TARGET:1
	SWAP TARGET, TARGET:1

	CALL SOURCE_CHECK_SIMPLE, SELECTCOM, ARG, 100, 1

	SWAP TARGET, TARGET:1
ENDIF

SWAP SELECTCOM, SELECTCOM:1
SWAP PREVCOM, PREVCOM:1

;ダウン判定
SIF BASE:ACTOR:体力 < 500 || BASE:ACTOR:精力 <= 0
	CALL ASSI_DOWN, ACTOR


;-------------------------------------------------
;テキストを表示しない、簡易コマンド選択
;ARGはSELECTCOMを代入し、ARG:1の値でPRINTの出力を決定する。
;ARG:2は強度でARG:3は行う回数
;-------------------------------------------------
@SOURCE_CHECK_SIMPLE, ARG, ARG:1, ARG:2, ARG:3
#DIM MEMO_SELECTCOM
#DIM MEMO_TFLAG

MEMO_SELECTCOM = SELECTCOM
SELECTCOM = ARG

MEMO_TFLAG = TFLAG:システム文スキップ
;システム文スキップフラグON
TFLAG:システム文スキップ = 1

TRYCALLFORM SHOW_COM_ORDER_COM, SELECTCOM, 1
TRYCALLFORM FLAG_COM{SELECTCOM}, ARG:2*ARG:3
TRYCALLFORM SOURCE_COM{SELECTCOM}, ARG:2*ARG:3
CALL SETFLAG, "快楽経験判定", SELECTCOM
CALL SAMEN_CHECK, ARG:2*ARG:3
CALL CALC_SOURCE, "調教コマンド"

;インサート解除（NEXTCOM等を考慮）
CALL SETFLAG, "体勢戻し"

;バイブなど付けっぱなしアイテムのチェック(主にSOURCE計算。快楽のSOURCEが増えないものは、ここにはなくても良い)
CALL EQUIP_1

;ソースの計算
CALL CALC_SOURCE
;絶頂処理_見込み
CALL ORGASM_ADD_A

;口上
SIF ARG:1
	CALL KOJO_MESSAGE_COM
;絶頂処理_本番
CALL ORGASM_ADD_A
CALL ORGASM_ADD_B

;各種経験の変動
CALL GET_EXP_COM, ARG:3

;コマンドによるムードの獲得
CALL MOOD_COM, SELECTCOM

;インサートフラグ獲得
CALL CHECK_INSERT

;ソースの計算
CALL CALC_SOURCE

;被虐快楽経験、奉仕快楽経験等のチェック
CALL EXP_GOT_CHECK

;特別経験チェック
SIF ARG:1
	CALL RECORD_SP_EXP

;体力の減少
CALL CALC, "体力減少"

;特殊な素質などの判定
CALL GET_EXABL2, TARGET

SELECTCOM = MEMO_SELECTCOM
TFLAG:システム文スキップ = MEMO_TFLAG

;-------------------------------------------------
;３Ｐにおける絶頂口上の処理
;-------------------------------------------------
@KOJO_MESSAGE_SAMEN_AND_EXTACY_3P
;Ｗ押し倒しなどTARGETが二人の時
IF TEQUIP:Ｗ押し倒し || SELECTCOM:1 >= 0
	SIF COND("絶頂", PLAYER)
		CALL BLANKLINE
	;TARGET:1を一時的にTARGETに
	SWAP TARGET, TARGET:1
	SWAP SELECTCOM, SELECTCOM:1
	SWAP PREVCOM, PREVCOM:1
	;射精箇所を、TARGET:1の行動に対応したものに
	SWAP TCVAR:PLAYER:プレイ区分メイン, TCVAR:PLAYER:プレイ区分サブ

	CALL KOJO_MESSAGE_SAMEN_AND_EXTACY

	SWAP TARGET, TARGET:1
	SWAP SELECTCOM, SELECTCOM:1
	SWAP PREVCOM, PREVCOM:1
	SWAP TCVAR:PLAYER:プレイ区分メイン, TCVAR:PLAYER:プレイ区分サブ
ENDIF
;PLAYERが二人の、いわゆる３Ｐの時
IF COND("３Ｐ") && COND("絶頂", PLAYER:1) && COND("絶頂", PLAYER) == 0
	CALL SETFLAG, "調教者交代"

	CALL KOJO_MESSAGE_SAMEN_AND_EXTACY

	CALL SETFLAG, "調教者交代"
ENDIF

;-------------------------------------------------
;もがくなど、コマンド途中でRETURN 0するコマンドでの発言に適している。
;-------------------------------------------------
@KOJO_MESSAGE_COM_MULTI
SIF TEQUIP:Ｗ押し倒し == 0
	RETURN 0

SWAP TARGET, TARGET:1
SWAP PREVCOM, PREVCOM:1

PRINTFORML 
CALL KOJO_MESSAGE_COM

SWAP TARGET, TARGET:1
SWAP PREVCOM, PREVCOM:1


