@SELF_CHECK
SIF COND("同衾イベント") || COND("同衾イベント", MASTER)
	RETURN 0
;放課後Ｈは時間がない
SIF FLAG:放課後Ｈ
	RETURN 0
;念のため、PLAYERをMASTERに直す
PLAYER = MASTER

;MASTERとの絡み
SIF ABL:欲望 && BASE:MASTER:魂 >= 100 && RAND:2
	CALL SELF_CHECK_PLAY, MASTER
;自慰
SIF ABL:欲望 && ABL:自慰中毒 && RAND:4 == 0
	CALL SELF_CHECK_PLAY, TARGET

;助手との絡み
IF CONFIG("部員間Ｈ") && ASSI && ABL:欲望 && RAND:2
	;PLAYERをASSIにする
	PLAYER = ASSI
	CALL SELF_CHECK_PLAY, ASSI
ENDIF

;PLAYERをMASTERに直す
PLAYER = MASTER


@SELF_CHECK_PLAY, ARG
;行為の回数
#DIM NUM_PLAY
#DIM PLAY_FISTING

;イベントの決定
CALL PLAY_SELF, ARG
SIF STR:イベント名 == ""
	RETURN 0

;基本回数は精力依存
NUM_PLAY = LIMIT(BASE:精力/20 + LIMIT(RAND:(BASE:精力/10 + 1), 1, BASE:精力/15), 1, 4)

SELECTCASE STR:イベント名
CASE "調教後セックス"
	;Ｖ性向、淫壷によるボーナス
	SIF TALENT:Ｖ性向
		NUM_PLAY += 1
	SIF TALENT:淫壷
		NUM_PLAY += 1

	IF NUM_PLAY > 0
		DRAWLINE

		CALL SIMULATE_TRAIN_FIRST

		IF PLAYER == MASTER
			IF BASE:PLAYER:精力 * 2 >= MAXBASE:PLAYER:精力
				PRINTFORM %CALLNAME:PLAYER%と%CALLNAME:TARGET%はまだ冷めやらぬ昂奮を抑えきれず、
				PRINTFORML 汗だくになってベッドの上でまぐわった…
				CALL SIMULATE_TRAIN_COM, 60, NUM_PLAY
			ELSEIF BASE:PLAYER:精力 * 4 >= MAXBASE:PLAYER:精力
				PRINTFORM 興奮冷めやらぬ%CALLNAME:TARGET%は、
				PRINTFORML %CALLNAME:PLAYER%を押し倒して貪るように己の欲望をぶつけてきた…
				TEQUIP:押し倒し = 1
				CALL SIMULATE_TRAIN_COM, 65, NUM_PLAY
			ELSE
				PRINTFORM 火照った身体をもてあましている%CALLNAME:TARGET%は、
				PRINTFORML 疲れきった%CALLNAME:PLAYER%に跨って限界を超えて精を絞ってきた…
				TEQUIP:押し倒し = 1
				CALL SIMULATE_TRAIN_COM, 65, NUM_PLAY
			ENDIF

			CALL KOJO_EVENT, STR:イベント名

			PRINTL 
			IF ABL:信頼 + ABL:欲望 + ABL:Ｖ感覚 >= 13 && COND("欲求不満")
				PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%のモノを舌で舐め清めながら、
				PRINTFORMW 上目遣いに名残惜しげに見つめてくる…
				PRINTFORMW しかしその視線を振り解き、%CALLNAME:PLAYER%は部屋を後にした…
				CALL SIMULATE_TRAIN_COM, 91, 1
			ELSEIF TALENT:相思相愛 && RAND:2
				IF TALENT:PLAYER:汚れ無視 && ABL:サドっ気 >= 3 && RAND:5 == 0
					PRINTFORMW %CALLNAME:PLAYER%は満足した様子の%CALLNAME:TARGET%のアソコを舌で綺麗に舐め清めて、部屋を後にした…
					CALL SIMULATE_TRAIN_COM, 1, NUM_PLAY
				ELSE
					PRINTFORMW %CALLNAME:PLAYER%は満足した様子の%CALLNAME:TARGET%の身体を綺麗に拭き清めて、部屋を後にした…
				ENDIF
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%は満足した様子の%CALLNAME:TARGET%にペニスを綺麗にさせて、部屋を後にした…
				CALL SIMULATE_TRAIN_COM, 91, 1
			ENDIF
		ELSE
			PRINTFORML 調教後%CALLNAME:TARGET%と%CALLNAME:ASSI%は、溢れる欲望に突き動かされて肌を重ねたようだ…
			PRINTFORML この時ばかりは%CALLNAME:MASTER%の事も二人の脳裏から消え、
			PRINTFORM 獣のように飽きることなく
			IF COND("夜") == 0
				PRINTFORM 日が暮れるまで
			ELSE
				PRINTFORM 一晩中
			ENDIF
			PRINTFORML 互いに快楽を貪りあった…
			CALL SIMULATE_TRAIN_COM, 61, NUM_PLAY
			CALL KOJO_EVENT, STR:イベント名
		ENDIF

		;経験や珠などの獲得量計算
		CALL SIMULATE_TRAIN_CALC
		;結果発表
		CALL SIMULATE_TRAIN_SHOW

		CALL CALC, "欲求不満解消", TARGET, NUM_PLAY*5
	ENDIF
CASE "調教後Ａセックス"
	;Ａ性向、尻穴狂いによるボーナス
	SIF TALENT:Ａ性向
		NUM_PLAY += 1
	SIF TALENT:尻穴狂い
		NUM_PLAY += 1

	IF NUM_PLAY > 0
		DRAWLINE

		CALL SIMULATE_TRAIN_FIRST

		IF PLAYER == MASTER
			IF BASE:PLAYER:精力 * 2 >= MAXBASE:PLAYER:精力
				PRINTFORML %CALLNAME:PLAYER%と%CALLNAME:TARGET%はまだ冷めやらぬ昂奮を抑えきれず、
				PRINTFORML お尻の穴で何度もベッドの上でまぐわった…
				CALL SIMULATE_TRAIN_COM, 71, NUM_PLAY
			ELSEIF BASE:PLAYER:精力 * 4 >= MAXBASE:PLAYER:精力
				PRINTFORML アナルが疼いて興奮冷めやらぬ%CALLNAME:TARGET%は、
				PRINTFORML %CALLNAME:PLAYER%を押し倒して貪るように己の欲望をぶつけてきた…
				TEQUIP:押し倒し = 1
				CALL SIMULATE_TRAIN_COM, 75, NUM_PLAY
			ELSE
				PRINTFORML 火照った身体をもてあましている%CALLNAME:TARGET%は、
				PRINTFORML 疲れきった%CALLNAME:PLAYER%に跨ってアナルで精を絞ってきた…
				TEQUIP:押し倒し = 1
				CALL SIMULATE_TRAIN_COM, 75, NUM_PLAY
			ENDIF
			CALL KOJO_EVENT, STR:イベント名

			IF ABL:信頼 + ABL:欲望 >= 13 && COND("欲求不満")
				PRINTL 
				PRINTFORMW %CALLNAME:TARGET%は名残惜しげに袖を引っ張る
				PRINTFORMW しかしその手を振り解き、%CALLNAME:PLAYER%は部屋を後にした…
			ENDIF
		ELSE
			PRINTFORML 調教後%CALLNAME:TARGET%と%CALLNAME:ASSI%は、火照った身体をもてあまして肌を重ねたようだ…
			PRINTFORML 溢れる欲望に突き動かされ、この時ばかりは%CALLNAME:MASTER%の事も二人の脳裏から消え
			PRINTFORM 獣のように飽きることなく
			IF COND("夜") == 0
				PRINTFORM 日が暮れるまで
			ELSE
				PRINTFORM 一晩中
			ENDIF
			PRINTFORML %CALLNAME:TARGET%の肛門で快楽を貪りあった…
			CALL SIMULATE_TRAIN_COM, 71, NUM_PLAY
			CALL KOJO_EVENT, STR:イベント名
		ENDIF

		;経験や珠などの獲得量計算
		CALL SIMULATE_TRAIN_CALC
		;結果発表
		CALL SIMULATE_TRAIN_SHOW

		CALL CALC, "欲求不満解消", TARGET, NUM_PLAY*5
	ENDIF
CASE "調教後レズプレイ"
	;Ｃ性向、淫核によるボーナス
	SIF TALENT:Ｃ性向
		NUM_PLAY += 1
	SIF TALENT:淫核
		NUM_PLAY += 1

	IF NUM_PLAY > 0
		DRAWLINE

		CALL SIMULATE_TRAIN_FIRST

		IF PLAYER == MASTER
			IF BASE:PLAYER:精力 * 4 >= MAXBASE:PLAYER:精力
				PRINTFORML %CALLNAME:PLAYER%と%CALLNAME:TARGET%はまだ冷めやらぬ昂奮を抑えきれず、
				PRINTFORML お互いの火照った身体を慰めあい何度もイッてしまった…
				CALL SIMULATE_TRAIN_COM, 1, RAND:NUM_PLAY + 1
				CALL SIMULATE_TRAIN_COM, 98, RAND:NUM_PLAY + 1
			ELSE
				PRINTFORML 興奮冷めやらぬ%CALLNAME:TARGET%は、
				SIF BASE:PLAYER:精力 * 2 < MAXBASE:PLAYER:精力
					PRINT 疲れきった
				PRINTFORML %CALLNAME:PLAYER%を押し倒して貪るように秘所を擦りあわせてきた…
				CALL SIMULATE_TRAIN_COM, 98, RAND:NUM_PLAY + 1
			ENDIF

			CALL SIMULATE_TRAIN_COM, 80, NUM_PLAY
			CALL KOJO_EVENT, STR:イベント名
			IF ABL:信頼 + ABL:欲望 >= 13 && COND("欲求不満")
				PRINTL 
				PRINTFORMW %CALLNAME:TARGET%は名残惜しげに袖を引っ張る
				PRINTFORMW しかしその手を振り解き、%CALLNAME:PLAYER%は部屋を後にした…
			ENDIF
		ELSE
			PRINTFORML 調教後%CALLNAME:TARGET%と%CALLNAME:ASSI%は、情欲のままに肌を重ねたようだ…
			PRINTFORML 溢れる欲望に突き動かされ、この時ばかりは%CALLNAME:MASTER%の事も二人の脳裏から消え
			PRINTFORM 獣のように飽きることなく
			IF COND("夜") == 0
				PRINTFORM 日が暮れるまで
			ELSE
				PRINTFORM 一晩中
			ENDIF

			CALL SIMULATE_TRAIN_COM, 1, RAND:NUM_PLAY + 1
			CALL SIMULATE_TRAIN_COM, 98, RAND:NUM_PLAY + 1
			CALL SIMULATE_TRAIN_COM, 80, NUM_PLAY/2 + 1

			IF TALENT:ふたなり && TALENT:PLAYER:ふたなり
				PRINTFORML 互いの秘所を擦りつけあい、ペニスをしゃぶりあって快楽を貪った…
				CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY/2 + 1
			ELSE
				PRINTFORML 互いの秘所を擦りつけあって、快楽を貪った…
			ENDIF
			CALL KOJO_EVENT, STR:イベント名

			SWAP PLAYER, TARGET
			CALL SIMULATE_TRAIN_COM, 80, NUM_PLAY/2 + 1
			CALL SIMULATE_TRAIN_COM, 91, NUM_PLAY/2 + 1
			SWAP PLAYER, TARGET
		ENDIF

		;経験や珠などの獲得量計算
		CALL SIMULATE_TRAIN_CALC
		;結果発表
		CALL SIMULATE_TRAIN_SHOW

		CALL CALC, "欲求不満解消", TARGET, NUM_PLAY*5
	ENDIF
CASE "調教後自慰"
	NUM_PLAY += RAND:(MIN(5, ABL:自慰中毒) + 1)
	IF NUM_PLAY > 0
		DRAWLINE
		PRINTFORM %CALLNAME:PLAYER%が出て行った後、%CALLNAME:TARGET%は
		SIF TALENT:恋慕
			PRINTFORML %CALLNAME:PLAYER%の事を想いながら、

		;フィストフラグ
		PLAY_FISTING = 0

		SELECTCASE FAV_ONANIE(SELECTCOM, "一番")
		CASE "Ｃ"
			IF PENIS(TARGET)
				PRINT ペニスを扱いて
			ELSE
				IF RAND:2 == 0
					PRINT クリトリス
				ELSE
					PRINT 秘所
				ENDIF
				PRINTFORM を自らの指で
				IF RAND:3 == 0
					PRINT 擦りながら
				ELSEIF RAND:2 == 0
					PRINT 弄りながら
				ELSE
					PRINT 捏ね回しながら
				ENDIF
			ENDIF
		CASE "Ｖ"
			IF RAND:2 == 0
				PRINT アソコ
			ELSE
				PRINT 秘所
			ENDIF
			IF EXP:Ｖフィスト経験 >= 5 && TALENT:処女 == 0 && ABL:Ｖ拡張 >= 4 && RAND:3 == 0
				IF RAND:2
					PRINT を自らの拳で掻き回して
				ELSE
					PRINT に自らの拳を出し入れして
				ENDIF
				PLAY_FISTING = 2
				NUM_PLAY = MAX(1, NUM_PLAY/2)
			ELSEIF RAND:3 == 0
				PRINT を自らの指で弄って
			ELSEIF RAND:2 == 0
				PRINT を自らの指で掻き回して
			ELSE
				PRINT に自らの指を出し入れして
			ENDIF
		CASE "Ａ"
			IF RAND:2 == 0
				PRINT アナル
			ELSE
				PRINT お尻の穴
			ENDIF
			IF EXP:Ａフィスト経験 >= 5 && ABL:Ａ拡張 >= 4 && RAND:3 == 0
				IF RAND:2
					PRINT を自らの拳で掻き回して
				ELSE
					PRINT に自らの拳を出し入れして
				ENDIF
				PLAY_FISTING = 2
				NUM_PLAY = MAX(1, NUM_PLAY/2)
			ELSEIF RAND:3 == 0
				PRINT を自らの指で弄って
			ELSEIF RAND:2 == 0
				PRINT を自らの指で掻き回して
			ELSE
				PRINT に自らの指を出し入れして
			ENDIF
		ENDSELECT

		PRINTFORML {NUM_PLAY}回自慰をしたようだ。

		CALL KOJO_EVENT, STR:イベント名

		PRINTFORML 自慰経験＋{NUM_PLAY}
		EXP:自慰経験 += NUM_PLAY

		PRINTFORML 絶頂経験＋{NUM_PLAY}
		EXP:絶頂経験 += NUM_PLAY
		IF TALENT:オンナ == 0
			PRINTFORML 射精経験＋{NUM_PLAY}
			EXP:射精経験 += NUM_PLAY
		ENDIF

		SELECTCASE FAV_ONANIE(SELECTCOM, "一番")
		CASE "Ｖ"
			PRINTFORML Ｖ経験＋{NUM_PLAY + PLAY_FISTING*5}
			EXP:Ｖ経験 += NUM_PLAY + PLAY_FISTING*5
			IF PLAY_FISTING
				PRINTFORML Ｖ拡張経験＋{NUM_PLAY * 2}
				EXP:Ｖ拡張経験 += NUM_PLAY * 2
			ENDIF
			PRINTFORML 快Ｖの珠＋{(NUM_PLAY + PLAY_FISTING*5) * 200}
			JUEL:快Ｖ += (NUM_PLAY + PLAY_FISTING*5) * 200
		CASE "Ａ"
			PRINTFORML Ａ経験＋{NUM_PLAY + PLAY_FISTING*5}
			EXP:Ａ経験 += NUM_PLAY + PLAY_FISTING*5
			IF PLAY_FISTING
				PRINTFORML Ａ拡張経験＋{NUM_PLAY * 2}
				EXP:Ａ拡張経験 += NUM_PLAY * 2
			ENDIF
			PRINTFORML 快Ａの珠＋{(NUM_PLAY + PLAY_FISTING*5) * 200}
			JUEL:快Ａ += (NUM_PLAY + PLAY_FISTING*5) * 200
		CASEELSE
			PRINTFORML 快Ｃの珠＋{NUM_PLAY * 500}
			JUEL:快Ｃ += NUM_PLAY * 500
		ENDSELECT

		PRINTFORML 恭順の珠＋{NUM_PLAY * 100}
		JUEL:恭順 += NUM_PLAY * 100
		PRINTFORMW 欲情の珠＋{(NUM_PLAY + PLAY_FISTING*5) * 250}
		JUEL:欲情 += (NUM_PLAY + PLAY_FISTING*5) * 250

		BASE:体力 = MAX(BASE:体力 - (NUM_PLAY + PLAY_FISTING*5)*50, 1)

		IF ABL:信頼 + ABL:欲望 + ABL:自慰中毒 >= 10
			PRINTFORML そのことを%CALLNAME:TARGET%は報告してきた。
			PRINTFORML 恥情の珠＋{(NUM_PLAY + PLAY_FISTING*5) * 200}
			JUEL:恥情 += (NUM_PLAY + PLAY_FISTING*5) * 200
		ENDIF

		SIF ABL:欲望 >= 5
			PRINTFORMW しかし何度自分を慰めてみても、%CALLNAME:PLAYER%への欲望は満たされなかった…
	ENDIF
ENDSELECT
DRAWLINE


@PLAY_SELF, ARG
#DIM LCOUNT
;起きうるイベントの判定に使う
#DIMS EVENT_TABLE

STR:イベント名 = 
;恋慕、淫乱による調教後性交渉チェック
SIF BASE:PLAYER:精力 == 0 || TALENT:淫乱 + TALENT:恋慕 == 0
	RETURN 0
;体力を使い果たした
SIF BASE:体力 < 500 || BASE:精力 <= 0
	RETURN 0
;性欲など満足
SIF COND("満足")
	RETURN 0

;助手とのプレイが発生するか
IF ARG == ASSI && ABL:欲望 >= 5
	;どちらかが男だとダメ
	SIF TALENT:オトコ || TALENT:ASSI:オトコ
		RETURN 0
	;二人とも両刀じゃないとダメ
	SIF TALENT:両刀 == 0 || TALENT:ASSI:両刀 == 0
		RETURN 0
	;二人ともレズ経験豊富じゃないとダメ
	SIF EXP:レズ経験 < 300 || EXP:ASSI:レズ経験 < 300
		RETURN 0
	;相性がよいことが条件
	SIF RELATION:(NO:ASSI) < 120 || RELATION:ASSI:(NO:TARGET) < 120
		RETURN 0
ENDIF

VARSET EVENT_TABLE

IF ARG == PLAYER
	;Ｖセックス
	CALL COM_ABLE65
	SIF RESULT && PENIS(PLAYER) && ABL:Ｖ感覚 >= 3 && TALENT:処女 + TALENT:PLAYER:童貞 == 0 && COND("女性主導生セックス")
		EVENT_TABLE += "調教後セックス/"
	;Ａセックス
	CALL COM_ABLE75
	SIF RESULT && PENIS(PLAYER) && EXP:PLAYER:Ａ挿入経験 && COND("Ａ熟達")
		EVENT_TABLE += "調教後Ａセックス/"
	;レズプレイ
	CALL COM_ABLE80
	SIF RESULT && COND("性別嫌悪") == 0
		EVENT_TABLE += "調教後レズプレイ/"
ELSEIF ARG == TARGET
	;自慰
	SIF ABL:自慰中毒 >= 3 && COND("身嗜み：自慰禁止") == 0 && PLAYER == MASTER
		EVENT_TABLE += "調教後自慰/"
ENDIF

STR:イベント名 = %TEXTR(EVENT_TABLE)%

RETURN 0

