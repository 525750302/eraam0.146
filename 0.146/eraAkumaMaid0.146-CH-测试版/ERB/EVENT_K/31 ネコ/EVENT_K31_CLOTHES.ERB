;-------------------------------------------------
;このキャラ独自の衣装を設定する。制服、体操服、水着は既存のものから選ぶのが楽です
;-------------------------------------------------
@CLOTHES_K31, ARGS

SELECTCASE ARGS
;高めの頻度で着用します。設定しなくてもかまいません
CASE "下着"
	CSTR:パンツ = ローライズパンツ/おとなしい色/
	CSTR:ブラ = タンクトップ/おとなしい色/
	
;衣装に関わらず常に付けているアクセサリです。設定しなくてもかまいません
CASE "アクセサリ"
	CALL SET_OTHER, "ペンダント"
;学園での普段着です
CASE "制服"
	CALL SET_SHIRT, "ジャージ"
	CALL SET_SKIRT, "チェックスカート"
	CALL SET_SHOES, "スニーカー"
	IF COND("秋") + COND("冬")
		CALL SET_OUTER, "スパッツ"
		CALL SET_SOCKS, "ストッキング"
	ELSE
		CALL SET_OUTER, "ハーフパンツ"
		CALL SET_SOCKS, "ソックス"
	ENDIF

;学園の体育の授業で着ます。この格好のまま部室に現れたりもする
CASE "体操服"
	CALL SET_COSTUME, "体操服ジャージ"
;学園の水泳の授業で着ます。この格好のまま部室に現れたりもする
CASE "水着"
	CALL SET_COSTUME, "スク水"

;自宅で着ます
CASE "普段着"
	IF COND("冬") && PLACE("自宅")
		CALL SET_SHIRT, "どてら"
		CALL SET_OUTER, "ジャージズボン"
	ELSEIF (COND("秋") || COND("冬")) && COND("夜")
		CALL SET_SHIRT, "前開きパーカー/灰色"
		CALL SET_OUTER, "ホットパンツ"
		CALL SET_SOCKS, "ストッキング"
	ELSEIF (COND("夏") || RAND:2) && CFLAG:シルシ <= 1
		CALL SET_SHIRT, "タンクトップ"
		CALL SET_OUTER, "ホットパンツ"
	ELSE
		IF RAND:2 && TALENT:恋慕 && CFLAG:シルシ <= 1
			CALL SET_SHIRT, "オフショルダー"
			CALL SET_OUTER, "ホットパンツ"
			CALL SET_SOCKS, "ストッキング"
		ELSE
			CALL SET_SHIRT, "変な柄のＴシャツ"
			CALL SET_OUTER, "ホットパンツ"
			CALL SET_SOCKS, "ソックス"
		ENDIF
	ENDIF

;自分の部屋(notあなたの部屋)で着ます
CASE "部屋着"
	IF COND("冬") && PLACE("自宅")
		CALL SET_SHIRT, "パーカー/灰色"
		CALL SET_OUTER, "ホットパンツ"
	ELSEIF COND("夏") && COND("夜") == 0
		CALL SET_SHIRT, ""
		CALL SET_OTHER, "前開きパーカー/灰色"
		CALL SET_BRA, ""
	ELSE
		CALL SET_SHIRT, "タンクトップ"
		CALL SET_OUTER, "ホットパンツ"	
	ENDIF
;デート以外で家から外に出る際に着ます。ラブホ呼出とかね
CASE "外出着"
	CALL SET_SHIRT, "前開きパーカー/灰色"
	CALL SET_OUTER, "ホットパンツ"
	IF COND("冬") || COND("秋")
		CALL SET_SOCKS, "ストッキング"
	ELSE
		CALL SET_SOCKS, "ソックス"
	ENDIF	
	CALL SET_SHOES, "スニーカー"
;もちろんデートの際に着ます
CASE "デート衣装"
	IF TALENT:恋慕
		IF COND("冬")
			CALL SET_SHIRT, "前開きパーカー/灰色"
			CALL SET_OUTER, "ホットパンツ"
			CALL SET_SOCKS, "ストッキング"
			CALL SET_SHOES, "スニーカー"
		ELSE
			;75％変Ｔ
			IF RAND:4
				CALL SET_SHIRT, "変な柄のＴシャツ"
				CALL SET_OUTER, "ホットパンツ"
				CALL SET_SOCKS, "ソックス"
				CALL SET_SHOES, "スニーカー"
			ELSEIF RAND:3
				CALL SET_SHIRT, "オフショルダー"
				CALL SET_OUTER, "ホットパンツ"
				CALL SET_SOCKS, "ストッキング"
				CALL SET_SHOES, "スニーカー"
			ELSE
				CALL SET_SHIRT, "サマードレス/黒"
				CALL SET_SHOES, "ミュール"
				CALL SET_OTHER, "ピクチャーハット"
			ENDIF
		ENDIF
	ELSE
		CALL SET_SHIRT, "変な柄のＴシャツ"
		CALL SET_OUTER, "ホットパンツ"
		CALL SET_SOCKS, "ソックス"
		CALL SET_SHOES, "スニーカー"
	ENDIF
ENDSELECT

;変な柄処理
IF CHECK_SHIRT("変な柄")
	IF RAND:4 == 0
		CSTR:服 += @"柄：＼（^o^）／/ＡＡ/"
	ELSE
		CSTR:服 += @"柄：%TEXTR("週休七日/猫じゃらせない/果報は寝て待て/恋人ではありません/三食昼寝付き/逃走本能/朝三暮四/三寒四温/一日三食")%/"
	ENDIF
ENDIF

;-------------------------------------------------
;時間帯や場所に応じた髪型、表情の変化を記述する。設定しなくてもかまいません
;-------------------------------------------------
@MAIDFACE_K31
IF CHECK_SHIRT("オフショル")
	CSTR:アレンジ = おさげ	
ELSEIF PLACE("自宅")
	CSTR:アレンジ = ポニーテール
ELSE
	CALL SETFLAG, "普段の髪型復元", TARGET
ENDIF
