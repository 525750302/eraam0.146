@COM2
;アナル愛撫
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

RETURN 1

@FLAG_COM2, ARG
IF PREVCOM == 5
	CALL FLAG_COM5, 30
ELSEIF PREVCOM == 6
	CALL FLAG_COM6, 30
ENDIF

IF TEQUIP:アナルバイブ
	CALL FLAG_COM33, 130
ELSEIF CHECK("Ａ指挿入れ")
	CALL FLAG_INSERTION, "Ａ指挿入れ", ARG, 100
ENDIF

@SOURCE_COM2, ARG
IF PREVCOM == 5
	CALL SOURCE_COM5, 30
ELSEIF PREVCOM == 6
	CALL SOURCE_COM6, 30
ENDIF
IF TEQUIP:アナルバイブ
	CALL SOURCE_INSERTION, "Ａバイブ", ARG, 130
ELSEIF CHECK("Ａ指挿入れ")
	CALL SOURCE_INSERTION, "Ａ指挿入れ", ARG, 100
ELSE
	CALL VARSET_COMF

	CALL SOURCE_DOWNBASE_HP, 2

	SOURCE:0:情愛 = ABL:Ａ感覚 * 100
	SOURCE:0:露出 = 400
	SOURCE:0:逸脱 = 1000

	;ABL:Ａ感覚をみる
	SOURCE:0:Ａ快楽 = CALCF("Ａ刺激", 1)
	;調教者の指使いをみる
	SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "指使い", PLAYER)

	;EXP:Ａ経験に応じたＡ快感、苦痛、恐怖、逸脱(反感)補正
	SELECTCASE EXP:Ａ経験
	CASE IS >= 300
		TIMES SOURCE:0:Ａ快楽, 1.60
		TIMES SOURCE:0:痛み, 0.50
		TIMES SOURCE:0:恐れ, 0.20
		TIMES SOURCE:0:逸脱, 0.20
	CASE IS >= 150
		TIMES SOURCE:0:Ａ快楽, 1.40
		TIMES SOURCE:0:痛み, 0.60
		TIMES SOURCE:0:恐れ, 0.40
		TIMES SOURCE:0:逸脱, 0.60
	CASE IS >= 50
		TIMES SOURCE:0:Ａ快楽, 1.20
		TIMES SOURCE:0:痛み, 0.80
		TIMES SOURCE:0:恐れ, 0.70
	CASE IS >= 20
		TIMES SOURCE:0:逸脱, 1.20
	CASE IS >= 5
		TIMES SOURCE:0:Ａ快楽, 0.50
		TIMES SOURCE:0:痛み, 1.50
		TIMES SOURCE:0:恐れ, 1.50
		TIMES SOURCE:0:逸脱, 1.50
	CASEELSE
		TIMES SOURCE:0:Ａ快楽, 0.10
		TIMES SOURCE:0:痛み, 2.00
		TIMES SOURCE:0:恐れ, 2.00
		TIMES SOURCE:0:逸脱, 2.00
	ENDSELECT

	;PALAM:欲情をみる
	SELECTCASE PALAM:欲情
	CASE IS < PALAMLV:1
		TIMES SOURCE:0:屈従, 0.30
	CASE IS < PALAMLV:2
		TIMES SOURCE:0:屈従, 0.50
	CASE IS < PALAMLV:3
		TIMES SOURCE:0:屈従, 0.80
	CASE IS < PALAMLV:4
	CASE IS >= PALAMLV:4
		TIMES SOURCE:0:屈従, 1.20
	ENDSELECT

	SIF TALENT:Ａ性向
		CALL CALC, "好みのプレイ", SOURCE:0:Ａ快楽

	CALL CALC, "調教ソース", ARG
ENDIF

@TRAIN_MESSAGE_COM2
IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	IF TEQUIP:アナルバイブ && CHECK_COM("結腸責め")
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の結腸の奥深くを%NAMES("Ａバイブ", TARGET)%で念入りにほぐし続けた…
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%のアナルを念入りにほぐした…
	ENDIF
;インサート中
ELSEIF INSERT("Ｖインサート") || INSERT("尿道インサート")
	IF INSERT("騎乗位")
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%に腰を振らせながら、
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は腰を突き入れながら、%CALLNAME:TARGET%の
	ENDIF
	IF TEQUIP:アナルバイブ
		IF CHECK_COM("結腸責め")
			PRINTFORMW お尻の穴から伸びるバイブを%TEXTR("出し入れして結腸の奥深くを擦り上げた/弄んで大腸の奥深くを刺激した/掴んで脇腹に動きが浮き出る程にグリグリ掻き回した")%…
		ELSE
			PRINTFORMW お尻の穴から伸びるバイブを%TEXTR("出し入れした/弄んだ/掴んでグリグリ掻き回した")%…
		ENDIF
	ELSEIF ABL:Ａ拡張 >= 2 && CHECK("Ａ指挿入れ") && INSERT("後背位")
		PRINTFORMW お尻の穴に束ねた中指と薬指を根元まで挿入れてグニグニと粘膜を揉み解した…
	ELSE
		PRINTFORMW アナルに悪戯した…
	ENDIF
;Ａインサート中
ELSEIF INSERT("Ａインサート")
	PRINTFORM %CALLNAME:PLAYER%は、
	IF TALENT:PLAYER:ふたなり || TALENT:PLAYER:オトコ
		PRINT 肉棒
	ELSE
		PRINT ディルドー
	ENDIF
	PRINTFORMW の動きに合わせて引きずり動かされている%CALLNAME:TARGET%のアナルに悪戯した…
;それ以外
ELSE
	SIF PLAYER != MASTER
		PRINTFORM %CALLNAME:PLAYER%は
	IF TEQUIP:キス
		PRINTFORM %CALLNAME:TARGET%%TEXTS("キス継続助詞付き")%、
		CALL SETFLAG, "追加キスフラグ", TARGET
	ELSEIF PREVCOM == 5
		PRINTFORM %CALLNAME:TARGET%の胸を愛撫しながら、
	ELSE
		;シックスナイン
		IF TEQUIP:シックスナイン
			PRINTFORM %TEXTS("６９")%
		;プレイを受け入れ可能かつ好色
		ELSEIF CHECK("Ａ指挿入れ") && TALENT:好色 && RAND:2 == 0
			PRINT 妖艶に微笑んでいる
		;縄
		ELSEIF TEQUIP:拘束
			PRINT 身動きの取れない
		ELSEIF CHECK("Ａ指挿入れ") && COND("脚の使用")
			;恥じらいで快Ａ or欲情が低いなら追加したり
			SIF TALENT:恥じらい && (PALAM:快Ａ <= PALAMLV:3 || PALAM:欲情 <= PALAMLV:4)
				PRINT 恥ずかしがりつつ
			PRINT 率先して脚を開いている
		;Ａ感覚1以下or反抗的or恥じらい
		ELSEIF (ABL:Ａ感覚 <= 1 || TALENT:反抗的 || TALENT:恥じらい) && RAND:3 >= 1
			;恥じらいで快Ａ or欲情が低いなら追加したり
			SIF TALENT:恥じらい && (PALAM:快Ａ <= PALAMLV:3 || PALAM:欲情 <= PALAMLV:4)
				PRINT 羞恥に震える
		ENDIF
		PRINTFORM %CALLNAME:TARGET%の
		;ラミアにお尻は無い方針で
		IF COND("脚が無い")
			SIF CHECK_CLO("Ａ露出") == 0
				CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
		;アナルが露出していないで、パンツをずらしていないなら自動的にずらす
		ELSE
			SIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
				CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET

			;小柄なら
			IF TALENT:小柄
				PRINT 小さな
			;Ａ性向なら
			ELSEIF TALENT:Ａ性向 && RAND:3 >= 1
				PRINT 柔らかい
			;淫乱なら
			ELSEIF TALENT:淫乱 && RAND:3 >= 1
				PRINT 弾力のある
			;それ以外
			ELSEIF RAND:2 == 0
				PRINT 形の良い
			ENDIF
			;乱数分岐
			IF RAND:2 == 0
				PRINT お尻を
			ELSE
				PRINT 尻たぶを
			ENDIF
			;Ａ感覚1以下or反抗的or恥じらい
			SIF (ABL:Ａ感覚 <= 1 || TALENT:反抗的 || TALENT:恥じらい)&& TALENT:恋慕 == 0 && RAND:3 >= 1
				PRINT 無理やり
			;恋慕orＡ指挿入れを楽しめる状態かつ信頼3以上で縛ってないと相手にお尻を広げさせます
			IF CHECK("Ａ指挿入れ") + TALENT:恋慕 && ABL:信頼 >= 3 && TEQUIP:拘束 == 0 && RAND:2 == 0
				PRINT 広げさせると、
			ELSE
				PRINT 開くと、
			ENDIF
		ENDIF
	ENDIF
	;アナルバイブorパールorアナルプラグorバルーンか、直前にアナル系調教かで分岐。
	IF COND("アナル栓")
		PRINT アナル
		;乱数分岐
		IF COND("アナルプラグ")
			PRINT を塞いでいる
		;道端のメロンパン加筆分
		ELSEIF ABL:Ａ拡張 - SIZE("Ａバイブ", TARGET) >= 2
			PRINT から落ちないようにしっかりと咥えられた
		;ここまで加筆分
		ELSEIF RAND:3 == 0
			PRINT をみっちりと広げている
		ELSEIF RAND:2 == 0
			PRINT から突き出た
		ELSE
			PRINT で咥えたままの
		ENDIF
		;Ａ潤3以上で追加
		SIF PALAM:Ａ潤 >= PALAMLV:3 && TEQUIP:アナルバイブ
			PRINT 粘液にまみれた
		IF COND("アナルパール")
			PRINT アナルパールを
		ELSEIF TEQUIP:アナルバイブ
			PRINT アナルバイブを
		ELSEIF COND("アナルプラグ")
			PRINT アナルプラグを
		ENDIF
		;乱数分岐
		IF COND("アナルプラグ")
			PRINT グリグリと
		ELSEIF RAND:3 == 0
			PRINT ズボズボと
		ELSEIF RAND:2 == 0
			PRINT 優しく
		ELSE
			PRINT ゆっくりと
		ENDIF
		IF CHECK_COM("結腸責め")
			PRINTFORMW 前後させた。

			IF EXP:結腸経験 >= 5 && RAND:3 == 0
				PRINTFORMW 繰り返される深すぎる挿入行為に%CALLNAME:TARGET%は自身のＳ状結腸の癖を掴んできたのか、出し入れの引っ掛かりを巧みに軽減させてスムーズに奥深くまで呑み込んでいく…
			ELSEIF SIZE("Ａバイブ") >= 2 && RAND:2
				PRINTFORMW お腹側を擦り上げるようにして結腸内を出し入れされる\@ SIZE("Ａバイブ") >= 3 ? バイブの凶悪な形状 # 野太いバイブの形 \@が、%CALLNAME:TARGET%の左脇腹\@ ABL:Ａ拡張 >= 6 ? からおへその上にかけて # 付近に \@浮き上がる…
			ELSE
				PRINTFORM 腕ほどの長さの異物を出し入れされる
				IF TALENT:結腸性感
					PRINTFORM のがたまらないのか、%CALLNAME:TARGET%は%TEXTR(@"深くゆっくりと呼吸をし\/%CALLNAME:PLAYER%に腰を突き出し\/口を半開きにし")%ながら
					PRINTFORMW %TEXTR("結腸/体の奥深く/気持ちの良い場所")%%TEXTR("に意識を集中させ/を犯される快感に夢中になっ/を虐めて貰える幸せを噛みしめ")%ている…
				ELSE
					PRINTFORMW 事に慣れていない%CALLNAME:TARGET%は、この異常すぎる行為がもたらす未知の感覚に%TEXTR("目を白黒させて/戸惑って/身体を強張らせて")%いる…
				ENDIF
			ENDIF
		ELSEIF COND("アナルパール") || COND("アナルプラグ")
			PRINTFORMW 弄んだ…
		ELSE
			PRINTFORMW 出し入れした…
		ENDIF
	ELSE
		PRINTFORM %TEXTS("アナル")%アヌス
		;アナル系調教やＡ性向やＡ経験150以上にあわせて乱数分岐
		IF (PREVCOM == 33 || (PREVCOM >= 60 && PREVCOM <= 69) || (PREVCOM >= 136 && PREVCOM <= 138) || PREVCOM == 142 || PREVCOM == 143 || PREVCOM == 202 || EXP:Ａ経験 >= 150) && RAND:3
			PRINT に指を何本も入れて
		ELSEIF (PREVCOM == 33 || (PREVCOM >= 60 && PREVCOM <= 69) || (PREVCOM >= 136 && PREVCOM <= 138) || PREVCOM == 142 || PREVCOM == 143 || PREVCOM == 202 || EXP:Ａ経験 >= 150) && RAND:2
			PRINT に指を根元まで入れて
		ELSEIF CHECK("Ａ指挿入れ")
			PRINTFORM に指を入れて
		ELSE
			PRINTFORM に指を這わせて
		ENDIF
		IF CUP:屈服 < 500 && (PREVCOM == 33 || (PREVCOM >= 60 && PREVCOM <= 69) || (PREVCOM >= 136 && PREVCOM <= 138) || PREVCOM == 142 || PREVCOM == 143 || PREVCOM == 202 || EXP:Ａ経験 >= 150) && RAND:3
			PRINT ズボズボと
		ELSEIF RAND:3 == 0 && CHECK("Ａ指挿入れ")
			PRINT 激しく
		ELSEIF RAND:2 == 0
			PRINT 優しく
		ELSE
			PRINT くるくると穴のふちに沿って
		ENDIF
		;乱数分岐
		IF RAND:3 == 0
			PRINTFORMW 弄った…
		ELSEIF RAND:2 && CHECK("Ａ指挿入れ")
			PRINTFORMW 出し入れした…
		ELSE
			PRINTFORMW ほぐした…
		ENDIF
	ENDIF
ENDIF

