@COM60
;正常位
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM60, ARG
CALL FLAG_INSERTION, "Ｖ挿入", ARG, 100

@SOURCE_COM60, ARG
CALL SOURCE_INSERTION, "Ｖ挿入", ARG, 100

CALL VARSET_COMF

SOURCE:0:露出 = 200
SOURCE:0:情愛 = 500

;根元まで受け入れられる場合にはクリを擦られてしまう
IF ABL:Ｖ拡張 > SIZE("阴茎", PLAYER) && PENIS(TARGET) == 0
	;ABL:Ｃ感覚をみる
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 2, 30)
	;調教者の腰使いをみる
	SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "腰靈活", PLAYER)
ENDIF

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:情愛, 0.30
CASE IS < PALAMLV:2
	TIMES SOURCE:0:情愛, 0.60
CASE IS < PALAMLV:3
	TIMES SOURCE:0:情愛, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:情愛, 1.50
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:情愛, 1.80
ENDSELECT

CALL CALC, "調教ソース", ARG

@TRAIN_MESSAGE_COM60
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT
CALL TRAIN_MESSAGE_INSERTION
SIF LINECOUNT > MEMO_LINECOUNT
	RETURN 1

;前回、挿入前に暴発してしまった
IF PREVCOM == TNUM("挿入前暴発") && COND("前Ｔ調教者") == PLAYER
	PRINTFORM 重新振作起來的%CALLNAME:PLAYER%、
ELSE
	PRINTFORM %CALLNAME:PLAYER%将
ENDIF
IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
	CALL ADD_TEXT, "性器から性器へ"
	PRINTFORM 將%CALLNAME:TARGET%的%TEXTS("Ｖ挿入前")%
;穴開きでないパンツをはいていて、ずらされていないなら、必ずずらす
ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
	PRINTFORM %CALLNAME:TARGET%的
	CALL SETFLAG, "ボトムを露出：テ接吻ト有些", TARGET
	PRINTFORM 、%TEXTS("Ｖ挿入前")%
;巨根分岐
ELSEIF ABL:PLAYER:施虐属性 && SIZE("阴茎", PLAYER) >= 2 && COND("視力") && CHOSECOMS("Ｖ挿入") == 0
	PRINTFORM %TEXTS("巨根：視覚", PLAYER)%放到腹部上方
	PRINTFORM 將%TEXTR("會侵犯到多深的位置/會從何處侵入/如果連根插入會讓這美麗的身體變成怎樣")%的事實
	PRINTFORM 明确地告知%CALLNAME:TARGET%之后、挺直腰肢將%TEXTS("Ｖ挿入前")%
ELSE
	PRINTFORM 將%CALLNAME:TARGET%的%TEXTS("Ｖ挿入前")%
ENDIF

IF TEQUIP:接吻
	PRINTFORM 在熱情地親吻著的同時、
	CALL SETFLAG, "追加接吻フラグ", TARGET
ELSEIF NUM("回合数") == 0
	PRINTFORM %TEXTR("完全沒有前戲的/沒有任何準備的")%
ELSE
	SIF PREVCOM == 13
		PRINT 保持著被手指擴張著的姿勢、
	SIF PALAM:Ｖ潤 < PALAMLV:4 && (PREVCOM == 91 || PREVCOM == 99 || PREVCOM == 140) && COND("前Ｔ調教者") == PLAYER && RAND:2 == 0
		PRINT 用被唾液潤滑準備完畢的陰莖、来
ENDIF
IF CHOSECOM(SELECTCOM) == 0 && CHOSECOMS("挿入") && SIZE("阴茎", PLAYER) >= 2 && (TALENT:娇小 || SIZE("阴茎", PLAYER) >= 3)
	PRINTFORMW 深深地貫穿、不斷變動著戳刺的位置來與子宮口啄吻著……
ELSE
	PRINTFORMW 深深地貫穿了……
ENDIF


@EQUIP_COM60
#DIM MEMO_SELECTCOM
#DIM MOTIVATION

;強度（通常は20）
MOTIVATION = 20

SELECTCASE SELECTCOM
CASE 60 TO 89, 120 TO 129
	RETURN 0
;何もしない
CASE 139
	IF COND("疲労")
		RETURN 0
	ELSE
		MOTIVATION = 5
		;騎乗位の場合はあんまり変わらない
		IF INSERT("騎乗位")
			MOTIVATION = 20
		;PLAYERが騎乗位している側
		ELSEIF INSERT("騎乗位")
			MOTIVATION = 5
		;PLAYERが犯されている時
		ELSEIF INSERT("挿入中")
			MOTIVATION = 20
		ENDIF
	ENDIF
ENDSELECT

MEMO_SELECTCOM = SELECTCOM


IF INSERT("Ｖ挿入")
	IF INSERT("Ｖ挿入", TARGET, PLAYER)
		SELECTCOM = INSERT("Ｖ対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
	ENDIF
	IF INSERT("Ｖ挿入", TARGET, PLAYER:1)
		SWAP PLAYER, PLAYER:1
		SELECTCOM = INSERT("Ｖ対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
		SWAP PLAYER, PLAYER:1
	ENDIF
ENDIF
IF INSERT("Ａ挿入")
	IF INSERT("Ａ挿入", TARGET, PLAYER)
		SELECTCOM = INSERT("Ａ対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
	ENDIF
	IF INSERT("Ａ挿入", TARGET, PLAYER:1)
		SWAP PLAYER, PLAYER:1
		SELECTCOM = INSERT("Ａ対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
		SWAP PLAYER, PLAYER:1
	ENDIF
ENDIF
IF INSERT("尿道挿入")
	IF INSERT("尿道挿入", TARGET, PLAYER)
		SELECTCOM = INSERT("尿道対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
	ENDIF
	IF INSERT("尿道挿入", TARGET, PLAYER:1)
		SWAP PLAYER, PLAYER:1
		SELECTCOM = INSERT("尿道対応指令")
		CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
		CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
		CALL SAMEN_CHECK, MOTIVATION
		SWAP PLAYER, PLAYER:1
	ENDIF
ENDIF
IF INSERT("Ｖ挿入", PLAYER, TARGET)
	SELECTCOM = INSERT("Ｖ対応指令", PLAYER)
	CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
	CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
	CALL SAMEN_CHECK, MOTIVATION
ENDIF
IF INSERT("Ａ挿入", PLAYER, TARGET)
	SELECTCOM = INSERT("Ａ対応指令", PLAYER)
	CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
	CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
	CALL SAMEN_CHECK, MOTIVATION
ENDIF
IF INSERT("尿道挿入", PLAYER, TARGET)
	SELECTCOM = INSERT("尿道対応指令", PLAYER)
	CALLFORM SOURCE_COM{SELECTCOM}, MOTIVATION
	CALLFORM FLAG_COM{SELECTCOM}, MOTIVATION
	CALL SAMEN_CHECK, MOTIVATION
ENDIF

SELECTCOM = MEMO_SELECTCOM

RETURN 1

@EQUIP_COM60_2
#DIM A_JUST
#DIM V_JUST
#DIM U_JUST

CALL TEXT, "挿入具合の描写"

V_JUST = 0
A_JUST = 0
U_JUST = 0

;３ＰなどがあるのでTARGETとPLAYERを省略せずに記入（A_JUSTとJ_JUSTは片方しか1以上にはならない）
SIF INSERT("Ｖ挿入", TARGET, PLAYER) && CHOSECOMS("Ｖ挿入") == 0
	V_JUST = CHECK("Ｖ完美契合")
SIF INSERT("Ａ挿入", TARGET, PLAYER) && CHOSECOMS("Ａ挿入") == 0
	A_JUST = CHECK("Ａ完美契合")
SIF INSERT("尿道挿入", TARGET, PLAYER) && CHOSECOMS("尿道挿入") == 0
	U_JUST = CHECK("尿道完美契合")

IF V_JUST || A_JUST || U_JUST
	CALL PRINT_STRL, "爱心粉紅_＜完美契合！＞"

	SELECTCASE MAX(V_JUST, A_JUST, U_JUST)
	CASE IS >= 90
		PRINT 被完全接納的
	CASE IS >= 75
		PRINT 被反复插入的
	CASEELSE
		PRINT 被整個侵入的
	ENDSELECT
	PRINTFORM 的%CALLNAME:PLAYER%的陰莖所%TEXTR("變成陰莖的形狀了/記憶住侵犯的感覺了/變成專用的飛機杯了")%的
	IF A_JUST
		PRINTFORM %CALLNAME:TARGET%的肛門
	ELSEIF U_JUST
		PRINTFORM %CALLNAME:TARGET%的尿道
	ELSE
		PRINTFORM %CALLNAME:TARGET%的膣穴
	ENDIF
	PRINTFORML %TEXTR("因為無法言喻的絕妙快感而溶化了/毫無間隙地緊緊纏繞著/如同獨立的活物一樣緊緊吸附著配合起來")%！
	;CALL PRINT_STRL, "ハートピンク_H_お互いの快楽にボーナスが発生しています_ハートピンク_H"
ENDIF

VARSET LOCALS
IF INSERT("挿入") || SELECTCOM == 80
	IF COND("喜欢的体位中")
		SETCOLOR DEF_COLOR("爱心粉紅")
		LOCALS = 喜欢的体位
	ENDIF

	SELECTCASE SELECTCOM
	CASE 60 TO 89
	;何もしない
	CASE 139
		SIF COND("疲労")
			RETURN 0
	CASEELSE
		SIF LOCALS != ""
			LOCALS = 喜欢的体位で
		IF INSERT("二穴挿入")
			LOCALS = %LOCALS%二穴挿入中
			EXP:Ｖ経験 += 1
			LOCALS:1 = Ｖ経験＋１
			EXP:Ａ経験 += 1
			LOCALS:2 = Ａ経験＋１
		ELSEIF INSERT("Ｖ挿入")
			LOCALS = %LOCALS%挿入中
			EXP:Ｖ経験 += 1
			LOCALS:1 = Ｖ経験＋１
		ELSEIF INSERT("Ａ挿入")
			LOCALS = %LOCALS%Ａ挿入中
			EXP:Ａ経験 += 1
			LOCALS:2 = Ａ経験＋１
		ELSEIF INSERT("尿道挿入")
			LOCALS = %LOCALS%尿道挿入中
			EXP:尿道経験 += 1
			LOCALS:2 = 尿道経験＋１
		ENDIF
	ENDSELECT
ENDIF

SELECTCASE SELECTCOM
CASE 120 TO 129
CASEELSE
	IF INSERT("Ｖ挿入", PLAYER, TARGET)
		LOCALS = 逆挿入中
		EXP:PLAYER:Ｖ経験 += 1
	ELSEIF INSERT("Ａ挿入", PLAYER, TARGET)
		LOCALS = 逆Ａ挿入中
		EXP:PLAYER:Ａ経験 += 1
	ENDIF
ENDSELECT

SIF LOCALS != ""
	PRINTFORML ＜%LOCALS%＞
RESETCOLOR
SIF LOCALS:1 != ""
	PRINTFORML %LOCALS:1%
SIF LOCALS:2 != ""
	PRINTFORML %LOCALS:2%

RETURN 1
