@COM148
;ピアッシング
PRINTL ピアッシング

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 148

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = ピアッシング

RETURN 1

@FLAG_COM148, ARG
;クリorラビアピアスなら、アソコを露出させる
IF COND("クリピアスフラグ") + COND("ラビアピアスフラグ") + COND("尿道ピアスフラグ") && !CHECK_CLO("Ｖ露出")
	CALL SET_PANTIES, ""
	CALL SET_BODYS, ""
	CALL SET_OUTER, ""
ENDIF

STAIN:PLAYER:手 = 0
IF COND("クリピアスフラグ")
	STAIN:Ｖ = SET_STAIN(0, "Ｖ")
ELSEIF COND("ラビアピアスフラグ") || COND("尿道ピアスフラグ")
	STAIN:Ｖ = SET_STAIN(0, "Ｖ")
ELSEIF COND("ニプルピアスフラグ")
	STAIN:Ｂ = 0
ENDIF

@SOURCE_COM148, ARG
;外した場合
SIF COND("ピアスＯＮフラグ") == 0
	RETURN 1

CALL SOURCE_SMPLAY, "ピアッシング", ARG

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 148

;基本はへそピアスの時
;初めてつける時は痛みのせいで、体力消費が凄いことになります

SOURCE:0:痛み = 3000
SOURCE:0:屈従 = 1000
SOURCE:0:逸脱 = 3000

IF COND("クリピアスフラグ")
	SOURCE:0:痛み += 17000
	SOURCE:0:屈従 += 9000
	SOURCE:0:逸脱 += 17000
ELSEIF COND("ラビアピアスフラグ") || COND("尿道ピアスフラグ")
	SOURCE:0:痛み += 7000
	SOURCE:0:屈従 += 9000
	SOURCE:0:逸脱 += 12000
ELSEIF COND("ニプルピアスフラグ")
	SOURCE:0:痛み += 12000
	SOURCE:0:屈従 += 9000
	SOURCE:0:逸脱 += 12000
ELSEIF COND("へそピアスフラグ")

ENDIF

;ABL:マゾっ気をみる
SELECTCASE ABL:マゾっ気
CASE 0
CASE 1
	TIMES SOURCE:0:屈従, 1.20
	TIMES SOURCE:0:逸脱, 0.90
CASE 2
	TIMES SOURCE:0:屈従, 1.40
	TIMES SOURCE:0:逸脱, 0.80
CASE 3
	TIMES SOURCE:0:屈従, 1.60
	TIMES SOURCE:0:逸脱, 0.50
CASE 4
	TIMES SOURCE:0:屈従, 1.80
	TIMES SOURCE:0:逸脱, 0.30
CASEELSE
	TIMES SOURCE:0:屈従, 2.00
	TIMES SOURCE:0:逸脱, 0.10
ENDSELECT

;麻酔を使ったら痛み激減
SIF COND("鎮痛麻酔フラグ")
	TIMES SOURCE:0:痛み, 0.01

;ビデオ撮影中
IF TEQUIP:ビデオカメラ
	TIMES SOURCE:0:屈従, 1.50
	TIMES SOURCE:0:逸脱, 1.50
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM148
#DIM MEMO_LINECOUNT
#DIM LCOUNT
;ピアスの形状
#DIMS PIERCES
;ピアスの場所
#DIMS PART_PIERCES

VARSET PART_PIERCES

REDRAW 0
MEMO_LINECOUNT = LINECOUNT

IF ABL:マゾっ気 < 3 && FIRSTCOM(SELECTCOM) == 0
	PRINTFORML このプレイはまだ早い気もしますが……今回はどうしましすか？
ELSE
	PRINTFORML さて、今回はどうしようか？
ENDIF

PRINTFORML  [ 0] \@ PENIS(TARGET) ? ペニス # クリトリス \@\@ TALENT:クリピアス ? からピアスを外す # にピアスをつける \@
SIF TALENT:オトコ
	SETCOLOR DEF_COLOR("灰色")
PRINTFORML  [ 1] 小陰唇\@ TALENT:ラビアピアス ? からピアスを外す # にピアスをつける \@
RESETCOLOR

SIF COND("真性陥没乳首")
	SETCOLOR DEF_COLOR("灰色")
PRINTFORML  [ 2] 乳首\@ TALENT:ニプルピアス ? からピアスを外す # にピアスをつける \@
RESETCOLOR

SIF ABL:尿道拡張 < 0
	SETCOLOR DEF_COLOR("灰色")
PRINTFORML  [ 3] 尿道口\@ TALENT:尿道ピアス ? からピアスを外す # にピアスをつける \@
RESETCOLOR

PRINTFORML  [ 4] おへそ\@ TALENT:へそピアス ? からピアスを外す # にピアスをつける \@
PRINTFORML [100] やめる

CALL INPUT_SELECT, 5, 100

IF RESULT == 100
	TFLAG:コマンドキャンセル = 1
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 0
	CALL SETFLAG, "クリピアスフラグ", TARGET
	SIF TALENT:クリピアス == 0
		CALL SETFLAG, "ピアスＯＮフラグ", TARGET
	CALL DEL_CEVENT_GROUP, "クリピアス"
	IF PENIS(TARGET)
		PART_PIERCES = ペニス
	ELSE
		PART_PIERCES = クリトリス
	ENDIF
ELSEIF RESULT == 1
	IF TALENT:オトコ
		PRINTFORMW ※オトコだと無理です※
	ELSE
		CALL SETFLAG, "ラビアピアスフラグ", TARGET
		SIF TALENT:ラビアピアス == 0
			CALL SETFLAG, "ピアスＯＮフラグ", TARGET
		CALL DEL_CEVENT_GROUP, "ラビアピアス"
		PART_PIERCES = 小陰唇
	ENDIF
ELSEIF RESULT == 2
	IF COND("真性陥没乳首")
		PRINTFORMW ※真性陥没乳首だと着けられません※
	ELSE
		CALL SETFLAG, "ニプルピアスフラグ", TARGET
		SIF TALENT:ニプルピアス == 0
			CALL SETFLAG, "ピアスＯＮフラグ", TARGET
		CALL DEL_CEVENT_GROUP, "ニプルピアス"
		PART_PIERCES = 乳首
	ENDIF
ELSEIF RESULT == 3
	IF ABL:尿道拡張 < 0
		PRINTFORMW ※尿道拡張が0LV以上でないと着けられません※
	ELSE
		CALL SETFLAG, "尿道ピアスフラグ", TARGET
		SIF TALENT:尿道ピアス == 0
			CALL SETFLAG, "ピアスＯＮフラグ", TARGET
		CALL DEL_CEVENT_GROUP, "尿道ピアス"
		PART_PIERCES = 尿道
	ENDIF
ELSEIF RESULT == 4
	CALL SETFLAG, "へそピアスフラグ", TARGET
	SIF TALENT:へそピアス == 0
		CALL SETFLAG, "ピアスＯＮフラグ", TARGET
	CALL DEL_CEVENT_GROUP, "へそピアス"
	PART_PIERCES = おへそ
ENDIF
IF PART_PIERCES == ""
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

;ピアスの詳細を文字列で記録
PIERCES = 

;ピアスをつけた
IF COND("ピアスＯＮフラグ")
	PRINTFORML どのようなピアスをつけますか？
	PRINTFORML  [ 0] リング型
	PRINTFORML  [ 1] バーベル型
	PRINTFORML  [ 2] 十字架型
	PRINTFORML  [ 3] ♂型
	PRINTFORML  [ 4] ♀型
	PRINTFORML  [90] 直接入力する
	PRINTFORML [100] 特にこだわらない（記述しません）

	CALL INPUT_SELECT, 5, 90, 100

	SELECTCASE RESULT
	CASE 100
	CASE 0
		PIERCES = リング型
	CASE 1
		PIERCES = バーベル型
	CASE 2
		PIERCES = 十字架型
	CASE 3
		PIERCES = ♂型
	CASE 4
		PIERCES = ♀型
	CASE 90
		PRINTL 全角で８文字以下（半角なら16文字まで）の文字列を入力してください
		PRINTL ("～のピアス"等のように使われても違和感が無いもの推奨)
		PRINTL また、何も入力しない場合は特にこだわらない（記述しない）事とします
		CALL INPUTS_SELECT, 16
		PIERCES = %RESULTS%
	ENDSELECT

	PRINTL 麻酔を使いますか？
	PRINTL [0] はい
	PRINTL [1] いいえ
	CALL INPUT_SELECT, 2
	SIF RESULT == 0
		CALL SETFLAG, "鎮痛麻酔フラグ", TARGET

	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の
	IF COND("クリピアスフラグ")
		IF PENIS(TARGET)
			PRINT ペニスに
		ELSE
			IF COND("陰核肥大")
				PRINT 肥大した
			ELSEIF TALENT:淫核
				PRINT 勃起した
			ELSEIF TALENT:Ｃ性向
				PRINT 敏感な
			ELSEIF TALENT:小柄
				PRINT 小さな
			ENDIF
			PRINT クリトリスに
		ENDIF
	ELSEIF COND("ラビアピアスフラグ")
		IF TALENT:淫壷
			PRINT 淫らにヒクつく
		ELSEIF TALENT:Ｖ性向
			PRINT 敏感な
		ELSEIF AGE(TARGET) == 1
			PRINT 幼い
		ELSEIF TALENT:小柄
			PRINT 小さな
		ENDIF
		PRINT 小陰唇に
	ELSEIF COND("ニプルピアスフラグ")
		IF COND("陥没乳首")
			PRINTFORM 埋没している\@ COND("乳首肥大") ? 立派な # \@乳首を露出させて
		ELSE
			IF COND("乳首肥大")
				PRINT 肥大した
			ELSEIF TALENT:淫乳
				PRINT 勃起した
			ELSEIF TALENT:Ｂ性向
				PRINT 敏感な
			ELSEIF AGE(TARGET) == 1
				PRINT 幼い
			ELSEIF TALENT:小柄
				PRINT 小さな
			ENDIF
			PRINT 乳首に
		ENDIF
	ELSEIF COND("尿道ピアスフラグ")
		PRINTFORM 広がった尿道口に
	ELSEIF COND("へそピアスフラグ")
		PRINT おへそに
	ENDIF
	SIF PIERCES != ""
		PRINTFORM %PIERCES%の
	PRINTFORMW ピアスをつけた

	IF COND("クリピアスフラグ")
		IF PENIS(TARGET)

		ELSE
			PRINTFORMW %CALLNAME:TARGET%の\@ COND("陰核肥大") ? 大きな # \@クリトリスはピアスを付けられたおかげで皮が捲られ、常時剥き出しの状態になってしまった…
		ENDIF
		PIERCES = クリピアス：%PIERCES%
	ELSEIF COND("ラビアピアスフラグ")
		SIF AGE(TARGET) >= 3
			PRINTFORMW %CALLNAME:TARGET%の小陰唇は付けられたピアスの重みに引っ張られ、一目で分かるほどに大陰唇からはみ出てしまっている…
		PIERCES = ラビアピアス：%PIERCES%
	ELSEIF COND("ニプルピアスフラグ")
		SIF COND("陥没乳首")
			PRINTFORMW %CALLNAME:TARGET%の乳首は通されたピアスのおかげで戻れなくなり、常時露出するように矯正されてしまった…
		PIERCES = ニプルピアス：%PIERCES%
	ELSEIF COND("尿道ピアスフラグ")
		PIERCES = 尿道ピアス：%PIERCES%
	ELSEIF COND("へそピアスフラグ")
		PIERCES = へそピアス：%PIERCES%
	ENDIF

	;ピアスの形状フラグセット
	SIF PIERCES != ""
		CALL SET_CEVENT, PIERCES
;ピアスを外した
ELSE
	PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の%PART_PIERCES%からピアスを外した
ENDIF

;つけたり外したり
IF COND("クリピアスフラグ")
	INVERTBIT TALENT:クリピアス, 0
ELSEIF COND("ラビアピアスフラグ")
	INVERTBIT TALENT:ラビアピアス, 0
ELSEIF COND("ニプルピアスフラグ")
	INVERTBIT TALENT:ニプルピアス, 0
ELSEIF COND("尿道ピアスフラグ")
	INVERTBIT TALENT:尿道ピアス, 0
ELSEIF COND("へそピアスフラグ")
	INVERTBIT TALENT:へそピアス, 0
ENDIF

;-------------------------------------------------
;ソースの計算(確定値)
;-------------------------------------------------
CALL CALC, "調教ソースの計算"


@COM_ORDER_COM148, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

SELECTCASE TALENT:彼氏持ち
CASE 2
	LOCAL -= 40
	LOCALS = %LOCALS% - %TALENT_NAME(GETNUM(TALENT, "彼氏持ち"), TALENT:彼氏持ち)%(40)
CASE 1
	LOCAL -= 30
	LOCALS = %LOCALS% - %TALENT_NAME(GETNUM(TALENT, "彼氏持ち"), TALENT:彼氏持ち)%(30)
ENDSELECT

;ABL:露出癖
IF ABL:露出癖
	LOCAL += MIN(ABL:露出癖, 10) * 2
	LOCALS = %LOCALS% + 露出癖LV{ABL:露出癖}({MIN(ABL:露出癖, 10) * 2})
ENDIF
;ABL:マゾっ気
IF ABL:マゾっ気
	LOCAL += MIN(ABL:マゾっ気, 5) * 5
	LOCALS = %LOCALS% + マゾっ気LV{ABL:マゾっ気}({MIN(ABL:マゾっ気, 5)})
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 15
	LOCALS = %LOCALS% - 恥じらい(15)
ENDIF
;恥薄い
IF TALENT:恥薄い
	LOCAL += 5
	LOCALS = %LOCALS% + 恥薄い(5)
ENDIF
;倒錯的
IF TALENT:倒錯的
	LOCAL += 10
	LOCALS = %LOCALS% + 倒錯的(10)
ENDIF

;合計を表示(80以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 80

RETURN RESULT

