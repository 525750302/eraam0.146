@COM107
;逆Ａバイブ
;PLAYER側がバイブで楽しむコマンド。通常のバイブコマンドとは違い、入れっぱなしにはしない。
PRINTL 逆Ａバイブ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 107

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = 逆Ａバイブ

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@TRAIN_MESSAGE_COM107
;バイブ選択
CALL SELECT_VIB_A, PLAYER

SIF REFUSE_CHECK()
	RETURN 0

PRINTFORM %CALLNAME:TARGET%は、%CALLNAME:PLAYER%のアナルを
SELECTCASE SIZE("Ａバイブ", PLAYER)
CASE IS >= 3
	PRINT 極太のバイブ
CASE 2
	PRINT 太めのバイブ
CASE 1
	PRINT バイブ
CASE 0
	PRINT アナルバイブ
ENDSELECT
IF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTW でかき回し続けている…
ELSE
	PRINTW でかき回した…
ENDIF


@FLAG_COM107, ARG

@SOURCE_COM107, ARG
CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "逆Ａバイブ"

CALL SOURCE_DOWNBASE_HP, 107

SOURCE:0:情愛 = 500
SOURCE:0:不潔 = 1000
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:逸脱 = 1500

CALL CALC, "調教ソース", ARG


@COM_ORDER_COM107, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:サドっ気
IF ABL:サドっ気
	LOCAL += MIN(ABL:サドっ気, 5) * 5
	LOCALS = %LOCALS% + サドっ気LV{ABL:サドっ気}({MIN(ABL:サドっ気, 5) * 5})
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF


;合計を表示(40以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 40

RETURN RESULT
