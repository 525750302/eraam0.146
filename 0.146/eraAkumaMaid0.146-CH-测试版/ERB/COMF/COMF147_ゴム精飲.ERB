@COM147
;ゴム精飲
CALL PRINT_TRAIN_NAME(SELECTCOM)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 147

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

RETURN 1

@FLAG_COM147, ARG
;口に精液汚れ追加
CALL ADD_STAIN, TARGET, "Ｍ", "精液"
;ゴム精飲フラグ
SETBIT TCVAR:イベント, 51


@SOURCE_COM147, ARG
CALL SOURCE_SMPLAY, "ゴム精飲", ARG

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 147

SOURCE:0:不潔 = (ITEM:ゴム内精液 + 4)/5 * 500
SOURCE:0:欲情追加 = (ITEM:ゴム内精液 + 4)/5 * 500 + CALCF("実行値による欲情追加")
SOURCE:0:屈従 = (ITEM:ゴム内精液 + 4)/5 * 1000
SOURCE:0:逸脱 = (ITEM:ゴム内精液 + 4)/5 * 500
SOURCE:0:充足 = (ITEM:ゴム内精液 + 4)/5 * 1000

;精液経験をみる
SELECTCASE EXP:精液経験
CASE IS < 5
	TIMES SOURCE:0:逸脱, 4.00
CASE IS < 20
	TIMES SOURCE:0:逸脱, 2.00
CASE IS < 50
	TIMES SOURCE:0:逸脱, 1.50
CASE IS < 100
	TIMES SOURCE:0:逸脱, 1.20
CASE IS < 300
	TIMES SOURCE:0:逸脱, 1.00
CASEELSE
	TIMES SOURCE:0:逸脱, 0.50
ENDSELECT

;ABL:精液中毒をみる
SELECTCASE ABL:精液中毒
CASE 0
	TIMES SOURCE:0:欲情追加, 0.10
	TIMES SOURCE:0:屈従, 0.50
CASE 1
	TIMES SOURCE:0:欲情追加, 1.00
	TIMES SOURCE:0:屈従, 1.00
CASE 2
	TIMES SOURCE:0:欲情追加, 1.20
	TIMES SOURCE:0:屈従, 1.30
CASE 3
	TIMES SOURCE:0:欲情追加, 1.40
	TIMES SOURCE:0:屈従, 1.60
CASE 4
	TIMES SOURCE:0:欲情追加, 1.80
	TIMES SOURCE:0:屈従, 2.00
CASEELSE
	TIMES SOURCE:0:欲情追加, 2.00
	TIMES SOURCE:0:屈従, 3.00
ENDSELECT

CALL CALC, "調教ソース", ARG

@TRAIN_MESSAGE_COM147

;前回ターンに出した直後のゴム有り
IF PREVPLAY("ゴム内射精", PLAYER)
	PRINTFORM %CALLNAME:PLAYER%のペニスからコンドームを取り外した
ELSE
	PRINTFORM 使用済みゴム置き場から中に精液の溜まっているコンドームを摘まみ出した
ENDIF
PRINTFORM %CALLNAME:TARGET%は、
IF ABL:精液中毒 >= 3 && RAND:2
	PRINTFORMW 逆さにしたソレから垂れる精液を%TEXTR("伸ばした舌/大きく開いた口")%で受け止めた。
ELSEIF ABL:精液中毒 && (RAND:2 || ABL:精液中毒 >= 3)
	PRINTFORMW 口で咥えて内部の精液を%TEXTR("勢いよく/ジュルジュルと")%吸い出した。
ELSE
	PRINTFORMW 躊躇いを見せつつ口から垂れる精液に舌を伸ばした。
ENDIF

PRINTFORM %CALLNAME:TARGET%は
SELECTCASE ABL:精液中毒
CASE IS < 3
	PRINTFORM %TEXTR("ゴム臭い精液/この行為/使用済みゴムを啜ること")%に%TEXTR("辟易した様子だが/眉をしかめつつも/慣れない様子だが")%、
	IF ABL:精液中毒 && RAND:2
		PRINTFORMW %TEXTR("身体の求めに従って/それほど苦労する事もなく/異常な行為に僅かな昂ぶりを感じつつ")%喉に流し込んでいく…
	ELSE
		PRINTFORMW 口の中に溜めた唾液を利用してなんとか喉に流し込んでいく…
	ENDIF
CASEELSE
	IF PREVPLAY("ゴム内射精", PLAYER) && RAND:2
		IF RAND:2
			PRINTFORM コンドームから流れ出てくる%TEXTR("新鮮な/まだ温かい/出したての")%精液を、
		ELSE
			PRINTFORM %CALLNAME:PLAYER%の出したての精液を、
		ENDIF
	ELSE
		PRINTFORM %TEXTR("ゴム臭い精液を意に介さず/使用済みゴムの後始末に乗り気な様子で/精液のゴム臭さを気にした風もなく")%、
	ENDIF
	IF ABL:精液中毒 >= 5 && RAND:2
		PRINTFORMW %TEXTR("なんとも美味しそうに/味わいながら/舌鼓を打ちながら/舌なめずりをしながら")%飲み干してしまった…
	ELSE
		PRINTFORMW %TEXTR("涼しい顔をしながら/ゴクゴクと喉を鳴らしながら/頬を上気させながら")%飲み干していく…
	ENDIF
ENDSELECT


@COM_ORDER_COM147, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 5
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 5})
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	LOCALS = %LOCALS% - 恥じらい(3)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF

;合計を表示(50以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 50

RETURN RESULT
