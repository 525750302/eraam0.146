@COM173
;新妻プレイ
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;エプロンのセット
;終了時は修正なし
IF TEQUIP:新妻プレイ
	TEQUIP:新妻プレイ = 0
	RETURN 1
ELSE
	TEQUIP:新妻プレイ = 1
ENDIF

RETURN 1

@FLAG_COM173, ARG

@SOURCE_COM173, ARG
CALL VARSET_COMF

SOURCE:0:情愛 = 500
SOURCE:0:逸脱 = 100

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
CASE 0
CASE 1
	TIMES SOURCE:0:情愛, 1.20
CASE 2
	TIMES SOURCE:0:情愛, 1.40
CASE 3
	TIMES SOURCE:0:情愛, 1.60
CASE 4
	TIMES SOURCE:0:情愛, 1.80
CASEELSE
	TIMES SOURCE:0:情愛, 2.00
ENDSELECT

SIF TALENT:好奇心
	TIMES SOURCE:0:情愛, 1.50
SIF TALENT:献身
	TIMES SOURCE:0:情愛, 1.50
SIF TALENT:内気
	TIMES SOURCE:0:逸脱, 1.50
SIF TALENT:恥じらい
	TIMES SOURCE:0:逸脱, 1.20
SIF TALENT:高慢
	TIMES SOURCE:0:逸脱, 2.00

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM173
IF TEQUIP:新妻プレイ
	PRINTFORMW %CALLNAME:TARGET%のエプロンを脱がしてやった…
ELSE
	PRINTFORM %CALLNAME:TARGET%
	IF CHECK_CLO("ハダカ")
		PRINTFORMW に裸エプロンになってもらった…
	ELSE
		PRINTFORMW にエプロンをつけさせた…
	ENDIF
	;相思相愛
	IF TALENT:相思相愛 && PLAYER == MASTER && !CHECK_CLO("ハダカ")
		PRINTFORMW %CALLNAME:TARGET%は嬉しそうに%CALLNAME:MASTER%に笑顔を向けている……
	ELSEIF ((TALENT:恋慕 && PLAYER == MASTER) || TALENT:惚れっぽい) && !CHECK_CLO("ハダカ")
		PRINTFORMW %CALLNAME:TARGET%は照れくさそうな顔でこちらを見ている……
	ELSEIF (TALENT:恥薄い || TALENT:好色) && CHECK_CLO("ハダカ")
		PRINTFORMW %CALLNAME:TARGET%は好色な笑みでこちらを見ている……
	ELSEIF (TALENT:小悪魔 || TALENT:生意気) && !CHECK_CLO("ハダカ")
		PRINTFORMW %CALLNAME:TARGET%は裾をちらちらと持ち上げてアピールしている……
	ELSEIF (TALENT:素直 || COND("種族：人形")) && !CHECK_CLO("ハダカ")
		PRINTFORMW %CALLNAME:TARGET%がキッチンに向かおうとしていたので、止めさせました……
	ENDIF
ENDIF


@EQUIP_COM173
SIF TEQUIP:新妻プレイ == 0
	RETURN 0
SIF SELECTCOM == 173
	RETURN 0
;新妻プレイ
CALL SOURCE_COM173, 30

RETURN 1

@EQUIP_COM173_2
SIF TEQUIP:新妻プレイ == 0
	RETURN 0
SIF SELECTCOM == 173
	RETURN 0
;新妻プレイ
PRINTL ＜新妻プレイ中＞

;PRINTFORML %EXPNAME:23%＋１
;EXP:手淫経験 += 1

SIF ASSIPLAY == 0 && ABL:信頼 >= 3
	TFLAG:好感度ボーナス += 2
SIF ASSIPLAY == 0 && TALENT:恋慕
	TFLAG:好感度ボーナス += 5


RETURN 1
