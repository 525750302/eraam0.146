@COM90
;手淫(手袋コキを活用するのはまた今度…)
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 90

;実行できない
IF TCVAR:やる気 == 0
	SIF PENIS(PLAYER)
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスに触るのすら嫌なようだ…
	RETURN 0
ENDIF

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
IF CHECK_COM("手コキフェラ")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 91
	CALL SAMEN_CHECK, 50
	SELECTCOM = 90
ELSEIF CHECK_COM("Ａ責め手コキ")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 96
	CALL SAMEN_CHECK, 50
	SELECTCOM = 90
ELSEIF CHECK_COM("ぱふぱふ手コキ")
	CALL SAMEN_CHECK, 70
	SELECTCOM = 104
	CALL SAMEN_CHECK, 50
	SELECTCOM = 90
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM90, ARG
IF CHECK_COM("手コキしあう") == 90
	CALL FLAG_COM10, 50
ELSEIF CHECK_COM("手コキフェラ") == 90
	CALL FLAG_COM91, 50
ELSEIF CHECK_COM("Ａ責め手コキ") == 90
	CALL FLAG_COM96, 50
ELSEIF CHECK_COM("ぱふぱふ手コキ") == 90
	CALL FLAG_COM104, 50
ENDIF
;対象の指⇔調教者のＰの汚れが移動
IF TALENT:PLAYER:一般人 || TALENT:PLAYER:オトコ  || TALENT:PLAYER:ふたなり
	CALL EQ_STAIN, TARGET, "手", PLAYER, "Ｐ"
;対象の指⇔調教者のＶの汚れが移動
ELSE
	CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "Ｐ"
ENDIF

;汚れは手袋でふき取る。
SIF EQUIP:手袋 && !CHECK_GLOVE("指露出")
	CALL RESET_STAIN_EX, "Ｐ", PLAYER

@SOURCE_COM90, ARG
IF CHECK_COM("手コキしあう") == 90
	CALL SOURCE_COM10, 50
ELSEIF CHECK_COM("手コキフェラ") == 90
	CALL SOURCE_COM91, 50
ELSEIF CHECK_COM("Ａ責め手コキ") == 90
	CALL SOURCE_COM96, 50
ELSEIF CHECK_COM("ぱふぱふ手コキ") == 90
	CALL SOURCE_COM104, 50
ENDIF

CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "手淫"

CALL SOURCE_DOWNBASE_HP, 90

IF PENIS(PLAYER)
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "手")*20 + 60
ELSE
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｖ", "手")*20 + 60
ENDIF

SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:屈従 = 500
SOURCE:0:逸脱 = 100

CALL CALC, "調教ソース", ARG

;性行動と達成感
@SOURCE_SERVICE_PLAY, ARGS
;技巧と対応した〜使いの和
#DIM ABL_SERVICE

;ABL:奉仕精神をみる
SELECTCASE ABL:奉仕精神
CASE 0
	SOURCE:0:性行動 = 200
	SOURCE:0:達成感 = 50
CASE 1
	SOURCE:0:性行動 = 500
	SOURCE:0:達成感 = 200
CASE 2 TO 5
	SOURCE:0:性行動 = 500 + (ABL:奉仕精神 - 1)*100
	SOURCE:0:達成感 = 200 + (ABL:奉仕精神 - 1)*50
CASE 6 TO 10
	SOURCE:0:性行動 = 900 + (ABL:奉仕精神 - 5)*60
	SOURCE:0:達成感 = 400 + (ABL:奉仕精神 - 5)*20
CASEELSE
	SOURCE:0:性行動 = 1200 + (ABL:奉仕精神 - 10)*30
	SOURCE:0:達成感 = 500 + (ABL:奉仕精神 - 10)*10
ENDSELECT

ABL_SERVICE = ABL:技巧
SELECTCASE ARGS
CASE "手淫", "胸愛撫させる", "オナホコキ", "逆バイブ", "逆Ａバイブ"
	ABL_SERVICE += ABL:指使い

CASE "フェラチオ"
	ABL_SERVICE += ABL:舌使い

	TIMES SOURCE:0:性行動, 1.50
	TIMES SOURCE:0:達成感, 1.50
CASE "奉仕クンニ"
	ABL_SERVICE += ABL:舌使い

	TIMES SOURCE:0:性行動, 1.80
	TIMES SOURCE:0:達成感, 1.80

CASE "イラマチオ", "アナル奉仕"
	ABL_SERVICE += ABL:舌使い

	TIMES SOURCE:0:性行動, 2.00
	TIMES SOURCE:0:達成感, 2.00
CASE "パイズリ", "ぱふぱふ"
	ABL_SERVICE += ABL:魔乳

CASE "素股"
	ABL_SERVICE += ABL:腰使い

CASE "泡踊り"
	ABL_SERVICE += ABL:腰使い + ABL:魔乳
CASE "足コキ"
	ABL_SERVICE += ABL:サドっ気
CASE "足を舐める"
	ABL_SERVICE += ABL:マゾっ気
CASE "シックスナイン"

ENDSELECT

SELECTCASE ABL_SERVICE
CASE 0 TO 5
	SOURCE:0:性行動 = MULTIPLY(SOURCE:0:性行動, 100 + ABL_SERVICE*20)
	SOURCE:0:達成感 = MULTIPLY(SOURCE:0:達成感, 100 + ABL_SERVICE*20)
CASE 6 TO 10
	SOURCE:0:性行動 = MULTIPLY(SOURCE:0:性行動, 200 + (ABL_SERVICE - 5)*10)
	SOURCE:0:達成感 = MULTIPLY(SOURCE:0:達成感, 200 + (ABL_SERVICE - 5)*10)
CASEELSE
	SOURCE:0:性行動 = MULTIPLY(SOURCE:0:性行動, 250 + (ABL_SERVICE - 10)*5)
	SOURCE:0:達成感 = MULTIPLY(SOURCE:0:達成感, 250 + (ABL_SERVICE - 10)*5)
ENDSELECT


@TRAIN_MESSAGE_COM90
IF CHECK_COM("Ｗ奉仕")
	;二人とも前回と同じ行動
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM == PREVCOM
		PRINTFORM 二人は
		IF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:4 == 0
			PRINTFORMW うっとりしながら%CALLNAME:PLAYER%のペニスを扱き続けている…
		ELSEIF ABL:(TARGET:1):奉仕精神 >= 3 && ABL:奉仕精神 >= 3 && RAND:3 == 0
			PRINTFORMW %CALLNAME:PLAYER%の顔を見つめながらペニスを扱き続けている…
		ELSEIF ABL:(TARGET:1):技巧 + ABL:技巧 >= 6 && RAND:2 == 0
			PRINTFORMW 緩急をつけながら%CALLNAME:PLAYER%のペニスを扱き続けている…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%のペニスを扱き続けている…
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:TARGET%と%CALLNAME:(TARGET:1)%は
		SIF TALENT:(TARGET:1):淫乱 + TALENT:(TARGET:1):恋慕 && TALENT:淫乱 + TALENT:恋慕 && RAND:2 == 0
			PRINTFORM 上気した瞳で%CALLNAME:MASTER%を見つめながら、
		IF RAND:2 == 0
			PRINTFORMW 競うようにしてペニスを扱きはじめた…
		ELSE
			PRINTFORMW お互いの手で挟むようにしてペニスを扱きはじめた…
		ENDIF
	ENDIF
;==90で二本手コキ、== 91ならフェラ＆手コキ、それ以外は犯されながら手コキ
ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
;同時押し倒し
ELSEIF TEQUIP:Ｗ押し倒し
	PRINTFORM %CALLNAME:TARGET%は
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM 、%TEXTS("Ｗ押し倒し中")%

	IF SELECTCOM == PREVCOM
		PRINTFORMW %CALLNAME:MASTER%のペニスをゴシゴシ扱き続けている…
	ELSE
		PRINTFORMW %CALLNAME:MASTER%のペニスを扱きはじめた…
	ENDIF
;前回と同じ
ELSEIF PREVCOM == SELECTCOM && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:電話中
		PRINTFORM %TEXTR(@"いつも通りの電話だと思っている/何も知らずに電話の向こう側にいる/疑いもせずに電話に出てくれた/何も知らない/こちらの出来事を全く知らない/浮気中だと思ってもいない/%CALLNAME:TARGET%を信じきっている")%
		PRINTFORM %TEXTS("彼氏")%%TEXTR("と会話しながら/と通話を続けながら/と日常会話を続けつつ/と近況を話しながら/と他愛もない話をしつつ")%、
	ENDIF
	IF TEQUIP:顔面騎乗
		PRINTFORM %CALLNAME:PLAYER%の顔に
		SELECTCASE COND("陰毛")
		CASE 1 TO 3
			PRINTFORM \@ RAND:2 ? つるつる # すべすべ \@の
		CASE 7
			PRINT 毛深い
		ENDSELECT
		PRINTFORM 股間をこすりつけつつ
	ENDIF
	PRINT 、
	IF TALENT:恋慕 && RAND:3 == 0
		;においに弱い分岐
		IF PLAYER == MASTER && TALENT:においに弱い && RAND:3
			IF RAND:2
				PRINTFORMW ペニスの%TEXTS("匂い")%を嗅ぎながら手で扱き続けている…
			ELSE
				PRINTFORMW 鼻を鳴らして汗ばんだペニスの%TEXTS("匂い")%を楽しみながら擦り続けている…
			ENDIF
		ELSE
			PRINTFORMW うっとりしながら%CALLNAME:PLAYER%のペニスを扱き続けている…
		ENDIF
	ELSEIF ABL:奉仕精神 >= 3 && TEQUIP:顔面騎乗 == 0 && RAND:3 == 0
		PRINTFORMW %CALLNAME:PLAYER%の顔を見つめながらペニスを扱き続けている…
	ELSEIF ABL:技巧 >= 3 && RAND:2 == 0
		PRINTFORMW 緩急をつけながら長いストロークでペニスを扱き続けている…
	ELSE
		PRINTFORMW 夢中になってペニスを扱き続けている…
	ENDIF
ELSEIF CHECK_COM("手コキしあう")
	PRINTFORM %CALLNAME:PLAYER%達は
	SIF TEQUIP:キス
		PRINTFORM 、%TEXTS("キス継続")%
	PRINTFORM 互いのペニスを
	IF RAND:2
		PRINTFORMW 緩急をつけつつ%TEXTR("撫で回し/擦りあげ")%続けた…
	ELSE
		PRINTFORMW %TEXTR("ガシガシと激しく/しゅっしゅっとリズミカルに")%扱き続けた…
	ENDIF
ELSEIF CHECK_COM("手コキフェラ")
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:電話中
		PRINTFORM %TEXTR(@"いつも通りの電話だと思っている/何も知らずに電話の向こう側にいる/疑いもせずに電話に出てくれた/何も知らない/こちらの出来事を全く知らない/浮気中だと思ってもいない/%CALLNAME:TARGET%を信じきっている")%
		PRINTFORM %TEXTS("彼氏")%%TEXTR("と会話しながら/と通話を続けながら/と日常会話を続けつつ/と近況を話しながら/と他愛もない話をしつつ")%、
	ENDIF
	;においに弱い分岐
	IF PLAYER == MASTER && TALENT:においに弱い && TALENT:恋慕 && (CHOSECOM(SELECTCOM) == 0 || RAND:3 == 0)
		IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ恥垢") && RAND:5
			PRINTFORM 、%TEXTR("こびりつく垢/チンカス")%の%TEXTS("匂い")%
		ELSE
			PRINTFORM 、ペニスの%TEXTS("匂い")%
		ENDIF
		PRINTFORM を%TEXTR("嗅ぎ/楽しみ")%ながら%CALLNAME:PLAYER%の
	ELSEIF TALENT:恋慕 && RAND:4 == 0
		PRINTFORM 、うっとりしながら%CALLNAME:PLAYER%の
	ELSEIF ABL:奉仕精神 >= 3 && RAND:3 == 0
		PRINTFORM %CALLNAME:PLAYER%の顔を見つめながら
	ELSE
		PRINTFORM %CALLNAME:PLAYER%の
	ENDIF
	IF RAND:4 == 0
		PRINTFORM 亀頭に舌を這わせつつ、
	ELSEIF RAND:3 == 0
		PRINTFORM 尿道を舌先で刺激しつつ、
	ELSEIF RAND:2
		PRINTFORM ペニスにキスを降らせつつ、
	ELSE
		PRINTFORM 亀頭を咥えて吸い上げつつ、
	ENDIF
	PRINTFORMW 指でペニスを弄んで%CALLNAME:PLAYER%の反応を楽しんでいる…
;押し倒されているとき
ELSEIF TEQUIP:押し倒し || CHECK_COM("ぱふぱふ手コキ")
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:電話中
		PRINTFORM %TEXTR(@"いつも通りの電話だと思っている/何も知らずに電話の向こう側にいる/疑いもせずに電話に出てくれた/何も知らない/こちらの出来事を全く知らない/浮気中だと思ってもいない/%CALLNAME:TARGET%を信じきっている")%
		PRINTFORM %TEXTS("彼氏")%%TEXTR("と会話しながら/と通話を続けながら/と日常会話を続けつつ/と近況を話しながら/と他愛もない話をしつつ")%、
	ENDIF
	IF TEQUIP:キス
		PRINTFORM %TEXTS("キス継続")%
		CALL SETFLAG, "追加キスフラグ", TARGET
	ELSEIF TEQUIP:顔面騎乗
		PRINT 顔に跨ったまま
	ELSEIF CHECK_COM("Ａ責め手コキ")
		PRINTFORM %CALLNAME:PLAYER%のお尻の穴を\@ RAND:2 ? 尖らせた # \@舌で
		IF RAND:3 == 0
			PRINT 弄びながら
		ELSEIF RAND:2
			SIF (ABL:奉仕精神 >= 3 || ABL:サドっ気 >= 3) && RAND:2
				PRINT 中まで
			PRINTFORM 舐めながら
		ELSE
			PRINT 責めながら
		ENDIF
	ELSEIF CHECK_COM("ぱふぱふ手コキ")
		PRINTFORM %CALLNAME:PLAYER%の顔を
		IF RAND:3 == 0
			PRINT 豊満な胸
		ELSEIF RAND:2 == 0
			PRINT たわわに実った乳房
		ELSE
			PRINT 柔らかなおっぱい
		ENDIF
		IF RAND:2 && TEQUIP:押し倒し
			PRINTFORM で押し潰した体勢で
		ELSEIF RAND:2
			PRINTFORM で包みながら
		ELSE
			PRINTFORM の谷間に埋めたままで
		ENDIF
	;腋の匂い嗅がせつつ
	ELSEIF CHECK_CLO("腋露出") && PREVCOM != 1 && PREVCOM != 9 && (CHOSECOM(SELECTCOM) == 0 || RAND:3 == 0)
		PRINTFORMW %CALLNAME:PLAYER%に%TEXTS("腋")%の%TEXTS("匂い")%を嗅がせつつ、楽しそうにペニスを扱いている…
		RETURN 1
	;プライド高いor淫乱orサドっ気Lv4以上
	ELSEIF (TALENT:高慢 || TALENT:淫乱 || ABL:サドっ気 >= 4) && RAND:2 == 0
		PRINT 愉快そうに微笑みながら
	;小悪魔
	ELSEIF TALENT:小悪魔
		PRINT ニヤニヤしながら
	;淫乱or欲情4LV以上
	ELSEIF (TALENT:淫乱 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
		PRINT 妖艶に微笑みながら
	ELSEIF (TALENT:淫乱 || PALAM:欲情 >= PALAMLV:4) && RAND:2 == 0
		PRINT 息を荒げながら
	ELSEIF RAND:2 == 0 || TEQUIP:アイマスク
		SIF RAND:2 == 0
			PRINT 楽しそうに
		PRINT 微笑みながら
	ENDIF
	PRINTFORM ペニスを弄び、
	IF PREVCOM == 1 || PREVCOM == 9
		PRINTFORM 股間を\@ RAND:2 ? 押し付け # 擦り付け \@たりしながら
	ELSEIF CHECK_COM("ぱふぱふ手コキ") && RAND:2
		PRINT 幸せそうな
	ELSE
		PRINT 快感に悶える
	ENDIF
	PRINTFORMW %CALLNAME:PLAYER%の反応を楽しんでいる…
;それ以外
ELSE
	PRINTFORM %CALLNAME:TARGET%は
	IF TEQUIP:電話中
		PRINTFORM %TEXTR(@"いつも通りの電話だと思っている/何も知らずに電話の向こう側にいる/疑いもせずに電話に出てくれた/何も知らない/こちらの出来事を全く知らない/浮気中だと思ってもいない/%CALLNAME:TARGET%を信じきっている")%
		PRINTFORM %TEXTS("彼氏")%%TEXTR("と会話しながら/と通話を続けながら/と日常会話を続けつつ/と近況を話しながら/と他愛もない話をしつつ")%、
	ENDIF
	IF TEQUIP:キス
		PRINTFORM %CALLNAME:PLAYER%%TEXTS("キス継続助詞付き")%、
		CALL SETFLAG, "追加キスフラグ", TARGET
	ELSEIF TEQUIP:シックスナイン
		PRINTFORM %TEXTS("６９")%
	ELSEIF CHECK_COM("Ａ責め手コキ")
		PRINTFORM %CALLNAME:PLAYER%のお尻の穴を\@ RAND:2 ? 尖らせた # \@舌で
		IF RAND:2
			SIF (ABL:奉仕精神 >= 3 || ABL:サドっ気 >= 3) && RAND:2
				PRINT 中まで
			PRINTFORM 舐めながら
		ELSE
			PRINT 責めながら
		ENDIF
	;においに弱い分岐
	ELSEIF PLAYER == MASTER && TALENT:においに弱い && TALENT:恋慕 && (CHOSECOM(SELECTCOM) == 0 || RAND:3 == 0)
		IF CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ｐ恥垢") && RAND:3
			SELECTCASE RANDF(3 + TALENT:高慢)
			CASE 0
				PRINTFORMW %CALLNAME:PLAYER%のペニスにこびりつくチンカスの%TEXTS("匂い")%をひとしきり嗅いでから、興奮で顔を赤らめながらペニスを扱きはじめた…
			CASE 1
				PRINTFORMW %CALLNAME:PLAYER%のペニスをニチャニチャと音を立てて扱きながら、立ち昇る%TEXTS("匂い")%を嗅いでさらに興奮している…
			CASE 2
				PRINTFORMW ペニスを手で扱きながら、時折手を止めてカリ首に鼻を近づけ%TEXTS("匂い")%を嗅いでいる…
			CASE 3
				PRINTFORMW %CALLNAME:PLAYER%のペニスにこびりつく垢の%TEXTS("匂い")%に悪態をつきながらも、擦り上げる事を止められない…
			ENDSELECT
			RETURN 1
		ELSEIF CHECK_STAIN(STAIN:PLAYER:Ｐ, "精液") && RAND:3 == 0
			IF RAND:2
				PRINTFORM 精液で汚れたペニスの%TEXTS("匂い")%を嗅いで興奮しながら
			ELSE
				PRINTFORM %CALLNAME:PLAYER%のペニスについた精液の%TEXTS("匂い")%を嗅ぎながら
			ENDIF
		ELSEIF RAND:3 == 0
			PRINTFORM %CALLNAME:PLAYER%のペニスの%TEXTS("匂い")%を嗅ぎながら
		ELSEIF RAND:2
			PRINTFORM ペニスから漂う強い%TEXTS("匂い")%を吸い込むと、うっとりしながら
		ELSE
			PRINTFORM 鼻を近づけて%CALLNAME:PLAYER%のペニスの%TEXTS("匂い")%を楽しみながら
		ENDIF
	ENDIF
	IF RAND:2
		IF EQUIP:手袋 && CHECK_GLOVE("指露出") == 0
			;精液ぬるぬる
			IF CHECK_STAIN(STAIN:手袋, "精液")
				IF RAND:2
					PRINT 精液がじっとり染み込んだ
				ELSE
					PRINT 精液でぬるぬるの
				ENDIF
			ENDIF
			PRINTFORM %GLOVE(TARGET, "略")%
		ELSE
			SIF CHECK_STAIN(STAIN:手, "精液")
				PRINT 精液でぬるぬるの
			PRINTFORM 手のひら
		ENDIF
		PRINTFORMW で亀頭をグリグリ撫で回した…
	ELSE
		;精液ぬるぬる
		SIF (CHECK_GLOVE("指露出") && CHECK_STAIN(STAIN:手袋, "精液") ) || CHECK_STAIN(STAIN:手, "精液")
			PRINT 精液でぬるぬるの
		PRINTFORMW 指をペニスに絡めて擦りあげた…
	ENDIF
ENDIF
;母乳体質とかだと母乳を飲めるかも
CALL TEXT, "母乳飲み"

@COM_ORDER_COM90, ARG
#DIM MEMO_STAIN

VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	LOCAL += MIN(ABL:精液中毒, 5) * 2
	LOCALS = %LOCALS% + 精液中毒LV{ABL:精液中毒}({MIN(ABL:精液中毒, 5) * 2})
ENDIF

;強気
IF TALENT:強気
	LOCAL += 10
	LOCALS = %LOCALS% + 強気(10)
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	LOCALS = %LOCALS% - 恥じらい(3)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF

IF PENIS(PLAYER)
	MEMO_STAIN = COM_ORDER_STAIN("Ｐ", "手") / 3
ELSE
	MEMO_STAIN = COM_ORDER_STAIN("Ｖ", "手") / 3
ENDIF
;汚れあり
IF MEMO_STAIN
	LOCAL -= MEMO_STAIN
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({MEMO_STAIN})
ENDIF

;合計を表示(14以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 14

RETURN RESULT
