;-------------------------------------------------
;インサート共通のFLAG, SOURCE, 地の文をここにまとめる
;PREVPLAY("騎乗位")は前のターンに騎乗位を選択した場合にON
;INSERT("騎乗位")は前のターンから騎乗位の体勢だとON。前のターンに騎乗位状態でのぱふぱふなどでもONとなるのに注意
;TRAIN_GENRE("騎乗位", SELECTCOM)は現在選んでいるコマンドが騎乗位ならばON
;-------------------------------------------------
@TRAIN_MESSAGE_INSERTION
;地の文の分岐で使う
#DIMS ACT_TABLE
;穴を変える場合などに使う
#DIMS PREVHOLE
#DIMS SELECTHOLE
#DIMS USEHOLE

;前のターンに使った穴
IF INSERT("Ａインサート")
	PREVHOLE = %TEXTR("アナル/肛門/後ろの穴")%
ELSEIF INSERT("尿道インサート")
	PREVHOLE = %TEXTR("尿道/おしっこの穴")%
ELSE
	PREVHOLE = %TEXTR("膣/膣穴/前の穴")%
ENDIF
;現在使っている穴
IF INSERT("Ａインサート")
	USEHOLE = %TEXTR("アナル")%
ELSEIF INSERT("尿道インサート")
	USEHOLE = \@ PENIS(TARGET) ? ペニス # 尿道 \@
ELSE
	USEHOLE = %TEXTR("膣内")%
ENDIF
;これから使う穴
IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
	SELECTHOLE = %TEXTS("アナル")%アナル
ELSEIF TRAIN_GENRE("尿道挿入", SELECTCOM)
	SELECTHOLE = %TEXTS("尿道")%尿道
ELSE
	SELECTHOLE = %TEXTS("Ｖ挿入前")%
ENDIF

IF CHECK_COM("Ｖ二本挿入")
	IF INSERT("Ｖ二本挿入")
		;ここから新規に書き起こした
		IF CONFIG("立位")
			;両方巨根以上である場合のみ
			IF SIZE("ペニス", PLAYER) >= 2 && SIZE("ペニス", PLAYER:1) >= 2 && RAND:3 == 0
				PRINTFORM %TEXTR("立ったままの姿勢で/立位で/立ったまま前後から")%%CALLNAME:PLAYER%と%CALLNAME:(PLAYER:1)%
				PRINTFORM %TEXTR("二人の/の大きすぎる")%%TEXTR("肉棒/ペニス")%に%TEXTR("串刺しにされた/貫かれた")%%CALLNAME:TARGET%の%TEXTR("膣/膣口/アソコ")%は%TEXTR("大きく広げられている/限界まで拡張されている")%…
			ELSEIF RAND:2 == 0
				PRINTFORM %TEXTR("立ったままの姿勢で/立位で/立ったまま前後から")%
				PRINTFORMW %CALLNAME:TARGET%の%TEXTR("広がった/広がりすぎた/拡張されきった")%%TEXTR("膣/膣穴/アソコ")%を同時に犯し続けた。
			ELSE
				IF RAND:3 == 0
					PRINTFORM 宙に浮いた
				ELSEIF RAND:2 == 0
					PRINTFORM %CALLNAME:(PLAYER:1)%にしがみついている
				ELSE
					PRINTFORM %CALLNAME:(PLAYER:1)%に抱きついている
				ENDIF
				PRINTFORM %CALLNAME:TARGET%の%TEXTR("膣穴/アソコ")%を
				IF RAND:2 == 0
					PRINTFORMW 同時に犯し続けた。
				ELSE
					IF PENIS(PLAYER) && PENIS(PLAYER:1)
						PRINTFORMW 二本のペニスで
					ELSE
						PRINTFORMW ペニスと双頭バイブで
					ENDIF
					PRINTFORMW 犯し続けた。
				ENDIF
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:TARGET%の膣穴を同時に犯し続けた。
		ENDIF
	ELSE
		IF INSERT("背面座位")
			IF CONFIG("立位")
				PRINTFORM %TEXTR("背面立位/立ったままの姿勢")%で%TEXTR("＿/両足を大きく開いて/背後から")%%CALLNAME:(PLAYER:1)%%TEXTR("に犯され/と繋がっ")%ている、%CALLNAME:TARGET%の%TEXTR("＿/両")%足を抱き上げて
			ELSE
				PRINTFORM 結合部が丸見えの体勢で%CALLNAME:(PLAYER:1)%に犯されている%CALLNAME:TARGET%の股を押し開き、
			ENDIF
		ELSEIF INSERT("騎乗位")
			PRINTFORM %CALLNAME:(PLAYER:1)%に跨って腰を振っている%CALLNAME:TARGET%に前傾姿勢をとらせて、
		ELSE
			IF CONFIG("立位")
				PRINTFORM %CALLNAME:(PLAYER:1)%に背面立位の体勢をとらせて、犯されている%CALLNAME:TARGET%の股を押し開き、
			ELSE
				PRINTFORM %CALLNAME:(PLAYER:1)%に背面座位の体勢をとらせて、犯されている%CALLNAME:TARGET%の股を押し開き、
			ENDIF
		ENDIF
		PRINTFORMW 余裕のありそうな膣穴に%NAMES("ペニス", PLAYER)%を捻じ込んだ…
	ENDIF
ELSEIF CHECK_COM("Ａ二本挿入")
	IF INSERT("Ａ二本挿入")
		PRINTFORMW %CALLNAME:TARGET%のアナルを同時に犯し続けた。
	ELSE
		IF INSERT("背面座位")
			PRINTFORM 結合部が丸見えの体勢で%CALLNAME:(PLAYER:1)%に犯されている%CALLNAME:TARGET%の股を押し開き、
		ELSEIF INSERT("騎乗位")
			PRINTFORM %CALLNAME:(PLAYER:1)%に跨って腰を振っている%CALLNAME:TARGET%に前傾姿勢をとらせて、
		ELSE
			PRINTFORM %CALLNAME:(PLAYER:1)%に背面座位の体勢をとらせて、犯されている%CALLNAME:TARGET%の股を押し開き、
		ENDIF
		PRINTFORMW 余裕のありそうなアナルに%NAMES("ペニス", PLAYER)%を捻じ込んだ…
	ENDIF
ELSEIF CHECK_COM("二穴挿入")
	IF INSERT("二穴挿入")
		PRINTFORM %CALLNAME:PLAYER%と%CALLNAME:(PLAYER:1)%は
		IF RAND:4 == 0
			PRINTFORM \@ RAND:2 ? 激しい # ゆっくりとした \@腰の動きで
		ELSEIF RAND:3 == 0
			PRINTFORM タイミングを%TEXTR("合わせて/ずらして")%
		ELSE
			PRINTFORM %TEXTR("肉襞を擦り上げながら/長いストロークで")%
		ENDIF
		PRINTFORMW %CALLNAME:TARGET%の%TEXTR("二つ/前後")%の穴を%TEXTR("犯し/掻き回し/突き上げ")%た…
	ELSEIF SELECTCOM >= 60 && SELECTCOM <= 69
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:(PLAYER:1)%
		IF INSERT("後背位") || INSERT("背面座位")
			IF CONFIG("立位")
				PRINTFORM に%TEXTR("立ったまま身体を預けた体勢/立ちバック")%で%TEXTR("アナル/お尻")%を犯されている%CALLNAME:TARGET%の膣穴に%NAMES("ペニス", PLAYER)%を
			ELSEIF INSERT("後背位")
				IF RAND:2 == 0
					PRINTFORM に%TEXTR("背後から/後ろから/獣の様な姿勢で/バックで")%犯されている%CALLNAME:TARGET%の%TEXTR("身体/上体")%を起こ%TEXTR("して/させて")%、膣穴に%NAMES("ペニス", PLAYER)%を
				ELSE
					PRINTFORM に%CALLNAME:TARGET%の%TEXTR("身体/上体")%を%TEXTR("もたれかからせ/預けさせ")%、%TEXTR("露わになった/剥きだしになった")%膣穴に%NAMES("ペニス", PLAYER)%を
				ENDIF
			ELSEIF INSERT("背面座位")
				PRINTFORM にもたれかかった体勢で犯されている%CALLNAME:TARGET%の脚の間に身体を割り込ませて、膣穴に%NAMES("ペニス", PLAYER)%を
			ENDIF
		ELSEIF INSERT("騎乗位")
			PRINTFORM に跨って腰を振っている%CALLNAME:TARGET%を押し倒して、無防備なアソコに%NAMES("ペニス", PLAYER)%を
		ELSE
			PRINTFORM に体勢を変えさせて、既に後ろの穴で咥えこんでいる%CALLNAME:TARGET%のアソコに%NAMES("ペニス", PLAYER)%を
		ENDIF
		IF RAND:2 && ABL:Ｖ拡張 > SIZE("ペニス", PLAYER)
			PRINTW 根元まで挿入した…
		ELSE
			PRINTW 深々と挿入した…
		ENDIF
	ELSEIF SELECTCOM >= 70 && SELECTCOM <= 79
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:(PLAYER:1)%
		IF INSERT("対面座位")
			IF CONFIG("立位")
				IF TALENT:小柄 
					IF RAND:2 == 0
						PRINTFORM に駅弁
					ELSE
						PRINTFORM に%TEXTR("しがみついた/抱きついた/身体を預けた/両足を抱えられた")%体勢
					ENDIF
				ELSE
					PRINTFORM に%TEXTR("しがみついた/抱きついた/身体を預けた/片足を抱きかかえられた")%体勢
				ENDIF
				PRINTFORM で犯されている%CALLNAME:TARGET%のアナルに%NAMES("ペニス", PLAYER)%を押し当て、
			ELSE
				PRINTFORM に抱きついた体勢で犯されている%CALLNAME:TARGET%のアナルに%NAMES("ペニス", PLAYER)%を押し当て、
			ENDIF
		ELSEIF INSERT("騎乗位")
			PRINTFORM に跨って腰を振っている%CALLNAME:TARGET%に前傾姿勢をとらせて、押し開いたアナルに%NAMES("ペニス", PLAYER)%を
		ELSE
			PRINTFORM に体勢を変えさせて、既に前の穴で咥えこんでいる%CALLNAME:TARGET%のアナルに%NAMES("ペニス", PLAYER)%を
		ENDIF
		IF RAND:2 && ABL:Ａ拡張 > SIZE("ペニス", PLAYER)
			PRINTW 根元まで挿入した…
		ELSE
			PRINTW 深々と挿入した…
		ENDIF
	ENDIF
;同時押し倒し(騎乗位のみ)
ELSEIF TEQUIP:Ｗ押し倒し
	PRINTFORM %CALLNAME:TARGET%は
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	IF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ押し倒し中")%
	ELSE
		SELECTCASE PREVCOM:1
		CASE TNUM("騎乗位"), TNUM("挿入Ｇスポ責め"), TNUM("挿入子宮口責め")
			PRINTFORM %CALLNAME:(TARGET:1)%%TEXTR("の下の口から解放されたばかりの/が膣から解放した")%
		CASE TNUM("アナル騎乗位")
			PRINTFORM %CALLNAME:(TARGET:1)%のアナルから解放されたばかりの
		CASE TNUM("尿道騎乗位")
			PRINTFORM %CALLNAME:(TARGET:1)%のオシッコの穴から解放されたばかりの
		ENDSELECT
	ENDIF
	PRINTFORM %CALLNAME:MASTER%のペニス
	;同じ穴でインサート継続
	IF CHECK_COM("抜かずに続行")
		PRINTFORMW を%TEXTR(@"、艶かしく腰を振りながら味わい続けて/%PREVHOLE%で締め付けながら、激しく腰を動かして愉しんで")%いる…
	;異なる穴でインサート継続
	ELSEIF CHECK_COM("穴を変えて続行")
		PRINTFORMW を%PREVHOLE%から抜き、今度は%SELECTHOLE%で%TEXTR("根元まで呑み込んで/咥え込んで/試して")%きた…
	;インサート開始
	ELSE
		PRINTFORMW にまたがり、ゆっくりと%TEXTR(@"%SELECTHOLE%で呑み込んで/腰を下ろして")%きた…
	ENDIF
	;使う場所を露出させる
	IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
		SIF CHECK_CLO("Ａ露出") == 0
			CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
	ELSEIF TRAIN_GENRE("尿道挿入", SELECTCOM)
		SIF CHECK_CLO("Ｖ露出") == 0
			CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
	ELSE
		SIF CHECK_CLO("Ｖ露出") == 0
			CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
	ENDIF

ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P

;ワイルドスウィング
ELSEIF INSERT("ワイルドスウィング")
	CALL TEXT, "ワイルドスウィング"

ELSEIF CHECK_COM("結腸責め")
	IF TRAIN_GENRE("騎乗位", SELECTCOM) && TEQUIP:押し倒し
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%を解放せず、大きなペニスを
		PRINTFORMW %TEXTR("Ｓ状結腸まで使って先端から根元まで念入りに刺激し/長いストロークで出し入れし/結腸に出し入れするのを楽しみ")%ながら再び腰を動かし始めた…
	ELSEIF PREVPLAY("結腸責め")
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%のお尻に腰をぴったりとつけて、Ｓ状結腸を%TEXTR("念入りに/丹念に/執拗に")%犯し続けた…
	ELSE
		PRINTFORMW %CALLNAME:TARGET%のお尻をしっかりと掴み、腰を突き出して%TEXTR("直腸の更に奥をペニスで/Ｓ状結腸の入り口をペニスで/大きなペニスを根元まで肛門に突き込んで激しく")%犯した…
	ENDIF

;騎乗位は攻め手が逆転しているので別枠。押し倒しの場合はPENIS(PLAYER)は常にON
ELSEIF TRAIN_GENRE("騎乗位", SELECTCOM)
	IF CHECK_COM("抜かずに続行") && CHECK_COM("同じ体位で続行")
		IF TEQUIP:押し倒し
			IF TEQUIP:キス
				PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%を解放せず、%TEXTS("キス継続")%
				CALL SETFLAG, "追加キスフラグ", TARGET
			ELSE
				PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%を解放せず、%TEXTR(@"ペニスを締め付けた/%PREVHOLE%をきつく締めた/繊細な粘膜を隙間なく密着させた")%まま再び
			ENDIF
		ELSE
			IF PENIS(PLAYER)
				IF ABL:露出癖 >= 3 && RAND:2
					PRINTFORM %CALLNAME:TARGET%は腰を上げて肉棒が
					IF INSERT("Ａインサート")
						PRINTFORM 粘液まみれになりながら%TEXTS("アナル：視覚")%を出入りする様を見せ付けた後に再び直腸内へと
					ELSEIF INSERT("尿道インサート")
						PRINTFORM オシッコで濡れ光る様を見せ付けた後に再び尿道に
					ELSE
						PRINTFORM 愛液で濡れ光る様を見せ付けた後に再び膣に
					ENDIF
					PRINTFORM 収めると、
				ELSE
					PRINTFORM %CALLNAME:TARGET%は挿入されたままのペニスを締め付け、
				ENDIF
			ELSE
				PRINTFORM %CALLNAME:TARGET%は挿入されたままの張型を締め付け、
			ENDIF
			IF TEQUIP:キス
				PRINTFORM %CALLNAME:PLAYER%%TEXTS("キス継続助詞付き")%
				CALL SETFLAG, "追加キスフラグ", TARGET
			ELSEIF INSERT("Ａインサート")
				PRINTFORM %TEXTR(@"激しく/お尻を%CALLNAME:PLAYER%の股間に擦りつけながら")%
			ELSEIF ABL:Ｖ感覚 >= 3 && RAND:3 == 0
				PRINTFORM 恍惚とした顔で下腹部を撫でながら
			ELSE
				PRINTFORM %TEXTR("激しく/恥骨を擦りつけながら")%
			ENDIF
		ENDIF
		ACT_TABLE = 腰を動かし始めた
		PRINTFORMW %TEXTR(ACT_TABLE)%。
	;これに限りあなたが主導
	ELSEIF CHECK_COM("抜かずに続行")
		PRINTFORMW %CALLNAME:TARGET%を抱き上げ、%NAMES("ペニス", PLAYER)%で%SELECTHOLE%を突き上げた。

	ELSEIF CHECK_COM("穴を変えて続行")
		PRINTFORM %CALLNAME:TARGET%は腰を浮かして%PREVHOLE%から%NAMES("ペニス", PLAYER)%を\@ PENIS(PLAYER) ? 解放する # 抜く \@と、
		IF INSERT("Ａインサート")
			PRINTFORM 腸液に塗れたソレを今度は%SELECTHOLE%で
		ELSEIF INSERT("尿道インサート")
			PRINTFORM 濡れたソレを今度は%SELECTHOLE%で
		ELSE
			PRINTFORM 愛液に塗れたソレを今度は%SELECTHOLE%で
		ENDIF
		IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
			IF ABL:Ａ拡張 > SIZE("ペニス", PLAYER) && RAND:3 == 0
				IF SIZE("ペニス", PLAYER) >= 3 && EXP:結腸経験
					PRINTFORM 結腸まで駆使して余すことなく
					CALL SETFLAG, "結腸責め", TARGET
				ELSE
					PRINTFORM 根元まで。
				ENDIF
			ENDIF
		ELSEIF TRAIN_GENRE("尿道挿入", SELECTCOM)
			IF RAND:2 && ABL:尿道拡張 >= SIZE("ペニス", PLAYER)
				PRINTFORM 根元まで
			ELSEIF ABL:尿道拡張 >= 1
				PRINTFORM 深々と
			ENDIF
		ELSE
			IF TALENT:妊娠 == 0
				IF RAND:2 && ABL:Ｖ拡張 > SIZE("ペニス", PLAYER)
					PRINTFORM 根元まで
				ELSEIF ABL:Ｖ拡張 >= 2
					PRINTFORM 深々と
				ENDIF
			ENDIF
		ENDIF
		ACT_TABLE = 呑み込んできた/咥え込んできた
		PRINTFORMW %TEXTR(ACT_TABLE)%。
	ENDIF

;Ａインサート連続実行
ELSEIF INSERT("Ａインサート") && CHECK_COM("連続実行")
	PRINTFORM %CALLNAME:TARGET%の
	IF TRAIN_GENRE("背面座位", SELECTCOM) && TALENT:オンナ
		IF CONFIG("立位")
			PRINTFORM 脚を引き寄せるようにして抱きかかえて
			IF TEQUIP:ビデオカメラ && RAND:3
				PRINTFORM 股間をビデオカメラに向かって大きく開かせながら、
			ELSEIF CHECK("助手参加可能") && RAND:2
				PRINTFORM 股間を%CALLNAME:ASSI%に向かって見せつけながら、
			ELSE
				PRINTFORM 股間を大きく開かせながら、
			ENDIF
		ELSEIF TEQUIP:ビデオカメラ && RAND:2
			PRINTFORM アソコをビデオカメラに向かって指で押し開きながら、
		ELSE
			PRINTFORM 胸やアソコを弄ったりしながら、
		ENDIF
	ELSE
		PRINTFORM 腰をしっかりと掴み、
	ENDIF
	SIF (PENIS(PLAYER)) && STATE("ハードスキン", PLAYER)
		PRINTFORM ガチガチのペニスで
	IF TRAIN_GENRE("対面座位", SELECTCOM) || TRAIN_GENRE("背面座位", SELECTCOM)
		PRINTFORMW 激しくアナルを突き上げた…
	ELSE
		PRINTFORMW しゃくりあげる様にして激しくアナルを犯した…
	ENDIF
;尿道インサート連続実行
ELSEIF INSERT("尿道インサート") && CHECK_COM("連続実行")
	PRINTFORM %CALLNAME:PLAYER%は
	IF TALENT:尿道性感 == 0 && SIZE("ペニスの直径", PLAYER) >  MIN_STR(CONDS("尿道拡張度") )
		PRINTFORM 腰の引けた%CALLNAME:TARGET%をグッと引き寄せて、
	ELSEIF CHECK("尿道インサート") && RAND:2
		PRINTFORM 余裕の表情で腰を動かす%CALLNAME:TARGET%に合わせて、
	ELSE
		IF PENIS(TARGET)
			PRINTFORM %CALLNAME:TARGET%の\@ SIZE("ペニス", TARGET) >= 3 ? 馬鹿でかい # \@ペニスをしっかりと握って、
			USEHOLE = ソコ
		ELSE
			PRINTFORM %CALLNAME:TARGET%の腰をしっかりと掴んで、
		ENDIF
	ENDIF
	;尿道内射精直後
	IF PREVEX("尿道内射精")
		IF TALENT:尿道性感 && RAND:2
			PRINTFORM 更なる精液を搾り取らんと締め付けてくる%USEHOLE%を
			SIF PENIS(TARGET) == 0
				PRINTFORM \@ SIZE("ペニス", PLAYER) >= 2 ? 太い # \@ペニスで
			PRINTFORMW 掘削し続けた…
		ELSE
			IF USEHOLE == "尿道"
				PRINTFORMW 精液を注ぎ込んだばかりの排泄器官を執拗に責め立て続けた…
			ELSE
				PRINTFORMW 精液を注ぎ込んだばかりの%USEHOLE%を執拗に責め立て続けた…
			ENDIF
		ENDIF
	ELSE
		IF TRAIN_GENRE("対面座位", SELECTCOM) && TALENT:恋慕 && CHECK_COM("キス")
			PRINTFORM 舌を絡めあいながら
		ELSEIF (ABL:PLAYER:サドっ気 >= 3 || TALENT:尿道性感) && RAND:3 == 0
			PRINTFORM セックスする時のように
		ENDIF
		IF (ABL:PLAYER:サドっ気 >= 3 || TALENT:尿道性感) && RAND:3 == 0
			PRINTFORMW 荒々しく%USEHOLE%を犯し続けた…
		ELSEIF PENIS(TARGET) == 0 && ABL:PLAYER:サドっ気 >= 3 || (TALENT:尿道性感 && RAND:2 == 0)
			PRINTFORMW 激しくペニスを突き立てた…
		ELSE
			PRINTFORMW 腰を使いながら\@ PENIS(TARGET) ? 海綿体 # 尿道 \@の締め付けを楽しんだ…
		ENDIF
	ENDIF

;同じ体位で続行。連続実行みたいな扱い
ELSEIF CHECK_COM("抜かずに続行") && CHECK_COM("同じ体位で続行")
	IF TEQUIP:キス
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%%TEXTS("キス継続助詞付き")%、
		CALL SETFLAG, "追加キスフラグ", TARGET
	ELSEIF TRAIN_GENRE("屈曲位", SELECTCOM)
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の脚を押し開いて股間を全開にさせた恥ずかしい体勢のまま、
	ELSEIF TRAIN_GENRE("背面座位", SELECTCOM) && CONFIG("立位") == 0
		IF TALENT:妊娠 && RAND:2 == 0
			PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の大きなお腹を撫でながら、
		ELSE
			PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%を背後から抱きしめながら、
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:TARGET%の%TEXTR(@"%USEHOLE%の感触を味わいながら/%PREVHOLE%から抜かずに")%、
		IF TRAIN_GENRE("後背位", SELECTCOM)
			IF RAND:2
				PRINTFORM %TEXTS("お尻：感触")%を鷲掴んで
				SIF RAND:2 && TRAIN_GENRE("Ａ挿入", SELECTCOM) == 0 && ABL:PLAYER:サドっ気 && USE_A(TARGET) == 0 && CHECK_CLO("Ａ露出")
					PRINTFORM アナルを眺めながら
			ELSE
				PRINTFORM %TEXTS("お尻：感触")%を引き寄せて
			ENDIF
		ENDIF
	ENDIF
	ACT_TABLE = 再び腰を動かし始めた
	SIF TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += "/再び激しく腰を打ち付け始めた"
	SIF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += @"/再び%SELECTHOLE%を犯しはじめた"
	SIF TRAIN_GENRE("対面座位", SELECTCOM)
		ACT_TABLE += "/再び腰を揺すり始めた"
	SIF TRAIN_GENRE("背面座位", SELECTCOM)
		ACT_TABLE += "/再び身体を揺さぶり始めた"
	SIF CONFIG("立位") && TALENT:PLAYER:力持ち && TRAIN_GENRE("対面座位", SELECTCOM) + TRAIN_GENRE("背面座位", SELECTCOM)
		ACT_TABLE += "/歩き回る事で身体の奥深くを強烈に刺激し始めた/歩き回りながらの抜き差しを再開した"
	PRINTFORMW %TEXTR(ACT_TABLE)%。

;同じ穴を使用して、異なる体位で続行
ELSEIF CHECK_COM("抜かずに続行")
	IF TRAIN_GENRE("正常位", SELECTCOM)
		IF INSERT("後背位") || INSERT("屈曲位")
			IF CONFIG("立位")
				PRINTFORM 一旦引き抜いたのちに%CALLNAME:TARGET%と向き合い、
			ELSE
				PRINTFORM 繋がりあったまま%CALLNAME:TARGET%を仰向けに寝かせ、
			ENDIF
		ELSEIF INSERT("対面座位")
			IF CONFIG("立位")
				PRINTFORM 抱き合ったまま%CALLNAME:TARGET%を下ろして自分で立たせ、
			ELSE
				PRINTFORM 抱き合ったまま%CALLNAME:TARGET%を押し倒すように寝かせ、
			ENDIF
		ELSEIF INSERT("背面座位")
			PRINTFORM %USEHOLE%から抜き去ることなく%CALLNAME:TARGET%を仰向けに寝かせ、
		ELSEIF INSERT("騎乗位")
			PRINTFORM %CALLNAME:TARGET%と体を入れ替えて仰向けに押し倒し、
		ENDIF
	ELSEIF TRAIN_GENRE("後背位", SELECTCOM)
		IF INSERT("正常位") || INSERT("屈曲位")
			IF CONFIG("立位")
				PRINTFORM 一旦引き抜いて%CALLNAME:TARGET%に背中を向かせつつ腰を突き出させると、
			ELSE
				PRINTFORM 繋がりあったまま%CALLNAME:TARGET%を四つん這いにして腰を高く上げさせると、
			ENDIF
		ELSEIF INSERT("対面座位")
			PRINTFORM %USEHOLE%から抜き去ることなく%CALLNAME:TARGET%の後ろに回り、
		ELSEIF INSERT("背面座位")
			IF CONFIG("立位")
				PRINTFORM 抱き合ったまま%CALLNAME:TARGET%を下ろして自分で立たせ、
			ELSE
				PRINTFORM %CALLNAME:TARGET%の身体を前に倒して四つん這いにし、
			ENDIF
		ELSEIF INSERT("騎乗位")
			PRINTFORM %USEHOLE%から抜き去ることなく%CALLNAME:TARGET%の後ろに回り、
		ENDIF
		IF RAND:2
			PRINTFORM %TEXTS("お尻：感触")%を鷲掴んで
			SIF RAND:2 && ABL:PLAYER:サドっ気 && USE_A(TARGET) == 0 && CHECK_CLO("Ａ露出")
				PRINTFORM アナルを眺めながら
		ELSE
			PRINTFORM %TEXTS("お尻：感触")%を引き寄せて
		ENDIF
	ELSEIF TRAIN_GENRE("屈曲位", SELECTCOM)
		IF RAND:2
			PRINTFORM %CALLNAME:TARGET%の両脚を掴んで腰を浮かせながら前に押し倒し、膝を肩に押し付けながら
		ELSEIF PENIS(TARGET)
			PRINTFORM %CALLNAME:TARGET%をひっくり返し、ペニスが顔の前に来るようにしながら
		ELSE
			PRINTFORM %CALLNAME:TARGET%をひっくり返し、アソコが顔の前に来るようにしながら
		ENDIF
	ELSEIF TRAIN_GENRE("対面座位", SELECTCOM)
		IF INSERT("正常位") || INSERT("屈曲位")
			IF CONFIG("立位")
				PRINTFORM %CALLNAME:TARGET%を自分に抱きつかせながら下からお尻を抱きかかえて持ち上げ、
			ELSE
				PRINTFORM %CALLNAME:TARGET%の身体を起こしてぎゅっと抱き寄せ、
			ENDIF
		ELSEIF INSERT("後背位")
			PRINTFORM %CALLNAME:TARGET%を仰向けにし、身体を抱き起こして
		ELSEIF INSERT("背面座位")
			PRINTFORM 繋がりあったまま%CALLNAME:TARGET%をこちらに向かせ、抱き合うようにして
		ELSEIF INSERT("騎乗位")
			PRINTFORM 体を起こして%CALLNAME:TARGET%をぎゅっと抱き寄せ、
		ENDIF
	ELSEIF TRAIN_GENRE("背面座位", SELECTCOM)
		IF CONFIG("立位")
			PRINTFORM %USEHOLE%から抜き去ることなく%CALLNAME:TARGET%を自分にもたれ掛けさせ、太ももの下に腕を通して抱きかかえるようにして立ち上がって
		ELSE
			PRINTFORM %USEHOLE%から抜き去ることなく%CALLNAME:TARGET%の身体を背中から抱き寄せ、
		ENDIF
	ENDIF
	ACT_TABLE = 再び腰を動かし始めた
	SIF TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += "/激しく腰を打ち付け始めた"
	SIF TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += "/犯しはじめた"
	SIF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
		ACT_TABLE += @"/%SELECTHOLE%を犯しはじめた"
	SIF TRAIN_GENRE("対面座位", SELECTCOM)
		ACT_TABLE += "/腰を揺すり始めた"
	SIF TRAIN_GENRE("背面座位", SELECTCOM)
		ACT_TABLE += "/身体を揺さぶり始めた"
	PRINTFORMW %TEXTR(ACT_TABLE)%。

;使う穴を変えてインサート続行
ELSEIF CHECK_COM("穴を変えて続行")
	;同じ体位のまま
	IF CHECK_COM("同じ体位で続行")
		IF TRAIN_GENRE("対面座位", SELECTCOM) || TRAIN_GENRE("背面座位", SELECTCOM)
			PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の%PREVHOLE%から%NAMES("ペニス", PLAYER)%を%TEXTR("引き抜いて/抜いて")%、今度は
		ELSEIF TRAIN_GENRE("屈曲位", SELECTCOM)
			IF RAND:2
				PRINTFORM %CALLNAME:PLAYER%は%NAMES("ペニス", PLAYER)%を%PREVHOLE%から引き抜き、大股を開かせた恥ずかしい体勢のまま今度は
			ELSE
				PRINTFORM 大股を開かされて腰を高く上げた体勢の%CALLNAME:TARGET%の%PREVHOLE%から%NAMES("ペニス", PLAYER)%を引き抜き、今度は
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:PLAYER%は%NAMES("ペニス", PLAYER)%を%PREVHOLE%から引き抜いて、勢いそのままに%CALLNAME:TARGET%の
		ENDIF
	;体位も変える
	ELSE
		IF TRAIN_GENRE("正常位", SELECTCOM)
			IF INSERT("屈曲位")
				PRINTFORM 大股を開かせた恥ずかしい体勢のまま、今度は%CALLNAME:TARGET%の
			ELSEIF INSERT("対面座位")
				PRINTFORM %NAMES("ペニス", PLAYER)%を%PREVHOLE%から引き抜いて、
				IF CONFIG("立位")
					PRINTFORM %CALLNAME:TARGET%を下ろして対面立位の体勢になりながら
				ELSE
					PRINTFORM %TEXTR(@"%CALLNAME:TARGET%を抱きしめたまま、今度は/勢いそのままに%CALLNAME:TARGET%の")%
				ENDIF
			ELSE
				PRINTFORM %CALLNAME:TARGET%を仰向けに寝かせ、%PREVHOLE%%TEXTR("を犯してい/から引き抜い")%た%NAMES("ペニス", PLAYER)%で
			ENDIF
		ELSEIF TRAIN_GENRE("後背位", SELECTCOM)
			IF INSERT("背面座位")
				IF CONFIG("立位")
					PRINTFORM 恥ずかしい背面駅弁で犯していた%CALLNAME:TARGET%を下ろして立ちバックの体勢になりながら、今度は
				ELSE
					PRINTFORM %CALLNAME:TARGET%の身体を前に倒して四つん這いにし、今度は
				ENDIF
			ELSE
				PRINTFORM %CALLNAME:TARGET%を四つん這いにして腰を高く上げさせ、
				IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
					PRINTFORM %TEXTR("よく見えるようになった/お尻を鷲掴みながら")%
				ELSE
					PRINTFORM %TEXTS("お尻：感触")%を%TEXTR("引き寄せて/鷲掴みながら")%
				ENDIF
			ENDIF
		ELSEIF TRAIN_GENRE("屈曲位", SELECTCOM)
			PRINTFORM %CALLNAME:TARGET%を脚を掴んでひっくり返し、%PREVHOLE%から%TEXTR("引き抜い/引きずり出し/抜い")%た%NAMES("ペニス", PLAYER)%で
		ELSEIF TRAIN_GENRE("対面座位", SELECTCOM) || TRAIN_GENRE("背面座位", SELECTCOM)
			IF CONFIG("立位")
				PRINTFORM %NAMES("ペニス", PLAYER)%を%PREVHOLE%から引き抜き、抱き上げて駅弁の体勢に移りながら今度は
			ELSE
				PRINTFORM %NAMES("ペニス", PLAYER)%を%PREVHOLE%から引き抜いて、体勢を変えながら今度は
			ENDIF
		ENDIF
	ENDIF
	ACT_TABLE = %SELECTHOLE%を犯しはじめた
	IF TRAIN_GENRE("Ａ挿入", SELECTCOM)
		IF TALENT:オトコ == 0
			SIF TRAIN_GENRE("正常位", SELECTCOM) + TRAIN_GENRE("対面座位", SELECTCOM)
				ACT_TABLE += "アソコの下でヒクついているアナルを犯しはじめた"
			SIF TRAIN_GENRE("後背位", SELECTCOM)
				ACT_TABLE += "アソコの上で息づいているアナルを犯しはじめた"
		ENDIF
		IF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
			SIF SIZE("ペニス", PLAYER) >= 2
				ACT_TABLE += @"/直腸の最奥まで突き入れた/%SELECTHOLE%を深々と貫いた"
		ENDIF
	ELSEIF TRAIN_GENRE("尿道挿入", SELECTCOM)
		IF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
			SIF SIZE("ペニス", PLAYER) >= 2
				ACT_TABLE += @"/膀胱に根元まで突き入れた/%SELECTHOLE%を深々と貫いた"
		ENDIF
	ELSE
		IF TRAIN_GENRE("正常位", SELECTCOM) || TRAIN_GENRE("後背位", SELECTCOM) || TRAIN_GENRE("屈曲位", SELECTCOM)
			SIF SIZE("ペニス", PLAYER) >= 2
				ACT_TABLE += @"/%SELECTHOLE%の最奥まで突き入れた/%SELECTHOLE%を深々と貫いた"
		ENDIF
	ENDIF
	PRINTFORMW %TEXTR(ACT_TABLE)%。
;駅弁はここに記述
ELSEIF CONFIG("立位")
	IF TRAIN_GENRE("対面座位", SELECTCOM)
		PRINTFORM %CALLNAME:PLAYER%は
		IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
			CALL ADD_TEXT, "性器から性器へ"
			PRINTFORM %CALLNAME:TARGET%に正面から抱きつかせながら、
		ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
			PRINTFORM %CALLNAME:TARGET%の
			CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET
			PRINTFORM 、正面から抱きつかせながら
		ELSE
			PRINTFORM %CALLNAME:TARGET%に正面から抱きつかせながら
		ENDIF
		PRINTFORMW %SELECTHOLE%でペニスを咥え込まさせ、立ち上がって駅弁スタイルで犯し始めた。
	ELSEIF TRAIN_GENRE("背面座位", SELECTCOM)
		PRINTFORM %CALLNAME:PLAYER%は
		IF COND("前Ｔから対象のみ変更") && VAU_INSERT_PLAY(PREVCOM:1)
			CALL ADD_TEXT, "性器から性器へ"
			PRINTFORM %CALLNAME:TARGET%に身体をもたれかけさせながら
		ELSEIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
			PRINTFORM %CALLNAME:TARGET%の
			CALL SETFLAG, "ボトムを露出：テキスト有り", TARGET
			PRINTFORM 、身体をもたれかけさせながら
		ELSE
			PRINTFORM %CALLNAME:TARGET%に身体をもたれかけさせながら
		ENDIF
		PRINTFORMW %SELECTHOLE%でペニスを咥え込まさせ、背中を反らしながら立ち上がっていわゆる背面駅弁スタイルで犯し始めた。
	ENDIF
ENDIF


@FLAG_INSERTION, ARGS, ARG, ARG:1
SELECTCASE ARGS
CASE "尿道インサート"
	CALL SETFLAG, "尿道性感フラグ", TARGET
	;尿の汚れ発生
	STAIN:PLAYER:Ｐ = SET_STAIN(STAIN:PLAYER:Ｐ, "尿")
	CALL SETFLAG, "Ｐ汚れ：尿"

	IF PENIS(TARGET)
		STAIN:Ｐ = SET_STAIN(STAIN:Ｐ, "尿")
		;対象のＰ⇔調教者のＰの汚れが移動
		CALL EQ_STAIN, TARGET, "Ｐ", PLAYER, "Ｐ"
	ELSE
		STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "尿")
	ENDIF

	;パンツをずらす
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET

CASE "尿道バイブ"
	CALL SETFLAG, "尿道性感フラグ", TARGET
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET

CASE "Ｖインサート", "子宮挿入", "子宮口責め"
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET

	IF TALENT:PLAYER:童貞
		CALL SETFLAG, "童貞狩りフラグ", TARGET
		CALL SETFLAG, "童貞喪失フラグ", PLAYER
	ENDIF
	;対象のＶ⇔調教者のＰの汚れが移動
	SIF TEQUIP:PLAYER:コンドーム == 0
		CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "Ｐ"

	CALL SETFLAG, "Ｐ汚れ：Ｖ"

	IF PLAYER == MASTER
		IF ABL:Ｖ感覚 >= 3
			TFLAG:好感度ボーナス += 2
		ELSE
			TFLAG:好感度ボーナス += 1
		ENDIF
	ENDIF
	;PLAYERのペニスのＰ潤とＶ潤の相互干渉
	IF PENIS(PLAYER) && TEQUIP:PLAYER:コンドーム == 0
		IF PALAM:PLAYER:Ｐ潤 >= PALAM:Ｖ潤
			CUP:Ｖ潤 += (PALAM:PLAYER:Ｐ潤 - PALAM:Ｖ潤)/2
		ELSE
			CUP:PLAYER:Ｐ潤 += (PALAM:Ｖ潤 - PALAM:PLAYER:Ｐ潤)/2
		ENDIF
	ENDIF
CASE "指挿入れ"
	;対象のＶ⇔調教者の指の汚れが移動
	CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "手"
CASE "バイブ"
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
CASE "Ｖフィスト"
	SIF !CHECK_CLO("Ｖ露出") && CHECK_CLO("Ｖ露出可")
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET

	;対象のＶ⇔調教者の手の汚れが移動
	CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "手"
CASE "Ａインサート"
	SIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET

	;結腸責めをしているならフラグをセット
	SIF CHECK_COM("結腸責め")
		CALL SETFLAG, "結腸責め", TARGET

	;対象のＡ⇔調教者のＰの汚れが移動
	SIF TEQUIP:PLAYER:コンドーム == 0
		CALL EQ_STAIN, TARGET, "Ａ", PLAYER, "Ｐ"

	CALL SETFLAG, "Ｐ汚れ：Ａ"

	IF PLAYER == MASTER
		IF ABL:Ａ感覚 >= 3
			TFLAG:好感度ボーナス += 2
		ELSE
			TFLAG:好感度ボーナス += 1
		ENDIF
	ENDIF
	IF BASE:排泄物
		;精液浣腸中は排泄欲が5%上昇
		IF COND("精液浣腸")
			BASE:排泄欲 += MULTIPLY(MAXBASE:排泄欲, 5 + BASE:排泄物) * ARG:1 / 100
		ELSEIF BASE:排泄欲 < MAXBASE:排泄欲 - 1
			BASE:排泄欲 = MIN(BASE:排泄欲 + MULTIPLY(MAXBASE:排泄欲, (5 + BASE:排泄物)/3) * ARG:1 / 100, MAXBASE:排泄欲 - 1)
		ENDIF
	ENDIF
	;PLAYERのペニスのＰ潤とＡ潤の相互干渉
	IF PENIS(PLAYER) && TEQUIP:PLAYER:コンドーム == 0
		IF PALAM:PLAYER:Ｐ潤 >= PALAM:Ａ潤
			CUP:Ａ潤 += (PALAM:PLAYER:Ｐ潤 - PALAM:Ａ潤)/2
		ELSE
			CUP:PLAYER:Ｐ潤 += (PALAM:Ａ潤 - PALAM:PLAYER:Ｐ潤)/2
		ENDIF
	ENDIF
CASE "Ａ指挿入れ"
	;対象のＡ⇔調教者の指の汚れが移動
	CALL EQ_STAIN, TARGET, "Ａ", PLAYER, "手"

	IF BASE:排泄物
		;精液浣腸中は排泄欲が1%上昇
		IF COND("精液浣腸")
			BASE:排泄欲 += MULTIPLY(MAXBASE:排泄欲, 1 + BASE:排泄物) * ARG:1 / 100
		ELSEIF BASE:排泄欲 < MAXBASE:排泄欲 - 1
			BASE:排泄欲 = MIN(BASE:排泄欲 + MULTIPLY(MAXBASE:排泄欲, 1) * ARG:1 / 100, MAXBASE:排泄欲 - 1)
		ENDIF
	ENDIF
CASE "Ａバイブ"
	SIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
	;結腸責めをしているならフラグをセット
	SIF CHECK_COM("結腸責め")
		CALL SETFLAG, "結腸責め", TARGET

	IF BASE:排泄物
		;精液浣腸中は排泄欲が5%上昇
		IF COND("精液浣腸")
			BASE:排泄欲 += MULTIPLY(MAXBASE:排泄欲, 5 + BASE:排泄物) * ARG:1 / 100
		ELSEIF BASE:排泄欲 < MAXBASE:排泄欲 - 1
			BASE:排泄欲 = MIN(BASE:排泄欲 + MULTIPLY(MAXBASE:排泄欲, (5 + BASE:排泄物)/3) * ARG:1 / 100, MAXBASE:排泄欲 - 1)
		ENDIF
	ENDIF
CASE "Ａフィスト"
	SIF !CHECK_CLO("Ａ露出") && CHECK_CLO("Ａ露出可")
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
	;結腸責めをしているならフラグをセット
	SIF CHECK_COM("結腸責め")
		CALL SETFLAG, "結腸責め", TARGET

	;対象のＡ⇔調教者の手の汚れが移動
	CALL EQ_STAIN, TARGET, "Ａ", PLAYER, "手"
	IF BASE:排泄物
		;精液浣腸中は排泄欲が20%上昇
		IF COND("精液浣腸")
			BASE:排泄欲 += MULTIPLY(MAXBASE:排泄欲, 20 + BASE:排泄物) * ARG:1 / 100
		ELSEIF BASE:排泄欲 < MAXBASE:排泄欲 - 1
			BASE:排泄欲 = MIN(BASE:排泄欲 + MULTIPLY(MAXBASE:排泄欲, (20 + BASE:排泄物)/3) * ARG:1 / 100, MAXBASE:排泄欲 - 1)
		ENDIF
	ENDIF
CASE "Ｖ逆インサート"
	IF TALENT:童貞
		CALL SETFLAG, "童貞狩りフラグ", PLAYER
		CALL SETFLAG, "童貞喪失フラグ", TARGET
	ENDIF
	CALL SETFLAG, "下半身を脱がす", PLAYER
	SIF PENIS(TARGET)
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
	;対象のＰ⇔調教者のＶの汚れが移動
	SIF TEQUIP:コンドーム == 0
		CALL EQ_STAIN, TARGET, "Ｐ", PLAYER, "Ｖ"
	CALL SETFLAG, "Ｐ汚れ：Ｖ", PLAYER

CASE "Ａ逆インサート"
	CALL SETFLAG, "下半身を脱がす", PLAYER
	SIF PENIS(TARGET)
		CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET
	;対象のＰ⇔調教者のＡの汚れが移動
	SIF TEQUIP:コンドーム == 0
		CALL EQ_STAIN, TARGET, "Ｐ", PLAYER, "Ａ"
	CALL SETFLAG, "Ｐ汚れ：Ａ", PLAYER
ENDSELECT

;処女でも挿入するコマンドなど、特殊な分岐
SELECTCASE ARGS
CASE "尿道インサート"
	IF TALENT:尿道処女
		CALL SETFLAG, "尿道処女喪失フラグ", TARGET
		CALL SETFLAG, "尿道処女簒奪フラグ", PLAYER
	ENDIF
CASE "子宮挿入"
	CALL SETFLAG, "子宮責めフラグ", TARGET
	CALL SETFLAG, "子宮挿入フラグ", TARGET
CASE "子宮口責め"
	CALL SETFLAG, "子宮責めフラグ", TARGET
	IF TALENT:処女
		CALL SETFLAG, "処女喪失フラグ", TARGET
		CALL SETFLAG, "処女簒奪フラグ", PLAYER
	ENDIF
CASE "Ｖインサート", "バイブ", "Ｖフィスト", "Ｖドラ姦"
	IF TALENT:処女
		CALL SETFLAG, "処女喪失フラグ", TARGET
		CALL SETFLAG, "処女簒奪フラグ", PLAYER
	ENDIF
CASE "Ｖ逆インサート"
	IF TALENT:PLAYER:処女
		CALL SETFLAG, "処女喪失フラグ", PLAYER
		CALL SETFLAG, "処女簒奪フラグ", TARGET
	ENDIF
CASE "Ａインサート", "Ａドラ姦"
	IF TALENT:Ａ処女
		CALL SETFLAG, "Ａ処女喪失フラグ", TARGET
		CALL SETFLAG, "Ａ処女簒奪フラグ", PLAYER
	ENDIF
	IF COND("好きな体位中")
		TFLAG:好感度ボーナス += (3 + RAND:3) * ARG:1 / 100
		FLAG:ムード += (3 + RAND:3) * ARG:1 / 100
	ENDIF
CASE "Ａ逆インサート"
	IF TALENT:PLAYER:Ａ処女
		CALL SETFLAG, "Ａ処女喪失フラグ", PLAYER
		CALL SETFLAG, "Ａ処女簒奪フラグ", TARGET
	ENDIF
ENDSELECT


@SOURCE_INSERTION, ARGS, ARG, ARG:1
#DIM SIZE_DEF
#DIM PALAM_RES
#DIM EXP_REQ
#DIM DAMAGE
#DIMS NAME_INSERT

;Ｖインサート等の激しさのSOURCE。@SOURCE_COM60等で呼ばれる
;ARG:1は主にインサートプレイの激しさで、痛みと快楽が両方増える（バイブやフィストなどは通常100のまま）
CALL VARSET_COMF
VARSET NAME_INSERT

;NAME_INSERTは挿入するもの
SELECTCASE ARGS
CASE "尿道インサート"
	NAME_INSERT = ペニス
	CALL SOURCE_DOWNBASE_HP, 83
	SOURCE:0:不潔 = 100 + COM_ORDER_STAIN("Ｐ", "尿道")*300
	SOURCE:0:屈従 = 10000
	SOURCE:0:逸脱 = 1000 + SIZE("ペニス", PLAYER)*5000
	SOURCE:0:痛み = 500
	SOURCE:0:恐れ = 500

	;ABL:尿道感覚をみる
	SOURCE:0:尿道快楽 = CALCF("尿道刺激", 2)
	;調教者の腰使いをみる
	SOURCE:0:尿道快楽 = SOURCE_EXABL(SOURCE:0:尿道快楽, "腰使い", PLAYER)

CASE "尿道指挿入れ"
	NAME_INSERT = 指
	CALL SOURCE_DOWNBASE_HP, 15
	SOURCE:0:不潔 = 100 + COM_ORDER_STAIN("手", "尿道")*300
	SOURCE:0:露出 = 300
	SOURCE:0:屈従 = 3000
	SOURCE:0:逸脱 = 1000
	SOURCE:0:痛み = 500
	SOURCE:0:恐れ = 100

	;ABL:尿道感覚をみる
	SOURCE:0:尿道快楽 = CALCF("尿道刺激", 1)
	;調教者の腰使いをみる
	SOURCE:0:尿道快楽 = SOURCE_EXABL(SOURCE:0:尿道快楽, "指使い", PLAYER)

CASE "尿道ブジー"
	NAME_INSERT = 尿道ブジー
	CALL SOURCE_DOWNBASE_HP, 151
	SOURCE:0:露出 = 2000
	SOURCE:0:屈従 = 5000
	SOURCE:0:逸脱 = 2000
	SOURCE:0:恐れ = 500

	;ABL:尿道感覚をみる
	SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 0), 90 + ITEM:尿道ブジー*10)

CASE "尿道バイブ"
	NAME_INSERT = 尿道バイブ
	CALL SOURCE_DOWNBASE_HP, 152

	SOURCE:0:露出 = 500
	SOURCE:0:屈従 = 3000
	SOURCE:0:逸脱 = 3000
	SOURCE:0:恐れ = 1000

	SELECTCASE SIZE("尿道バイブ")
	CASE 3
		SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 2), 90 + ITEM:極太バイブ*10)
	CASE 4
		SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 2), 90 + ITEM:腕型バイブ*10)
	CASE 5
		SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 2), 90 + ITEM:馬型バイブ*10)
	CASEELSE
		SOURCE:0:尿道快楽 = MULTIPLY(CALCF("尿道刺激", 2), 90 + ITEM:バイブセット*10)
	ENDSELECT
	;調教者の指使いをみる
	SOURCE:0:尿道快楽 = SOURCE_EXABL(SOURCE:0:尿道快楽, "指使い", PLAYER)


;子宮挿入の逸脱のソースは下で決定
CASE "子宮挿入"
	NAME_INSERT = ペニス
	CALL SOURCE_DOWNBASE_HP, 68
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｖ")*500 + 100
	SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 2)

	;調教者の腰使いをみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "腰使い", PLAYER)
	;子宮性感がないと気持ちよくない
	IF TALENT:子宮性感
		TIMES SOURCE:0:Ｖ快楽, 1.25
	ELSE
		TIMES SOURCE:0:Ｖ快楽, 0.25
		SOURCE:0:恐れ = 1000
	ENDIF
CASE "子宮口責め"
	NAME_INSERT = ペニス
	CALL SOURCE_DOWNBASE_HP, 67
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｖ")*300 + 100
	SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 2)

	;調教者の腰使いをみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "腰使い", PLAYER)
	;子宮性感がないと気持ちよくない
	IF TALENT:子宮性感
		TIMES SOURCE:0:Ｖ快楽, 1.25
	ELSE
		TIMES SOURCE:0:Ｖ快楽, 0.25
		SOURCE:0:恐れ = 500
	ENDIF
CASE "Ｖインサート"
	NAME_INSERT = ペニス
	CALL SOURCE_DOWNBASE_HP, 60
	SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ｖ")*100 + 100
	IF PENIS(PLAYER) == 0
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:ペニバン)
	ELSE
		SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 2)
	ENDIF

	;調教者の腰使いをみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "腰使い", PLAYER)

CASE "双頭バイブプレイ"
	NAME_INSERT = バイブ
	CALL SOURCE_DOWNBASE_HP, 31
	SOURCE:0:逸脱 = 1000
	SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:バイブセット*10)

	;調教者の腰使いをみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "腰使い", PLAYER)
CASE "指挿入れ"
	NAME_INSERT = 指
	CALL SOURCE_DOWNBASE_HP, 8
	SOURCE:0:不潔 = COM_ORDER_STAIN("手", "Ｖ")*100 + 100
	SOURCE:0:露出 = 300
	SOURCE:0:屈従 = 200 + ABL:Ｖ感覚*200
	SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 1)
	;Gスポット
	IF CHECK_COM("Ｇスポ責め")
		SIF ABL:Ｖ感覚 >= 3
			SOURCE:0:Ｖ快楽 += 500
		SOURCE:0:逸脱 += 500
	ENDIF
	;調教者の指使いをみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "指使い", PLAYER)
CASE "バイブ"
	NAME_INSERT = バイブ
	CALL SOURCE_DOWNBASE_HP, 31
	SOURCE:0:逸脱 = 500
	SELECTCASE SIZE("バイブ")
	CASE 3
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:極太バイブ*10)
	CASE 4
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:腕型バイブ*10)
	CASE 5
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:馬型バイブ*10)
	CASEELSE
		SOURCE:0:Ｖ快楽 = MULTIPLY(CALCF("Ｖ刺激", 2), 90 + ITEM:バイブセット*10)
	ENDSELECT
	;調教者の指使いをみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "指使い", PLAYER)
CASE "Ｖフィスト"
	NAME_INSERT = フィスト
	CALL SOURCE_DOWNBASE_HP, 141
	SOURCE:0:不潔 = COM_ORDER_STAIN("手", "Ｖ")*150 + 100
	SOURCE:0:露出 = 1200
	SOURCE:0:屈従 = 2000
	SOURCE:0:逸脱 = 10000
	SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 3)
	;調教者の指使いをみる
	SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "指使い", PLAYER)
CASE "Ａインサート"
	NAME_INSERT = ペニス
	CALL SOURCE_DOWNBASE_HP, 70

	IF COND("合意：アナルセックス")
		SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ａ")*100 + 100
		SOURCE:0:屈従 = 2000
		SOURCE:0:逸脱 = 3000
	ELSE
		SOURCE:0:不潔 = COM_ORDER_STAIN("Ｐ", "Ａ")*100 + 800
		SOURCE:0:屈従 = 3000
		SOURCE:0:逸脱 = 6000
	ENDIF
	IF PENIS(PLAYER) == 0
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:ペニバン)
	ELSE
		SOURCE:0:Ａ快楽 = CALCF("Ａ刺激", 2)
	ENDIF

	;調教者の腰使いをみる
	SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "腰使い", PLAYER)
CASE "Ａ指挿入れ"
	NAME_INSERT = Ａバイブ
	CALL SOURCE_DOWNBASE_HP, 2
	SOURCE:0:不潔 = COM_ORDER_STAIN("手", "Ａ")*100 + 100
	SOURCE:0:露出 = 500
	SOURCE:0:屈従 = 500
	SOURCE:0:逸脱 = 1000
	SOURCE:0:Ａ快楽 = CALCF("Ａ刺激", 1)
	;調教者の指使いをみる
	SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "指使い", PLAYER)
CASE "Ａバイブ"
	NAME_INSERT = Ａバイブ
	CALL SOURCE_DOWNBASE_HP, 33
	SELECTCASE SIZE("Ａバイブ")
	CASE 1, 2
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:バイブセット*10)
	CASE 3
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:極太バイブ*10)
	CASE 4
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:腕型バイブ*10)
	CASE 5
		SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:馬型バイブ*10)
	CASEELSE
		IF COND("アナルパール")
			SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:アナルパール*10)
		ELSE
			SOURCE:0:Ａ快楽 = MULTIPLY(CALCF("Ａ刺激", 2), 90 + ITEM:アナルバイブ*10)
		ENDIF
	ENDSELECT

	;調教者の指使いをみる
	SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "指使い", PLAYER)
	SOURCE:0:不潔 = 1000
	SOURCE:0:恐れ = 100
	SOURCE:0:屈従 = 1500
	SOURCE:0:逸脱 = 3000
CASE "Ａフィスト"
	NAME_INSERT = フィスト
	CALL SOURCE_DOWNBASE_HP, 142
	SOURCE:0:不潔 = COM_ORDER_STAIN("手", "Ａ")*150 + 800
	SOURCE:0:露出 = 1200
	SOURCE:0:屈従 = 2000
	SOURCE:0:逸脱 = 20000
	SOURCE:0:Ａ快楽 = CALCF("Ａ刺激", 3)
	;調教者の指使いをみる
	SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "指使い", PLAYER)
ENDSELECT

;SIZE_DEFは挿入しているもののSIZEから拡張度を引いた値で抵抗を表している。トントンなら０で大きいほど痛い
SELECTCASE ARGS
CASE "尿道インサート", "尿道指挿入れ", "尿道バイブ"
	SIZE_DEF = SIZE(NAME_INSERT) - ABL:尿道拡張
CASE "尿道ブジー"
	;尿道拡張は7mmに付き1LVあがる
	SELECTCASE SIZE(NAME_INSERT) - MIN_STR(CONDS("尿道拡張度"))*21/20
	CASE IS < -1
		SIZE_DEF = -2
	CASE -1 TO 1
		SIZE_DEF = SIZE(NAME_INSERT) - MIN_STR(CONDS("尿道拡張度"))*21/20
	CASEELSE
		SIZE_DEF = 1 + (SIZE(NAME_INSERT) - MIN_STR(CONDS("尿道拡張度"))*21/20 )/2
	ENDSELECT
CASE "子宮挿入"
	SIZE_DEF = 1 + TALENT:小柄 - TALENT:淫壷
	SELECTCASE GET_EXP_LV(EXP:子宮姦経験) + TALENT:子宮性感
	CASE 3
		SIZE_DEF -= 1
	CASE IS >= 4
		SIZE_DEF -= 2
	ENDSELECT
	SIF TALENT:妊娠 || MARK:淫紋 >= 4
		SIZE_DEF -= 1
	SIF SIZE(NAME_INSERT) >= 3
		SIZE_DEF += SIZE(NAME_INSERT)/2
	SIZE_DEF = MAX(SIZE_DEF, -1)
CASE "Ｖインサート", "子宮口責め"
	IF INSERT("Ｖ二本挿入") || CHECK_COM("Ｖ二本挿入")
		SIZE_DEF = SIZE("Ｗペニス") - ABL:Ｖ拡張
	ELSEIF INSERT("二穴挿入") || CHECK_COM("二穴挿入")
		SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ｖ拡張 + 1
	ELSE
		SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ｖ拡張
	ENDIF
	SIF ABL:Ｖ使い/2 > SIZE(NAME_INSERT) && SIZE_DEF >= 0
		SIZE_DEF -= 1
CASE "指挿入れ", "バイブ", "Ｖフィスト", "双頭バイブプレイ"
	SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ｖ拡張
	SIF ABL:Ｖ使い/2 > SIZE(NAME_INSERT) && SIZE_DEF >= 0
		SIZE_DEF -= 1
CASE "Ａインサート"
	IF INSERT("Ａ二本挿入") || CHECK_COM("Ａ二本挿入")
		SIZE_DEF = SIZE("Ｗペニス") - ABL:Ａ拡張
	ELSEIF INSERT("二穴挿入") || CHECK_COM("二穴挿入")
		SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ａ拡張 + 1
	ELSE
		SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ａ拡張
	ENDIF
	;アナルは抵抗がある
	SIF (TALENT:Ａ性向 == 0 || ABL:Ａ感覚 == 0) && EXP:Ａ経験 < EXP_LV(SIZE(NAME_INSERT) + 2)
		SIZE_DEF += 1
	SIF ABL:Ａ使い > SIZE(NAME_INSERT) && SIZE_DEF >= 0
		SIZE_DEF -= 1
CASE "Ａ指挿入れ", "Ａバイブ", "Ａフィスト"
	SIZE_DEF = SIZE(NAME_INSERT) - ABL:Ａ拡張
	;アナルは抵抗がある
	SIF (TALENT:Ａ性向 == 0 || ABL:Ａ感覚 == 0) && EXP:Ａ経験 < EXP_LV(SIZE(NAME_INSERT) + 2)
		SIZE_DEF += 1
	SIF ABL:Ａ使い/2 > SIZE(NAME_INSERT) && SIZE_DEF >= 0
		SIZE_DEF -= 1
ENDSELECT

SIF TALENT:拡張適性 && SIZE_DEF >= 0
	SIZE_DEF -= 1

SELECTCASE TEQUIP:弛緩
CASE 1
	SIF SIZE_DEF > 0
		SIZE_DEF -= 1
CASE 2
	SIF SIZE_DEF >= 0
		SIZE_DEF -= 1
CASE 3
	SIF SIZE_DEF >= 0
		SIZE_DEF -= 1
	SIF SIZE_DEF >= 0
		SIZE_DEF -= 1
ENDSELECT

;余裕あり
IF SIZE_DEF < 0
	;ジャスト
	IF SIZE_DEF == -1
		TIMES SOURCE:0:Ｖ快楽, 1.20
		TIMES SOURCE:0:Ａ快楽, 1.20
		TIMES SOURCE:0:尿道快楽, 1.20
	;緩い
	ELSE
		SOURCE:0:Ｖ快楽 = MULTIPLY(SOURCE:0:Ｖ快楽, MAX(120 + SIZE_DEF*20, 50) )
		SOURCE:0:Ａ快楽 = MULTIPLY(SOURCE:0:Ａ快楽, MAX(120 + SIZE_DEF*20, 50) )
		SOURCE:0:尿道快楽 = MULTIPLY(SOURCE:0:尿道快楽, MAX(120 + SIZE_DEF*20, 50) )
	ENDIF
	;余裕がある場合に限り、大きく硬い方が悦ばれることにする
	SOURCE:0:Ｖ快楽 = MULTIPLY(SOURCE:0:Ｖ快楽, HARD(NAME_INSERT)*10 + 80)
	SOURCE:0:Ｖ快楽 = MULTIPLY(SOURCE:0:Ｖ快楽, SIZE(NAME_INSERT)*10 + 90)
	;膣に大きなものを挿入する場合には子宮性感の有無で少々ボーナスあり
	SIF TALENT:子宮性感 && SIZE(NAME_INSERT) >= 2
		TIMES SOURCE:0:Ｖ快楽, 1.20

	SOURCE:0:Ａ快楽 = MULTIPLY(SOURCE:0:Ａ快楽, HARD(NAME_INSERT)*10 + 80)
	SOURCE:0:Ａ快楽 = MULTIPLY(SOURCE:0:Ａ快楽, SIZE(NAME_INSERT)*10 + 90)

	SOURCE:0:尿道快楽 = MULTIPLY(SOURCE:0:尿道快楽, HARD(NAME_INSERT)*10 + 80)
	SOURCE:0:尿道快楽 = MULTIPLY(SOURCE:0:尿道快楽, SIZE(NAME_INSERT)*10 + 90)

	;大きいものを収められると達成感
	SIF SIZE(NAME_INSERT) >= 2
		SOURCE:0:達成感 += SIZE(NAME_INSERT) * SIZE(NAME_INSERT) * 500
;余裕無し
ELSE
	DOWNBASE:0:体力 = MULTIPLY(DOWNBASE:0:体力, 100 + SIZE_DEF*20)
	SOURCE:0:Ｖ快楽 /= MAX(SIZE_DEF*2, 1)
	SOURCE:0:Ａ快楽 /= MAX(SIZE_DEF*2, 1)
	SOURCE:0:尿道快楽 /= MAX(SIZE_DEF*2, 1)
ENDIF

;PALAMLVの値は、順に1:100,2:500,3:3000,4:1万,5:3万,6:6万,7:10万,8:15万,9:25万
;HARDの基本値はペニスなら１、バイブや拳なら３
SOURCE:0:痛み += PALAM_LV(SIZE_DEF + (HARD(NAME_INSERT) + 1)/2)
SOURCE:0:恐れ += PALAM_LV(SIZE_DEF + (HARD(NAME_INSERT) + 1)/2)
SOURCE:0:逸脱 += PALAM_LV(SIZE_DEF)

;前か後ろかで分岐
SELECTCASE ARGS
CASE "尿道インサート", "尿道指挿入れ", "尿道ブジー", "尿道バイブ"
	;PALAM_RESは入れてるものの大きさを加味した潤滑具合
	SELECTCASE ARGS
	CASE "尿道インサート"
		;初めて
		IF TALENT:尿道処女 && TALENT:倒錯的 == 0 && ABL:マゾっ気 < 3
			SOURCE:0:恐れ += 5000
			SOURCE:0:逸脱 += 20000
		ENDIF
		PALAM_RES = 3
	CASEELSE
		;ここでの最大は３
		PALAM_RES = 3
	ENDSELECT

	IF TALENT:尿道狂い
		TIMES SOURCE:0:痛み, 0.10
		TIMES SOURCE:0:恐れ, 0.10
		TIMES SOURCE:0:逸脱, 0.05
	ELSEIF TALENT:尿道性感
		TIMES SOURCE:0:痛み, 0.20
		TIMES SOURCE:0:恐れ, 0.20
		TIMES SOURCE:0:逸脱, 0.20
	ENDIF

	;尿道に筋弛緩剤
	SIF GETBIT(TEQUIP:筋弛緩剤, 1)
		TIMES SOURCE:0:痛み, 0.20

	;経験は足りている？（HARDによって上限あり）
	EXP_REQ = MIN(GET_EXP_LV(EXP:尿道経験 + 1) - SIZE(NAME_INSERT) - 1 - MAX(SIZE_DEF, 0), 5 - HARD(NAME_INSERT) )
CASE "子宮挿入"
	;淫紋持ちは充足のソースも得る
	SIF MARK:淫紋
		SOURCE:0:充足 += MARK:淫紋 * 1000

	SELECTCASE GET_EXP_LV(EXP:子宮姦経験) + TALENT:子宮性感
	CASE IS <= 1
		TIMES SOURCE:0:Ｖ快楽, 0.20
		SOURCE:0:痛み += 1000
		SOURCE:0:恐れ += 3000
		SOURCE:0:逸脱 += 30000
	CASE 2
		TIMES SOURCE:0:Ｖ快楽, 0.40
		SOURCE:0:痛み += 500
		SOURCE:0:恐れ += 1000
		SOURCE:0:逸脱 += 6000
	CASE 3
		TIMES SOURCE:0:Ｖ快楽, 0.70
		SOURCE:0:痛み += 100
		SOURCE:0:恐れ += 500
		SOURCE:0:逸脱 += 2000
	ENDSELECT
	IF TALENT:淫壷 || MARK:淫紋 >= 4
		SOURCE:0:痛み /= 3
		SOURCE:0:恐れ /= 3
		SOURCE:0:逸脱 /= 3
	ELSEIF MARK:淫紋
		SOURCE:0:痛み = MULTIPLY(SOURCE:0:痛み, 100 - MARK:淫紋*15)
		SOURCE:0:恐れ = MULTIPLY(SOURCE:0:恐れ, 100 - MARK:淫紋*15)
		SOURCE:0:逸脱 = MULTIPLY(SOURCE:0:逸脱, 100 - MARK:淫紋*15)
	ENDIF

	;PALAM_RESは入れてるものの大きさを加味した潤滑具合
	PALAM_RES = GET_PALAMLV(PALAM:Ｖ潤 + PALAM:PLAYER:Ｐ潤) - MAX(SIZE_DEF, 0) - 1

	;経験は足りている？（HARDによって上限あり）
	EXP_REQ = MIN(GET_EXP_LV(EXP:子宮口経験 + 1) - SIZE(NAME_INSERT) - 1 - MAX(SIZE_DEF, 0), 5 - HARD(NAME_INSERT) )

	;損傷あり？
	DAMAGE = TALENT:Ｖ損傷
CASE "Ｖインサート", "子宮口責め", "指挿入れ", "バイブ", "Ｖフィスト", "双頭バイブプレイ"
	;淫紋持ちは充足のソースも得る
	SIF MARK:淫紋
		SOURCE:0:充足 += MARK:淫紋 * 500

	SIF TALENT:処女
		SOURCE:0:痛み += 1000

	IF TALENT:淫壷 || MARK:淫紋 >= 4
		SOURCE:0:痛み /= 3
		SOURCE:0:恐れ /= 3
		SOURCE:0:逸脱 /= 3
	ELSEIF MARK:淫紋
		SOURCE:0:痛み = MULTIPLY(SOURCE:0:痛み, 100 - MARK:淫紋*15)
		SOURCE:0:恐れ = MULTIPLY(SOURCE:0:恐れ, 100 - MARK:淫紋*15)
		SOURCE:0:逸脱 = MULTIPLY(SOURCE:0:逸脱, 100 - MARK:淫紋*15)
	ENDIF
	;アソコに筋弛緩剤
	IF GETBIT(TEQUIP:筋弛緩剤, 2)
		TIMES SOURCE:0:Ｖ快楽, 0.70
		TIMES SOURCE:0:痛み, 0.50
	ENDIF

	;PALAM_RESは入れてるものの大きさを加味した潤滑具合
	SELECTCASE ARGS
	CASE "Ｖインサート"
		PALAM_RES = GET_PALAMLV(PALAM:Ｖ潤 + PALAM:PLAYER:Ｐ潤) - MAX(SIZE_DEF, 0)
	CASEELSE
		;ここでの最大は３
		PALAM_RES = MIN(GET_PALAMLV(PALAM:Ｖ潤) - MAX(SIZE_DEF, 0), 3)
	ENDSELECT

	;経験は足りている？（HARDによって上限あり）
	EXP_REQ = MIN(GET_EXP_LV(EXP:Ｖ経験 + 1) - SIZE(NAME_INSERT) - 1 - MAX(SIZE_DEF, 0), 5 - HARD(NAME_INSERT) )

	;常時挿入による痛みの軽減
	IF TALENT:常時Ｖバイブ >= 4
		SIF ABL:Ｖ拡張 >= MAX(SIZE(NAME_INSERT), 1)
			TIMES SOURCE:0:痛み, 0.10
	ELSE
		SIF TALENT:常時Ｖバイブ >= MAX(SIZE(NAME_INSERT), 1)
			TIMES SOURCE:0:痛み, 0.10
	ENDIF

	;損傷あり？
	DAMAGE = TALENT:Ｖ損傷
CASE "Ａインサート", "Ａ指挿入れ", "Ａバイブ", "Ａフィスト"

	;結腸挿入
	IF COND("結腸責めフラグ")
		SELECTCASE GET_EXP_LV(EXP:結腸経験) + TALENT:結腸性感
		CASE 0
			SOURCE:0:屈従 += 500
			SOURCE:0:痛み += 1000
			SOURCE:0:恐れ += 10000
			SOURCE:0:逸脱 += 10000
		CASE 1
			SOURCE:0:屈従 += 1000
			SOURCE:0:痛み += 500
			SOURCE:0:恐れ += 3000
			SOURCE:0:逸脱 += 3000
		CASE 2
			TIMES SOURCE:0:Ａ快楽, 1.20
			SOURCE:0:屈従 += 2000
			SOURCE:0:痛み += 100
			SOURCE:0:恐れ += 500
			SOURCE:0:逸脱 += 1000
		CASEELSE
			TIMES SOURCE:0:Ａ快楽, 1.50
			SOURCE:0:屈従 += 3000
		ENDSELECT
	ENDIF

	IF TALENT:尻穴狂い
		SOURCE:0:痛み /= 3
		SOURCE:0:恐れ /= 3
		SOURCE:0:逸脱 /= 3
	ENDIF
	;アナルに筋弛緩剤
	IF GETBIT(TEQUIP:筋弛緩剤, 0)
		TIMES SOURCE:0:Ａ快楽, 0.70
		TIMES SOURCE:0:痛み, 0.50
	ENDIF

	;PALAM_RESは入れてるものの大きさを加味した潤滑具合
	SELECTCASE ARGS
	CASE "Ａインサート"
		PALAM_RES = GET_PALAMLV(PALAM:Ａ潤 + PALAM:PLAYER:Ｐ潤) - MAX(SIZE_DEF, 0)
	CASEELSE
		;ここでの最大は３
		PALAM_RES = MIN(GET_PALAMLV(PALAM:Ａ潤) - MAX(SIZE_DEF, 0), 3)
	ENDSELECT

	;経験は足りている？（HARDによって上限あり）
	EXP_REQ = MIN(GET_EXP_LV(EXP:Ａ経験 + 1) - SIZE(NAME_INSERT) - 1 - MAX(SIZE_DEF, 0), 5 - HARD(NAME_INSERT) )

	;常時挿入による痛みの軽減
	IF TALENT:常時Ａプラグ >= 4
		SIF ABL:Ａ拡張 >= MAX(SIZE(NAME_INSERT), 1)
			TIMES SOURCE:0:痛み, 0.10
	ELSE
		SIF TALENT:常時Ａプラグ >= MAX(SIZE(NAME_INSERT), 1)
			TIMES SOURCE:0:痛み, 0.10
	ENDIF

	;損傷あり？
	DAMAGE = TALENT:Ａ損傷
ENDSELECT

;PRINTFORMW SIZE_DEF = {SIZE_DEF}, PALAM_RES = {PALAM_RES}, EXP_REQ = {EXP_REQ}, SOURCE:0:痛み = {SOURCE:0:痛み}

;インサートかどうか等で分岐
SELECTCASE ARGS
CASE "尿道インサート"
	IF CALCF("ペニス好き度", TARGET) > 0 && PENIS(PLAYER) && ABL:尿道拡張 >= SIZE(NAME_INSERT)
		SOURCE:0:屈従 += SOURCE:0:恐れ * CALCF("ペニス好き度", TARGET)
		SOURCE:0:痛み /= CALCF("ペニス好き度", TARGET) + 1
		SOURCE:0:恐れ /= CALCF("ペニス好き度", TARGET) + 1
	ENDIF
	IF COND("好きな体位中")
		TIMES SOURCE:0:Ｖ快楽, 1.20
		TIMES SOURCE:0:Ａ快楽, 1.20
		TIMES SOURCE:0:尿道快楽, 1.20
		TIMES SOURCE:0:痛み, 0.80
		TIMES SOURCE:0:恐れ, 0.80
		TIMES SOURCE:0:逸脱, 0.80
	ENDIF
CASE "子宮挿入"
	IF CALCF("ペニス好き度", TARGET) > 0 && PENIS(PLAYER) && 1 >= SIZE(NAME_INSERT)
		SOURCE:0:屈従 += SOURCE:0:恐れ * CALCF("ペニス好き度", TARGET)
		SOURCE:0:痛み /= CALCF("ペニス好き度", TARGET) + 1
		SOURCE:0:恐れ /= CALCF("ペニス好き度", TARGET) + 1
	ENDIF
	IF COND("好きな体位中")
		TIMES SOURCE:0:Ｖ快楽, 1.20
		TIMES SOURCE:0:痛み, 0.80
		TIMES SOURCE:0:恐れ, 0.80
		TIMES SOURCE:0:逸脱, 0.80
	ENDIF
CASE "Ｖインサート", "子宮口責め"
	IF CALCF("ペニス好き度", TARGET) > 0 && PENIS(PLAYER) && ABL:Ｖ拡張 >= SIZE(NAME_INSERT)
		SOURCE:0:屈従 += SOURCE:0:恐れ * CALCF("ペニス好き度", TARGET)
		SOURCE:0:痛み /= CALCF("ペニス好き度", TARGET) + 1
		SOURCE:0:恐れ /= CALCF("ペニス好き度", TARGET) + 1
	ENDIF
	IF COND("好きな体位中")
		TIMES SOURCE:0:Ｖ快楽, 1.20
		TIMES SOURCE:0:痛み, 0.80
		TIMES SOURCE:0:恐れ, 0.80
		TIMES SOURCE:0:逸脱, 0.80
	ENDIF
CASE "Ａインサート"
	IF CALCF("ペニス好き度", TARGET) > 0 && PENIS(PLAYER) && ABL:Ａ拡張 >= SIZE(NAME_INSERT)
		SOURCE:0:屈従 += SOURCE:0:恐れ * CALCF("ペニス好き度", TARGET)
		SOURCE:0:痛み /= CALCF("ペニス好き度", TARGET) + 1
		SOURCE:0:恐れ /= CALCF("ペニス好き度", TARGET) + 1
	ENDIF
	IF COND("好きな体位中")
		TIMES SOURCE:0:Ａ快楽, 1.20
		TIMES SOURCE:0:痛み, 0.80
		TIMES SOURCE:0:恐れ, 0.80
		TIMES SOURCE:0:逸脱, 0.80
	ENDIF

CASE "双頭バイブプレイ"
	;締まり対決。値が大きいほどTARGET有利（受ける快楽等が少なく、与える快楽が大きい）
	;全部の値に作用するのでARG:1を増減させる
	SELECTCASE ABL:Ｖ使い - ABL:Ｖ拡張 - (ABL:PLAYER:Ｖ使い - ABL:PLAYER:Ｖ拡張)
	CASE IS >= 3
		TIMES ARG:1, 0.30
	CASE 2
		TIMES ARG:1, 0.50
	CASE 1
		TIMES ARG:1, 0.80
	CASE -1
		TIMES ARG:1, 1.20
	CASE -2
		TIMES ARG:1, 1.50
	CASE IS <= -3
		TIMES ARG:1, 2.00
	ENDSELECT
CASE "バイブ"
	IF TEQUIP:バイブ == 0
		TIMES SOURCE:0:Ｃ快楽, 0.50
		TIMES SOURCE:0:Ｖ快楽, 0.50
		TIMES SOURCE:0:痛み, 0.50
		TIMES SOURCE:0:逸脱, 0.50
	ENDIF
CASE "Ａバイブ"
	IF TEQUIP:アナルバイブ == 0
		TIMES SOURCE:0:Ａ快楽, 0.50
		TIMES SOURCE:0:痛み, 0.50
		TIMES SOURCE:0:逸脱, 0.50
		TIMES SOURCE:0:屈従, 0.50
		TIMES SOURCE:0:恐れ, 0.50
	ENDIF
CASE "尿道バイブ"
	IF TEQUIP:尿道バイブ == 0
		TIMES SOURCE:0:尿道快楽, 0.50
		TIMES SOURCE:0:痛み, 0.50
		TIMES SOURCE:0:逸脱, 0.50
		TIMES SOURCE:0:屈従, 0.50
		TIMES SOURCE:0:恐れ, 0.50
	ENDIF
CASE "指挿入れ", "Ａ指挿入れ", "尿道指挿入れ"
	SELECTCASE ABL:PLAYER:指使い
	CASE 1
		TIMES SOURCE:0:痛み, 0.80
	CASE 2
		TIMES SOURCE:0:痛み, 0.50
	CASE IS >= 3
		SOURCE:0:痛み = MIN(SOURCE:0:痛み/5, 500)
	ENDSELECT
ENDSELECT

IF TEQUIP:押し倒し == 0
	SELECTCASE CONDS("調教知識", PLAYER)
	CASE "性器拡張知識"
		TIMES SOURCE:0:痛み, 0.50
		TIMES SOURCE:0:恐れ, 0.50
	ENDSELECT
ENDIF

;挿入するものの大きさが該当箇所の拡張ＬＶ+１以下の場合
IF SIZE_DEF <= 1 && TALENT:好色 + TALENT:淫乱 + TALENT:拡張適性
	TIMES SOURCE:0:痛み, 0.50
	TIMES SOURCE:0:恐れ, 0.50
	TIMES SOURCE:0:屈従, 0.50

	;今までの絶頂具合による脱力などで痛み軽減措置（とりあえずは暫定）
	SELECTCASE EX:絶頂数合計 + TEQUIP:媚薬/25
	CASE IS >= MAX(20, MAXBASE:性欲*2)
		TIMES SOURCE:0:痛み, 0.20
	CASE IS >= MAX(10, MAXBASE:性欲)
		TIMES SOURCE:0:痛み, 0.50
	CASE IS >= MAX(5, MAXBASE:性欲/2)
		TIMES SOURCE:0:痛み, 0.80
	ENDSELECT
ENDIF

IF TALENT:淫乱
	TIMES SOURCE:0:逸脱, 0.20
ELSEIF TALENT:好色
	TIMES SOURCE:0:逸脱, 0.50
ENDIF

;経験は足りている？
SELECTCASE EXP_REQ
CASE IS >= 3
	TIMES SOURCE:0:痛み, 0.05
	TIMES SOURCE:0:恐れ, 0.05
	TIMES SOURCE:0:逸脱, 0.05
CASE 2
	TIMES SOURCE:0:痛み, 0.30
	TIMES SOURCE:0:恐れ, 0.30
	TIMES SOURCE:0:逸脱, 0.30
CASE 1
	TIMES SOURCE:0:痛み, 0.70
	TIMES SOURCE:0:恐れ, 0.70
	TIMES SOURCE:0:逸脱, 0.70
CASE 0
	TIMES SOURCE:0:恐れ, 0.70
CASE -1
	TIMES SOURCE:0:痛み, 1.50
	TIMES SOURCE:0:逸脱, 1.20
CASE -2
	TIMES SOURCE:0:痛み, 2.00
	TIMES SOURCE:0:恐れ, 1.50
	TIMES SOURCE:0:逸脱, 2.00
CASEELSE
	TIMES SOURCE:0:痛み, 3.00
	TIMES SOURCE:0:恐れ, 3.00
	TIMES SOURCE:0:逸脱, 3.00
ENDSELECT

;PALAM:Ｖ潤orＡ潤をみる
;挿入されているもののサイズとか拡張度は既に加味されている
;だいたい、挿入されているもののSIZEと拡張度が等しいときに対応した潤滑のLVが２あれば問題ない
SELECTCASE PALAM_RES
CASE IS >= 4
	TIMES SOURCE:0:痛み, 0.20
CASE 3
	TIMES SOURCE:0:痛み, 0.50
CASE 2
	TIMES SOURCE:0:Ｖ快楽, 0.80
	TIMES SOURCE:0:Ａ快楽, 0.80
	TIMES SOURCE:0:痛み, 0.80
CASE 1
	TIMES SOURCE:0:Ｖ快楽, 0.40
	TIMES SOURCE:0:Ａ快楽, 0.40
	SOURCE:0:痛み += 100
CASEELSE
	TIMES SOURCE:0:Ｖ快楽, 0.10
	TIMES SOURCE:0:Ａ快楽, 0.10
	SOURCE:0:痛み += 500
ENDSELECT

;損傷あり？
IF DAMAGE
	TIMES SOURCE:0:痛み, 2.00
	TIMES SOURCE:0:恐れ, 2.00
	TIMES SOURCE:0:逸脱, 2.00
	SOURCE:0:痛み += PALAM_LV(SIZE(NAME_INSERT) + 2)
	SOURCE:0:恐れ += PALAM_LV(SIZE(NAME_INSERT) + 2)
	SOURCE:0:逸脱 += PALAM_LV(SIZE(NAME_INSERT) + 1)
ENDIF

;巨根は屈従のソースにボーナス
SIF PLAYER == MASTER && NAME_INSERT == "ペニス" && SIZE(NAME_INSERT) >= 2
	SOURCE:0:屈従 += 1000 * SIZE(NAME_INSERT)

;潤滑増加の処理。その後に、場所ごとに性向の補正とジャストフィット
SELECTCASE ARGS
;膣を使っているプレイは痛みに比例して愛液増加
CASE "子宮挿入", "Ｖインサート", "子宮口責め"
	SOURCE:0:Ｖ液体追加 += 100*MIN(PALAM_LV(SOURCE:0:痛み), 5)
	SIF TALENT:Ｖ性向
		CALL CALC, "好みのプレイ", SOURCE:0:Ｖ快楽

	;ジャストフィットは快楽と屈従を増大させる
	SELECTCASE CHECK("Ｖジャストフィット")
	CASE IS >= 100
		TIMES SOURCE:0:Ｖ快楽, 3.00
		TIMES SOURCE:0:屈従, 3.00
	CASE IS >= 90
		TIMES SOURCE:0:Ｖ快楽, 2.50
		TIMES SOURCE:0:屈従, 2.50
	CASE IS >= 80
		TIMES SOURCE:0:Ｖ快楽, 2.00
		TIMES SOURCE:0:屈従, 2.00
	CASE IS >= 70
		TIMES SOURCE:0:Ｖ快楽, 1.75
		TIMES SOURCE:0:屈従, 1.75
	CASE IS >= 60
		TIMES SOURCE:0:Ｖ快楽, 1.50
		TIMES SOURCE:0:屈従, 1.50
	CASE IS >= 50
		TIMES SOURCE:0:Ｖ快楽, 1.25
		TIMES SOURCE:0:屈従, 1.25
	ENDSELECT
CASE "指挿入れ", "バイブ", "Ｖフィスト", "双頭バイブプレイ"
	SOURCE:0:Ｖ液体追加 += 100*MIN(PALAM_LV(SOURCE:0:痛み), 5)
	SIF TALENT:Ｖ性向
		CALL CALC, "好みのプレイ", SOURCE:0:Ｖ快楽
;アナルプレイなら痛みや異物の大きさなどによって腸液分泌
CASE "Ａインサート"
	SOURCE:0:Ａ液体追加 += MAX((GET_PALAMLV(SOURCE:0:痛み) + SIZE(NAME_INSERT))*100, 100)
	SIF TALENT:Ａ性向
		CALL CALC, "好みのプレイ", SOURCE:0:Ａ快楽

	;ジャストフィットは快楽と屈従を増大させる
	SELECTCASE CHECK("Ａジャストフィット")
	CASE IS >= 100
		TIMES SOURCE:0:Ａ快楽, 3.00
		TIMES SOURCE:0:屈従, 3.00
	CASE IS >= 90
		TIMES SOURCE:0:Ａ快楽, 2.50
		TIMES SOURCE:0:屈従, 2.50
	CASE IS >= 80
		TIMES SOURCE:0:Ａ快楽, 2.00
		TIMES SOURCE:0:屈従, 2.00
	CASE IS >= 70
		TIMES SOURCE:0:Ａ快楽, 1.75
		TIMES SOURCE:0:屈従, 1.75
	CASE IS >= 60
		TIMES SOURCE:0:Ａ快楽, 1.50
		TIMES SOURCE:0:屈従, 1.50
	CASE IS >= 50
		TIMES SOURCE:0:Ａ快楽, 1.25
		TIMES SOURCE:0:屈従, 1.25
	ENDSELECT

	;縦割れアナルは痛みに強い
	SIF TALENT:縦割れアナル
		SOURCE:0:痛み = MAX(MIN(DOWN_PALAMLV(SOURCE:0:痛み), SOURCE:0:痛み - 100), 0)
CASE "Ａ指挿入れ", "Ａバイブ", "Ａフィスト"
	SOURCE:0:Ａ液体追加 += MAX((GET_PALAMLV(SOURCE:0:痛み) + SIZE(NAME_INSERT))*100, 100)
	SIF TALENT:Ａ性向
		CALL CALC, "好みのプレイ", SOURCE:0:Ａ快楽
	;縦割れアナルは痛みに強い
	SIF TALENT:縦割れアナル
		SOURCE:0:痛み = MAX(MIN(DOWN_PALAMLV(SOURCE:0:痛み), SOURCE:0:痛み - 100), 0)
CASE "尿道インサート", "尿道指挿入れ", "尿道ブジー", "尿道バイブ"
	SIF TALENT:尿道狂い
		CALL CALC, "好みのプレイ", SOURCE:0:尿道快楽

	;ジャストフィットは快楽と屈従を増大させる
	SELECTCASE CHECK("尿道ジャストフィット")
	CASE IS >= 100
		TIMES SOURCE:0:尿道快楽, 3.00
		TIMES SOURCE:0:屈従, 3.00
	CASE IS >= 90
		TIMES SOURCE:0:尿道快楽, 2.50
		TIMES SOURCE:0:屈従, 2.50
	CASE IS >= 80
		TIMES SOURCE:0:尿道快楽, 2.00
		TIMES SOURCE:0:屈従, 2.00
	CASE IS >= 70
		TIMES SOURCE:0:尿道快楽, 1.75
		TIMES SOURCE:0:屈従, 1.75
	CASE IS >= 60
		TIMES SOURCE:0:尿道快楽, 1.50
		TIMES SOURCE:0:屈従, 1.50
	CASE IS >= 50
		TIMES SOURCE:0:尿道快楽, 1.25
		TIMES SOURCE:0:屈従, 1.25
	ENDSELECT
ENDSELECT

ARG = ARG * ARG:1 / 100

CALL CALC, "調教ソース", ARG
