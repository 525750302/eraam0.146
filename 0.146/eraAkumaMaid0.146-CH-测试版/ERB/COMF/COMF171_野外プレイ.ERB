@COM171
;野外プレイ
;TEUIP:54のビットが 0人里, 1森林, 2花園, 3水辺, 4海
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

IF TEQUIP:野外プレイ == 0
	PREVCOM = 171
	RETURN 0
ELSEIF GETBIT(TFLAG:地の文分岐, 0)
	STR:前回コマンド = 野外プレイ(人里)
ELSEIF GETBIT(TFLAG:地の文分岐, 1)
	STR:前回コマンド = 野外プレイ(森林)
ELSEIF GETBIT(TFLAG:地の文分岐, 2)
	STR:前回コマンド = 野外プレイ(花園)
ELSEIF GETBIT(TFLAG:地の文分岐, 3)
	STR:前回コマンド = 野外プレイ(水辺)
ELSEIF GETBIT(TFLAG:地の文分岐, 4)
	STR:前回コマンド = 野外プレイ(海)
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM171
#DIM MEMO_KISEKAE
#DIM MEMO_LINECOUNT

IF TEQUIP:野外プレイ == 0
	PRINTFORML %CALLNAME:TARGET%を何処に連れ出しますか？
	PRINTFORML 
	PRINTL  [ 0] 人里に連れて行く
	PRINTL  [ 1] 森林に連れて行く
	PRINTL  [ 2] 花園に連れて行く
	PRINTL  [ 3] 水辺に連れて行く
	PRINTL  [ 4] 海に連れて行く
	PRINTL [100] やめる
	CALL INPUT_SELECT, 5, 100
ELSEIF TEQUIP:野外プレイ
	;野外プレイ行き先フラグ
	TFLAG:地の文分岐 = TEQUIP:野外プレイ
	PRINTFORMW 部屋に戻ってきた…
	TEQUIP:野外プレイ = 0
	SIF ASSI
		TEQUIP:ASSI:野外プレイ = 0
	CALL KOJO_MESSAGE_COM
	RETURN 1
ENDIF

IF RESULT == 100
	TFLAG:コマンドキャンセル = 1
	RETURN 0
ELSEIF RESULT == 0
	LOCALS = 人里
ELSEIF RESULT == 1
	LOCALS = 森林
ELSEIF RESULT == 2
	LOCALS = 花園
ELSEIF RESULT == 3
	LOCALS = 水辺
ELSEIF RESULT == 4
	LOCALS = 海
ENDIF

SETBIT TEQUIP:野外プレイ, RESULT
SIF ASSI
	TEQUIP:ASSI:野外プレイ = TEQUIP:野外プレイ

;野外プレイ行き先フラグ
TFLAG:地の文分岐 = TEQUIP:野外プレイ

;着せ替えさせた状態をチェック
MEMO_KISEKAE = 0

;選択肢の存在判定に用いる
VARSET LOCAL
;選択肢の二重出現を防ぐ
MEMO_LINECOUNT = LINECOUNT

;その他の場合
IF MEMO_LINECOUNT == LINECOUNT && EQUIP:パンツ
	PRINTFORML …外出に先駆けて%CALLNAME:TARGET%のパンツを脱がせますか？
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORM 現在の服装：
	CALL PRINT_DRESSL, TARGET
	RESETCOLOR
	PRINTFORML 
	PRINTFORML  [ 0] はい
	PRINTFORML  [ 1] いいえ

	CALL INPUT_SELECT, 2

	SELECTCASE RESULT
	CASE 0
		CALL SET_PANTIES, ""
		MEMO_KISEKAE = 3
	ENDSELECT
	PRINTL 
ENDIF

IF MEMO_KISEKAE == 1
	PRINT 着替えさせた
ELSEIF CHECK_CLO("ハダカ")
	PRINT ハダカの
ENDIF
PRINTFORM %CALLNAME:TARGET%
IF MEMO_KISEKAE == 2
	PRINTFORM に%SHOES(TARGET, "略")%
	IF CHECK_CLO("ハダカ")
		PRINT だけ履かせて
	ELSE
		PRINT を履かせて
	ENDIF
ELSEIF MEMO_KISEKAE == 3 && CHECK_CLO("ハダカ") == 0
	PRINT からパンツを没収して
ELSEIF MARK:屈服刻印 == 3 || TALENT:恋慕
	PRINT の手をとって
ENDIF
PRINTFORMW %LOCALS%に連れ出した…
PRINTFORM %CALLNAME:TARGET%は
IF (ABL:露出癖 >= 3 || TALENT:恥じらい) && GETBIT(TEQUIP:野外プレイ, 0)
	PRINTFORMW 何者かの視線を感じ、顔を紅潮させ震えている…
ELSEIF ABL:露出癖 >= 3
	PRINTFORMW 自分に向けられる視線を想像し、顔を紅潮させ震えている…
ELSEIF TALENT:恥じらい
	PRINTFORMW わずかな物音にも敏感に反応し身体を震わせている…
ELSEIF TALENT:小悪魔 || TALENT:意地悪
	PRINTFORM 歩きながら%CALLNAME:MASTER%に抱きつき、顔を見ながら
	IF TALENT:小柄
		PRINT 未発達な
	ELSEIF TALENT:巨乳
		PRINT 豊満な
	ELSE
		PRINT 柔らかな
	ENDIF
	PRINTW 体を擦りつけている…
ELSEIF TALENT:好奇心
	PRINTFORMW 周りの景色に興味津々のようだ…
ELSE
	PRINTFORMW これから先のことを想像しているようだ…
ENDIF

RETURN 1

@FLAG_COM171, ARG

@SOURCE_COM171, ARG
CALL VARSET_COMF

IF GETBIT(TEQUIP:野外プレイ, 0)
	SOURCE:0:露出 = 2000
	SOURCE:0:逸脱 = 400
ELSEIF GETBIT(TEQUIP:野外プレイ, 1)
	SOURCE:0:露出 = 400
ELSEIF GETBIT(TEQUIP:野外プレイ, 2)
	SOURCE:0:露出 = 400
ELSEIF GETBIT(TEQUIP:野外プレイ, 3) || GETBIT(TEQUIP:野外プレイ, 4)
	SOURCE:0:露出 = 600
ENDIF
SOURCE:0:逸脱 += 500

SOURCE:0:充足 += ABL:露出癖 * 300

;服の有無で羞恥、逸脱度が上昇。人里だと物凄い事に。
IF CHECK_CLO("ハダカ")
	;種族衣装がハダカでない
	IF CLO_ORI(TARGET, "服", "ボディスーツ", "アウター", "パンツ")
		IF GETBIT(TEQUIP:野外プレイ, 0)
			SOURCE:0:露出 += 6000
			SOURCE:0:逸脱 += 1000
		ELSE
			SOURCE:0:露出 += 2000
			SOURCE:0:逸脱 += 500
		ENDIF
	ENDIF
ELSEIF CHECK_CLO("下着姿")
	;種族衣装が服orボディスーツorアウターを含む
	IF CLO_ORI(TARGET, "服", "ボディスーツ", "アウター")
		IF GETBIT(TEQUIP:野外プレイ, 0)
			SOURCE:0:露出 += 4000
			SOURCE:0:逸脱 += 500
		ELSE
			SOURCE:0:露出 += 1000
			SOURCE:0:逸脱 += 200
		ENDIF
	ENDIF
ELSEIF CHECK_CLO("ノーパン")
	;種族衣装に下着あり
	IF CLO_ORI(TARGET, "パンツ")
		IF GETBIT(TEQUIP:野外プレイ, 0)
			SOURCE:0:露出 += 2000
			SOURCE:0:逸脱 += 300
		ELSE
			SOURCE:0:露出 += 600
			SOURCE:0:逸脱 += 100
		ENDIF
	ENDIF
ENDIF

;昼だと恥ずかしい
IF COND("夜") == 0
	TIMES SOURCE:0:露出, 2.00
	TIMES SOURCE:0:逸脱, 2.00
ENDIF

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:露出, 0.80
CASE IS < PALAMLV:2
	TIMES SOURCE:0:露出, 0.90
CASE IS < PALAMLV:3
	TIMES SOURCE:0:露出, 1.00
CASE IS < PALAMLV:4
	TIMES SOURCE:0:露出, 1.10
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:露出, 1.20
ENDSELECT
;ABL:マゾっ気をみる
SELECTCASE ABL:マゾっ気
CASE 0
	TIMES SOURCE:0:露出, 0.80
CASE 1
	TIMES SOURCE:0:露出, 1.00
CASE 2
	TIMES SOURCE:0:露出, 1.30
CASE 3
	TIMES SOURCE:0:露出, 1.60
CASE 4
	TIMES SOURCE:0:露出, 2.00
CASEELSE
	TIMES SOURCE:0:露出, 3.00
ENDSELECT

SIF TALENT:目立ちたがり
	TIMES SOURCE:0:露出, 1.50
SIF TALENT:内気
	TIMES SOURCE:0:逸脱, 2.00
SIF TALENT:恥じらい
	TIMES SOURCE:0:逸脱, 2.00

SOURCE:0:恐れ += SOURCE:0:逸脱 / 2

CALL CALC, "調教ソース", ARG


@EQUIP_COM171
SIF TEQUIP:野外プレイ == 0
	RETURN 0

CALL SOURCE_COM171, 100

@EQUIP_COM171_2
SIF TEQUIP:野外プレイ == 0
	RETURN 0
;露出経験の値を管理
LOCAL = 1
IF GETBIT(TEQUIP:野外プレイ, 0)
	PRINTL ＜野外プレイ中(人里)＞
	IF COND("夜") == 0
		LOCAL = 3
	ELSE
		LOCAL = 2
	ENDIF
ELSEIF GETBIT(TEQUIP:野外プレイ, 1)
	PRINTL ＜野外プレイ中(森林)＞

	;獣耳ならボーナス
	SIF PLAYER == MASTER && TALENT:動物耳
		TFLAG:好感度ボーナス += 1
	;一部種族にボーナス
	SIF BASE:種族 == 3
		TFLAG:好感度ボーナス += 1
ELSEIF GETBIT(TEQUIP:野外プレイ, 2)
	PRINTL ＜野外プレイ中(花園)＞

	;花園補正。好感度にボーナス。
	SIF PLAYER == MASTER
		TFLAG:好感度ボーナス += 1
ELSEIF GETBIT(TEQUIP:野外プレイ, 3) || GETBIT(TEQUIP:野外プレイ, 4)
	PRINTL ＜野外プレイ中(水辺)＞

	;夏ならボーナス
	SIF PLAYER == MASTER && FLAG:季節 == 1
		TFLAG:好感度ボーナス += 2

	;水辺補正。汚れを洗い流せる。
	;汚れを初期化
	CALL RESET_STAIN_EX, "全部", MASTER
	CALL RESET_STAIN_EX, "全部", TARGET
	;助手がいる場合は、助手もきれいに
	SIF ASSI
		CALL RESET_STAIN_EX, "全部", ASSI
ENDIF

EXP:露出経験 += LOCAL
PRINTFORML %EXPNAME:34%＋{LOCAL}

SIF PLAYER == MASTER && ABL:露出癖 >= 3
	TFLAG:好感度ボーナス += 1
