@COM175
;着せ替え
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
;判定が要らない状態
IF TEQUIP:押し倒し || TEQUIP:拘束

ELSEIF CHECK_PANTIES("ずれ不可") == 0
	CALL SHOW_COM_ORDER_COM, 175

	;実行できない
	SIF TCVAR:やる気 == 0
		RETURN 0
ENDIF
;-------------------------------------------------
;着せ替えのフラグ、スクリプト、説明等
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

RETURN 1

@FLAG_COM175, ARG

@SOURCE_COM175, ARG
CALL VARSET_COMF

SOURCE:0:情愛 = 100

;ABL:露出癖をみる
SELECTCASE ABL:露出癖
CASE 0
	SOURCE:0:露出 += 100
	SOURCE:0:屈従 += 100
CASE 1
	SOURCE:0:露出 += 500
	SOURCE:0:屈従 += 300
CASE 2
	SOURCE:0:露出 += 100
	SOURCE:0:屈従 += 50
CASE 3
	SOURCE:0:露出 += 50
CASE 4

CASEELSE

ENDSELECT
;ABL:信頼をみる
SELECTCASE ABL:信頼
CASE 0
CASE 1
	TIMES SOURCE:0:情愛, 1.20
CASE 2
	TIMES SOURCE:0:情愛, 1.40
CASE 3
	TIMES SOURCE:0:情愛, 1.50
CASE 4
	TIMES SOURCE:0:情愛, 2.10
CASEELSE
	TIMES SOURCE:0:情愛, 3.00
ENDSELECT

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM175
IF TEQUIP:押し倒し || ( (TEQUIP:拘束 || TEQUIP:室内露出プレイ || TEQUIP:野外プレイ) && (ABL:信頼 >= 4 || ABL:露出癖 >= 3) )
	IF EQUIP:パンツ && EQUIP:ボディスーツ + EQUIP:アウター == 0 && CHECK_CLO("パンツ露出")
		PRINTFORMW %CALLNAME:TARGET%はパンツを脱いだ…
		CALL SET_PANTIES, ""
	ELSEIF CHECK_CLO("ハダカ") == 0
		PRINTFORMW %CALLNAME:TARGET%は服を脱いで裸になった…
		CALL SETFLAG, "ハダカにする", TARGET
	ENDIF
ELSEIF CHECK_PANTIES("ずれ不可")
	SELECTCASE PANTIES(TARGET, "略")
	CASE "前貼り"
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の前貼りを剥がした…
	CASE "貞操帯"
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の貞操帯を外した…
	CASEELSE
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の%PANTIES(TARGET, "略")%を脱がした…
	ENDSELECT
	CALL SET_PANTIES, ""
ELSEIF TEQUIP:拘束 || TEQUIP:室内露出プレイ || TEQUIP:野外プレイ
	IF EQUIP:パンツ && EQUIP:ボディスーツ + EQUIP:アウター == 0 && CHECK_CLO("パンツ露出")
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%のパンツを手早く脱がせた…
		CALL SET_PANTIES, ""
	ELSEIF CHECK_CLO("ハダカ") == 0
		PRINTFORMW %CALLNAME:PLAYER%は%CALLNAME:TARGET%の服を無理やり剥ぎ取り裸にした…
		CALL SETFLAG, "ハダカにする", TARGET
	ENDIF
ELSEIF TCVAR:やる気 == 0 && CHECK_CLO("アウター露出") && CHECK_CLO("Ｖ露出可") == 0
	PRINTFORMW %CALLNAME:TARGET%は%OUTER(TARGET)%を脱いだ。
	CALL SET_OUTER, ""
ELSE
	CALL KISEKAE
ENDIF


@COM_ORDER_COM175, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:露出癖
IF ABL:露出癖
	LOCAL += MIN(ABL:露出癖, 10) * 5
	LOCALS = %LOCALS% + 露出癖LV{ABL:露出癖}({MIN(ABL:露出癖, 10) * 5})
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 10
	LOCALS = %LOCALS% - 恥じらい(10)
ENDIF
;恥薄い
IF TALENT:恥薄い
	LOCAL += 5
	LOCALS = %LOCALS% + 恥薄い(5)
ENDIF

;拘束中
IF TEQUIP:拘束
	LOCAL += 30
	LOCALS = %LOCALS% + 拘束中(30)
ENDIF
;ビデオ撮影
IF TEQUIP:ビデオカメラ
	LOCAL -= 10
	LOCALS = %LOCALS% - 撮影中(10)
ENDIF
;野外or室内露出
IF TEQUIP:野外プレイ
	LOCAL -= 10
	LOCALS = %LOCALS% - 野外プレイ(10)
ELSEIF TEQUIP:室内露出プレイ
	LOCAL -= 10
	LOCALS = %LOCALS% - 室内露出(10)
ENDIF

;合計を表示(35以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 35

RETURN RESULT
