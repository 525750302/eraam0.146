@COM145
;剃毛
;露出癖やマゾ度が高い場合、かなり興奮する

PRINTL 剃毛

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 145

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
SIF TEQUIP:拘束
	TCVAR:干劲 = MAX(TCVAR:干劲 - 30, 0)

CALL TRAIN_MESSAGE_B

;どのように剃ったか
SELECTCASE TFLAG:地の文分岐
CASE 1
	BASE:陰毛 = 300
CASE 2
	BASE:陰毛 = 200
CASE 3
	BASE:陰毛 = 500
ENDSELECT

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = 剃毛

RETURN 1


@FLAG_COM145, ARG
CALL SETFLAG, "剃毛フラグ", TARGET
;対象のＶ⇔調教者の指の汚れが移動
CALL EQ_STAIN, TARGET, "Ｖ", PLAYER, "手"
;アソコを露出させる
CALL SETFLAG, "ボトムを露出：テ接吻ト没有", TARGET
;日付を5にする。上手だと日付が0になります。ツルツルに剃った場合も。
SIF ABL:PLAYER:技巧 < 4 && COND("陰毛") > 3
	BASE:陰毛 += 5

@SOURCE_COM145, ARG
CALL SOURCE_SMPLAY, "剃毛", ARG

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 145

SOURCE:0:露出 = 6000
SOURCE:0:屈従 = 1000
SOURCE:0:逸脱 = 1000

;縛って無理やり剃ると…
SIF TEQUIP:拘束 && TCVAR:干劲 <= 0
	SOURCE:0:逸脱 += 3000

SELECTCASE COND("陰毛")
CASE 3
	SOURCE:0:露出 += 4000
	SOURCE:0:屈従 += 1000
	SOURCE:0:逸脱 += 1000
CASE 2
	SOURCE:0:露出 += 6000
	SOURCE:0:屈従 += 1500
	SOURCE:0:逸脱 += 2000
ENDSELECT

;ABL:露出癖とマゾっ気をみる
SELECTCASE (ABL:露出癖 + 1)/2 + ABL:受虐属性
CASE IS >= 9
	SOURCE:0:充足 += 5000
	TIMES SOURCE:0:屈従, 0.30
CASE IS >= 7
	SOURCE:0:充足 += 3000
	TIMES SOURCE:0:屈従, 0.60
CASE IS >= 5
	SOURCE:0:充足 += 1000
	TIMES SOURCE:0:屈従, 0.70
CASE IS >= 3

CASE 1
	TIMES SOURCE:0:屈従, 1.20
CASEELSE
	TIMES SOURCE:0:屈従, 1.50
ENDSELECT

;ビデオ撮影、室内露出、野外プレイは経験等にプラス
IF TEQUIP:攝像機
	TIMES SOURCE:0:露出, 1.50
	TIMES SOURCE:0:屈従, 1.50
	TIMES SOURCE:0:逸脱, 1.50
ENDIF
IF TEQUIP:室内露出PLAY || TEQUIP:野外PLAY
	TIMES SOURCE:0:露出, 1.50
	TIMES SOURCE:0:屈従, 1.50
	TIMES SOURCE:0:逸脱, 1.50
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM145
;陰毛描写
#DIMS NAME_BUSH

SELECTCASE CONDS("陰毛")
CASE "随意生長"
	NAME_BUSH = %TEXTR("完全沒有做過任何修剪的/將整個陰唇都覆蓋遮掩住的/經常被液體浸泡而殘留了異味的")%陰毛
CASE "剃り跡"
	NAME_BUSH = 留有剃痕的陰毛
CASE "还没長出来"
	NAME_BUSH = 稍稍茂密的
CASEELSE
	IF PENIS(TARGET)
		NAME_BUSH = 陰毛
	ELSE
		NAME_BUSH = 陰部毛髮
	ENDIF
ENDSELECT
IF PREVCOM == 13
	PRINTFORM %CALLNAME:PLAYER%將面前重要的秘處慢慢展開、將\@ CHECK("陰毛管理") ? 等待處理的 # 被清楚暴露著的 \@
;緊縛中
ELSEIF GETBIT(TEQUIP:拘束, 21)
	PRINTFORM 將保持著大張著股間的姿勢
	IF BIND("縄", TEQUIP:拘束)
		PRINT 被粗繩捆綁著的
	ELSE
		PRINT 被束具拘束住的
	ENDIF
ELSEIF CHECK("陰毛管理") && COND("脚が無い") == 0
	PRINTFORM 將乖順的大張著股間準備好的
ENDIF
PRINTFORMW %CALLNAME:TARGET%的股間的毛髮修剪一下吧。
PRINTFORML 所以、現在的狀態是[%CONDS("陰毛")%]……現在、該怎麼做？

PRINTL  [ 0] 整個的剔個精光吧！
PRINTL  [ 1] 乾脆做一次脫毛處理吧！
SIF COND("陰毛") >= 6
	PRINTL  [ 2] 將陰毛的形態修整一下就好了吧。
PRINTL [100] 不、再考慮一下吧。

$INPUT_LOOP
INPUT

TFLAG:地の文分岐 = RESULT + 1

IF RESULT == 100
	TFLAG:指令中止 = 1
	RETURN 0
ELSEIF RESULT == 0 || RESULT == 1
	IF PENIS(TARGET)
		PRINTFORM %CALLNAME:PLAYER%將%CALLNAME:TARGET%的%NAME_BUSH%
		IF TFLAG:地の文分岐 == 2
			PRINTFORMW \@ABL:PLAYER:技巧 >= 4 ? 仔細地 #  \@脫去毛髮……
		ELSE
			PRINTFORMW \@ABL:PLAYER:技巧 >= 4 ? 漂亮地 #  \@剃除了陰毛……
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:PLAYER%將%CALLNAME:TARGET%的%NAME_BUSH%
		IF TFLAG:地の文分岐 == 2
			PRINTFORM \@ABL:PLAYER:技巧 >= 4 ? 仔細地 #  \@脫去毛髮、
		ELSE
			PRINTFORM \@ABL:PLAYER:技巧 >= 4 ? 漂亮地 # 精心地 \@剃除了陰毛、
		ENDIF
		IF TALENT:陰蒂環 + TALENT:陰唇環 && COND("陰毛") >= 6 && RAND:3 == 0
			IF TALENT:害羞 && RAND:3 == 0
				PRINT 令人害羞的
			ELSEIF RAND:3 == 0
				IF TALENT:陰蒂環 && RANDIF(1, TALENT:陰唇環)
					IF PENIS(TARGET)
						PRINT 将怒挺的陰莖装飾著的
					ELSE
						PRINT 將勃起的陰蒂穿透而過的
					ENDIF
				ELSE
					PRINT 將饱满的陰唇
					IF AGE(TARGET) >= 4
						PRINT 向下拉扯著暴露出內部的
					ELSE
						PRINT 装飾著的
					ENDIF
				ENDIF
			ELSEIF RAND:2
				PRINT 安装在通常來說不可能的地方的
			ELSE
				PRINT 閃爍淫靡光澤的
			ENDIF
			PRINTFORMW 金属穿环被清楚地观赏着……
		ELSEIF TALENT:害羞 && COND("陰毛") >= 6 && COND("脚が無い") == 0 && RAND:3 == 0
			PRINTFORMW 只是稍稍地張開雙腿、令人害羞的部分就會一目了然地暴露出來……
		ELSEIF RAND:2 || COND("陰毛") < 6
			IF RAND:2
				IF AGE(TARGET) <= 2
					PRINT 現在、她的股間就像是剝殼雞蛋一樣
				ELSE
					PRINT 現在、她的股間就像是初生幼童一様
				ENDIF
				PRINTFORMW 光滑了……
			ELSE
				IF TALENT:娇小
					PRINT 現在、她的股間是與嬌小身體十分相稱的
				ELSEIF AGE(TARGET) >= 4
					PRINT 現在、她的股間是與成熟性器呈現出反差感、更加令人興奮的
				ELSE
					PRINT 現在、她的股間是能更好地展現出女體優美曲線的
				ENDIF
				PRINTFORMW 光溜溜的樣子……
			ENDIF
		ELSE
			IF AGE(TARGET) >= 4
				IF EXP:Ｖ経験 >= 1000
					PRINT 外部因長期的使用而變得肥美的陰唇与
				ELSEIF EXP:Ｖ経験 >= 300
					PRINT 外部稍稍突出的美麗陰唇以及
				ELSE
					PRINT 柔軟而具有彈性的
				ENDIF
			ELSEIF TALENT:娇小 && RAND:3
				PRINT 隨著呼吸而微微顫抖著的
			ELSE
				PRINT 可愛的
			ENDIF
			PRINTFORMW 秘裂被格外清楚的觀賞著……
		ENDIF
		IF TALENT:大力士 && AGE(TARGET) >= 2 && COND("脚が無い") == 0
			PRINTFORM 同時進入視野的、是
			IF RAND:2
				PRINTFORM 緊繃而富含彈性的
			ELSE
				PRINTFORM 隨著呼吸而微微顫抖著柔軟肉體的
			ENDIF
			IF RAND:2
				PRINTFORM 小腹
			ELSE
				PRINTFORM 大腿
			ENDIF
			PRINTFORM 兩者形成了美麗的相乘效應、
			IF RAND:2
				PRINTFORMW 在凝視著極美的風景時、不禁讓人深陷其中、無法拔出目光……
			ELSE
				PRINTFORMW 讓人會在不知不覺之時、魯莽地去觸摸的誘人魅力、外溢而出……
			ENDIF
		ENDIF
	ENDIF
ELSEIF RESULT == 2 && COND("陰毛") >= 6
	;陰毛の状態リセット
	CALL DEL_CEVENT_GROUP, "陰毛の形"
	CALL DEL_CEVENT_GROUP, "陰毛の名称"

	IF PENIS(TARGET)
		PRINTFORMW %CALLNAME:PLAYER%將%CALLNAME:TARGET%的%NAME_BUSH%的形態\@ABL:PLAYER:技巧 >= 4 ? 嫻熟而利落的 #  \@修整了……
	ELSE
		PRINTFORML どのような形にしますか？
		PRINTFORML  [ 0] ▼ 逆三角形
		PRINTFORML  [ 1] ● 卵型
		PRINTFORML  [ 2]  %UNICODE(0x258E)% 長方形
		PRINTFORM  [ 3] 
		CALL HEARTMARK
		PRINTFORML  ❤ 心型
		PRINTFORML  [90] 直接輸入
		PRINTFORML [100] 不拘泥於特定的形態（通常無記述）

		CALL INPUT_SELECT, 4, 90, 100

		SELECTCASE RESULT
		CASE 100

		CASE 0
			CALL SET_CEVENT, "陰毛の形：▼"
			CALL SET_CEVENT, "陰毛の名称：逆三角形"
		CASE 1
			CALL SET_CEVENT, "陰毛の形：●"
			CALL SET_CEVENT, "陰毛の名称：卵型"
		CASE 2
			CALL SET_CEVENT, @"陰毛の形： %UNICODE(0x258E)%"
			CALL SET_CEVENT, "陰毛の名称：長方形"
		CASE 3
			CALL SET_CEVENT, "陰毛の形：HEARTMARK"
			CALL SET_CEVENT, "陰毛の名称：心型"
		CASE 90
			PRINTL 請將全角８文字（半角則16文字）以下的字符串輸入。
			PRINTL (推薦使用"～剃除整理了一番"一類不會產生違和感的說法)
			PRINTL 另外、什麼也不輸入的場合、則不拘泥於特定的形態。
			CALL INPUTS_SELECT, 16

			LOCALS = 陰毛の形：%RESULTS%
			CALL SET_CEVENT, LOCALS
		ENDSELECT

		PRINTFORMW %CALLNAME:PLAYER%将%CALLNAME:TARGET%的%NAME_BUSH%好好地剃了、将装飾恥丘的陰毛剃成了\@ STRLENS(CEVENTS("陰毛の名称") ) ? %CEVENTS("陰毛の名称")% # 漂亮的 \@形状…
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF
CFLAG:剃毛者 = BASE:PLAYER:識別番号

@COM_ORDER_COM145, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

SELECTCASE TALENT:有男朋友
CASE 2
	LOCAL -= 30
	LOCALS = %LOCALS% - %TALENT_NAME(GETNUM(TALENT, "有男朋友"), TALENT:有男朋友)%(30)
CASE 1
	LOCAL -= 15
	LOCALS = %LOCALS% - %TALENT_NAME(GETNUM(TALENT, "有男朋友"), TALENT:有男朋友)%(15)
ENDSELECT

;ABL:露出癖
IF ABL:露出癖
	LOCAL += MIN(ABL:露出癖, 10) * 2
	LOCALS = %LOCALS% + 露出癖LV{ABL:露出癖}({MIN(ABL:露出癖, 10) * 2})
ENDIF
;ABL:マゾっ気
IF ABL:受虐属性
	LOCAL += MIN(ABL:受虐属性, 5) * 3
	LOCALS = %LOCALS% + 受虐属性LV{ABL:受虐属性}({MIN(ABL:受虐属性, 5) * 3})
ENDIF
;恥じらい
IF TALENT:害羞
	LOCAL -= 15
	LOCALS = %LOCALS% - 害羞(15)
ENDIF
;恥薄い
IF TALENT:不知羞恥
	LOCAL += 5
	LOCALS = %LOCALS% + 不知羞恥(5)
ENDIF
;倒錯的
IF TALENT:倒錯的
	LOCAL += 10
	LOCALS = %LOCALS% + 倒錯的(10)
ENDIF
;縄
IF TEQUIP:拘束
	LOCAL += 30
	LOCALS = %LOCALS% + 拘束中(30)
ENDIF
;ビデオ撮影
IF TEQUIP:攝像機
	LOCAL -= 20
	LOCALS = %LOCALS% - 撮影中(20)
ENDIF

;合計を表示(60以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 60

RETURN RESULT
