@COM96
;アナル奉仕
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 96

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
IF CHECK_COM("Ａ責め手コキ")
	CALL SAMEN_CHECK, 70
	SELECTCOM = PREVCOM
	CALL SAMEN_CHECK, 50
	SELECTCOM = 96
ELSE
	CALL SAMEN_CHECK
ENDIF

RETURN 1

@FLAG_COM96, ARG
SIF TEQUIP:押し倒し == 0
	CALL SETFLAG, "Ａ奉仕フラグ", TARGET
SIF CHECK_COM("Ａ責め手コキ") == 96
	CALL FLAG_COM90, 50
;対象の口⇔調教者のAの汚れが移動
CALL EQ_STAIN, TARGET, "Ｍ", PLAYER, "Ａ"

@SOURCE_COM96, ARG
SIF CHECK_COM("Ａ責め手コキ") == 96
	CALL SOURCE_COM90, 50

CALL VARSET_COMF

CALL SOURCE_SERVICE_PLAY, "アナル奉仕"

CALL SOURCE_DOWNBASE_HP, 96

SOURCE:0:不潔 = COM_ORDER_STAIN("Ａ", "Ｍ")*100 + 100
SOURCE:0:欲情追加 = CALCF("実行値による欲情追加")
SOURCE:0:逸脱 = 3000

IF TEQUIP:押し倒し
	SOURCE:0:性行動 += 1000
ELSE
	SOURCE:0:屈従 = 5000
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM96
;同時押し倒し
IF TEQUIP:Ｗ押し倒し
	PRINTFORM %CALLNAME:TARGET%は
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	SIF SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ押し倒し中")%
	IF SELECTCOM == PREVCOM
		PRINTFORMW %CALLNAME:MASTER%のアナルに、舌を這わせて舐めまわし続けている…
	ELSE
		IF ABL:奉仕精神 >= 3 && ABL:サドっ気 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:MASTER%のお尻の穴を、尖らせた舌で中まで舐めてきた…
		ELSEIF ABL:奉仕精神 >= 3
			PRINTFORMW %CALLNAME:MASTER%のアナルを、しっかりと舌で舐め清めた…
			STAIN:MASTER:Ａ = SET_STAIN(0, "Ａ")
		ELSE
			PRINTFORMW %CALLNAME:MASTER%のお尻の穴を、チロチロと舌で責めてきた…
		ENDIF
	ENDIF
ELSEIF CHECK_COM("３Ｐ奉仕")
	CALL TRAIN_MESSAGE_3P
ELSE
	PRINTFORM %CALLNAME:TARGET%は
	;前回逆アナル
	IF PREVPLAY("Ａインサート", PLAYER)
		PRINTFORM 、埋めるものが無くなっても口を僅かに開いたままの%CALLNAME:PLAYER%のアナル
		IF ABL:奉仕精神 >= 3 && RAND:2 == 0
			PRINTW を舌で労わった…
		ELSE
			PRINTW に舌を這わせた…
		ENDIF
	ELSE
		IF CHECK_COM("Ａ責め手コキ")
			IF PREVCOM == 97
				LOCALS = %CLO("ぱんつコキ")%
				LOCALS:1 = %LOCALS%
			ELSE
				LOCALS = 手
				LOCALS:1 = 掌
			ENDIF
			IF TALENT:恋慕 && RAND:4 == 0
				PRINTFORM ペニスを愛しそうに%LOCALS%で扱きながら、
			ELSEIF ABL:技巧 >= 3 && ABL:奉仕精神 >= 3 && RAND:3 == 0
				PRINTFORM 亀頭を%LOCALS:1%でこね回しながら、
			ELSEIF ABL:技巧 >= 3 && RAND:2 == 0
				PRINTFORM ペニスを絶妙な力加減で刺激しながら、
			ELSEIF RAND:2 == 0
				PRINTFORM ペニスをゆっくりと%LOCALS%で扱きながら、
			ELSE
				PRINTFORM ペニスを%LOCALS%で扱きながら、
			ENDIF
		ENDIF
		IF ABL:奉仕精神 >= 3 && ABL:サドっ気 >= 3 && RAND:2 == 0
			PRINTFORMW %CALLNAME:PLAYER%のお尻の穴を尖らせた舌で中まで舐めてきた…
		ELSEIF ABL:奉仕精神 >= 3
			PRINTFORMW しっかりとアナルを舌で舐め清めはじめた…
			STAIN:PLAYER:Ａ = SET_STAIN(0, "Ａ")
		ELSEIF ABL:サドっ気 >= 3 || TEQUIP:押し倒し
			PRINTFORMW %CALLNAME:PLAYER%のお尻の穴を舌で責めてきた…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%のアナルをおそるおそる舐めてきた…
		ENDIF
	ENDIF
ENDIF


@COM_ORDER_COM96, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF

;恥じらい
IF TALENT:恥じらい
	LOCAL -= 5
	LOCALS = %LOCALS% - 恥じらい(5)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF

;汚れあり
IF COM_ORDER_STAIN("Ａ", "Ｍ")
	LOCAL -= COM_ORDER_STAIN("Ａ", "Ｍ")
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ａ", "Ｍ")})
ENDIF

;合計を表示(40以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 40

RETURN RESULT
