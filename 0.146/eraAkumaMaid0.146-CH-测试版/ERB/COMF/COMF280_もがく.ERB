@COM280
#DIM COMORDER_280
;もがく
CALL PRINT_TRAIN_NAME(SELECTCOM)

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
CALL SOURCE_COM280, 100

;FIRSTCOMフラグON
CALL SET_CHOSECOM, TARGET, 280

;成否判定
SIF TFLAG:もがき数 <= 0
	TFLAG:もがき数 = 0

STR:実行値 = 
COMORDER_280 = 0

IF ABL:サドっ気
	COMORDER_280 += MIN(ABL:サドっ気, 5)
	STR:実行値 = %STR:実行値% + サドっ気LV{ABL:サドっ気}({MIN(ABL:サドっ気, 5)})
ENDIF
IF TALENT:強気
	COMORDER_280 += 1
	STR:実行値 = %STR:実行値% + 強気(1)
ENDIF
IF TALENT:長身
	COMORDER_280 += 1
	STR:実行値 = %STR:実行値% + 長身(1)
ENDIF
IF TALENT:力持ち
	COMORDER_280 += TALENT:力持ち*3
	STR:実行値 = %STR:実行値% + %TALENT_NAME(GETNUM(TALENT, "力持ち"), TALENT:力持ち)%({TALENT:力持ち*3})
ENDIF
IF TALENT:好色
	COMORDER_280 += 1
	STR:実行値 = %STR:実行値% + 好色(1)
ENDIF
;体操服着用なら
IF CHECK_SHIRT("体操服")
	COMORDER_280 += 2
	STR:実行値 = %STR:実行値% + 体操服(2)
ENDIF
;二人で押し倒してるなら
IF TEQUIP:Ｗ押し倒し
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + Ｗ押し倒し中(5)
ENDIF
;縛られているなら
IF TEQUIP:PLAYER:拘束 || STATE("バインド", PLAYER)
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + 束縛中(5)
ENDIF
;騎乗位でインサート状態
IF INSERT("騎乗位")
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + 騎乗位(5)
ENDIF
;前ターンの絶頂具合
IF PREVEX("射精", PLAYER)
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + %CALLNAME:PLAYER%が射精直後(5)
ENDIF

;マイナス補正はこの後
IF TALENT:小柄
	COMORDER_280 -= 1
	STR:実行値 = %STR:実行値% - 小柄(1)
ENDIF
;前ターンの絶頂具合
IF PREVEX("絶頂数合計")
	COMORDER_280 -= 10
	STR:実行値 = %STR:実行値% - 絶頂直後(10)
ENDIF
IF COND("陶酔")
	COMORDER_280 -= 5
	STR:実行値 = %STR:実行値% - 陶酔(5)
ELSEIF COND("弛緩")
	COMORDER_280 -= 2
	STR:実行値 = %STR:実行値% - 弛緩(2)
ENDIF
IF COND("疲労")
	COMORDER_280 -= 10
	STR:実行値 = %STR:実行値% - 疲労困憊(10)
ELSEIF BASE:体力 < MAXBASE:体力/2
	COMORDER_280 -= 5
	STR:実行値 = %STR:実行値% - 体力半減(5)
ENDIF
IF CFLAG:マグロ
	COMORDER_280 -= 10
	STR:実行値 = %STR:実行値% - やる気なし(10)
ENDIF
;PLAYER側の補正
IF ABL:PLAYER:技巧 - ABL:技巧
	COMORDER_280 -= ABL:PLAYER:技巧 - ABL:技巧
	STR:実行値 = %STR:実行値% \@ ABL:PLAYER:技巧 - ABL:技巧 > 0 ? - # + \@ 技巧差({ABS(ABL:PLAYER:技巧 - ABL:技巧)})
ENDIF
IF TALENT:PLAYER:力持ち
	COMORDER_280 -= TALENT:PLAYER:力持ち*3
	STR:実行値 = %STR:実行値% - %CALLNAME:PLAYER%が%TALENT_NAME(GETNUM(TALENT, "力持ち"), TALENT:PLAYER:力持ち)%({TALENT:PLAYER:力持ち*3})
ENDIF
IF TFLAG:もがき数
	COMORDER_280 -= TFLAG:もがき数*3
	STR:実行値 = %STR:実行値% - もがき数{TFLAG:もがき数}回({TFLAG:もがき数*3})
ENDIF
IF RANDF(7) - 3
	COMORDER_280 += RANDF(7) - 3
	STR:実行値 = %STR:実行値% \@ RANDF(7) - 3 > 0 ? - # + \@ 時の運({ABS(RANDF(7) - 3)})
ENDIF
PRINTL 
PRINTFORM %SUBSTRING(STR:実行値, 3)% = {COMORDER_280} 
SELECTCASE COMORDER_280
CASE IS < 0
	PRINT <
CASE 0
	PRINT =
CASEELSE
	PRINT >
ENDSELECT
PRINTFORMW  実行値{0}
PRINTL 
IF COMORDER_280 <= 0
	TFLAG:もがき数 = 0
	IF TEQUIP:PLAYER:拘束 || STATE("バインド", PLAYER)
		PRINTFORMW %CALLNAME:PLAYER%は何とか拘束から逃れた！
		TEQUIP:PLAYER:拘束 = 0
		CALL REMOVE_STATE, PLAYER, "バインド"
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は
		SIF PREVEX("絶頂数合計")
			PRINTFORM 、絶頂の余韻に浸っている%CALLNAME:TARGET%の隙を突いて
		PRINTFORMW 主導権を取り返した！
		CVARSET TEQUIP, GETNUM(TEQUIP, "押し倒し")
		CALL KOJO_MESSAGE_COM
		;Ｗ押し倒し等での口上処理
		CALL KOJO_MESSAGE_COM_MULTI
		;助手とのインサートはここで解消
		CALL SETFLAG, "Ｗ押し倒し解除"
		CALL SETFLAG, "今回コマンド"

		;TARGETにPLAYERがインサート中なら正常位に移行
		IF INSERT("Ｖインサート", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 60
		ELSEIF INSERT("Ａインサート", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 70
		ELSEIF INSERT("尿道インサート", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 83
		ENDIF
	ENDIF
	RETURN 0
ELSE
	;前回の調教によって文章変化
	;顔面騎乗系
	IF PREVCOM == 1 || PREVCOM == 9 || TEQUIP:顔面騎乗 || TEQUIP:シックスナイン
		PRINTFORM 顔に跨っている%CALLNAME:TARGET%を押しのけて、
	;逆イラマチオ
	ELSEIF PREVCOM == 4
		PRINTFORM %CALLNAME:PLAYER%の口を犯している%CALLNAME:TARGET%から、
	;奉仕系
	ELSEIF (PREVCOM == 90 || PREVCOM == 91 || PREVCOM == 92 || PREVCOM == 95 || PREVCOM == 97 || PREVCOM == 99) && PENIS(PLAYER)
		SIF PREVCOM == 90 || PREVCOM == 91 || PREVCOM == 92 || PREVCOM == 99
			PRINT 圧し掛かって
		PRINTFORM ペニスを弄んでいる%CALLNAME:TARGET%から、
	;騎乗位系
	ELSEIF (PREVCOM >= 65 && PREVCOM <= 67) || PREVCOM == 75 || PREVCOM == 80 || PREVCOM == 88 || PREVCOM == 98 || PREVCOM == 120 || PREVCOM == 126
		PRINTFORM %CALLNAME:PLAYER%を犯して愉しんでいる%CALLNAME:TARGET%から、
	;アナル奉仕系
	ELSEIF PREVCOM == 96
		PRINTFORM アナルを弄んでいる%CALLNAME:TARGET%から、
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は
	ENDIF
	PRINTFORML 何とか主導権を取り返そうともがいたが、%CALLNAME:TARGET%\@ TEQUIP:Ｗ押し倒し ? 達 # \@は隙を見せなかった。
	IF TEQUIP:キス && ABL:欲望 >= 5 && RAND:3 == 0
		PRINTFORMW それどころか、%CALLNAME:TARGET%は%CALLNAME:PLAYER%の唇を奪うと、舌を絡めて唾液をたっぷりと流し込んで飲ませてきた！
		CALL SETFLAG, "追加キスフラグ", TARGET
	ELSE
		PRINTFORMW それどころか、さらに%CALLNAME:PLAYER%を攻めてきた！
	ENDIF
	TFLAG:もがき数 += 1

	CALL KOJO_MESSAGE_COM
	PRINTL 

	CALL SET_COUNTER_ABL

	SIF TEQUIP:Ｗ押し倒し
		CALL SET_SELECTCOM_MULTI, "Ｗ押し倒し"

	TRYJUMPFORM COM{SELECTCOM}
ENDIF

RETURN 1

@FLAG_COM280, ARG

@SOURCE_COM280, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 280

SOURCE:0:性行動 = 160

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:性行動, 1.00
CASE IS < PALAMLV:2
	TIMES SOURCE:0:性行動, 1.20
CASE IS < PALAMLV:3
	TIMES SOURCE:0:性行動, 1.40
CASE IS < PALAMLV:4
	TIMES SOURCE:0:性行動, 1.60
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:性行動, 1.80
ENDSELECT

;ABL:サドッ気をみる
SELECTCASE ABL:サドっ気
CASE 0
CASE 1
	TIMES SOURCE:0:性行動, 1.20
CASE 2
	TIMES SOURCE:0:性行動, 1.40
CASE 3
	TIMES SOURCE:0:性行動, 1.60
CASE 4
	TIMES SOURCE:0:性行動, 1.80
CASEELSE
	TIMES SOURCE:0:性行動, 2.00
ENDSELECT

CALL CALC, "調教ソース", ARG

