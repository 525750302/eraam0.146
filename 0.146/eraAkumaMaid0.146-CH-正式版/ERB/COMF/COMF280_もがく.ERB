@COM280
#DIM COMORDER_280
;もがく
CALL PRINT_TRAIN_NAME(SELECTCOM)

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
CALL SOURCE_COM280, 100

;FIRSTCOMフラグON
CALL SET_CHOSECOM, TARGET, 280

;成否判定
SIF TFLAG:もがき数 <= 0
	TFLAG:もがき数 = 0

STR:実行値 = 
COMORDER_280 = 0

IF ABL:施虐属性
	COMORDER_280 += MIN(ABL:施虐属性, 5)
	STR:実行値 = %STR:実行値% + 施虐属性LV{ABL:施虐属性}({MIN(ABL:施虐属性, 5)})
ENDIF
IF TALENT:強気
	COMORDER_280 += 1
	STR:実行値 = %STR:実行値% + 強気(1)
ENDIF
IF TALENT:高大
	COMORDER_280 += 1
	STR:実行値 = %STR:実行値% + 高大(1)
ENDIF
IF TALENT:大力士
	COMORDER_280 += TALENT:大力士*3
	STR:実行値 = %STR:実行値% + %TALENT_NAME(GETNUM(TALENT, "大力士"), TALENT:大力士)%({TALENT:大力士*3})
ENDIF
IF TALENT:好色
	COMORDER_280 += 1
	STR:実行値 = %STR:実行値% + 好色(1)
ENDIF
;体操服着用なら
IF CHECK_SHIRT("体操服")
	COMORDER_280 += 2
	STR:実行値 = %STR:実行値% + 体操服(2)
ENDIF
;二人で押し倒してるなら
IF TEQUIP:Ｗ推倒
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + Ｗ推倒中(5)
ENDIF
;縛られているなら
IF TEQUIP:PLAYER:拘束 || STATE("バインド", PLAYER)
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + 束縛中(5)
ENDIF
;騎乗位でインサート状態
IF INSERT("騎乗位")
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + 騎乗位(5)
ENDIF
;前ターンの絶頂具合
IF PREVEX("射精", PLAYER)
	COMORDER_280 += 5
	STR:実行値 = %STR:実行値% + %CALLNAME:PLAYER%が射精直後(5)
ENDIF

;マイナス補正はこの後
IF TALENT:娇小
	COMORDER_280 -= 1
	STR:実行値 = %STR:実行値% - 娇小(1)
ENDIF
;前ターンの絶頂具合
IF PREVEX("絶頂数合計")
	COMORDER_280 -= 10
	STR:実行値 = %STR:実行値% - 絶頂直後(10)
ENDIF
IF COND("陶酔")
	COMORDER_280 -= 5
	STR:実行値 = %STR:実行値% - 陶酔(5)
ELSEIF COND("弛緩")
	COMORDER_280 -= 2
	STR:実行値 = %STR:実行値% - 弛緩(2)
ENDIF
IF COND("疲労")
	COMORDER_280 -= 10
	STR:実行値 = %STR:実行値% - 疲労困憊(10)
ELSEIF BASE:体力 < MAXBASE:体力/2
	COMORDER_280 -= 5
	STR:実行値 = %STR:実行値% - 体力半減(5)
ENDIF
IF CFLAG:マグロ
	COMORDER_280 -= 10
	STR:実行値 = %STR:実行値% - 干劲无(10)
ENDIF
;PLAYER側の補正
IF ABL:PLAYER:技巧 - ABL:技巧
	COMORDER_280 -= ABL:PLAYER:技巧 - ABL:技巧
	STR:実行値 = %STR:実行値% \@ ABL:PLAYER:技巧 - ABL:技巧 > 0 ? - # + \@ 技巧差({ABS(ABL:PLAYER:技巧 - ABL:技巧)})
ENDIF
IF TALENT:PLAYER:大力士
	COMORDER_280 -= TALENT:PLAYER:大力士*3
	STR:実行値 = %STR:実行値% - %CALLNAME:PLAYER%が%TALENT_NAME(GETNUM(TALENT, "大力士"), TALENT:PLAYER:大力士)%({TALENT:PLAYER:大力士*3})
ENDIF
IF TFLAG:もがき数
	COMORDER_280 -= TFLAG:もがき数*3
	STR:実行値 = %STR:実行値% - もがき数{TFLAG:もがき数}回({TFLAG:もがき数*3})
ENDIF
IF RANDF(7) - 3
	COMORDER_280 += RANDF(7) - 3
	STR:実行値 = %STR:実行値% \@ RANDF(7) - 3 > 0 ? - # + \@ 時の運({ABS(RANDF(7) - 3)})
ENDIF
PRINTL 
PRINTFORM %SUBSTRING(STR:実行値, 3)% = {COMORDER_280} 
SELECTCASE COMORDER_280
CASE IS < 0
	PRINT <
CASE 0
	PRINT =
CASEELSE
	PRINT >
ENDSELECT
PRINTFORMW  実行値{0}
PRINTL 
IF COMORDER_280 <= 0
	TFLAG:もがき数 = 0
	IF TEQUIP:PLAYER:拘束 || STATE("バインド", PLAYER)
		PRINTFORMW %CALLNAME:PLAYER%想辦法從拘束中逃離了！
		TEQUIP:PLAYER:拘束 = 0
		CALL REMOVE_STATE, PLAYER, "バインド"
	ELSE
		PRINTFORM %CALLNAME:PLAYER%将
		SIF PREVEX("絶頂数合計")
			PRINTFORM 從沉浸在絕頂的餘韻中的%CALLNAME:TARGET%的空隙中發起突襲、
		PRINTFORMW 將主導權奪回了！
		CVARSET TEQUIP, GETNUM(TEQUIP, "推倒")
		CALL KOJO_MESSAGE_COM
		;Ｗ押し倒し等での口上処理
		CALL KOJO_MESSAGE_COM_MULTI
		;助手とのインサートはここで解消
		CALL SETFLAG, "Ｗ推倒解除"
		CALL SETFLAG, "今回指令"

		;TARGETにPLAYERがインサート中なら正常位に移行
		IF INSERT("Ｖ挿入", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 60
		ELSEIF INSERT("Ａ挿入", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 70
		ELSEIF INSERT("尿道挿入", TARGET, PLAYER)
			CALL SET_INSERT, TARGET, PLAYER, 83
		ENDIF
	ENDIF
	RETURN 0
ELSE
	;前回の調教によって文章変化
	;顔面騎乗系
	IF PREVCOM == 1 || PREVCOM == 9 || TEQUIP:面部騎乗 || TEQUIP:六九式
		PRINTFORM 從跨坐在臉上的%CALLNAME:TARGET%那裡、
	;逆イラマチオ
	ELSEIF PREVCOM == 4
		PRINTFORM 從帶著愉快的表情強硬的衝擊著%CALLNAME:PLAYER%口腔的%CALLNAME:TARGET%那裡、
	;奉仕系
	ELSEIF (PREVCOM == 90 || PREVCOM == 91 || PREVCOM == 92 || PREVCOM == 95 || PREVCOM == 97 || PREVCOM == 99) && PENIS(PLAYER)
		SIF PREVCOM == 90 || PREVCOM == 91 || PREVCOM == 92 || PREVCOM == 99
			PRINT 壓在身上
		PRINTFORM 蹂躪著陰莖的%CALLNAME:TARGET%那裡、
	;騎乗位系
	ELSEIF (PREVCOM >= 65 && PREVCOM <= 67) || PREVCOM == 75 || PREVCOM == 80 || PREVCOM == 88 || PREVCOM == 98 || PREVCOM == 120 || PREVCOM == 126
		PRINTFORM 從亢奮的侵犯著%CALLNAME:PLAYER%的%CALLNAME:TARGET%那裡、
	;アナル奉仕系
	ELSEIF PREVCOM == 96
		PRINTFORM 從愉快的蹂躪著肛門的%CALLNAME:TARGET%那裡、
	ELSE
		PRINTFORM %CALLNAME:PLAYER%将
	ENDIF
	PRINTFORML 试图夺回主导权、但%CALLNAME:TARGET%\@ TEQUIP:Ｗ推倒 ? 她们 # \@完全没有破绽。
	IF TEQUIP:接吻 && ABL:欲望 >= 5 && RAND:3 == 0
		PRINTFORMW 不止如此、%CALLNAME:TARGET%像是在享受%CALLNAME:PLAYER%的反抗一樣再次奪去了嘴唇、強行用舌頭交纏再一次、強硬的灌下香甜的唾液。
		CALL SETFLAG, "追加接吻フラグ", TARGET
	ELSE
		PRINTFORMW 不但如此、像是在懲罰%CALLNAME:PLAYER%的抵抗一樣、更加猛烈地侵犯起來。
	ENDIF
	TFLAG:もがき数 += 1

	CALL KOJO_MESSAGE_COM
	PRINTL 

	CALL SET_COUNTER_ABL

	SIF TEQUIP:Ｗ推倒
		CALL SET_SELECTCOM_MULTI, "Ｗ推倒"

	TRYJUMPFORM COM{SELECTCOM}
ENDIF

RETURN 1

@FLAG_COM280, ARG

@SOURCE_COM280, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 280

SOURCE:0:性行動 = 160

;PALAM:欲情をみる
SELECTCASE PALAM:欲情
CASE IS < PALAMLV:1
	TIMES SOURCE:0:性行動, 1.00
CASE IS < PALAMLV:2
	TIMES SOURCE:0:性行動, 1.20
CASE IS < PALAMLV:3
	TIMES SOURCE:0:性行動, 1.40
CASE IS < PALAMLV:4
	TIMES SOURCE:0:性行動, 1.60
CASE IS >= PALAMLV:4
	TIMES SOURCE:0:性行動, 1.80
ENDSELECT

;ABL:サドッ気をみる
SELECTCASE ABL:施虐属性
CASE 0
CASE 1
	TIMES SOURCE:0:性行動, 1.20
CASE 2
	TIMES SOURCE:0:性行動, 1.40
CASE 3
	TIMES SOURCE:0:性行動, 1.60
CASE 4
	TIMES SOURCE:0:性行動, 1.80
CASEELSE
	TIMES SOURCE:0:性行動, 2.00
ENDSELECT

CALL CALC, "調教ソース", ARG

