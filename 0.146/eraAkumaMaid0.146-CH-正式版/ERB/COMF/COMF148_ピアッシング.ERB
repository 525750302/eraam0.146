@COM148
;ピアッシング
PRINTL 穿环

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 148

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0

;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = 穿环

RETURN 1

@FLAG_COM148, ARG
;クリorラビアピアスなら、アソコを露出させる
IF COND("陰蒂環フラグ") + COND("陰唇環フラグ") + COND("尿道穿環フラグ") && !CHECK_CLO("Ｖ露出")
	CALL SET_PANTIES, ""
	CALL SET_BODYS, ""
	CALL SET_OUTER, ""
ENDIF

STAIN:PLAYER:手 = 0
IF COND("陰蒂環フラグ")
	STAIN:Ｖ = SET_STAIN(0, "Ｖ")
ELSEIF COND("陰唇環フラグ") || COND("尿道穿環フラグ")
	STAIN:Ｖ = SET_STAIN(0, "Ｖ")
ELSEIF COND("乳环フラグ")
	STAIN:Ｂ = 0
ENDIF

@SOURCE_COM148, ARG
;外した場合
SIF COND("穿環ＯＮフラグ") == 0
	RETURN 1

CALL SOURCE_SMPLAY, "穿环", ARG

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 148

;基本はへそピアスの時
;初めてつける時は痛みのせいで、体力消費が凄いことになります

SOURCE:0:疼痛 = 3000
SOURCE:0:屈従 = 1000
SOURCE:0:逸脱 = 3000

IF COND("陰蒂環フラグ")
	SOURCE:0:疼痛 += 17000
	SOURCE:0:屈従 += 9000
	SOURCE:0:逸脱 += 17000
ELSEIF COND("陰唇環フラグ") || COND("尿道穿環フラグ")
	SOURCE:0:疼痛 += 7000
	SOURCE:0:屈従 += 9000
	SOURCE:0:逸脱 += 12000
ELSEIF COND("乳环フラグ")
	SOURCE:0:疼痛 += 12000
	SOURCE:0:屈従 += 9000
	SOURCE:0:逸脱 += 12000
ELSEIF COND("肚脐环フラグ")

ENDIF

;ABL:マゾっ気をみる
SELECTCASE ABL:受虐属性
CASE 0
CASE 1
	TIMES SOURCE:0:屈従, 1.20
	TIMES SOURCE:0:逸脱, 0.90
CASE 2
	TIMES SOURCE:0:屈従, 1.40
	TIMES SOURCE:0:逸脱, 0.80
CASE 3
	TIMES SOURCE:0:屈従, 1.60
	TIMES SOURCE:0:逸脱, 0.50
CASE 4
	TIMES SOURCE:0:屈従, 1.80
	TIMES SOURCE:0:逸脱, 0.30
CASEELSE
	TIMES SOURCE:0:屈従, 2.00
	TIMES SOURCE:0:逸脱, 0.10
ENDSELECT

;麻酔を使ったら痛み激減
SIF COND("鎮痛麻酔フラグ")
	TIMES SOURCE:0:疼痛, 0.01

;ビデオ撮影中
IF TEQUIP:攝像機
	TIMES SOURCE:0:屈従, 1.50
	TIMES SOURCE:0:逸脱, 1.50
ENDIF

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM148
#DIM MEMO_LINECOUNT
#DIM LCOUNT
;ピアスの形状
#DIMS PIERCES
;ピアスの場所
#DIMS PART_PIERCES

VARSET PART_PIERCES

REDRAW 0
MEMO_LINECOUNT = LINECOUNT

IF ABL:受虐属性 < 3 && FIRSTCOM(SELECTCOM) == 0
	PRINTFORML 這種激烈的PLAY對她來說還太早了點吧？……也好。這次、又要怎麼做呢……？
ELSE
	PRINTFORML 所以、這次要怎麼做呢……？
ENDIF

PRINTFORML  [ 0] 將\@ PENIS(TARGET) ? 阴茎 # 阴蒂 \@\@ TALENT:陰蒂環 ? 上的穿環取下 # 用金屬穿環装飾起來 \@
SIF TALENT:男性
	SETCOLOR DEF_COLOR("灰色")
PRINTFORML  [ 1] 將小陰唇\@ TALENT:陰唇環 ? 上的穿環取下 # 用金屬穿環装飾起來 \@
RESETCOLOR

SIF COND("真性陥没乳頭")
	SETCOLOR DEF_COLOR("灰色")
PRINTFORML  [ 2] 將乳頭\@ TALENT:乳环 ? 上的穿環取下 # 用金屬穿環装飾起來 \@
RESETCOLOR

SIF ABL:尿道拡張 < 0
	SETCOLOR DEF_COLOR("灰色")
PRINTFORML  [ 3] 將尿道口\@ TALENT:尿道穿環 ? 上的穿環取下 # 用穿環装飾起來 \@
RESETCOLOR

PRINTFORML  [ 4] 將肚臍\@ TALENT:肚脐环 ? 上的穿環取下 # 用穿環装飾起來 \@
PRINTFORML [100] 不、再考慮一下吧。

CALL INPUT_SELECT, 5, 100

IF RESULT == 100
	TFLAG:指令中止 = 1
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 0
	CALL SETFLAG, "陰蒂環フラグ", TARGET
	SIF TALENT:陰蒂環 == 0
		CALL SETFLAG, "穿環ＯＮフラグ", TARGET
	CALL DEL_CEVENT_GROUP, "陰蒂環"
	IF PENIS(TARGET)
		PART_PIERCES = 阴茎
	ELSE
		PART_PIERCES = 阴蒂
	ENDIF
ELSEIF RESULT == 1
	IF TALENT:男性
		PRINTFORMW ※男性怎么想都不可能吧※
	ELSE
		CALL SETFLAG, "陰唇環フラグ", TARGET
		SIF TALENT:陰唇環 == 0
			CALL SETFLAG, "穿環ＯＮフラグ", TARGET
		CALL DEL_CEVENT_GROUP, "陰唇環"
		PART_PIERCES = 小陰唇
	ENDIF
ELSEIF RESULT == 2
	IF COND("真性陥没乳頭")
		PRINTFORMW ※真性陥没乳頭无法装着※
	ELSE
		CALL SETFLAG, "乳环フラグ", TARGET
		SIF TALENT:乳环 == 0
			CALL SETFLAG, "穿環ＯＮフラグ", TARGET
		CALL DEL_CEVENT_GROUP, "乳环"
		PART_PIERCES = 乳頭
	ENDIF
ELSEIF RESULT == 3
	IF ABL:尿道拡張 < 0
		PRINTFORMW ※需要尿道拡張LV0以上才可以装着※
	ELSE
		CALL SETFLAG, "尿道穿環フラグ", TARGET
		SIF TALENT:尿道穿環 == 0
			CALL SETFLAG, "穿環ＯＮフラグ", TARGET
		CALL DEL_CEVENT_GROUP, "尿道穿環"
		PART_PIERCES = 尿道
	ENDIF
ELSEIF RESULT == 4
	CALL SETFLAG, "肚脐环フラグ", TARGET
	SIF TALENT:肚脐环 == 0
		CALL SETFLAG, "穿環ＯＮフラグ", TARGET
	CALL DEL_CEVENT_GROUP, "肚脐环"
	PART_PIERCES = 肚脐
ENDIF
IF PART_PIERCES == ""
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

;ピアスの詳細を文字列で記録
PIERCES = 

;ピアスをつけた
IF COND("穿環ＯＮフラグ")
	PRINTFORML 這次、要安装上那種穿環呢？
	PRINTFORML  [ 0] 圓環型
	PRINTFORML  [ 1] 啞鈴型
	PRINTFORML  [ 2] 正十字型
	PRINTFORML  [ 3] ♂型
	PRINTFORML  [ 4] ♀型
	PRINTFORML  [90] 直接輸入
	PRINTFORML [100] 不拘泥於特定的形態（通常無記述）

	CALL INPUT_SELECT, 5, 90, 100

	SELECTCASE RESULT
	CASE 100
	CASE 0
		PIERCES = 圓環型
	CASE 1
		PIERCES = 啞鈴型
	CASE 2
		PIERCES = 十字架型
	CASE 3
		PIERCES = ♂型
	CASE 4
		PIERCES = ♀型
	CASE 90
		PRINTL 請將全角８文字（半角則16文字）以下的字符串輸入。
		PRINTL (像是"耳環"之類被使用也不會產生不協感的記錄是推薦選項。)
		PRINTL 另外、如果是什麼也不輸入的場合就會被視作不算特別拘泥的場合（也即是無記述）。
		CALL INPUTS_SELECT, 16
		PIERCES = %RESULTS%
	ENDSELECT

	PRINTL 使用麻醉劑嗎？
	PRINTL [0] 是
	PRINTL [1] 否
	CALL INPUT_SELECT, 2
	SIF RESULT == 0
		CALL SETFLAG, "鎮痛麻酔フラグ", TARGET

	PRINTFORM %CALLNAME:PLAYER%將%CALLNAME:TARGET%的
	IF COND("陰蒂環フラグ")
		IF PENIS(TARGET)
			PRINT 陰莖
		ELSE
			IF COND("陰核肥大")
				PRINT 腫脹肥大的
			ELSEIF TALENT:淫核
				PRINT 興奮勃起的
			ELSEIF TALENT:Ｃ性向
				PRINT 敏感而淫亂的
			ELSEIF TALENT:娇小
				PRINT 小小的
			ENDIF
			PRINT 阴蒂
		ENDIF
	ELSEIF COND("陰唇環フラグ")
		IF TALENT:淫壷
			PRINT 淫亂地顫抖著的
		ELSEIF TALENT:Ｖ性向
			PRINT 敏感而淫亂的
		ELSEIF AGE(TARGET) == 1
			PRINT 幼嫩的
		ELSEIF TALENT:娇小
			PRINT 小小的
		ENDIF
		PRINT 小陰唇
	ELSEIF COND("乳环フラグ")
		IF COND("陥没乳頭")
			PRINTFORM 陷沒在乳肉中的\@ COND("乳頭肥大") ? 碩大的 # \@乳頭剝出
		ELSE
			IF COND("乳頭肥大")
				PRINT 腫脹肥大的
			ELSEIF TALENT:淫乳
				PRINT 興奮勃起的
			ELSEIF TALENT:Ｂ性向
				PRINT 敏感而淫亂的
			ELSEIF AGE(TARGET) == 1
				PRINT 幼嫩的
			ELSEIF TALENT:娇小
				PRINT 小小的
			ENDIF
			PRINT 乳頭
		ENDIF
	ELSEIF COND("尿道穿環フラグ")
		PRINTFORM 広開的尿道口
	ELSEIF COND("肚脐环フラグ")
		PRINT 肚臍
	ENDIF
	SIF PIERCES != ""
		PRINTFORM %PIERCES%の
	PRINTFORMW 穿環装上了。

	IF COND("陰蒂環フラグ")
		IF PENIS(TARGET)

		ELSE
			PRINTFORMW %CALLNAME:TARGET%的\@ COND("陰核肥大") ? 格外肥大的 # \@陰蒂在金屬穿環的牽引下使包皮外卷、成為每時每刻都裸露在外的狀態了……
		ENDIF
		PIERCES = 陰蒂環：%PIERCES%
	ELSEIF COND("陰唇環フラグ")
		SIF AGE(TARGET) >= 3
			PRINTFORMW %CALLNAME:TARGET%的小陰唇因被装上的金屬穿環的重量所拉扯、即使不特地撥開大陰唇也能一目了然地看見了……
		PIERCES = 陰唇環：%PIERCES%
	ELSEIF COND("乳环フラグ")
		SIF COND("陥没乳頭")
			PRINTFORMW %CALLNAME:TARGET%經常陷沒在乳肉中的乳頭在金屬穿環的阻隔下變得不能返回、被矯正成為平常也裸露在外的狀態了……
		PIERCES = 乳环：%PIERCES%
	ELSEIF COND("尿道穿環フラグ")
		PIERCES = 尿道穿環：%PIERCES%
	ELSEIF COND("肚脐环フラグ")
		PIERCES = 肚脐环：%PIERCES%
	ENDIF

	;ピアスの形状フラグセット
	SIF PIERCES != ""
		CALL SET_CEVENT, PIERCES
;ピアスを外した
ELSE
	PRINTFORMW %CALLNAME:PLAYER%将%CALLNAME:TARGET%的%PART_PIERCES%的穿環取下了
ENDIF

;つけたり外したり
IF COND("陰蒂環フラグ")
	INVERTBIT TALENT:陰蒂環, 0
ELSEIF COND("陰唇環フラグ")
	INVERTBIT TALENT:陰唇環, 0
ELSEIF COND("乳环フラグ")
	INVERTBIT TALENT:乳环, 0
ELSEIF COND("尿道穿環フラグ")
	INVERTBIT TALENT:尿道穿環, 0
ELSEIF COND("肚脐环フラグ")
	INVERTBIT TALENT:肚脐环, 0
ENDIF

;-------------------------------------------------
;ソースの計算(確定値)
;-------------------------------------------------
CALL CALC, "調教ソースの計算"


@COM_ORDER_COM148, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

SELECTCASE TALENT:有男朋友
CASE 2
	LOCAL -= 40
	LOCALS = %LOCALS% - %TALENT_NAME(GETNUM(TALENT, "有男朋友"), TALENT:有男朋友)%(40)
CASE 1
	LOCAL -= 30
	LOCALS = %LOCALS% - %TALENT_NAME(GETNUM(TALENT, "有男朋友"), TALENT:有男朋友)%(30)
ENDSELECT

;ABL:露出癖
IF ABL:露出癖
	LOCAL += MIN(ABL:露出癖, 10) * 2
	LOCALS = %LOCALS% + 露出癖LV{ABL:露出癖}({MIN(ABL:露出癖, 10) * 2})
ENDIF
;ABL:マゾっ気
IF ABL:受虐属性
	LOCAL += MIN(ABL:受虐属性, 5) * 5
	LOCALS = %LOCALS% + 受虐属性LV{ABL:受虐属性}({MIN(ABL:受虐属性, 5)})
ENDIF
;恥じらい
IF TALENT:害羞
	LOCAL -= 15
	LOCALS = %LOCALS% - 害羞(15)
ENDIF
;恥薄い
IF TALENT:不知羞恥
	LOCAL += 5
	LOCALS = %LOCALS% + 不知羞恥(5)
ENDIF
;倒錯的
IF TALENT:倒錯的
	LOCAL += 10
	LOCALS = %LOCALS% + 倒錯的(10)
ENDIF

;合計を表示(80以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 80

RETURN RESULT

