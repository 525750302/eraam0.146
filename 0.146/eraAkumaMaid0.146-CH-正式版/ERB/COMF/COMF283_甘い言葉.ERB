@COM283
;甘い言葉
;成功率判定を行う。成功時には反感を買わずに恭順・欲望が上昇。
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;話を聞いてくれる
IF CALCF("甜言蜜语成功")
	TEQUIP:甜言蜜语 = 5
	;専用の素質を持ってる？
	SIF TALENT:PLAYER:甜言蜜语
		TEQUIP:甜言蜜语 += TALENT:PLAYER:甜言蜜语
	;アドバイスに堕天使の甘言がある？
	SIF TEQUIP:堕天使の甘言
		TEQUIP:甜言蜜语 += 2
;話を聞いてくれない
ELSE
	TEQUIP:甜言蜜语 = 0
ENDIF

CALL SOURCE_CHECK_SHORT

RETURN 0

@TRAIN_MESSAGE_COM283
IF PREVCOM >= 0
	SELECTCASE TRAINNAME_EX(PREVCOM)
	CASE "剃毛"
		IF TALENT:男性 == 0
			PRINTFORM %CALLNAME:PLAYER%将
			SELECTCASE CONDS("陰毛")
			CASE "脱毛処理"
				PRINTFORM %TEXTR("脱毛处理后变成了像兒童一様光滑无毛的/因为完全脱毛而变得十分干净的")%
			CASE "光滑无毛"
				PRINTFORM %TEXTR("精心剃净陰毛的/被剃得光滑无毛/能看得十分清楚的")%
			CASE "保养完毕"
				IF CEVENTS("陰毛の名称") != ""
					PRINTFORM 綺麗%CEVENTS("陰毛の名称")%的
				ELSE
					PRINTFORM %TEXTR("精心打理的/漂亮的/轻松的")%
				ENDIF
				PRINTFORM 陰毛剃整齐的
			ENDSELECT
			PRINTFORM %CALLNAME:TARGET%的%TEXTS("割れ目：表現短め")%
			IF RAND:2
				PRINTFORMW 、用指尖%TEXTR("温柔地描着/推開/咕滋咕滋地玩弄")%的同时夸奖起来…
			ELSE
				PRINTFORMW 用脸靠近、%TEXTR("確認结果/检查毛茬的痕迹/仔细鑑賞")%的同时夸奖起来…
			ENDIF
			RETURN 1
		ENDIF
	CASE "喝套内精液"
		PRINTFORM %CALLNAME:PLAYER%将
		SELECTCASE ABL:精液中毒
		CASE IS >= 3
			PRINTFORMW %CALLNAME:TARGET%出色的飲用姿势赞美了一番…
		CASEELSE
			PRINTFORMW 、把精液好好地喝干的%CALLNAME:TARGET%赞美了一番…
		ENDSELECT
		RETURN 1
	CASE "让对方浴尿"
		IF PREVPLAY("尿飲み干し")
			PRINTFORMW %CALLNAME:PLAYER%赞美了没有溢出来而是漂亮飲尽了尿液的%CALLNAME:TARGET%…
			RETURN 1
		ENDIF
	CASE "拳交", "両穴拳交"
		PRINTFORM %CALLNAME:PLAYER%将
		PRINTFORM %TEXTR("看着沾满愛液的手/抚摸着股間/继续慢慢用手进出")%的同时、
		IF ABL:Ｖ弛緩
			PRINTFORMW %TEXTR(@"扩大到令人瞠目结舌的%CALLNAME:TARGET%的膣穴\/%CALLNAME:TARGET%的膣穴的宽松的状态\/完全失去紧张感的%CALLNAME:TARGET%的開發完成的膣穴")%赞美了一番…
		ELSE
			PRINTFORMW %TEXTR(@"大大広開的%CALLNAME:TARGET%的膣穴\/%CALLNAME:TARGET%的膣穴深处柔軟的様子\/进化到可以接受手腕程度的%CALLNAME:TARGET%的膣穴")%赞美了一番…
		ENDIF
		RETURN 1
	CASE "肛門拳交"
	ENDSELECT
ENDIF

PRINTFORM %CALLNAME:PLAYER%
;褒める
IF PREVPLAY("Ａ清洁口交")
	PRINTFORM 對给肛門性交直後的阴茎进行口交掃除的
ELSEIF PREVPLAY("清洁口交")
	PRINTFORM 對给脏污的阴茎进行口交掃除的
;ELSEIF PREVCOM == 14
;	PRINTFORMW は%CALLNAME:TARGET%のアナルのガバガバ具合を褒めた…
;	RETURN 1
;インサート中
ELSEIF INSERT("合体中") && RANDIF(1, TEQUIP:接吻)
	PRINTFORM 對%TEXTS("合体中", PLAYER)%
ELSEIF TEQUIP:接吻
	PRINTFORM 對%TEXTS("接吻継続")%、
	CALL SETFLAG, "追加接吻フラグ", TARGET
ELSEIF TEQUIP:深喉 && PREVCOM == TNUM("深喉")
	PRINTFORMW 抚摸了正在做着深喉的%CALLNAME:TARGET%的头、說著溫柔的誇獎…
	RETURN 1
ELSE
	SELECTCASE PREVCOM
	CASE 91
		PRINTFORM 對将阴茎用嘴唇侍奉着的
	CASE 98
		PRINTFORM 對將臉頰埋在股間侍奉著的
	CASE TNUM("抱上去")
		PRINTFORMW 抱着%CALLNAME:TARGET%、說著甜蜜的情話…
		RETURN 1
	CASEELSE
		IF ASSI && TALENT:ASSI:甜言蜜语 && TALENT:MASTER:甜言蜜语 && ASSIPLAY
			PRINTFORM 和%CALLNAME:MASTER%對
		ELSEIF ASSI && TALENT:ASSI:甜言蜜语 && TALENT:MASTER:甜言蜜语 && ASSIPLAY == 0
			PRINTFORM 和%CALLNAME:ASSI%對
		ELSE
			PRINTFORM 對
		ENDIF
	ENDSELECT
ENDIF

IF AGE(TARGET) < AGE(PLAYER) && INSERT("合体中") == 0
	PRINTFORMW %CALLNAME:TARGET%說著溫柔的誇獎……
ELSE
	PRINTFORMW %CALLNAME:TARGET%說著甜蜜的情話……
ENDIF


@EQUIP_COM283_2
SIF TEQUIP:甜言蜜语 == 0
	RETURN 0
;実行ターンの成功時のSOURCEはここで処理
IF SELECTCOM == 283
	;成功率Downの素質がある場合、取得sourceにボーナスがつく
	;ソースの基準量や補正倍率はまだ調整途上
	LOCAL = CALCF("甜言蜜语")

	SOURCE:情愛 += LOCAL
	IF TALENT:男性 == 0
		IF TALENT:容易濕
			SOURCE:Ｖ液体追加 += UP_PALAMLV(LOCAL / 10)
		ELSEIF TALENT:不易濕
			SOURCE:Ｖ液体追加 += DOWN_PALAMLV(LOCAL / 10)
		ELSE
			SOURCE:Ｖ液体追加 += LOCAL / 10
		ENDIF
	ENDIF
	SOURCE:恭順追加 += LOCAL / 2
	SOURCE:欲情追加 += LOCAL / 4
	;反抗的な人は恭順が上がらな過ぎるので、補正
	SIF TALENT:叛逆
		SOURCE:恭順追加 += LOCAL * 3
	;ムード加算
	FLAG:情緒 += 30 + TEQUIP:甜言蜜语*5

	;好感度
	SIF PLAYER == MASTER
		TFLAG:好感度BOUNS += 5 + TEQUIP:甜言蜜语
ENDIF
;ムードアップ中
PRINTFORML ＜甜言蜜语効果発揮中＞
TEQUIP:甜言蜜语 -= 1
;好感度
SIF PLAYER == MASTER
	TFLAG:好感度BOUNS += 2

RETURN 1

;-------------------------------------------------
;汎用口上
;-------------------------------------------------
@KOJO_MESSAGE_COM283
;汎用成功
IF TEQUIP:甜言蜜语
	IF TALENT:高慢 && RAND:2
		PRINTFORMW %CALLNAME:TARGET%對此好像並不滿足……
	ELSEIF TALENT:開朗
		IF RAND:2
			PRINTFORM %CALLNAME:TARGET%因此而露出了大大的笑容、將%CALLNAME:PLAYER%
			IF AGE(TARGET) >= 3
				PRINTW 緊緊抱住了。
			ELSE
				PRINTW 輕輕擁住了。
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:TARGET%小聲的反復嘀咕著%CALLNAME:PLAYER%的情話、露出了略帶羞澀的開心笑容。
		ENDIF
	ELSEIF TALENT:害羞
		PRINTFORMW %CALLNAME:TARGET%的臉頰變得通紅、扭扭捏捏地看過來了……
	ELSE
		PRINTFORMW %CALLNAME:TARGET%開心的笑著。
	ENDIF
;汎用失敗
ELSE
	IF TALENT:高慢 && RAND:2
		PRINTFORMW %CALLNAME:TARGET%對著%CALLNAME:PLAYER%的奉承話嗤笑起來……
	ELSE
		PRINTFORMW %CALLNAME:TARGET%並沒有認真的聆聽%CALLNAME:PLAYER%的情話……
	ENDIF
ENDIF
