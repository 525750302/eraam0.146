@COM6
;キス
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 6

;実行できない
SIF TCVAR:干劲 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回指令 = %STR:今回指令%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM6, ARG
;対象の口⇔調教者の口の汚れが移動
CALL EQ_STAIN, TARGET, "Ｍ", PLAYER, "Ｍ"


@SOURCE_COM6, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 6

;汚れデータを元にする
SOURCE:0:不潔 = COM_ORDER_STAIN("Ｍ", "Ｍ")*100 + 100

SOURCE:0:屈従 = 100
SOURCE:0:情愛 = 300
SOURCE:0:性行動 = 300
SOURCE:0:達成感 = 300

;ABL:Ｍ感覚をみる
SOURCE:0:Ｍ快楽 = CALCF("Ｍ刺激", 1)
;調教者の舌使いをみる
SOURCE:0:Ｍ快楽 = SOURCE_EXABL(SOURCE:0:Ｍ快楽, "舌靈活", PLAYER)

;テンダーキッス
IF CHECK_COM("テンダー接吻")
	TIMES SOURCE:0:Ｍ快楽, 0.10
	SOURCE:0:情愛 += 700
ENDIF

;キスをどのぐらい続けたかでボーナス
SOURCE:0:情愛 += 200 * TEQUIP:接吻
SOURCE:0:性行動 += 200 * TEQUIP:接吻
SOURCE:0:達成感 += 100 * TEQUIP:接吻
SOURCE:0:屈従 += 100 * TEQUIP:接吻

SIF TALENT:Ｍ性向
	CALL CALC, "好みのPLAY", SOURCE:0:Ｍ快楽

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM6
;テンダーキッス
IF CHECK_COM("テンダー接吻")
	IF TEQUIP:推倒
		PRINTFORM %CALLNAME:TARGET%將%CALLNAME:PLAYER%的臉頰用雙手輕輕撫摸著的同時、溫柔地
	ELSE
		SIF PLAYER != MASTER
			PRINTFORM %CALLNAME:PLAYER%将
		;相思相愛
		IF TALENT:相思相愛 &&  PLAYER == MASTER && RAND:3 == 0
			PRINTFORM 在%CALLNAME:TARGET%的耳側低聲訴說著愛意的同時、溫柔地
		ELSEIF TALENT:娇小 && RAND:2 == 0
			PRINTFORM 將%CALLNAME:TARGET%的頭頂輕輕地撫摸著的同時、輕輕地
		ELSE
			PRINTFORM 將%CALLNAME:TARGET%輕輕擁入懷中的同時、溫柔地
		ENDIF
	ENDIF
	IF RAND:2 == 0
		PRINTFORMW 親吻著……
	ELSE
		PRINTFORMW 重疊雙唇……
	ENDIF
;押し倒し中
ELSEIF TEQUIP:推倒
	PRINTFORM %CALLNAME:TARGET%
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	SIF TEQUIP:Ｗ推倒 && SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ推倒中")%、
	IF TEQUIP:接吻
		IF TEQUIP:Ｗ推倒 == 0
			PRINTFORMW 將像是在渴求空氣一樣抗拒地逃離後喘息起來的%CALLNAME:PLAYER%雙唇再度緊緊貼住、一直到臉頰因缺氧而浮現紅暈才停下……
		ELSE
			PRINTFORMW 將神情恍惚地被動接受著的%CALLNAME:PLAYER%雙唇吸住、深深地濕吻著……
		ENDIF
	ELSEIF INSERT("合体中") && TEQUIP:Ｗ推倒 == 0
		PRINTFORM %TEXTS("合体中", TARGET)%
		IF (ABL:欲望 >= 4 || TALENT:強気) && RAND:2
			PRINTFORMW 將%CALLNAME:PLAYER%的唇強硬地奪走了……
		ELSE
			PRINTFORMW 與%CALLNAME:PLAYER%忘我地親吻著……
		ENDIF
	ELSE
		PRINTFORM 將%CALLNAME:PLAYER%的雙唇奪取、
		IF PREVPLAY("飲尿") && CHECK_STAIN(STAIN:Ｍ, "尿") && ABL:施虐属性 >= 3
			PRINTFORMW 像是在回報之前的淫戲一樣、將口腔中積存下來的混雜著腥臭尿液的唾液全部灌注進來了……
		ELSEIF (PREVCOM == 91 || PREVCOM == 99 || PREVCOM == 140) && CHECK_STAIN(STAIN:Ｍ, "精液") && ABL:施虐属性 >= 3
			PRINTFORMW 在緊緊地糾纏住舌頭的同時、將口中積存下來的精液與唾液吐到口腔中、並強迫著要求飲下……
		ELSE
			IF RAND:2 == 0
				PRINTFORMW 讓兩人的舌頭相互糾纏起來、貪求著對方所帶來的快樂……
			ELSE
				PRINTFORMW 呸嘍呸嘍的將自己的唾液強行灌注進來了……
			ENDIF
		ENDIF
	ENDIF
;キス継続中
ELSEIF TEQUIP:接吻 && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:PLAYER%将%TEXTS("電話中の")%%CALLNAME:TARGET%
	IF INSERT("正常位")
		PRINTFORM 相互迎合的擺動腰肢的同時、輕輕地啄吻著
	ELSEIF INSERT("対面座位")
		PRINTFORM 相互擁抱著、在不斷搖晃腰肢的同時、持續親吻著
	ELSE
		IF TALENT:相思相愛 && PLAYER == MASTER && RAND:2
			PRINTFORM 感受著對方心中愛意的同時、深深舌吻著
		ELSE
			PRINTFORM 相互擁抱著、
			IF RAND:3 == 0
				PRINT 將彼此口腔的每一寸角落用舌尖舔舐著
			ELSEIF RAND:2
				PRINT 將彼此的靈巧舌頭相互交纏、交換著唾液
			ELSE
				PRINT 彼此與濕吻著、投入到連唇角都被溢出的粘稠唾液沾滿了
			ENDIF
		ENDIF
	ENDIF
	PRINTFORMW ……
;インサート中
ELSEIF INSERT("合体中")
	PRINTFORM %CALLNAME:PLAYER%将%TEXTS("合体中", PLAYER)%
	IF EX:Ｖ中出し == 1 && PREVPLAY("生中出しした", PLAYER) && PREVPLAY("Ｖ挿入中", PLAYER)
		PRINTFORM %TEXTR("膣内射精の余韻に浸っている/生中出しを受け入れてくれた/慢慢地呼吸して息を整えている")%%CALLNAME:TARGET%
	ELSE
		PRINTFORM %CALLNAME:TARGET%%TEXTS("に電話させたまま、")%
	ENDIF
	IF INSERT("挿入中") && CONDS("体位", PLAYER) != "騎乗位"
		PRINTFORMW 吻死乞白賴的奪取了……
	ELSE
		PRINTFORMW の%TEXTR("唇を塞ぎ唾液を流し込んだ/口腔を隅々まで舐め回した/唇を奪った")%…
	ENDIF
ELSE
	PRINTFORM %CALLNAME:PLAYER%将
	IF PREVPLAY("飲尿")
		PRINTFORM 在飲尿PLAY之後一邊呼出散發著氨臭的口氣、一邊緩緩調整呼吸的節奏的
	ELSEIF COND("前回口交")
		PRINTFORM 中断了陰莖口交之後、将
	ELSEIF COND("前Ｔ調教者") == PLAYER && (PREVCOM == 98 || PREVCOM == 99)
		PRINTFORM 中断了阴部奉仕之後、将
	ELSEIF COND("前Ｔ調教者") == PLAYER && PREVCOM == 96
		PRINTFORM 进行了尻穴奉仕的
	ELSEIF TEQUIP:電話中
		PRINTFORM %TEXTS("電話中の")%
	ENDIF
	PRINTFORM %CALLNAME:TARGET%
	IF PREVCOM == 285
		PRINTFORM 抱住的同时
	ELSEIF RAND:3 == 0 && (COND("前回口交") || (COND("前Ｔ調教者") == PLAYER && (PREVCOM == 96 || PREVCOM == 98 || PREVCOM == 99)) )
		PRINTFORM 使用舌頭的技巧加以褒揚、溫柔地
	ELSE
		PRINTFORM %TEXTR("深深地擁入懷中/的頭頂輕輕撫摸/的秀髮摩挲著/的臉頰輕輕撫摸著")%的同時、
	ENDIF
	PRINTFORMW 親吻著。
ENDIF

IF INSERT("挿入") && (ABL:信頼 >= 5 || ABL:欲望 >= 5) && ABL:Ｖ感覚 >= 5 && PREVCOM >= 66 && PREVCOM <= 68
	PRINTFORM %CALLNAME:TARGET%%TEXTR("由于敏感的地方襲来的快感而不由得漏出灼熱的喘息/因为敏感的地方被攻击不住漏出甘甜的呻吟声")%的同时、
	IF TEQUIP:推倒 || RAND:3 == 0
		PRINTFORMW 熱情地索求著%CALLNAME:PLAYER%所給予的快樂……
	ELSEIF RAND:2
		PRINTFORMW 交纏著舌頭……
	ELSE
		PRINTFORMW 積極地回吻著……
	ENDIF
ELSEIF TEQUIP:推倒 == 0 && ABL:信頼 >= 3 && (ABL:信頼 >= 5 || ABL:Ｍ感覚 + ABL:PLAYER:舌靈活 >= 3)
	PRINTFORM %CALLNAME:TARGET%
	;テンダーキス
	IF CHECK_COM("テンダー接吻")
		IF RAND:3 == 0
			PRINTFORMW %TEXTR("閉上了眼睛/小幅度喘息的同时")%、將身體託付給了%CALLNAME:PLAYER%…
		ELSEIF RAND:2 == 0
			PRINTFORMW 閉上了眼睛、臉頰染上薄薄的紅暈、迎合著%CALLNAME:PLAYER%的動作…
		ELSE
			PRINTFORMW %TEXTR("閉上了眼睛/小幅度喘息的同时")%恍惚地沉浸在快感之中…
		ENDIF
	ELSEIF TALENT:娇小 && TALENT:PLAYER:高大 && INSERT("挿入") + INSERT("挿入中") == 0 && RAND:2
		PRINTFORMW 踮着脚用力弥补身高差、为了回应%CALLNAME:PLAYER%而積極地缠绕着舌头…
	ELSE
		PRINTFORMW 为了回应%CALLNAME:PLAYER%、積極地缠绕着舌头…
	ENDIF
ENDIF


@EQUIP_COM6
SIF TEQUIP:接吻 == 0 || COND("追加接吻フラグ") == 0
	RETURN 0

CALL SOURCE_COM6, 30
CALL FLAG_COM6, 30

RETURN 1

@EQUIP_COM6_2
SIF TEQUIP:接吻 == 0 || COND("追加接吻フラグ") == 0
	RETURN 0

PRINTFORML ＜一有破绽就接吻中＞

IF TEQUIP:推倒
	LOCAL = MIN(LIMIT(TEQUIP:接吻 - 1, 0, ABL:舌靈活) + TALENT:蕩唇, 4) / 2
ELSE
	LOCAL = MIN(LIMIT(TEQUIP:接吻 - 1, 0, ABL:PLAYER:舌靈活) + TALENT:蕩唇, 4) / 2
ENDIF

EXP:接吻経験 += 1 + LOCAL
EXP:PLAYER:接吻経験 += 1 + LOCAL
PRINTFORML 接吻経験＋{1 + LOCAL}
PRINTFORML 接吻経験＋{1 + LOCAL}（%CALLNAME:PLAYER%）

@COM_ORDER_COM6, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;貞操無頓着
IF TALENT:不在乎貞操
	LOCAL += 20
	LOCALS = %LOCALS% + 不在乎貞操(20)
ENDIF
;恥じらい
IF TALENT:害羞
	LOCAL -= 3
	LOCALS = %LOCALS% - 害羞(3)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:否定快感
	LOCAL -= 1
	LOCALS = %LOCALS% - 否定快感(1)
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	LOCAL += 5
	LOCALS = %LOCALS% + 恋慕(5)
ENDIF
;Ｍ性向
IF TALENT:Ｍ性向
	LOCAL += 10
	LOCALS = %LOCALS% + Ｍ性向(10)
ENDIF

;汚れあり
IF COM_ORDER_STAIN("Ｍ", "Ｍ")
	LOCAL -= COM_ORDER_STAIN("Ｍ", "Ｍ")
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｍ", "Ｍ")})
ENDIF

;合計を表示(15以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 15

RETURN RESULT
