;特殊なギミックなどがないものはここに記述
;選択肢を内包するなど書くのが困難なものについては、TRAIN_MESSAGE_COM(コマンド番号)を用いて、呼び出すようにすること。
@TRAIN_MESSAGE_B
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT_KOJO
#DIM MEMO_CUP, 20
#DIM MEMO_HP

;-------------------------------------------------
;写真撮影コマンドで、メモリに余裕がない場合にはアルバム整理画面へ
;-------------------------------------------------
IF SELECTCOM == TNUM("写真撮影") && NUM("写真") >= 300
	CALL BLANKLINE
	PRINTFORML 尽管还想再拍、但是已经存不下了。
	PRINTFORMW 请删掉不需要的写真閲覧画面。
	CALL VIEW_PHOTO
	IF NUM("写真") >= 300
		CALL BLANKLINE
		PRINTFORMW 由于储存器已经没有多余空间、写真撮影停止了。
		TFLAG:指令中止 = 1
		RETURN 0
	ENDIF
ENDIF

;-------------------------------------------------
;インサートなど、体位を表すフラグの管理
;-------------------------------------------------
CALL SETFLAG, "体勢戻し"

;-------------------------------------------------
;CUPの記録
;-------------------------------------------------
FOR LCOUNT, 0, 20
	MEMO_CUP:LCOUNT = CUP:LCOUNT
NEXT
MEMO_HP = DOWNBASE:体力

;-------------------------------------------------
;ソースの計算(予測値)
;-------------------------------------------------
CALL CALC, "調教ソースの試算"

;-------------------------------------------------
;処女チェック
;-------------------------------------------------
;選択肢の表示をチェック
MEMO_LINECOUNT = LINECOUNT
IF TEQUIP:推倒 == 0
	SELECTCASE SELECTCOM
	CASE 60 TO 69
		IF TALENT:処女 == 1 && TEQUIP:推倒 == 0
			PRINTFORM %CALLNAME:TARGET%的处女要以
			IF PENIS(PLAYER) && (CHECK_STAIN(STAIN:PLAYER:Ｐ, "Ａ") || (PREVCOM >= 70 && PREVCOM <= 79) || INSERT("Ａ挿入") )
				PRINTFORM 被肛門弄脏的阴茎
			ELSE
				PRINTFORM %TRAINNAME:SELECTCOM%
			ENDIF
			PRINTFORML 来夺走吗？
		;潤滑不足
		ELSEIF CHOSECOMS("Ｖ挿入") == 0 && COND("Ｖ潤充分") == 0
			PRINTFORML %CALLNAME:TARGET%那里好象准备不足,真的要插入吗？
		ENDIF
;-------------------------------------------------
;Ａ処女の喪失チェック
;-------------------------------------------------
	CASE 70 TO 79
		IF TALENT:Ａ処女 && TEQUIP:推倒 == 0
			PRINTFORML %CALLNAME:TARGET%的\@ TALENT:Ａ処女 ? 可以抢走屁股处女吗？ # 真的要侵犯肛門吗？ \@
		;潤滑不足
		ELSEIF CHOSECOMS("Ａ挿入") == 0 && COND("Ａ潤充分") == 0
			PRINTFORML %CALLNAME:TARGET%的屁穴好像准备不足、可以插入吗？
		ENDIF
;-------------------------------------------------
;双頭バイブプレイでの処女喪失チェック
;-------------------------------------------------
	CASE 80
		IF TEQUIP:推倒 == 0 && TALENT:PLAYER:処女 && CHECK_COM("双頭振動棒PLAY")
			PRINTFORML 要用双头振动棒的话%CALLNAME:PLAYER%的处女会丢失
			PRINTFORML 即使那样也执行吗？
		ENDIF
	CASE 83 TO 88
		IF EXP:尿道性交経験 == 0
			IF PENIS(TARGET)
				PRINTFORML 要用%CALLNAME:PLAYER%的阴茎把%CALLNAME:TARGET%的阴茎处女夺走吗？
			ELSE
				PRINTFORML 夺取%CALLNAME:TARGET%尿道处女、真的要这样吗？
			ENDIF
		ENDIF
	ENDSELECT

	IF LINECOUNT > MEMO_LINECOUNT
		CALL PRINT_SELECT, "确认/还是算了吧"
		SIF RESULT == 1
			TFLAG:指令中止 = 1
	ENDIF


;-------------------------------------------------
;コマンドの否定判定
;-------------------------------------------------
	SIF REFUSE_CHECK()
		RETURN 0


;-------------------------------------------------
;サイズチェックと潤滑チェック
;サイズが大きい場合にはそっち優先
;-------------------------------------------------
	VARSET LOCALS
	SELECTCASE SELECTCOM
	CASE 60 TO 69
		;大きすぎる
		IF EXP_GAIN("Ｖ性交経験") == 0 && PENIS(PLAYER) && SIZE("阴茎", PLAYER) >= ABL:Ｖ拡張 + 3 + (TALENT:拡張適性 > 0) + ( (TALENT:処女 && CALCF("Ｖ拡張の上限：処女無視", TARGET) > 0) > 0)
			LOCALS:0 = %CALLNAME:PLAYER%的家伙太大了、%CALLNAME:TARGET%的小穴可能承受不了这么大的
			LOCALS:2 = 真的要插入吗？（※有[Ｖ損傷]的可能！※）
		ENDIF
	CASE 70 TO 79
		IF EXP_GAIN("Ａ性交経験") == 0 && PENIS(PLAYER) && SIZE("阴茎", PLAYER) >= ABL:Ａ拡張 + 3 + (TALENT:拡張適性 > 0)
			LOCALS:0 = %CALLNAME:PLAYER%的家伙太大了、%CALLNAME:TARGET%的屁穴可能承受不了这么大的
			LOCALS:2 = 真的可以插入吗？（※有[Ａ損傷]的可能！※）
		ENDIF
	ENDSELECT

	;助手の助言
	IF ASSI && ASSI != TARGET
		;信頼LV減少予定をチェックするのは有りか
		;IF CALCF("反発刻印獲得", TARGET)
		;	LOCALS:1 = 「このプレイは%CALLNAME:TARGET%\@ COND("種族：人形", ASSI) ? 様 # \@には早\@ CALCF("反発刻印獲得", TARGET) >= 2 ? すぎる # い \@かも」
		IF CALCF("苦痛刻印獲得", TARGET)
			LOCALS:1 = 「このPLAYは\@ CALCF("苦痛刻印獲得", TARGET) >= 2 ? 伴随着相当的痛苦 # 有些疼 \@かも」
			SELECTCASE SELECTCOM
			CASE 2, 33
				SIF PALAM:Ａ潤 < 10000
					LOCALS:1 = 「%CALLNAME:TARGET%\@ COND("種族：人形", ASSI) ? 様 # \@的未成熟的肛門享受着快乐、可以先用口水润滑…」
			;Ｖ潤が必要と助言
			CASE 60 TO 79
				SELECTCASE GET_PALAMLV(PALAM:Ｖ潤 + PALAM:PLAYER:Ｐ潤) - MAX(SIZE("阴茎", PLAYER) - ABL:Ｖ拡張, 0)
				CASE 1

				CASE IS <= 0

				ENDSELECT
			;Ａ潤が必要と助言
			CASE 70 TO 79
				SELECTCASE GET_PALAMLV(PALAM:Ａ潤 + PALAM:PLAYER:Ｐ潤) - MAX(SIZE("阴茎", PLAYER) - ABL:Ａ拡張, 0)
				CASE 1
					LOCALS:1 = 「%CALLNAME:TARGET%\@ COND("種族：人形", ASSI) ? 様 # \@的未成熟的肛門享受着快乐、可以先用口水或润滑液润滑……」
				CASE IS <= 0
					LOCALS:1 = 「%CALLNAME:TARGET%\@ COND("種族：人形", ASSI) ? 様 # \@的未成熟的肛門享受着快乐、可以先用口水润滑…」
				ENDSELECT
			ENDSELECT
		ENDIF
		IF LOCALS:1 != ""
			IF LOCALS:0 != ""
				LOCALS:1 = %CALLNAME:ASSI%も%LOCALS:1%说道。
			ELSE
				LOCALS:1 = %CALLNAME:ASSI%被%LOCALS:1%说道
			ENDIF
			SIF LOCALS:2 == ""
				LOCALS:2 = …即使那样也要实行吗?
		ENDIF
	ENDIF

	MEMO_LINECOUNT = LINECOUNT
	FOR LCOUNT, 0, 3
		SIF LOCALS:LCOUNT != ""
			PRINTFORML %LOCALS:LCOUNT%
	NEXT

	IF LINECOUNT > MEMO_LINECOUNT
		CALL PRINT_SELECT, "确认/还是算了吧"
		SIF RESULT == 1
			TFLAG:指令中止 = 1
	ENDIF
;-------------------------------------------------
;コマンドの否定判定
;-------------------------------------------------
	SIF REFUSE_CHECK()
		RETURN 0

;-------------------------------------------------
;コンドーム装着イベント(押し倒したら生セックス）
;-------------------------------------------------
	CALL BLANKLINE
	;イベントが起きなかったら改行を消す用
	MEMO_LINECOUNT = LINECOUNT

	IF TRAIN_GENRE("Ｖ挿入", SELECTCOM) && PENIS(PLAYER) && TEQUIP:PLAYER:避孕套 == 0 && INSERT("Ｖ挿入中", PLAYER) == 0
		IF TCVAR:Ｖ生挿入許可
		;生挿入許可になった場合に一回だけ来る
		ELSEIF COND("特別に生挿入許可")
			;イベント閲覧フラグON
			TCVAR:特別に生挿入イベ閲覧済 = 1

			PRINTFORM %CALLNAME:TARGET%沉溺在被给予的快楽中、
			SIF PREVCOM == TNUM("扒開阴道")
				PRINTFORM 展示着押開了的発情阴部
			PRINTFORMW 像在梦里一样看向了%CALLNAME:PLAYER%……
			MEMO_LINECOUNT_KOJO = LINECOUNT
			CALL KOJO_EVENT, "特別に生挿入許可"
			IF LINECOUNT == MEMO_LINECOUNT_KOJO
				CALL PRINT_STRW, "「…………_H_」"
				PRINTFORMW 理应平时不让不带避孕套的%CALLNAME:TARGET%、这次明显抵抗力很弱……这令%CALLNAME:PLAYER%的腰部都动了起来。
				PRINTFORMW ……看起来、尽管本来不行、但是只要让%CALLNAME:PLAYER%觉得舒服的话\@ PREVPLAY("Ｖ挿入") ? 继续做 #  \@也是可以的。
				IF CHOSECOMS("Ｖ挿入") == 0
					IF COND("危険日")
						PRINTFORM 今天是%CALLNAME:TARGET%的危険日、
					ELSE
						PRINTFORM 今天%CALLNAME:TARGET%既不是危険日也不是安全日、
					ENDIF
				ENDIF
				PRINTL 该怎么做呢？
				IF ITEM:避孕套
					CALL PRINT_SELECT, "花言巧语地糊弄着无套挿入/戴套"
					SIF RESULT
						CALL SETFLAG, "戴上套套", PLAYER
				ELSE
					CALL PRINT_SELECT, "花言巧语地糊弄着无套挿入/因为没有套了所以放弃挿入"
					IF RESULT
						TFLAG:指令中止 = 1
						RETURN 0
					ENDIF
				ENDIF
				SIF TEQUIP:PLAYER:避孕套 == 0
					CALL PRINT_STRW, @"爱心粉紅_(※从[陶酔]中的%CALLNAME:TARGET%得到了许可、特別允许无套插入了！)"
			ELSE
				IF CHOSECOMS("Ｖ挿入") == 0
					IF COND("危険日")
						PRINTFORM 今天是%CALLNAME:TARGET%的危険日、
					ELSE
						PRINTFORM 今天%CALLNAME:TARGET%既不是危険日也不是安全日、
					ENDIF
				ENDIF
				PRINTL 该怎么做呢？
				IF ITEM:避孕套
					CALL PRINT_SELECT, "生で挿入する/避孕套を着ける"
					SIF RESULT
						CALL SETFLAG, "戴上套套", PLAYER
				ELSE
					CALL PRINT_SELECT, "无套插入/没有避孕套所以不挿入"
					IF RESULT
						TFLAG:指令中止 = 1
						RETURN 0
					ENDIF
				ENDIF
			ENDIF
			IF TEQUIP:PLAYER:避孕套 == 0
				CALL SETFLAG, "Ｖ生挿入許可"
			ELSE
				CALL SETFLAG, "Ｖ避孕套挿入許可"
			ENDIF
		ELSEIF TCVAR:Ｖ避孕套挿入許可
			IF ITEM:避孕套
				CALL SETFLAG, "戴上套套", PLAYER
			;以下はゴムが切れた場合
			ELSEIF COND("ワンモア生挿入可能")
				IF TCVAR:避孕套切れ交渉
					PRINTFORMW ……果然还是直接无套插进去吧？
					CALL PRINT_SELECT, "就这样/放弃"
				ELSE
					PRINTFORMW 在挿入前被%CALLNAME:TARGET%阻止了 ……看来在不知道什么时候避孕套用完了的样子。
					PRINTFORMW ……但、稍微思考了一下之后%CALLNAME:TARGET%又放松了下来。
					PRINTFORMW 看起来、只有一次的话无套插入\@ PREVPLAY("Ｖ挿入") ? 继续做 #  \@也是可以的
					IF COND("危険日")
						CALL PRINT_SELECT, "花言巧语地糊弄着危険日无套挿入/放弃"
					ELSE
						CALL PRINT_SELECT, "花言巧语地糊弄着无套挿入/放弃"
					ENDIF
				ENDIF
				IF RESULT == 0
					TCVAR:ワンモア生挿入中 = 1
				ELSE
					TFLAG:指令中止 = 1
				ENDIF
			;ワンモア生挿入後
			ELSEIF TCVAR:ワンモア生挿入終了
				PRINTFORMW ……被%CALLNAME:TARGET%在挿入之前制止了。
				PRINTFORMW 今天就到这里为止了。
				TFLAG:指令中止 = 1
			ELSE
				IF TCVAR:避孕套切れ交渉 == 0
					PRINTFORMW ……看来在不知道什么时候避孕套用完了的样子。
					;生挿入も受け入れている
					IF COND("妊娠可能") && (COND("女性主導生性交") || COND("合意：危険日生性交") )
						CALL KOJO_EVENT, "Ｖ避孕套装着"
						SIF REFUSE_CHECK()
							RETURN 0

						PRINTFORMW %CALLNAME:TARGET%一副戴不戴都无所谓的態度、要无套插入吗？
						IF COND("避妊不要")
							CALL PRINT_SELECT, "无套插入/算了吧"
						ELSEIF COND("危険日")
							CALL PRINT_SELECT, "危険日无套插入/算了吧"
						ELSEIF COND("安全日")
							CALL PRINT_SELECT, "安全日无套插入/算了吧"
						ELSE
							CALL PRINT_SELECT, "无套插入/算了吧"
						ENDIF
						IF RESULT
							TFLAG:指令中止 = 1
							RETURN 0
						ENDIF
					ELSE
						PRINTL 该怎么做呢？
						CALL PRINT_SELECT, "无套插入/因为没有套了所以放弃挿入"
						IF RESULT
							TFLAG:指令中止 = 1
							RETURN 0
						ENDIF
						MEMO_LINECOUNT_KOJO = LINECOUNT
						CALL KOJO_EVENT, "Ｖ避孕套装着"
						;セリフ無くても避妊したり
						SIF LINECOUNT == MEMO_LINECOUNT_KOJO
							CALL SETFLAG, "生挿入可否判定"
					ENDIF
				ELSE
					PRINTFORMW 因为没有避孕套了所以尝試无套挿入。
					MEMO_LINECOUNT_KOJO = LINECOUNT
					CALL KOJO_EVENT, "Ｖ避孕套装着"
					;セリフ無くても避妊したり
					SIF LINECOUNT == MEMO_LINECOUNT_KOJO
						CALL SETFLAG, "生挿入可否判定"
				ENDIF

				CALL SETFLAG, "避孕套切れ交渉"

				SIF REFUSE_CHECK()
					RETURN 0
				SIF TEQUIP:PLAYER:避孕套 == 0
					CALL SETFLAG, "Ｖ生挿入許可"
			ENDIF
		ELSE
			IF CHOSECOMS("Ｖ挿入") == 0
				IF COND("妊娠可能")
					IF COND("危険日")
						PRINTFORMW 今天是%CALLNAME:TARGET%的危険日。
					ELSEIF COND("受精確認")
						PRINTFORMW %CALLNAME:TARGET%的子宫已经被受精了。
					ELSEIF COND("安全日")
						PRINTFORMW 今天是%CALLNAME:TARGET%的安全日。
					ELSE
						PRINTFORMW 今天既不是%CALLNAME:TARGET%的危険日也不是安全日。
					ENDIF
				ELSEIF TALENT:未成熟
					PRINTFORMW %CALLNAME:TARGET%的身体是被直接中出也不需要担心妊娠的体质。
				ENDIF
			ENDIF
			IF ITEM:避孕套
				IF TCVAR:ご褒美生Ｈ
					PRINTFORMW 今天是奖励Ｈ所以也许会特别地允许生性交吗……该怎么做呢？
				ELSE
					PRINTFORML 要使用避孕套吗？
				ENDIF
				IF TCVAR:ご褒美生Ｈ
					CALL PRINT_SELECT, "谨慎起见还是戴上避孕套吧/当然不要戴了"
				ELSEIF TALENT:未成熟
					CALL PRINT_SELECT, "谨慎起见还是戴上避孕套吧/不戴"
				ELSEIF COND("危険日") && COND("合意：危険日生性交")
					CALL PRINT_SELECT, "是的/因为无套会更舒服一点所以不戴"
				ELSE
					CALL PRINT_SELECT, "是的/不戴"
				ENDIF
				;着ける
				SIF RESULT == 0
					CALL SETFLAG, "戴上套套", PLAYER
			ELSE
				IF TCVAR:ご褒美生Ｈ
					PRINTFORMW 今天是奖励Ｈ所以也许会特别地允许生性交吗……该怎么做呢？
				ELSE
					PRINTFORML 已经没有储备的避孕套了、该怎么做呢？
				ENDIF
				IF TCVAR:ご褒美生Ｈ
					CALL PRINT_SELECT, "当然是不戴直接Ｈ/因为没有套了所以放弃挿入"
				ELSEIF TALENT:未成熟
					CALL PRINT_SELECT, "因为不用担心受精所以就直接无套生挿入/以防万一还是放弃挿入了"
				ELSEIF COND("危険日") && COND("合意：危険日生性交")
					CALL PRINT_SELECT, "因为对方也同意了所以就直接无套插入/因为没有套了所以放弃挿入"
				ELSE
					CALL PRINT_SELECT, "无套插入/因为没有套了所以放弃挿入"
				ENDIF
				IF RESULT
					TFLAG:指令中止 = 1
					RETURN 0
				ENDIF
			ENDIF

			MEMO_LINECOUNT_KOJO = LINECOUNT
			CALL KOJO_EVENT, "Ｖ避孕套装着"

			;セリフ無くても避妊したり
			SIF LINECOUNT == MEMO_LINECOUNT_KOJO
				CALL SETFLAG, "生挿入可否判定"

			SIF REFUSE_CHECK()
				RETURN 0
			IF TEQUIP:PLAYER:避孕套
				CALL SETFLAG, "Ｖ避孕套挿入許可"
			ELSE
				CALL SETFLAG, "Ｖ生挿入許可"
			ENDIF
		ENDIF
	ELSEIF TRAIN_GENRE("Ａ挿入", SELECTCOM) && PENIS(PLAYER) && TEQUIP:PLAYER:避孕套 == 0 && INSERT("Ａ挿入中", PLAYER) == 0
		;方針決定済み
		IF TCVAR:Ａ生挿入許可
		ELSEIF TCVAR:Ａ避孕套挿入許可
			IF ITEM:避孕套
				CALL SETFLAG, "戴上套套", PLAYER
			;ゴムが切れた！
			ELSE
				IF TCVAR:避孕套切れ交渉 == 0
					PRINTFORMW ……看来在不知道什么时候避孕套用完了的样子。
					PRINTL 该怎么做呢？
					CALL PRINT_SELECT, "无套插入/因为没有避孕套所以先停下来"
					IF RESULT
						TFLAG:指令中止 = 1
						RETURN 0
					ENDIF
				ELSE
					PRINTFORMW 因为避孕套用完了所以直接无套插入。
				ENDIF

				CALL KOJO_EVENT, "Ａ避孕套装着"
				CALL SETFLAG, "避孕套切れ交渉"

				SIF REFUSE_CHECK()
					RETURN 0
				SIF TEQUIP:PLAYER:避孕套 == 0
					CALL SETFLAG, "Ａ生挿入許可"
			ENDIF
		ELSE
			IF ITEM:避孕套
				PRINTL 要戴套吗？
				CALL PRINT_SELECT, "是的/不戴"
				;着ける
				SIF RESULT == 0
					CALL SETFLAG, "戴上套套", PLAYER
			ELSE
				PRINTL 没有備蓄的避孕套了、要怎么办？
				CALL PRINT_SELECT, "无套插入/因为没有避孕套了所以放弃肛交"
				IF RESULT
					TFLAG:指令中止 = 1
					RETURN 0
				ENDIF
			ENDIF

			CALL KOJO_EVENT, "Ａ避孕套装着"

			SIF REFUSE_CHECK()
				RETURN 0
			IF TEQUIP:PLAYER:避孕套
				CALL SETFLAG, "Ａ避孕套挿入許可"
			ELSE
				CALL SETFLAG, "Ａ生挿入許可"
			ENDIF
		ENDIF
	ELSEIF TRAIN_GENRE("Ｖ逆挿入", SELECTCOM) && PENIS(TARGET) && TEQUIP:避孕套 == 0 && INSERT("Ｖ挿入中") == 0
		IF TCVAR:PLAYER:Ｖ生挿入許可
		ELSEIF TCVAR:PLAYER:Ｖ避孕套挿入許可
			IF ITEM:避孕套
				CALL SETFLAG, "戴上套套", TARGET
			;以下はゴムが切れた場合
			ELSE
				IF TCVAR:PLAYER:避孕套切れ交渉 == 0
					PRINTFORMW ……看来在不知道什么时候避孕套用完了的样子。
					CALL KOJO_EVENT, "Ｖ避孕套装着"
					SIF REFUSE_CHECK()
						RETURN 0
					PRINTL 该怎么做呢？
					CALL PRINT_SELECT, "无套插入/因为没有套了所以放弃挿入"
					IF RESULT
						TFLAG:指令中止 = 1
						RETURN 0
					ENDIF
				ELSE
					PRINTFORMW 因为避孕套用完了所以直接无套插入。
					CALL KOJO_EVENT, "Ｖ避孕套装着"
				ENDIF

				CALL SETFLAG, "避孕套切れ交渉", PLAYER

				SIF REFUSE_CHECK()
					RETURN 0
				SIF TEQUIP:避孕套 == 0
					CALL SETFLAG, "Ｖ生挿入許可", PLAYER
			ENDIF
		ELSE
			IF CHOSECOMS("Ｖ逆挿入") == 0&& COND("妊娠可能", PLAYER)
				IF COND("危険日", PLAYER)
					PRINTFORMW 今天是%CALLNAME:PLAYER%的危険日。
				ELSEIF COND("受精確認", PLAYER)
					PRINTFORMW %CALLNAME:PLAYER%的子宫已经被受精了。
				ELSEIF COND("安全日", PLAYER)
					PRINTFORMW 今天是%CALLNAME:PLAYER%的安全日。
				ELSE
					PRINTFORMW 今天既不是危険日也不是安全日。
				ENDIF
			ENDIF
			IF ITEM:避孕套
				PRINTFORML 要让%CALLNAME:TARGET%戴避孕套吗？
				CALL PRINT_SELECT, "是/否"
				;着ける
				SIF RESULT == 0
					CALL SETFLAG, "戴上套套", TARGET
			ELSE
				PRINTL 没有備蓄的避孕套了、要怎么办？
				CALL PRINT_SELECT, "无套插入/因为没有套了所以放弃挿入"
				IF RESULT
					TFLAG:指令中止 = 1
					RETURN 0
				ENDIF
			ENDIF

			CALL KOJO_EVENT, "Ｖ避孕套装着"

			SIF REFUSE_CHECK()
				RETURN 0

			IF TEQUIP:避孕套
				CALL SETFLAG, "Ｖ避孕套挿入許可", PLAYER
			ELSE
				CALL SETFLAG, "Ｖ生挿入許可", PLAYER
			ENDIF
		ENDIF
	ELSEIF TRAIN_GENRE("Ａ逆挿入", SELECTCOM) && PENIS(TARGET) && TEQUIP:避孕套 == 0 && INSERT("Ａ挿入中") == 0

		IF TCVAR:PLAYER:Ａ生挿入許可
		ELSEIF TCVAR:PLAYER:Ａ避孕套挿入許可
			IF ITEM:避孕套
				CALL SETFLAG, "戴上套套", TARGET
			;以下はゴムが切れた場合
			ELSE
				IF TCVAR:PLAYER:避孕套切れ交渉 == 0
					PRINTFORMW ……看来在不知道什么时候避孕套用完了的样子。
					CALL KOJO_EVENT, "Ａ避孕套装着"
					SIF REFUSE_CHECK()
						RETURN 0
					PRINTL 该怎么做呢？
					CALL PRINT_SELECT, "无套插入/因为没有套了所以放弃挿入"
					IF RESULT
						TFLAG:指令中止 = 1
						RETURN 0
					ENDIF
				ELSE
					PRINTFORMW 因为避孕套用完了所以直接无套插入。
					CALL KOJO_EVENT, "Ａ避孕套装着"
				ENDIF

				CALL SETFLAG, "避孕套切れ交渉", PLAYER

				SIF REFUSE_CHECK()
					RETURN 0
				;Ａゴムフィルタ用のフラグ設定
				SIF TEQUIP:避孕套 == 0
					CALL SETFLAG, "Ａ生挿入許可", PLAYER
			ENDIF
		ELSE
			IF ITEM:避孕套
				PRINTL 希望戴避孕套吗？
				CALL PRINT_SELECT, "是的/不戴"
				;着ける
				SIF RESULT == 0
					CALL SETFLAG, "戴上套套", TARGET
			ELSE
				PRINTL 没有備蓄的避孕套了、要怎么办？
				CALL PRINT_SELECT, "无套插入/因为没有避孕套了所以放弃肛交"
				IF RESULT
					TFLAG:指令中止 = 1
					RETURN 0
				ENDIF
			ENDIF

			CALL KOJO_EVENT, "Ａ避孕套装着"

			SIF REFUSE_CHECK()
				RETURN 0
			IF TEQUIP:避孕套
				CALL SETFLAG, "Ａ避孕套挿入許可", PLAYER
			ELSE
				CALL SETFLAG, "Ａ生挿入許可", PLAYER
			ENDIF
		ENDIF
	ENDIF
	;イベントが起きなかったら改行を消す用
	SIF MEMO_LINECOUNT == LINECOUNT
		CLEARLINE 1
ENDIF

;-------------------------------------------------
;コマンドの否定判定
;-------------------------------------------------
SIF REFUSE_CHECK()
	RETURN 0

;-------------------------------------------------
;コマンド実行直前の口上
;-------------------------------------------------
CALL KOJO_MESSAGE_BEFORE_COM

;-------------------------------------------------
;コマンドの否定判定
;-------------------------------------------------
SIF REFUSE_CHECK()
	RETURN 0

;-------------------------------------------------
;CUPの復元
;-------------------------------------------------
FOR LCOUNT, 0, 20
	CUP:LCOUNT = MEMO_CUP:LCOUNT
NEXT
DOWNBASE:体力 = MEMO_HP


;-------------------------------------------------
;ソースの計算(確定値)
;-------------------------------------------------
SELECTCASE SELECTCOM
;例外コマンド
CASE TNUM("振動棒"), TNUM("肛用振動棒"), TNUM("穿环"), TNUM("尿道探针"), TNUM("尿道振動棒")

CASEELSE
	CALL CALC, "調教ソースの計算"
ENDSELECT

;-------------------------------------------------
;調教時メッセージ
;-------------------------------------------------
SELECTCASE SELECTCOM
;選択肢が出てくるコマンドは、ここでなくそのコマンド内部でスキップを行う
CASE 31, 33
CASEELSE
	;地の文を飛ばすフラグを立てている(ターン開始時にOFFになる)
	SIF TFLAG:地の文スキップ
		RETURN 0
ENDSELECT

CALL BLANKLINE
TRYCALLFORM TRAIN_MESSAGE_COM{SELECTCOM}


;以下の追加地の文はどれかひとつしか表示させない
MEMO_LINECOUNT = LINECOUNT

;調教ごとの初回限定の地の文
SELECTCASE SELECTCOM
CASE 60 TO 69
	CALL TEXT, "追加地の文：Ｖ挿入後"
CASE 70 TO 79
	CALL TEXT, "追加地の文：Ａ挿入後"
CASE 83 TO 88
	CALL TEXT, "追加地の文：尿道挿入後"
ENDSELECT

SIF LINECOUNT > MEMO_LINECOUNT || COND("３Ｐ")
	RETURN 0

;Ａ感覚LV3+絶頂あり+連続アナルセックス
IF SELECTCOM >= 70 && SELECTCOM <= 79 && SELECTCOM == PREVCOM && TEQUIP:推倒 == 0 && ABL:Ａ感覚 >= 3 && EX:絶頂数合計 && INSERT("Ａ挿入") && COND("前Ｔ調教者") == PLAYER && TALENT:男性 == 0
	PRINTFORM %TEXTR("从因激烈抽拔而扭曲的/由于来自肛門的快乐、从/被毫不留情的深入突进着、从")%%CALLNAME:TARGET%的阴部里、
	;今回Ｖ中出しされた
	IF EXP_GAIN("膣内精液経験")
		PRINTFORM 中出后混杂精液的
	ELSEIF TALENT:処女 && RAND:3 == 0
		PRINTFORM 不像是処女般的大量
	ELSE
		PRINTFORM %TEXTR("明明没有被碰到却有/汩汩的/羞耻的/粘糊糊的")%
	ENDIF
	PRINTFORMW 爱液流了出来……
;騎乗位の追加テキスト
ELSEIF TRAIN_GENRE("騎乗位", SELECTCOM) && (INSERT("挿入") == 0 || COND("前Ｔ調教者") != PLAYER)
	CALL TEXT, "追加地の文：騎乗位"
ELSEIF TRAIN_GENRE("騎乗位", SELECTCOM) && TEQUIP:推倒 && INSERT("騎乗位") && (PREVPLAY("騎乗位") || RAND:4 == 0)
	CALL TEXT, "追加地の文：騎乗位"
;どちらかがそろそろイきそうな状態
;コマンドごとに固有の追加台詞などはここに書く
ELSEIF SELECTCOM >= 60 && SELECTCOM <= 89 && PENIS(PLAYER) && COND("要去了") + COND("要去了", PLAYER)
	CALL TEXT, "追加地の文：騎乗位以外の挿入"
;インサート中の愛撫
ELSEIF RAND:3 == 0 && SELECTCOM >= 0 && SELECTCOM <= 19 && INSERT("挿入") && (COND("要去了") || RAND:5 == 0)
	;ある程度の対応した部分の感度が必要
	SELECTCASE SELECTCOM
	CASE 0
		SIF MAX(ABL:Ｃ感覚, ABL:Ｂ感覚, ABL:Ｍ感覚) < 3
			RETURN 0
	CASE 1, 7
		SIF ABL:Ｃ感覚 < 3
			RETURN 0
	CASE 8
		SIF ABL:Ｖ感覚 < 3
			RETURN 0
	CASE 2, 9
		SIF ABL:Ａ感覚 < 3
			RETURN 0
	CASE 5
		SIF ABL:Ｂ感覚 < 3
			RETURN 0
	CASE 6
		SIF ABL:Ｍ感覚 < 3
			RETURN 0
	CASE 12
		SIF PENIS(TARGET) == 0 && ABL:Ｃ感覚 < 3
			RETURN 0
	CASE 15
		SIF TALENT:尿道性感 == 0
			RETURN 0
	ENDSELECT

	PRINTFORM %TEXTR("在被挿入的同时愛撫的/被玩弄了敏感場所的/被责弄了舒服的地方的/被突击了弱点的")%%CALLNAME:TARGET%
	PRINTFORM %TEXTR("因刻骨铭心的而全身颤抖/因瞬间的刺激绷紧了颤抖的身体/肢体瞬间跳了起来")%、
	PRINTFORMW %CALLNAME:PLAYER%のモノを%TEXTR("ざわめく粘膜で意図せず舐め回してきた/無意識に締め付けてしまう/一際強く締め付けてくる")%…
	CALL SETFLAG, "締め付けフラグ", TARGET
ENDIF


@TRAIN_MESSAGE_A
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT2

;ウェイトの発生判定
MEMO_LINECOUNT = LINECOUNT

;絶頂
IF NOWEX:絶頂数合計
	IF COND("性欲解消実行中") && COND("絶頂", PLAYER) && PENIS(PLAYER) && TEQUIP:PLAYER:避孕套 == 0
		SELECTCASE NAMES("性処理")
		CASE "Ｖ性交", "Ａ性交", "尿道性交"
			PRINTFORM 被%CALLNAME:PLAYER%大量的精液注込了的
		ENDSELECT
	ENDIF
	PRINTFORM %CALLNAME:TARGET%
	;浴尿
	IF COND("飲尿フラグ")
		PRINTFORM %CALLNAME:PLAYER%的%TEXTR("品味奥妙者/作为一个小便器的任务的完成者")%
	ELSEIF COND("子宮内浴尿フラグ")
		PRINTFORM %TEXTR("子宮/重要的秘所")%%TEXTR("被人当做厕所代替/被小便沐浴着。")%
	ELSEIF COND("膣内浴尿フラグ")
		IF RAND:3 == 0
			PRINTFORM 子宫口被小便刺激
		ELSEIF RAND:2
			PRINTFORM 在阴道内被小便冲洗
		ELSE
			PRINTFORM 把%TEXTR("膣/重要的秘所")%变成厕所
		ENDIF
	ELSEIF COND("温泉浣腸フラグ")
		IF RAND:3 == 0
			PRINTFORM 在大肠中尿入小便、
		ELSEIF RAND:2
			PRINTFORM 因为用来排泄的地方反过来被排泄
		ELSE
			PRINTFORM 肠内的厕所
		ENDIF
	ELSEIF COND("乳内浴尿フラグ")
		IF RAND:3 == 0
			PRINTFORM 用小便刺激乳房内部
		ELSEIF RAND:2
			PRINTFORM 乳房有小便臭味
		ELSE
			PRINTFORM 乳房变成厕所
		ENDIF
	ELSEIF COND("膀胱内浴尿フラグ")
		IF RAND:3 == 0
			PRINTFORM 尿道反过来小便被冲入
		ELSEIF RAND:2
			PRINTFORM 因为用来排泄的地方反过来被排泄
		ELSE
			PRINTFORM 膀胱成为厕所
		ENDIF
	ELSEIF NOWEX:尿道絶頂 && SELECTCOM != 3 && (NOWEX:尿道絶頂 == EX:尿道絶頂 || RAND:5 == 0)
		SELECTCASE SELECTCOM
		CASE 10
			;尿道インサート中
			IF INSERT("尿道挿入", TARGET, PLAYER)
				IF RAND:3 == 0
					PRINTFORM 被当作性处理的工具
				ELSEIF RAND:2
					PRINTFORM 把阴茎当作%CALLNAME:PLAYER%的斧头吧
				ELSE
					PRINTFORM 让阴茎从内外心情舒畅
				ENDIF
			ELSE
				PRINTFORM 阴茎被操纵着
			ENDIF
		CASE 15
			PRINTFORM 尿道被手指虐待
		CASE 83 TO 88
			IF TEQUIP:推倒
				IF TALENT:尿道狂
					PRINTFORM 从尿道涌出的快乐令%TEXTR("大脑一片空白/垂涎口水流出/疯狂地颤抖腰身")%
				ELSEIF PENIS(TARGET)
					PRINTFORM 在阴茎内部侵犯了%CALLNAME:PLAYER%
				ELSE
					PRINTFORM 用尿道做爱侵犯了%CALLNAME:PLAYER%
				ENDIF
			ELSE
				IF PENIS(TARGET)
					PRINTFORM 用激烈的动腰使侵犯阴茎
				ELSE
					PRINTFORM 用激烈的动腰使被侵犯尿道
				ENDIF
			ENDIF
		CASE 151
			PRINTFORM 敏感的粘膜被虐待
		CASEELSE
			PRINTFORM 尿道被欺负
		ENDSELECT
		;挿入ボーナス
		SIF INSERT("尿道挿入") && PENIS(PLAYER)
			BASE:PLAYER:絶頂 += ABL:尿道名器度 * 50
	ELSEIF INSERT("挿入") && PENIS(PLAYER) && RAND:3 == 0
		IF INSERT("対面座位") && RAND:2
			PRINTFORM 紧紧地拥抱%CALLNAME:PLAYER%
		ELSE
			IF INSERT("二穴挿入")
				PRINT 两边的洞
				LOCAL = MIN(ABL:Ｖ名器度, ABL:Ａ名器度)
			ELSEIF INSERT("Ｖ挿入")
				PRINT 阴道
				LOCAL = ABL:Ｖ名器度
			ELSEIF INSERT("Ａ挿入")
				PRINT 屁眼
				LOCAL = ABL:Ａ名器度
			ELSEIF INSERT("尿道挿入")
				PRINT 尿道
				LOCAL = ABL:尿道名器度
			ENDIF
			IF LOCAL >= 80 && RAND:3 == 0
				PRINT 用绝妙的力量
			ELSEIF LOCAL >= 60 && RAND:2
				PRINT 用强大的力量
			ELSEIF LOCAL >= 40
				PRINT 突然
			ENDIF
			PRINT 紧固
			BASE:PLAYER:絶頂 += LOCAL * 50
		ENDIF
	ELSEIF NOWEX:射精
		SELECTCASE SELECTCOM
		CASE 4
			PRINTFORM %CALLNAME:PLAYER%的口\@ NOWEX:射精 >= 2 ? 充分地 # \@射精
		CASE 11
			PRINTFORM %CALLNAME:PLAYER%的胸\@ NOWEX:射精 >= 2 ? 充分地 # \@射精
		CASE 12
			PRINTFORM %CALLNAME:PLAYER%的足\@ NOWEX:射精 >= 2 ? 充分地 # \@射精
		CASE 37
			PRINTFORM 在里面\@ NOWEX:射精 >= 2 ? 充分地 # \@射精
		CASEELSE
			IF INSERT("Ｖ挿入", PLAYER, TARGET)
				PRINTFORM %CALLNAME:PLAYER%的膣内\@ NOWEX:射精 >= 2 ? 大量的 # \@中出
			ELSEIF INSERT("Ａ挿入", PLAYER, TARGET)
				PRINTFORM %CALLNAME:PLAYER%的腸内的精液\@ NOWEX:射精 >= 2 ? 大量的 # \@吐出
			;道端のメロンパン加筆分
			ELSEIF NOWEX:Ｖ絶頂
				PRINTFORM 从阴茎里%TEXTR("流出/灑落")%了\@ NOWEX:射精 >= 2 ? 大量的 # \@精液
			ELSEIF NOWEX:Ａ絶頂
				PRINTFORM 从阴茎里\@ NOWEX:射精 >= 2 ? 大量的 # \@精液%TEXTR("噗嚕噗嚕/缓缓地/咕嚕咕嚕")%地流下了
			;ここまで
			ELSE
				PRINTFORM 从阴茎噴出\@ NOWEX:射精 >= 2 ? 大量的 # \@精液
			ENDIF
		ENDSELECT
	ELSEIF NOWEX:噴乳 && (NOWEX:噴乳 == EX:噴乳 || RAND:5 == 0)
		PRINT 从胸前端滴下母乳
	ELSEIF TALENT:男性 == 0
		;潮噴き
		IF COND("潮噴き可能") && RAND:3 == 0
			IF TEQUIP:导管
				PRINTFORM 从尿道中插入的管道
			ELSEIF PENIS(TARGET)
				PRINTFORM 从那里
			ELSE
				PRINTFORM 从%TEXTR("尿穴口/尿道口/小便的穴")%中
			ENDIF
			PRINTFORM %TEXTR("透明的液体/透明的潮水/熱的飛沫")%.
			;尿意ゲージを減らす
			IF MAX(NOWEX:Ｃ絶頂, NOWEX:Ｖ絶頂, NOWEX:尿道絶頂) + TALENT:絶頂失禁体質 >= 5 && BASE:尿意 >= 10000 && RAND:3
				PRINT 华丽地
				BASE:尿意 -= RAND:5000 + 3000
			ELSEIF MAX(NOWEX:Ｃ絶頂, NOWEX:Ｖ絶頂, NOWEX:尿道絶頂) + TALENT:絶頂失禁体質 >= 3 && BASE:尿意 >= 6000 && RAND:3
				PRINT 气势磅礴
				BASE:尿意 -= RAND:3000 + 2000
			ELSEIF RAND:2
				PRINT biubiu地
				BASE:尿意 -= RAND:2000 + 1000
			ELSE
				PRINT 超级可爱
				BASE:尿意 -= RAND:1000 + 500
			ENDIF
			IF RAND:2
				PRINTFORM 迸发
			ELSE
				PRINTFORM %TEXTR("噴出/噴射")%
			ENDIF
		ELSE
			IF NOWEX:絶頂数合計 >= 4 && CHECK_ECST(TARGET) >= 3 && RAND:2
				PRINT 贯穿全身的快感
			ELSEIF NOWEX:絶頂数合計 >= 3 && RAND:2
				PRINT 激烈的快感
			ELSEIF NOWEX:絶頂数合計 >= 2 && RAND:2
				PRINT 过头了的快感
			ELSEIF RAND:2
				IF COND("ながら自慰フラグ") || SELECTCOM == 3
					PRINT 快感
				ELSE
					PRINT 被给予的快感
				ENDIF
			ENDIF
			IF RAND:3 == 0 && COND("口の使用") && ORAL_PLAY() == 0
				IF RAND:2
					IF TALENT:強気 && NOWEX:絶頂数合計 == 1 && VAU_INSERT_PLAY(SELECTCOM)
						PRINT 咬紧牙関
					ELSE
						PRINT 嘴大開着
					ENDIF
				ELSE
					PRINT 口角流涎
				ENDIF
			ELSEIF RAND:2 && COND("口の使用") && ORAL_PLAY() == 0
				IF NOWEX:絶頂数合計 >= 3 && RAND:3
					PRINT 忘我得大声呻吟
				ELSEIF NOWEX:絶頂数合計 >= 2 && RAND:3
					IF TALENT:害羞
						PRINT 用手捂住了快要喊出来的嘴
					ELSE
						PRINT 高声呼喊
					ENDIF
				ELSE
					IF RAND:2 && TALENT:好色
						PRINT 露出恼人的喘息声
					ELSEIF TALENT:害羞 || RAND:3 == 0
						IF NOWEX:絶頂数合計 >= 2
							PRINT 不由得发出娇滴滴的声音
						ELSE
							PRINT 发出低调的喘息声
						ENDIF
					ELSE
						PRINT 大声呐喊
					ENDIF
				ENDIF
			ELSE
				IF RAND:3 == 0
					IF ORAL_PLAY() == 0 && RAND:2
						PRINT 激烈地摇头
					ELSE
						PRINTFORM \@ NOWEX:絶頂数合計 <= 2 ? 半途 # \@放心
					ENDIF
				ELSEIF RAND:2 || NOWEX:Ｍ絶頂
					IF TALENT:強気 && NOWEX:絶頂数合計 == 1
						PRINT 皱起眉头
					ELSEIF TALENT:害羞 && RAND:2
						PRINT 紧紧地闭上双眼
					ELSE
						PRINT 瞳孔湿润
					ENDIF
				ELSE
					IF NOWEX:Ｃ絶頂 + NOWEX:Ｖ絶頂 + NOWEX:Ａ絶頂 + NOWEX:尿道絶頂 && RAND:2 == 0
						PRINT 让肚子起伏
					ELSE
						PRINT 把背大大地向后仰
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ELSE
		PRINT 把背大大地向后仰
	ENDIF
	PRINT 的同時、
	LOCAL = 0
	IF TALENT:巨乳 && RAND:3 == 0
		IF RAND:3 == 0
			PRINT 把有张力的巨乳
		ELSEIF RAND:2
			PRINT 沉重的胸膛
		ELSE
			PRINT 丰满的胸膛
		ENDIF
		LOCAL = TALENT:巨乳
	ELSEIF RAND:2
		PRINT 全身
	ELSE
		SIF TALENT:娇小
			PRINT 小小的
		PRINT 将身体
	ENDIF

	IF LOCAL && RAND:2
		IF RAND:2
			IF INSERT("騎乗位")
				PRINT 突然
			ELSE
				PRINT 噗噜噗噜地
			ENDIF
		ELSE
			PRINT 华丽地
		ENDIF
	ELSEIF RAND:3 == 0 || U
		PRINT 一点一点地
	ELSEIF RAND:2 == 0
		PRINT 战战兢兢
	ELSE
		PRINT 抖了一下地
	ENDIF
	IF LOCAL && RAND:2
		PRINT 摇晃着
	ELSEIF RAND:3 == 0 || U
		PRINT 颤抖着
	ELSEIF RAND:2 == 0
		PRINT 令人痙攣
	ELSE
		PRINT 令人跳跃
	ENDIF
	IF RAND:2
		PRINTL 達到了絶頂。
	ELSE
		PRINTL 迎来了絶頂。
	ENDIF
ENDIF

SIF COND("性欲解消実行中") && LINECOUNT > MEMO_LINECOUNT
	WAIT

;ウェイトの発生判定その２
MEMO_LINECOUNT2 = LINECOUNT

;逆レイプで射精
IF INSERT("挿入", PLAYER, TARGET) && NOWEX:射精
	PRINTFORM %CALLNAME:PLAYER%的
	IF INSERT("Ｖ挿入", PLAYER, TARGET)
		PRINT 肉褶、
	ELSEIF INSERT("Ａ挿入", PLAYER, TARGET)
		PRINT 肛門、
	ELSEIF INSERT("尿道挿入", PLAYER, TARGET)
		PRINT 尿道的孔、
	ENDIF
	SIF ABL:欲望 >= 3 && NOWEX:射精 >= 2
		PRINT 让浮现出恍惚的神情的
	PRINTFORML %CALLNAME:TARGET%被你榨取了精液……
;絶頂
ELSEIF NOWEX:絶頂数合計
	LOCAL = 0
	IF SELECTCOM == 8 && TEQUIP:振動棒 == 0
		SIF RAND:(EXP_GAIN("舔指経験")*2 + 1) == 0
			LOCAL = 1
	ELSEIF INSERT("挿入")
		SIF RAND:(EXP_GAIN("舔指経験")*3 + 6) == 0
			LOCAL = 1
	ENDIF
	;指ちゅぱ(指挿入れorインサート)
	IF COND("口の使用") && ABL:信頼 >= 3 && TEQUIP:推倒 == 0 && COND("性欲解消実行中") == 0 && LOCAL
		;LOCAL == 0 なら口に指を入れる。それ以外なら口元に差し出す。
		LOCAL = RAND:3
		IF NOWEX:絶頂数合計 >= 4 && RAND:4 == 0
			PRINTFORM 沉浸在快感中
		ELSEIF NOWEX:絶頂数合計 >= 2 && RAND:3 == 0
			PRINTFORM 恍惚
		ELSE
			PRINTFORM %TEXTR("似乎很满足的/一息尚存的")%
		ENDIF
		PRINTFORM %CALLNAME:TARGET%的%TEXTR("口中/嘴边")%
		IF CHECK_STAIN(STAIN:PLAYER:手, "精液")
			PRINT 被精液涂上
		ELSEIF CHECK_STAIN(STAIN:PLAYER:手, "Ｖ")
			PRINT 被爱液弄脏了
		ELSEIF STAIN:PLAYER:手
			PRINT 被体液弄脏了
		ENDIF
		PRINTFORM %TEXTR("伸開手指/将食指")%
		IF LOCAL == 0
			IF RAND:2
				PRINT 含弄
				LOCALS = る
			ELSE
				PRINT 挤入
				LOCALS = む
			ENDIF
		ELSE
			IF RAND:2
				PRINT 寄出
				LOCALS = す
			ELSE
				PRINT 靠近
				LOCALS = る
			ENDIF
		ENDIF
		IF LOCAL == 0 && RAND:3 == 0
			PRINTFORM %TEXT_CONJ(LOCALS)%挠弄粘膜
		ELSEIF STAIN:PLAYER:手 && ABL:奉仕精神 >= 3
			PRINTFORM 为了把%TEXT_CONJ(LOCALS)%变干净而\@ COND("目上") ? 请求 # 催促 \@了就、
		ELSE
			PRINTFORM %LOCALS%与
		ENDIF
		IF TALENT:内気 + TALENT:清楚 && RANDIF(2, TALENT:好色, TALENT:開朗, TALENT:害羞, TALENT:献身)
			PRINT 胆怯、怯懦
		ELSEIF TALENT:好色 && RANDIF(2, TALENT:開朗, TALENT:害羞, TALENT:献身)
			PRINT 好像很開心
		ELSEIF TALENT:開朗 && RANDIF(2, TALENT:害羞, TALENT:献身)
			IF LOCAL == 0
				PRINT 突然,一下子
				LOCAL = 1
			ELSE
				PRINT 作为回礼
			ENDIF
		ELSEIF TALENT:献身 && RANDIF(2, TALENT:害羞)
			PRINT 温柔地
		ELSEIF TALENT:害羞 && RAND:2
			PRINT 害羞地
		ELSEIF MARK:屈服刻印 >= 2 && RAND:3 == 0
			;フェラ経験豊富
			IF LOCAL == 0 && ABL:欲望 >= 3 && INSERT("挿入") && EXP:口交経験 >= 50 && PENIS(PLAYER) && RAND:3
				PRINT 兴奋的样子
			ELSE
				PRINT 习惯了的样子
			ENDIF
		ELSEIF RAND:2
			PRINT 老实地
		ELSE
			IF TALENT:献身
				PRINT 温柔地
			ELSEIF TALENT:害羞
				PRINT 害羞地
			ELSE
				PRINT 无精打采地
			ENDIF
		ENDIF
		;指を咥える
		IF LOCAL
			PRINT 含在嘴里
			IF COND("視力") && RAND:3 == 0
				PRINTFORM 把%CALLNAME:PLAYER%
				IF MARK:屈服刻印 >= 2
					PRINT 一边凝视着
				ELSE
					PRINT 一边窥视着
				ENDIF
			ENDIF
		ENDIF
		;対象の口⇔調教者の指の汚れが移動
		CALL EQ_STAIN, TARGET, "Ｍ", PLAYER, "手"
		;お掃除ルート
		IF STAIN:PLAYER:手 && ABL:奉仕精神 >= 3
			IF CHECK_STAIN(STAIN:PLAYER:手, "精液")
				PRINTL 将附着的精液漂亮地舔去……
				EXP:精液経験 += 1
				PRINTFORML 精液経験+1
			ELSE
				PRINTL 啪嗒啪嗒地舔着清理了一下……
			ENDIF
			STAIN:PLAYER:手 = 0
		ELSE
			PRINTFORML %TEXTR("甜蜜地品味着/開始吮吸起来/用舌头舔了起来")%…
		ENDIF
		;指ちゅぱ経験+1
		EXP:舔指経験 += 1
	ELSEIF NOWEX:絶頂数合計 && (COND("性欲解消実行中") || NOWEX:絶頂数合計 >= 2 || BASE:性欲 >= MAXBASE:性欲)
		IF COND("性欲解消実行中") && TALENT:絶頂失禁体質 && PENIS(TARGET) == 0 && BASE:尿意 >= MAX(9000 - NOWEX:絶頂数合計*1000, 5000)
			IF EXP:聖水経験 && SELECTCOM == TNUM("舐陰") && CHECK("口内放尿", MASTER)
				PRINTFORML 那个瞬间、%CALLNAME:MASTER%……
				CALL PRINT_SELECT, "阴部の前で口を開いて備える/出口て被弾を避ける"
				SIF RESULT == 0
					CALL SETFLAG, "飲尿", PLAYER
			ENDIF
			IF COND("飲尿フラグ", PLAYER)
				PRINTFORM %CALLNAME:TARGET%%TEXTR("満足げな表情で/快楽に蕩けた様子で")%%CALLNAME:MASTER%
				PRINTFORM %TEXTR("の口にオシッコを遠慮なく排泄し/の口にジョロジョロとおしっこを御馳走し/の開かれた口めがけて気持ちよさそうにオシッコを垂れ流し")%的同时、
			ELSE
				PRINTFORM %CALLNAME:TARGET%%TEXTR("絶頂のはずみ/満足げな表情/快楽に蕩けた様子")%で
				PRINTFORM %TEXTR("哗哗地漏出小便/小便失禁/舒服地小便垂流")%的同时、
			ENDIF
			NOWEX:絶頂漏尿 = NOWEX:絶頂数合計
		ELSEIF COND("指の使用") && TALENT:男性 == 0 && RAND:3 == 0
			PRINTFORM %CALLNAME:TARGET%
			IF RAND:2
				PRINT 张開脸
				IF EX:絶頂数合計 >= MAXBASE:性欲 && RAND:2
					PRINT 在多重的绝顶
					SIF TALENT:接受快感
						PRINT 不检点的
				ELSE
					PRINT 用快感
				ENDIF
				IF NOWEX:絶頂数合計 >= 3 && RAND:2
					PRINT 心神荡漾、
				ELSE
					PRINT 放松、
				ENDIF
			ELSE
				IF TALENT:恋慕 && RAND:2
					PRINT 是幸福的表情。
				ELSEIF NOWEX:絶頂数合計 >= 2 && RAND:2
					PRINT 是出神的表情、
				ELSE
					PRINT 是心情舒畅的表情。
				ENDIF
			ENDIF
			IF COND("口淫中")
				PRINTFORM 连动舌头的事也忘记
			ELSEIF PENIS(TARGET) && RANDIF(3, TALENT:貧乳 == 0, NOWEX:Ｖ絶頂, NOWEX:Ｃ絶頂 +  NOWEX:尿道絶頂)
				IF ABL:精液中毒 >= 3 && NOWEX:射精 && COND("口の使用") && INSERT("屈曲位") && RAND:2
					PRINT 大张着嘴接受着自己的精液
					EXP:精液経験 += 1
					STAIN:Ｍ = SET_STAIN(STAIN:Ｍ, "精液")
				ELSEIF RAND:2
					PRINT 一边玩弄着阴茎
				ELSE
					PRINT 慢慢地一边处理阴茎
				ENDIF
			ELSEIF NOWEX:Ｃ絶頂 + NOWEX:Ｖ絶頂 + NOWEX:尿道絶頂 && RANDIF(3, TALENT:貧乳 == 0, NOWEX:Ｖ絶頂)
				;顔面騎乗
				IF TEQUIP:推倒 && TEQUIP:面部騎乗
					IF ABL:施虐属性 >= 3 && RANDIF(3, ABL:自慰中毒 >= 3)
						IF RAND:2
							PRINTFORM 让%CALLNAME:PLAYER%用舌头一边打扫
						ELSE
							PRINTFORM 让%CALLNAME:PLAYER%用舌头做后戏
						ENDIF
					ELSEIF ABL:自慰中毒 >= 3 && RAND:3 == 0
						PRINTFORM 在%CALLNAME:PLAYER%的脸上擦着舒服的地方
					ELSEIF TALENT:好色 && RAND:3 == 0
						PRINTFORM 在%CALLNAME:PLAYER%中展现闪耀的那个
					ELSEIF RAND:2
						PRINTFORM 在%CALLNAME:PLAYER%的脸上大步张開
					ELSE
						PRINTFORM 就这样跨过%CALLNAME:PLAYER%的脸
					ENDIF
				ELSE
					IF PENIS(TARGET) && RAND:2
						PRINTFORM 陰莖
					ELSE
						PRINTFORM 胯股之间
					ENDIF
					IF ABL:自慰中毒 && RAND:2
						IF TALENT:好色 + TALENT:淫乱 && RAND:2
							PRINT 一边展示着玩弄
						ELSE
							PRINT 一边玩弄
						ENDIF
					ELSE
						PRINT 一边抚摸
					ENDIF
				ENDIF
			ELSEIF NOWEX:Ｖ絶頂 && RANDIF(3, TALENT:貧乳 == 0)
				IF RAND:2 && TALENT:恋慕
					PRINT 抚摸着子宫附近
				ELSE
					PRINT 抚摸着肚子
				ENDIF
			ELSEIF TALENT:貧乳 == 0 && RAND:3 == 0
				PRINT 一边轻柔地揉着胸部
			ELSEIF MARK:淫紋 >= 2 && RAND:2
				PRINTFORM 撫摸着%TEXTR("刻在下腹部的/被刻在子宮的某处的")%紋章
			ELSEIF RAND:2
				PRINT 一边咬着手指
			ELSE
				PRINT 抚摸着身体
			ENDIF
		ELSEIF RAND:2
			PRINTFORM %CALLNAME:TARGET%
			IF INSERT("挿入") && COND("脚の使用") && RAND:3 == 0
				IF TEQUIP:推倒
					PRINTFORM 让%CALLNAME:PLAYER%将身体托付给她、
				ELSE
					PRINTFORM 散漫地開着大步、
				ENDIF
			ELSEIF RAND:2
				PRINT 是断断续地一边反复绝顶、
			ELSE
				PRINT 时不时地一边使之跳跃颤抖和身体、
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:TARGET%
			IF INSERT("挿入") && RAND:3 == 0
				IF COND("脚の使用") && RAND:2
					PRINT 缓慢地移动着腰、
				ELSEIF TALENT:男性
					PRINT 液体从肉褶上滴下、
				ELSE
					PRINT 从肉褶上滴下粘着的液体、像喘息一样地阴唇一边晃动、
				ENDIF
			ELSEIF NOWEX:絶頂数合計 >= 3 && RAND:3 == 0
				IF RAND:2
					PRINT 是兴奋不已的样子、一边大喘气、
				ELSE
					PRINT 一边喘着粗暴的气息、
				ENDIF
			ELSEIF RAND:2
				PRINT 用深呼吸调整呼吸、
			ELSE
				PRINT 一边吐着满足的甜蜜呼吸、
			ENDIF
		ENDIF
		;本来はフェラチオ希望
		IF COND("性欲解消実行中") && STR:PLAY要望 == "口交"
			PRINTFORM %TEXTR("嘴角感触着/脸頬擦付着")%
			SELECTCASE NAMES("性処理")
			CASE "Ｖ性交"
				PRINTFORM %TEXTR("中出性交时的污渍残留着的/性交直後散发着强烈气味的")%
			CASE "Ａ性交"
				PRINTFORM %TEXTR("生肛門性交时的污渍残留着的/肛門性交直後散发着强烈味道的")%
			CASE "尿道性交"
				PRINTFORM %TEXTR("尿道性交时的污渍残留着的/散发着强烈氨水气味的")%
			ENDSELECT
			PRINTFORML 阴茎%TEXTR("用舌头缠上/吸吮着")%按照%TEXTR("最初的计划/之前预定好那样")%開始%TEXTR("试图用精液潤喉/祈求精液/用口搾取精液")%…
		ELSEIF RAND:3 == 0
			PRINTFORML \@ RAND:2 ? 筋疲力尽 # くたっ \@脱力地说……
		ELSE
			PRINTFORML \@ RAND:2 ? 快感 # 絶頂 \@的余韻中\@ RAND:2 ? 浸 # 酔 \@在…
		ENDIF
	ENDIF
ENDIF

IF COND("性欲解消実行中")
	SIF LINECOUNT > MEMO_LINECOUNT2
		WAIT
	RETURN 1
ENDIF

IF NOWEX:絶頂数合計 + NOWEX:射精 >= RAND:3 + 1 && STATE("バインド：Ｐ", PLAYER)
	PRINTFORML ……由于%CALLNAME:TARGET%的绝顶、阴茎受到的拘束稍微放松了！
	CALL SETFLAG, "Ｐバインド弱体フラグ", PLAYER
ENDIF

;緊張緩和
IF NOWEX:絶頂数合計
	IF TEQUIP:弛緩 == 0 && EX:絶頂数合計
		TEQUIP:弛緩 = 1
		CALL PRINT_STRL, @"絶頂令%CALLNAME:TARGET%的身体的紧绷的状态得到了缓解、变成了_黄色_[緊張緩和]_状態！"
		CALL PRINT_STRL, "（这意味着挿入行為造成的痛苦_黄色_弱_減少了）"
	ELSEIF TEQUIP:弛緩 <= 1 && EX:絶頂数合計 >= 5
		TEQUIP:弛緩 = 2
		CALL PRINT_STRL, @"多重絶頂令%CALLNAME:TARGET%变成了_黄色_[弛緩]_状態！"
		CALL PRINT_STRL, "（这意味着挿入行為造成的痛苦_黄色_強_減少了）"
	ELSEIF TEQUIP:弛緩 <= 2 && EX:絶頂数合計 >= 10
		TEQUIP:弛緩 = 3
		CALL PRINT_STRL, @"得到预想中快楽令%CALLNAME:TARGET%达成了_爱心粉紅_[陶酔]_状態！"
		CALL PRINT_STRL, "（这意味着挿入行為造成的痛苦_黄色_极大_減少了、抵抗也_爱心粉紅_减少了_）"
		;生挿入許可になった？
		IF COND("特別に生挿入許可")
			IF TCVAR:Ｖ避孕套挿入許可
				CALL PRINT_STRL, @"也许、可以被允许_黄色_\@ COND("ワンモア生挿入実行済") ? 更进一步的 # 这次没让做的 \@无套挿入\@ COND("无崩しでＡ挿入可") ? 屁穴 # \@吧……？"
			ELSE
				CALL PRINT_STRL, @"也许、_黄色_就算无套生挿入\@ COND("无崩しでＡ挿入可") ? 用屁穴来做 # \@也可以_吧……？"
			ENDIF
		ELSEIF COND("无崩しでＡ挿入可")
			CALL PRINT_STRL, "也许、_黄色_能做肛交…………？"
		ENDIF
	ENDIF
ENDIF

;絶頂おもらし…利尿剤が無いと、まずお漏らしはしない。絶頂失禁体質があると、しやすくなる。
VARSET LOCAL
LOCAL = MAX(RAND:(CALCF("漏尿", TARGET) + 1) - 2, 0)
SIF CALCF("漏尿", TARGET) >= 8
	LOCAL = CALCF("漏尿", TARGET)

SIF LOCAL > 0
	CALL BLANKLINE

IF LOCAL >= 4 && ABL:欲望 >= 4 && RAND:2
	PRINTFORML %CALLNAME:TARGET%一边使身体痉挛一边止住不住\@ EXP_GAIN("膀胱精液経験") ? 混有精液的 # \@尿垂流下来了
	PRINTFORML 看来排尿行为本身就是一种快乐……
;アソコが露出していない
ELSEIF LOCAL >= 1 && !CHECK_CLO("Ｖ露出")
	PRINTFORM %CALLNAME:TARGET%的快感之大
	;アウター(ブルマとか)をはいている+ずらしていない+股間が露出していない
	IF CHECK_CLO("外套露出")
		PRINTFORM 穿著%OUTER(TARGET, "略")%
	;ボディスーツ+股間をずらしていない+ボトムを脱いでない+股間が露出していない
	ELSEIF CHECK_CLO("贴身制服露出")
		PRINTFORM 穿着%BODYS(TARGET, "略")%
	;パンスト+破ってない
	ELSEIF CHECK_CLO("连褲襪露出")
		PRINTFORM 穿著%OUTER(TARGET, "略")%
	;パンツ+パンツをずらしていない+パンツを脱いでない+股間が露出していない
	ELSEIF CHECK_CLO("胖次露出")
		PRINTFORM 穿著%PANTIES(TARGET, "略")%
	ENDIF
	PRINTL 漏出来了
	SIF !CHECK_CLO("连褲襪露出")
		PRINTFORML 从布料边缘溢出\@ EXP_GAIN("膀胱精液経験") ? 混有精液的 # \@尿、从胯股之间開始下面变得厉害的状态……
ELSEIF LOCAL >= 1 && TEQUIP:面部騎乗 && TALENT:男性 == 0
	PRINTFORM %CALLNAME:TARGET%
	IF ABL:施虐属性 >= 5
		IF NOWEX:絶頂数合計 >= 2 && RAND:2
			PRINT 快乐地颤抖着、
		ELSEIF TEQUIP:面部騎乗 == 1
			PRINT 一边浮现出嗜虐性的笑容、
		ENDIF
	ELSEIF NOWEX:絶頂数合計 >= 2 && RAND:2
		PRINT 过于快乐、
	ENDIF
	IF TEQUIP:面部騎乗 == 1 && ABL:施虐属性 >= 5
		PRINTFORM 直接在%CALLNAME:PLAYER%的口中
	ELSE
		PRINTFORM 对着%CALLNAME:PLAYER%的脸坐着的姿势
	ENDIF
	IF ABL:施虐属性 >= 5
		PRINTL 開始小便了……
	ELSE
		PRINTL 露出了……
	ENDIF
ELSEIF LOCAL >= 3
	IF NOWEX:絶頂数合計 >= 8
		PRINT 痙攣着
	ELSEIF NOWEX:絶頂数合計 >= 4
		PRINT 筋疲力尽
	ENDIF
	PRINTFORML %CALLNAME:TARGET%的尿道口像喷泉一样喷出了尿……
ELSEIF LOCAL >= 2
	IF GETBIT(TEQUIP:拘束, 21)
		PRINT 脚被拘束的大開的姿势
	ELSEIF NOWEX:絶頂数合計 >= 4
		PRINT 筋疲力尽
	ENDIF
	IF TALENT:男性 || RAND:2 == 0
		PRINTFORML 从%CALLNAME:TARGET%的尿道口滴下水流、积出了水洼…
	ELSE
		PRINTFORML 从%CALLNAME:TARGET%的尿孔中水流汹涌而出、形成了弧线……
	ENDIF
ELSEIF LOCAL >= 1
	IF RAND:3 == 0
		PRINTFORML %CALLNAME:TARGET%因过于快乐而失禁了……
	ELSEIF RAND:2
		PRINTFORML %CALLNAME:TARGET%在绝顶颤抖、无法抑制尿意就失禁了……
	ELSE
		PRINTFORML 或许是绝顶的缘故吧、从因快乐而松弛的小洞里尿液一下子漏出来了……
	ENDIF
ELSEIF RAND:3 == 0
	IF PENIS(TARGET) == 0 && CALCF("漏尿", TARGET) >= 5 && EXP_GAIN("放尿経験") + EXP_GAIN("漏尿経験") == 0
		CALL BLANKLINE
		PRINTFORML %CALLNAME:TARGET%为了忍受什么而使内腿紧张。
		PRINTFORML 看来是忍着尿漏了……
	ENDIF
	LOCAL = 0
ENDIF

SIF LOCAL
	CALL EVENT_OMORASHI, LOCAL

;テキストを読むためのウエイト（表示があるときのみ）
SIF LINECOUNT > MEMO_LINECOUNT
	WAIT

IF COND("液体浣腸") + COND("精液浣腸") && COND("肛門栓") == 0
	;限界値を越えているかどうか
	IF COND("排泄：限界")
		CALL KOJO_EVENT, "塞子没有浣腸イベント"
		;アナルがふさがれていれば漏れない
		IF INSERT("Ａ挿入") == 0
			PRINTFORML 
			PRINTFORM %CALLNAME:TARGET%
			SELECTCASE SELECTCOM
			CASE 14
				PRINTFORM 面向%CALLNAME:PLAYER%推開肛門的姿势的那样、不能忍耐了
			CASE 33
				PRINTFORM 被拖出堵住屁股洞的东西
			CASE 142, 143
				PRINTFORM 从屁股的洞里拔出手臂的弹性
			CASEELSE
				IF NOWEX:絶頂数合計
					PRINTFORM 无法忍受绝顶的快乐
				ELSE
					IF CUP:快Ａ >= 1000
						PRINTFORM 忍受不了肛門的刺激
					ELSE
						PRINTFORM 忍不住便意
					ENDIF
				ENDIF
			ENDSELECT
			SELECTCASE BASE:排泄物
			CASE 0
			;うんちなし
				;混合
				IF COND("精液浣腸") && COND("温泉浣腸") && CHECK_STAIN(STAIN:TARGET:Ａ, "Ｐ恥垢") 
					PRINTFORM %TEXTR("混着恥垢和尿的鸡尾酒/白浊の漂浮的尿/混着精液的黄金水/精液与尿混合的泥")%
				;純粋精液+恥垢
				ELSEIF COND("精液浣腸") && CHECK_STAIN(STAIN:TARGET:Ａ, "Ｐ恥垢") 
					PRINTFORM %TEXTR("精液与恥垢混合的泥/混有精液和恥垢的粪便/精子和恥垢的残渣/肠道里的浑浊的精液、")%
				;純粋精液+尿
				ELSEIF COND("精液浣腸") && COND("温泉浣腸")
					PRINTFORM %TEXTR("精液中混着尿的鸡尾酒/白浊の漂浮的尿/他人的尿与精液的混合物")%
				;純粋小便浣腸
				ELSEIF COND("温泉浣腸")
					PRINTFORM %TEXTR("被强迫注入的别人的小便/混着粪臭的黄金水/直腸内留下的他人的黄金水")%
				;純粋精液浣腸
				ELSEIF COND("精液浣腸")
					PRINTFORM %TEXTR("混有精液的粪便/腸液混着白濁液的鸡尾酒/腸液混着粪便的精液/滑溜溜的粪便精液")%
				ELSE
					PRINTFORM 灌肠液
				ENDIF
				PRINTFORML 打開\@ COND("大量浣腸") ? 盛大的 # 排泄\@了……
			;ここからうんちがまざる
			CASE IS <= 4
;				IF COND("精液浣腸") && COND("温泉浣腸")
;					PRINTFORM 精液で彩られた
;				ELSEIF COND("精液浣腸")
;					PRINTFORM 精液で彩られた
;				ELSEIF COND("温泉浣腸")
;					PRINTFORM 小便混じりの
;				ELSE
;					PRINTFORM 薬液で柔らかくされた
;				ENDIF
				IF COND("精液浣腸") && COND("温泉浣腸") && CHECK_STAIN(STAIN:TARGET:Ａ, "Ｐ恥垢") 
					PRINTFORM %TEXTR("恥垢与尿撹拌的精液大便的残渣/尿与精液混着恥垢的汚物/尿混着精液的宿便/精液与尿混在一起的大便")%
				;精液+恥垢
				ELSEIF COND("精液浣腸") && CHECK_STAIN(STAIN:TARGET:Ａ, "Ｐ恥垢") 
					PRINTFORM %TEXTR("恥垢混着精液的大便/白色与茶色混合的粪便/精液附着的大便/精液与恥垢混合的茶色残渣")%
				;精液+尿
				ELSEIF COND("精液浣腸") && COND("温泉浣腸")
					PRINTFORM %TEXTR("尿与精液溶在一起的軟便/精液与尿和大便的混合物/精液与尿的飛沫压不住的大便")%
				;小便浣腸
				ELSEIF COND("温泉浣腸")
					PRINTFORM %TEXTR("他人的小便混合的軟便/粪便和黄金水的鸡尾酒/黄金水混杂的茶色的残渣")%
				;精液浣腸
				ELSEIF COND("精液浣腸")
					PRINTFORM %TEXTR("混杂精液的粘糊糊的大便/白色与茶色的粪便/精液混合的大便/精液与茶色的粪渣/白酱一般的大便")%
				ELSE
					PRINTFORM 灌肠液注入后接着大便
				ENDIF
				PRINTFORMW 咔嚓咔嚓\@ COND("大量浣腸") ? 盛大的 # \@開始排出了……
			CASE IS <= 6
				PRINTFORMW 肠内积存了几天的污物\@ COND("精液浣腸") ? 与注入的精液一起 # \@气势汹汹地拉出来了……
			CASEELSE
				PRINTFORMW \@ COND("精液浣腸") ? 精液塗抹的 # \@\@ RAND:2 ? 排泄物 # 腸内的汚物 \@を盛大地排泄出来了
				PRINTFORMW 也许是因为便秘的原因、最開始很软的东西以很厉害的气势飞了出来之後、\@ RAND:2 ? 看上去很硬的 # 连成一条的 \@像粗巧克力棒一样的污物用了一些时间慢慢地推開肛門出来了…
			ENDSELECT

			CALL SETFLAG, "排泄"
			
			;排泄した時のSOURCEを加算
			CALL SOURCE_COM136, 100

			CALL KOJO_EVENT, "塞子没有浣腸イベント"
		ELSEIF INSERT("Ａ挿入") 
			PRINTFORML 肚子咕噜咕噜地响……%CALLNAME:TARGET%如果没有塞子、马上就会泄漏的。
		ENDIF
	ELSEIF COND("液体浣腸")
		PRINTFORML 
		SELECTCASE COND("液体浣腸")
		CASE 4
			IF ABL:受虐属性 >= 5
				PRINTFORML %CALLNAME:TARGET%以苦闷和快乐而放松的表情享受着排泄感……
			ELSE
				PRINTFORML %CALLNAME:TARGET%忍受着似乎令人绝望的量的灌肠液……
			ENDIF
		CASE 3
			PRINTFORML %CALLNAME:TARGET%是大量的\@ COND("温泉浣腸") ? 小便注入的排泄感 # 灌肠液 \@好像勉强忍住了……
		CASE 2
			PRINTFORML %CALLNAME:TARGET%拼命忍受着必死的排泄感…
		CASE 1
			IF ABL:受虐属性 >= 5
				PRINTFORML %CALLNAME:TARGET%一边忍受着排泄感、一边时不时向你投去不满足的目光……
			ELSE
				PRINTFORML %CALLNAME:TARGET%为了不失禁、忍受着便意……
			ENDIF
		ENDSELECT
		CALL KOJO_EVENT, "塞子没有浣腸イベント"
	ENDIF
ENDIF


@EVENT_OMORASHI, ARG
IF ARG >= 2
	PRINTFORML 漏尿経験＋2
	EXP:漏尿経験 += 2
	BASE:尿意 = 0
ELSE
	PRINTFORML 漏尿経験＋1
	EXP:漏尿経験 += 1
	BASE:尿意 = MAX(BASE:尿意 - 20000, 0)
ENDIF
TEQUIP:利尿剤 = 0

;放尿のソース獲得
CALL SOURCE_COM144, 80

;絶頂放尿
NOWEX:絶頂漏尿 = ARG
EX:絶頂漏尿 += ARG
;尿の汚れ発生
IF TALENT:男性 == 0
	CALL ADD_STAIN, TARGET, "Ｖ", "尿"
	STAIN:Ｖ = SET_STAIN(STAIN:Ｖ, "尿")
	;聖水プレイ
	SIF TEQUIP:面部騎乗
		STAIN:PLAYER:Ｍ = SET_STAIN(STAIN:PLAYER:Ｍ, "尿")
ENDIF

;-------------------------------------------------
;ラグジャラスヒットと恍惚判定
;ARGSは今のところ"コマンド"と"精液"がある
;PLAYERかTARGETかどうかで別物だけどここで一括
;-------------------------------------------------
;事前チェックと実行が分かれているのは"コマンド"のみ
;-------------------------------------------------
@CHECK_LUXURIOUS_HIT, ARGS, ARG
#DIM LCOUNT
#DIM CHOICE

SIF ARG == 0 && TARGET
	ARG = TARGET

SIF NOWEX:ARG:Luxurious
	RETURN 0

TCVAR:ARG:Luxurious = 0

SELECTCASE ARGS
CASE "指令"
	SELECTCASE ARG
	CASE PLAYER
		;今のところプレイヤーで判定が起きるのは主人だけ
		SIF PLAYER != MASTER
			RETURN 0
		SIF CALCF("弱点突かれ", SELECTCOM) == 0
			RETURN 0

		TCVAR:PLAYER:Luxurious = TCVAR:PLAYER:絶頂増加 + CALCF("弱点突かれ", SELECTCOM)*1000

		;母乳を飲んでいると恍惚度合い上昇
		TCVAR:PLAYER:Luxurious = MULTIPLY(TCVAR:PLAYER:Luxurious, 100 + EXP_GAIN("飲乳経験", PLAYER)*10)

		;性癖合致度
		SIF CALCF("属性突かれ", PLAYER)
			TCVAR:PLAYER:Luxurious += (1000 + TCVAR:PLAYER:Luxurious/5)*CALCF("属性突かれ", PLAYER)

		;相思相愛
		IF PLAYER == MASTER
			SIF TALENT:相思相愛
				TCVAR:PLAYER:Luxurious += TCVAR:PLAYER:Luxurious*30/100 + 2000
		ENDIF

		SIF TCVAR:PLAYER:Luxurious < 1000 || TCVAR:PLAYER:Luxurious + BASE:PLAYER:絶頂 < MAX(MAXBASE:PLAYER:絶頂/4, 5000) || RAND:3
			RETURN 0

		NOWEX:PLAYER:Luxurious = 1

	CASE TARGET
		;弱点等を用意してから設定する（PALAM:連続絶頂COMBO数でも判定）
		;まずは弱点を突かれたかで判定
		;GET_PALAMLV(ARG)の値は、順に1:100,2:500,3:3000,4:1万,5:3万,6:6万,7:10万,8:15万,9:25万

		;快感での汎用判定
		SIF TALENT:Ｃ性向 || TALENT:淫核
			TCVAR:Luxurious = MAX(GET_PALAMLV(CUP:快Ｃ), TCVAR:Luxurious)
		SIF TALENT:Ｖ性向 || TALENT:淫壷
			TCVAR:Luxurious = MAX(GET_PALAMLV(CUP:快Ｖ), TCVAR:Luxurious)
		SIF TALENT:Ａ性向 || TALENT:尻穴狂い
			TCVAR:Luxurious = MAX(GET_PALAMLV(CUP:快Ａ), TCVAR:Luxurious)
		SIF TALENT:Ｂ性向 || TALENT:淫乳
			TCVAR:Luxurious = MAX(GET_PALAMLV(CUP:快Ｂ), TCVAR:Luxurious)
		SIF TALENT:Ｍ性向 || TALENT:蕩唇
			TCVAR:Luxurious = MAX(GET_PALAMLV(CUP:快Ｍ), TCVAR:Luxurious)

		SIF TCVAR:Luxurious <= RAND:10 || RAND:3
			RETURN 0

		NOWEX:Luxurious = 1
	ENDSELECT
ENDSELECT


@LUXURIOUS_HIT, ARGS, ARG
#DIM LCOUNT
#DIM CHOICE

SIF ARG == 0 && TARGET
	ARG = TARGET

SELECTCASE ARGS
CASE "指令"
	SIF NOWEX:ARG:Luxurious == 0
		RETURN 0

	EX:ARG:Luxurious += NOWEX:ARG:Luxurious

	SELECTCASE ARG
	CASE PLAYER
		BASE:PLAYER:絶頂 += TCVAR:PLAYER:Luxurious
		TCVAR:PLAYER:絶頂増加 += TCVAR:PLAYER:Luxurious

		SETCOLOR DEF_COLOR("爱心粉紅")
		PRINTFORML ＜官能一击！（%CALLNAME:PLAYER%）＞
		PRINTFORML %CALLNAME:PLAYER%让%CALLNAME:TARGET%意乱情迷了！
		RESETCOLOR

		IF TCVAR:PLAYER:Luxurious >= MAX(MAXBASE:PLAYER:絶頂/3, 5000) && RAND:4 == 0
			PRINTFORML %CALLNAME:PLAYER%恍惚了……
			IF TEQUIP:PLAYER:理性主義
				FONTSTYLE 1
				PRINTFORMW 弹簧（原文：パチン）！
				FONTSTYLE 0
				PRINTFORMW 出乎意料之外、%CALLNAME:(ASSI:1)%发出巨大的声音使自己重新恢复理智！
			ELSE
				CHOICE = 0
				IF CONFIG("恍惚耐性") && BASE:PLAYER:精力 >= 2
					IF CONFIG("恍惚耐性") == 2
						CHOICE = 1
					ELSE
						CALL PRINT_SELECT, "恍惚感に任憑擺佈/抵抗！（精力-１）"
						CHOICE = RESULT
					ENDIF
				ENDIF
				IF CHOICE
					BASE:PLAYER:精力 -= 1
					PRINTFORML 精力-１(%CALLNAME:PLAYER%)
					PRINTFORMW %CALLNAME:PLAYER%竭尽全力找回了自我！
				ELSE
					TEQUIP:推倒 = 1
					CALL ADD_STATE, PLAYER, "恍惚"
					;手加減中なら、解除
					IF TEQUIP:手下留情
						TEQUIP:手下留情 = 0
						PRINTFORML 主导权被%CALLNAME:TARGET%夺走、%CALLNAME:PLAYER%不能从其手下逃脱…！
					ENDIF
					SIF CONFIG("恍惚耐性") == 0 || BASE:PLAYER:精力 < 2
						FORCEWAIT

					SIF COND("疲労") == 0
						CALL KOJO_COUNTER, "恍惚"
				ENDIF
			ENDIF
		ENDIF
	CASE TARGET
		SETCOLOR DEF_COLOR("爱心粉紅")
		PRINTFORML ＜官能一击！＞
		PRINTFORML %CALLNAME:PLAYER%突入了%CALLNAME:TARGET%的弱点！
		RESETCOLOR

		;IF TCVAR:ラグジャラスヒット*10 + PALAM:連続絶頂COMBO数*5 > RAND:100 && RAND:2 == 0
		;	PRINTFORML %CALLNAME:TARGET%は恍惚としている……
		;	CALL ADD_STATE, TARGET, "恍惚"
		;	FORCEWAIT
		;ENDIF
	ENDSELECT
CASE "精液"
	;イくのをこらえた状態で無いor既に絶頂済み
	SIF NOWEX:絶頂数合計 || NOWEX:绝顶忍耐 == 0
		RETURN 0
	;精液に弱くないなら、ここで終了
	SIF TALENT:精液弱点 == 0
		RETURN 0

	IF SHOOT("口")
		SIF COND("精飲弱点") == 0
			RETURN 0
		SIF EXP_GAIN("精飲経験") && RAND:3
			RETURN 0
		TIMES CUP:快Ｍ, 2.00
	ELSEIF SHOOT("Ｖ中出し")
		SIF COND("膣内射精弱点") == 0
			RETURN 0
		SIF EXP_GAIN("膣内精液経験") && RAND:3
			RETURN 0
		TIMES CUP:快Ｖ, 2.00
	ELSEIF SHOOT("Ａ中出し")
		SIF COND("肛内射精弱点") == 0
			RETURN 0
		SIF EXP_GAIN("肛内精液経験") && RAND:3
			RETURN 0
		TIMES CUP:快Ａ, 2.00
	ELSEIF SHOOT("乳头奸")
		SIF COND("乳内射精弱点") == 0
			RETURN 0
		SIF EXP_GAIN("乳内精液経験") && RAND:3
			RETURN 0
		TIMES CUP:快Ｂ, 2.00
	ELSEIF SHOOT("尿道中出し")
		SIF COND("尿道内射精弱点") == 0
			RETURN 0
		SIF EXP_GAIN("膀胱精液経験") && RAND:3
			RETURN 0
		NOWEX:尿道絶頂 = 1
		EX:尿道絶頂 += 1

		TIMES CUP:Ｖ潤, 2.00
		TIMES CUP:欲情, 2.00
		TIMES CUP:屈服, 2.00
		TIMES CUP:恥情, 2.00
	ENDIF

	NOWEX:精液絶頂 = 1
	EX:精液絶頂 += 1
	NOWEX:Luxurious = 1
	EX:Luxurious += 1
	NOWEX:绝顶忍耐 = 0
	EX:绝顶忍耐 = 0

	CALL ORGASM_ADD_A
	CALL ORGASM_ADD_C

	SETCOLOR DEF_COLOR("爱心粉紅")
	PRINTFORML ＜官能一击！＞
	RESETCOLOR

	IF SHOOT("口")
		IF COND("絶頂", PLAYER) >= 2 && RAND:2
			PRINTFORM 被大量灌注了精液
		ELSE
			IF COND("絶頂", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量的/溢出来的/非常多的")%
			ELSE
				PRINTFORM %TEXTR("勢头正旺的/熱呼呼的/濃厚的")%
			ENDIF
			IF SELECTCOM == 140
				PRINTFORM 精液%TEXTR("从食道冲下去了/被浇在喉咙深处")%
			ELSE
				PRINTFORM 精液%TEXTR("在口内射的満満的/从口内流进去了/被浇在喉咙深处")%
			ENDIF
		ENDIF
		PRINTFORM %CALLNAME:TARGET%、
		PRINTFORM %TEXTR("过于兴奋了/兴奋不堪了/興奮的过头了/浑身都在颤抖了/从鼻子里呼出热烈的喘息/无法忍受了")%
		PRINTFORMW 绝顶了！
	ELSEIF SHOOT("Ｖ中出し")
		IF BASE:胎内精子量 < 100 && NOWEX:Ｖ中出し == EX:Ｖ中出し && RAND:2
			PRINTFORM 饥渴的子宫被款待了一顿精液
		ELSEIF RAND:2
			IF COND("絶頂", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量的/溢出来的/非常多的")%
			ELSE
				PRINTFORM %TEXTR("勢头正旺的/熱呼呼的/濃厚的")%
			ENDIF
			PRINTFORM 精液%TEXTR("把子宮叩击着/在膣内肆意地流淌着/把弱点沐浴着")%
		ELSEIF COND("絶頂", PLAYER) >= 2 && RAND:2
			PRINTFORM 被大量中出了
		ELSE
			PRINTFORM 被做了非常喜欢的阴道内射精
		ENDIF
		PRINTFORM %CALLNAME:TARGET%、
		PRINTFORM %TEXTR("过于兴奋了/兴奋不堪了/興奮的过头了/浑身都在颤抖了/从鼻子里呼出热烈的喘息/无法忍受了")%
		PRINTFORMW 绝顶了！
	ELSEIF SHOOT("Ａ中出し")
		IF RAND:2
			IF COND("絶頂", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量的/溢出来的/非常多的")%
			ELSE
				PRINTFORM %TEXTR("勢头正旺的/熱呼呼的/濃厚的")%
			ENDIF
			PRINTFORM 精液%TEXTR("把直腸深处叩击着/在肠内肆意地流淌着/把弱点沐浴着")%
		ELSEIF COND("絶頂", PLAYER) >= 2 && RAND:2
			PRINTFORM 肛門被大量中出了
		ELSE
			PRINTFORM 被做了最喜欢的肛内射精
		ENDIF
		PRINTFORM %CALLNAME:TARGET%、
		PRINTFORM %TEXTR("过于兴奋了/兴奋不堪了/興奮的过头了/浑身都在颤抖了/从鼻子里呼出热烈的喘息/无法忍受了")%
		PRINTFORMW 绝顶了！
	ELSEIF SHOOT("乳头奸")
		IF RAND:2
			IF COND("絶頂", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量的/溢出来的/非常多的")%
			ELSE
				PRINTFORM %TEXTR("勢头正旺的/熱呼呼的/濃厚的")%
			ENDIF
			PRINTFORM %TEXTR("在胸部随意流淌/在乳沟内流入/从弱点注入")%的精液
		ELSEIF COND("絶頂", PLAYER) >= 2 && RAND:2
			PRINTFORM 对着乳房大量地中出了
		ELSE
			PRINTFORM 被做了最喜欢的乳内射精
		ENDIF
		PRINTFORM %CALLNAME:TARGET%、
		PRINTFORM %TEXTR("过于兴奋了/兴奋不堪了/興奮的过头了/浑身都在颤抖了/从鼻子里呼出热烈的喘息/无法忍受了")%
		PRINTFORMW 绝顶了！
	ELSEIF SHOOT("尿道中出し")
		IF RAND:2
			IF COND("絶頂", PLAYER) >= 2 && RAND:2
				PRINTFORM %TEXTR("大量的/溢出来的/非常多的")%
			ELSE
				PRINTFORM %TEXTR("勢头正旺的/熱呼呼的/濃厚的")%
			ENDIF
			PRINTFORM %TEXTR("在喜欢的場所随意流淌/从尿尿的小穴流入/从弱点注入")%的精液
		ELSEIF COND("絶頂", PLAYER) >= 2 && RAND:2
			PRINTFORM 在膀胱里大量地中出了
		ELSE
			PRINTFORM 被做了最喜欢的尿道内射
		ENDIF
		PRINTFORM %CALLNAME:TARGET%、
		PRINTFORM %TEXTR("过于兴奋了/兴奋不堪了/興奮的过头了/浑身都在颤抖了/从鼻子里呼出热烈的喘息/无法忍受了")%
		PRINTFORMW 绝顶了！
	ENDIF
	CALL ORGASM_ADD_TEXT, TARGET
ENDSELECT

