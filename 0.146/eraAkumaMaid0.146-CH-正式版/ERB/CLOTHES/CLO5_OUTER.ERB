;-------------------------------------------------
;キャラARGにARGSを着せる。ARGS == ""なら脱がす
;-------------------------------------------------
@SET_OUTER, ARGS, ARG
SIF ARG == 0 && TARGET > 0
	ARG = TARGET
;汚れ＆ずれたりしたフラグ等を初期化して新品にする
STAIN:ARG:外套 = 0

CALL PALAM_OUTER, ARGS, ARG

;コモンフラグを設定する
CSTR:ARG:外套 = %SET_CLO_COMMON(CSTR:ARG:外套, ARG)%
EQUIP:ARG:外套 = STRLENS(CSTR:ARG:外套)

;パンストを履いたら靴下は脱ぐ
SIF CHECK_OUTER("连褲襪", ARG)
	CALL SET_SOCKS, "", ARG
TFLAG:着替えた = 1

;-------------------------------------------------
;ARGのアウターの名前。ARGSに"略"と入れると略称になる
;-------------------------------------------------
@OUTER(ARG, ARGS)
#FUNCTIONS
#DIM LCOUNT
#DIMS COLOR_ABOUT

VARSET LOCALS
SELECTCASE ARGS
CASE "CIP種類"
	IF CHECK_OUTER("布鲁马", ARG)
		LOCALS = 布鲁马%CLOCOLOR_MAIDFACE("布鲁马", CHECK_CLOCOLOR(CSTR:ARG:外套), ARG)%
	ELSEIF CHECK_OUTER("紧身短褲", ARG)
		LOCALS = 紧身短褲%CLOCOLOR_MAIDFACE("紧身短褲", CHECK_CLOCOLOR(CSTR:ARG:外套), ARG)%
	ELSE
		RETURNF ""
	ENDIF
CASE "略"
	IF CHECK_OUTER("布鲁马", ARG)
		LOCALS = 布鲁马
	ELSEIF CHECK_OUTER("热褲", ARG)
		LOCALS = 热褲
	ELSEIF CHECK_OUTER("紧身短褲", ARG)
		LOCALS = 紧身短褲
	ELSEIF CHECK_OUTER("睡衣", ARG)
		LOCALS = 睡衣長褲
	ELSEIF CHECK_OUTER("五分褲", ARG)
		LOCALS = 五分褲
	ELSEIF CHECK_OUTER("短褲", ARG)
		LOCALS = 短褲
	ELSEIF CHECK_OUTER("运动衫", ARG)
		LOCALS = 运动衫
	;ELSEIF CHECK_OUTER("ローライズジーンズ", ARG)
	;	LOCALS = ローライズジーンズ
	ELSEIF CHECK_OUTER("牛仔褲", ARG)
		LOCALS = 牛仔褲
	ELSEIF CHECK_OUTER("褲裙", ARG)
		LOCALS = キュロット
	ELSEIF CHECK_OUTER("渔网连褲襪", ARG)
		LOCALS = 渔网连褲襪
	ELSEIF CHECK_OUTER("開放型连褲襪", ARG)
		LOCALS = 開放型连褲襪
	ELSEIF CHECK_OUTER("连褲襪", ARG)
		LOCALS = 连褲襪
	ELSEIF CHECK_OUTER("打底褲", ARG)
		LOCALS = 打底褲
	ELSE
		LOCALS = 長褲
	ENDIF
CASEELSE
	;LOCALS:0に名称、LOCALS:1～に色が入る場合がある
	SPLIT CSTR:ARG:外套, "/", LOCALS

	COLOR_ABOUT = 
	FOR LCOUNT, 1, 10
		SELECTCASE LOCALS:LCOUNT
		CASE ""
			BREAK
		CASE "條紋", "水玉模様", "豹纹"
			COLOR_ABOUT = %LOCALS:LCOUNT%
			BREAK
		CASE "赤", "青", "黒", "白", "米色", "棕色", "粉紅"
			COLOR_ABOUT = %LOCALS:LCOUNT%
			BREAK
		CASE "紫", "真紅", "薄桃色", "緑色", "黄色", "紺色", "薄緑", "橙色", "薄青色", "純白"
			COLOR_ABOUT = %LOCALS:LCOUNT%
			BREAK
		ENDSELECT
	NEXT

	IF ARGS == "CIPカラー：種類"
		SELECTCASE COLOR_ABOUT
		CASE "赤", "粉紅", "紫", "真紅", "薄桃色"
			RETURNF "赤"
		CASE "白", "米色", "純白"
			RETURNF "白"
		CASEELSE
			RETURNF "黒"
		ENDSELECT
	ELSE
		SIF COLOR_ABOUT != ""
			LOCALS += @"（%COLOR_ABOUT%）"
	ENDIF
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;ARG番のアウターの名称
;-------------------------------------------------
@NAME_OUTER(ARG)
#FUNCTIONS
VARSET LOCALS

SELECTCASE ARG
CASE 0
	LOCALS = 布鲁马
;CASE 1
;	LOCALS = ハイレグブルマ
CASE 2
	LOCALS = 紧身短褲
CASE 3
	LOCALS = 热褲
CASE 4
	LOCALS = 短褲
CASE 5
	LOCALS = 五分褲
CASE 6
	LOCALS = 長褲
CASE 7
	LOCALS = 运动衫長褲
CASE 8
	LOCALS = 牛仔褲
CASE 9
	LOCALS = 低腰牛仔褲
CASE 10
	LOCALS = 褲裙
CASE 11
	LOCALS = 打底褲
CASE 12
	LOCALS = 连褲襪
CASE 13
	LOCALS = 渔网连褲襪
CASE 14
	LOCALS = 開放型连褲襪
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;キャラARGにアウターARGSを着せた場合のCSTR処理
;-------------------------------------------------
@PALAM_OUTER, ARGS, ARG
;追加で記述された属性を一時的に保管する
#DIMS STR_ADD

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

STR_ADD = %EXTSTR(ARGS, "/")%
ARGS = %EXTSTR(ARGS, "", "/")%

SELECTCASE ARGS
CASE ""
	CSTR:ARG:外套 = 
CASE "布鲁马"
	CSTR:ARG:外套 = 布鲁马/%CLOCOLOR_MAIDFACE("布鲁马", "", ARG)%/
;CASE "ハイレグブルマ"
;	CSTR:ARG:アウター = ハイレグブルマ/%CLOCOLOR_MAIDFACE("ブルマ", "", ARG)%/Ｔバック/
CASE "紧身短褲"
	CSTR:ARG:外套 = 紧身短褲/不可移開/
CASE "热褲"
	CSTR:ARG:外套 = 热褲/不可移開/不可手交/可从缝隙偷窥的/長褲/社会の窓/
CASE "热褲"
	CSTR:ARG:外套 = 热褲/不可移開/不可手交/可从缝隙偷窥的/長褲/社会の窓/
CASE "短褲"
	CSTR:ARG:外套 = 短褲/不可移開/不可手交/可从缝隙偷窥的/長褲/社会の窓/
CASE "五分褲"
	CSTR:ARG:外套 = 五分褲/不可移開/不可手交/可从缝隙偷窥的/長褲/男物可/
CASE "运动衫長褲"
	CSTR:ARG:外套 = 运动衫長褲/不可移開/不可手交/長褲/男物可/
CASE "牛仔褲"
	CSTR:ARG:外套 = 牛仔褲/不可移開/不可手交/長褲/男物可/社会の窓/
CASE "低腰牛仔褲"
	CSTR:ARG:外套 = 低腰牛仔褲/不可移開/不可手交/長褲/社会の窓/
CASE "打底褲"
	CSTR:ARG:外套 = 打底褲/不可移開/不可手交/長褲/

CASE "连褲襪"
	CSTR:ARG:外套 = 连褲襪/不可移開/不可手交/透明/
CASE "黑色连褲襪"
	CSTR:ARG:外套 = 连褲襪/黒/不可移開/不可手交/透明/
CASE "渔网连褲襪"
	CSTR:ARG:外套 = 渔网连褲襪/不可移開/不可手交/蕾丝/
CASE "開放型连褲襪"
	CSTR:ARG:外套 = 開放型连褲襪/不可移開/不可手交/Ｖ露出/Ａ露出/

CASE "長褲"
	CSTR:ARG:外套 = 長褲/不可移開/不可手交/長褲/男物可/社会の窓/
CASE "西褲"
	CSTR:ARG:外套 = 西褲/不可移開/不可手交/長褲/男物可/社会の窓/
CASE "褲裙"
	CSTR:ARG:外套 = 褲裙/不可移開/不可手交/可从缝隙偷窥的/長褲/
CASEELSE
	CSTR:ARG:外套 = %ARGS%/
ENDSELECT

CSTR:ARG:外套 = %MERGE_STR(CSTR:ARG:外套, STR_ADD)%


;-------------------------------------------------
;ARGのアウターが条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_OUTER(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:外套 == 0
	RETURNF 0

SELECTCASE ARGS
;主に埴輪ルックの判定で使う
CASE "丈長長褲"
	;ズボンじゃないとダメ
	SIF STRCOUNT(CSTR:ARG:外套, "長褲") == 0
		RETURNF 0
	;丈の短いもの
	SIF STRCOUNTS(CSTR:ARG:外套, "热褲", "短")
		RETURNF 0
	RETURNF 1
CASE "股間の隙間"
	;太ももの付け根までを布地が締め付けている服
	SIF STRCOUNTS(CSTR:ARG:外套, "紧身短褲", "热褲")
		RETURNF 1
;主に"アソコのチラ見せ撮影"の判定で使う
CASE "めくり可能"
	;めくれなさそうなもの
	SIF STRCOUNTS(CSTR:ARG:外套, "连褲襪", "不可移開")
		RETURNF 0
	RETURNF 1
;ずれ不可でなく、ずらしていない
CASE "ずらし可", "ずらせる"
	SIF STRCOUNT(CSTR:ARG:外套, "Ｖ露出")
		RETURNF 0
	IF STRCOUNT(CSTR:ARG:外套, "带拉链的")
		SIF STRCOUNT(CSTR:ARG:外套, "带拉链的開")
			RETURNF 0
		RETURNF 1
	ENDIF
	SIF STRCOUNTS(CSTR:ARG:外套, "移開的", "不可移開")
		RETURNF 0
	RETURNF 1
;パンストで、破いていない
CASE "破ける"
	SIF STRCOUNT(CSTR:ARG:外套, "破開的")
		RETURNF 0
	SIF STRCOUNT(CSTR:ARG:外套, "连褲襪")
		RETURNF 1
;ファスナーがあり、あけていない
CASE "带拉链的閉"
	SIF STRCOUNT(CSTR:ARG:外套, "带拉链的開")
		RETURNF 0
	SIF STRCOUNT(CSTR:ARG:外套, "带拉链的")
		RETURNF 1
CASE "ＶＡ露出"
	SIF STRCOUNT(CSTR:ARG:外套, "Ｖ露出") && STRCOUNT(CSTR:ARG:外套, "Ａ露出")
		RETURNF 1
CASE "開洞"
	SIF STRCOUNTS(CSTR:ARG:外套, "带拉链的開", "移開的", "破開的", "Ｖ露出", "開衩")
		RETURNF 0
	RETURNF 1
CASE "開洞あり连褲襪"
	SIF STRCOUNTS(CSTR:ARG:外套, "带拉链的開", "移開的", "破開的", "Ｖ露出", "開衩", "開放型")
		RETURNF 0
	SIF STRCOUNT(CSTR:ARG:外套, "连褲襪")
		RETURNF 1
CASE "Ａ開洞"
	SIF STRCOUNTS(CSTR:ARG:外套, "带拉链的開", "移開的", "破開的", "Ａ露出", "開衩")
		RETURNF 0
	RETURNF 1
CASE "勒紧的"
	SIF STRCOUNTS(CSTR:ARG:外套, "移開的", "带拉链的開", "Ｖ露出", "開衩", "破開的")
		RETURNF 0
	SIF STRCOUNTS(CSTR:ARG:外套, "高開衩")
		RETURNF 1
CASE "低腰"
	SIF STRCOUNTS(CSTR:ARG:外套, "低腰", "超低腰")
		RETURNF 1
CASE "闷热的"
	SIF STRCOUNTS(CSTR:ARG:外套, "移開的", "带拉链的開", "Ｖ露出", "開衩", "破開的")
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:外套, "闷热的")
		RETURNF 1
CASE "带污迹的"
	SIF STRCOUNTS(CSTR:ARG:外套, "移開的", "带拉链的開", "Ｖ露出", "開衩", "破開的", "通気性没有")
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:外套, "湿的")
		RETURNF 1
CASEELSE
	SIF STRCOUNT(CSTR:ARG:外套, ARGS)
		RETURNF 1
ENDSELECT
RETURNF 0
