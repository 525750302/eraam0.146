;-------------------------------------------------
;キャラARGにパンツARGSを着せる。ARGS == ""なら脱がす
;-------------------------------------------------
@SET_PANTIES, ARGS, ARG
SIF ARG == 0 && TARGET > 0
	ARG = TARGET

;汚れ＆ずれたりしたフラグ等を初期化して新品にする
STAIN:ARG:胖次 = 0

CALL PALAM_PANTIES, ARGS, ARG

;コモンフラグを設定する
CSTR:ARG:胖次 = %SET_CLO_COMMON(CSTR:ARG:胖次, ARG)%
CSTR:ARG:胸罩 = %SET_CLO_COMMON(CSTR:ARG:胸罩, ARG)%

EQUIP:ARG:胖次 = STRLENS(CSTR:ARG:胖次)
EQUIP:ARG:胸罩 = STRLENS(CSTR:ARG:胸罩)

;自分の下着に着替えた場合には色を合わせる
SIF TFLAG:自分の内衣に着替えた
	CALL EQ_CLOCOLOR, TARGET

;ボディスーツは脱がす
SIF EQUIP:ARG:胖次 && EQUIP:ARG:贴身制服
	CALL SET_BODYS, "", ARG

TFLAG:着替えた = 1

;-------------------------------------------------
;ARGの穿いているパンツの名前。ARGSに"略"と入れると略称になる
;-------------------------------------------------
@PANTIES(ARG, ARGS)
#FUNCTIONS
#DIMS COLOR_ABOUT

VARSET LOCALS
SELECTCASE ARGS
CASE "蝴蝶結"
	SIF CHECK_PANTIES("蝴蝶結", ARG) == 0
		RETURNF ""
	RETURNF EXTSTR(CSTR:ARG:胖次, "蝴蝶結", "/")
CASE "CIP正面"
	IF CHECK_PANTIES("Ｖ露出", ARG) || CHECK_PANTIES("Ｃ胖次", ARG)
		RETURNF ""
	ELSEIF CHECK_PANTIES("前貼", ARG)
		LOCALS = 前貼
	ELSEIF CHECK_PANTIES("襯褲", ARG)
		LOCALS = 襯褲
	ELSEIF CHECK_PANTIES("高開衩布鲁马", ARG)
		LOCALS = 高開衩布鲁马
	ELSEIF CHECK_PANTIES("叶子胖次", ARG)
		LOCALS = 叶子胖次
	ELSEIF CHECK_PANTIES("兜襠布", ARG)
		LOCALS = 兜襠布
	ELSEIF CHECK_PANTIES("儿童胖次", ARG)
		LOCALS = 儿童胖次
	ELSEIF CHECK_PANTIES("四角内褲", ARG)
		LOCALS = 四角内褲
	ELSEIF CHECK_PANTIES("淚珠泳衣", ARG)
		LOCALS = 淚珠泳衣
	ELSEIF CHECK_PANTIES("Micro", ARG) && CHECK_PANTIES("比基尼", ARG) == 0
		LOCALS = Micro內褲
	ELSEIF CHECK_PANTIES("丁字褲", ARG) || CHECK_PANTIES("Micro", ARG)
		LOCALS = Micro
	ELSEIF CHECK_PANTIES("超低腰", ARG)
		LOCALS = 超低腰
	ELSEIF CHECK_PANTIES("低腰胖次", ARG)
		LOCALS = 低腰
	ELSEIF CHECK_PANTIES("高開衩", ARG)
		LOCALS = 高開衩
	ELSEIF CHECK_PANTIES("褶边", ARG)
		LOCALS = 褶边
	ELSEIF CHECK_PANTIES("蕾丝", ARG) || CHECK_PANTIES("褶边", ARG) || CHECK_PANTIES("透明", ARG)
		LOCALS = 蕾丝
	ELSEIF CHECK_PANTIES("條紋", ARG)
		LOCALS = 縞胖次
	ELSEIF CHECK_PANTIES("Ｔ字褲", ARG) || CHECK_PANTIES("繩系胖次", ARG)
		LOCALS = Ｔ字褲
	ELSEIF CHECK_PANTIES("內褲", ARG) || CHECK_PANTIES("全後衛", ARG)
		LOCALS = 內褲
	ELSEIF CHECK_PANTIES("三角胖次", ARG)
		LOCALS = 三角胖次
	ELSEIF EQUIP:ARG:胖次 == 0
		SIF PENIS(ARG)
			RETURNF ""
		LOCALS = すじ
	ELSE
		RETURNF ""
	ENDIF
CASE "CIPバック"
	IF CHECK_PANTIES("Ｃ胖次", ARG)
		RETURNF ""
	ELSEIF CHECK_PANTIES("淚珠泳衣", ARG) || CHECK_PANTIES("丁字褲", ARG) || CHECK_PANTIES("Micro", ARG)
		LOCALS = 丁字褲
	ELSEIF CHECK_PANTIES("超低腰", ARG)
		RETURNF ""
	ELSEIF CHECK_PANTIES("Ｔ字褲", ARG)
		LOCALS = Ｔ字褲
	ELSE
		RETURNF ""
	ENDIF
CASE "CIP種類"
	IF CHECK_PANTIES("Ｃ胖次", ARG)
		RETURNF ""
	ELSEIF CHECK_PANTIES("前貼", ARG)
		LOCALS = 前貼
	ELSEIF CHECK_PANTIES("襯褲", ARG)
		LOCALS = 襯褲
	ELSEIF CHECK_PANTIES("高開衩布鲁马", ARG)
		LOCALS = 高開衩布鲁马
	ELSEIF CHECK_PANTIES("叶子胖次", ARG)
		LOCALS = 叶子胖次
	ELSEIF CHECK_PANTIES("兜襠布", ARG)
		LOCALS = 兜襠布
	ELSEIF CHECK_PANTIES("儿童胖次", ARG)
		LOCALS = 儿童胖次
	ELSEIF CHECK_PANTIES("四角内褲", ARG)
		LOCALS = 四角内褲
	ELSEIF CHECK_PANTIES("淚珠泳衣", ARG)
		LOCALS = 淚珠泳衣
	ELSEIF CHECK_PANTIES("丁字褲", ARG)
		LOCALS = 丁字褲
	ELSEIF CHECK_PANTIES("Micro", ARG) && CHECK_PANTIES("比基尼", ARG) == 0
		LOCALS = Micro內褲
	ELSEIF CHECK_PANTIES("Micro", ARG)
		LOCALS = Micro
	ELSEIF CHECK_PANTIES("超低腰", ARG)
		LOCALS = 超低腰
	ELSEIF CHECK_PANTIES("低腰胖次", ARG)
		LOCALS = 低腰
	ELSEIF CHECK_PANTIES("高開衩", ARG)
		LOCALS = 高開衩
	ELSEIF CHECK_PANTIES("褶边", ARG)
		LOCALS = 褶边
	ELSEIF CHECK_PANTIES("蕾丝", ARG)
		LOCALS = 蕾丝
	ELSEIF CHECK_PANTIES("Ｔ字褲", ARG)
		LOCALS = Ｔ字褲
	ELSEIF CHECK_PANTIES("條紋", ARG)
		LOCALS = 縞胖次
	ELSEIF CHECK_PANTIES("內褲", ARG) || CHECK_PANTIES("全後衛", ARG)
		LOCALS = 內褲
	ELSEIF CHECK_PANTIES("三角胖次", ARG)
		LOCALS = 三角胖次
	ELSEIF EQUIP:ARG:胖次 == 0
		SIF PENIS(ARG)
			RETURNF ""
		LOCALS = すじ
	ELSE
		RETURNF ""
	ENDIF
CASE "CIPカラー：種類", "CIPカラー：正面", "CIPカラー：バック"
	SELECTCASE PANTIES(ARG, @"CIP%EXTSTR(ARGS, "：")%")
	;色指定が無いもの
	CASE "", "すじ"
		RETURNF ""
	;特殊
	CASE "儿童胖次"
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		;デフォなので変化させなくてよい
		CASE "白"
			RETURNF ""
		CASEELSE
			RETURNF CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		ENDSELECT
	CASE "前貼"
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASEELSE
			RETURNF "白"
		ENDSELECT
	CASE "高開衩布鲁马"
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		CASE "赤"
			RETURNF "赤"
		CASE "水色"
			RETURNF "水色"
		CASE "紺色"
			RETURNF "紺色"
		CASE "緑色"
			RETURNF "緑色"
		CASEELSE
			RETURNF "紺色"
		ENDSELECT
	;白赤黒紫青分岐
	CASE "Micro內褲"
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		;デフォなので変化させなくてよい
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤", "薄桃色"
			RETURNF "赤"
		CASE "青", "紺色", "薄青色"
			RETURNF "青"
		;用意されていない色
		CASE "緑色", "薄緑", "黄色", "橙色"
			RETURNF ""
		ENDSELECT
	CASE "Ｔ字褲"
		IF STRCOUNT(CSTR:ARG:胖次, "水玉模様")
			SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
			CASE "黒"
				RETURNF "水玉黒"
			CASE "粉紅"
				RETURNF "水玉粉紅"
			CASE "薄青色", "水色"
				RETURNF "水玉水色"
			CASE "黄色", "橙色"
				RETURNF "水玉黄色"
			ENDSELECT
		ELSEIF STRCOUNT(CSTR:ARG:胖次, "豹纹")
			RETURNF "豹纹"
		ENDIF
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		;デフォなので変化させなくてよい
		CASE "黒"
			RETURNF ""
		CASE "紫"
			RETURNF "紫"
		CASE "真紅"
			RETURNF "真紅"
		CASE "赤", "薄桃色"
			RETURNF "赤"
		CASE "粉紅"
			RETURNF "粉紅"
		CASE "青", "紺色"
			RETURNF "青"
		CASE "薄青色", "水色"
			RETURNF "水色"
		CASE "緑色"
			RETURNF "緑色"
		CASE "薄緑"
			RETURNF "薄緑"
		CASE "灰色"
			RETURNF "灰色"
		CASE "黄色", "橙色"
			RETURNF "黄色"
		ENDSELECT
	CASE "丁字褲", "Micro", "淚珠泳衣"
		SIF STRCOUNT(CSTR:ARG:胖次, "豹纹")
			RETURNF "豹纹"
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		;デフォなので変化させなくてよい
		CASE "白", "純白"
			RETURNF ""
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤", "薄桃色"
			RETURNF "赤"
		CASE "青", "紺色", "薄青色"
			RETURNF "青"
		;用意されていない色
		CASE "緑色", "薄緑", "黄色", "橙色"
			RETURNF ""
		ENDSELECT
	CASE "縞胖次"
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		CASE "紅白", "緑白", "黄白", "黒白"
			RETURNF CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		ENDSELECT
	;ピンクが無い
	CASE "超低腰"
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤"
			RETURNF "赤"
		CASE "薄桃色", "桃"
			RETURNF ""
		CASE "青", "紺色", "薄青色"
			RETURNF "青"
		CASE "緑色", "薄緑"
			RETURNF "緑色"
		CASE "黄色", "橙色"
			RETURNF "黄色"
		ENDSELECT
	;赤が無い
	CASE "低腰胖次", "褶边", "四角内褲"
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤"
			RETURNF ""
		CASE "薄桃色", "桃"
			RETURNF "桃"
		CASE "青", "紺色", "薄青色"
			RETURNF "青"
		CASE "緑色", "薄緑"
			RETURNF "緑色"
		CASE "黄色", "橙色"
			RETURNF "黄色"
		ENDSELECT
	;その他は８カラ―
	CASEELSE
		SELECTCASE CLOCOLOR(CSTR:ARG:胖次, "色のみ")
		CASE "白", "純白"
			RETURNF "白"
		CASE "黒"
			RETURNF "黒"
		CASE "紫"
			RETURNF "紫"
		CASE "真紅", "赤"
			RETURNF "赤"
		CASE "薄桃色", "桃"
			RETURNF "桃"
		CASE "青", "紺色", "薄青色", "條紋", "水玉"
			RETURNF "青"
		CASE "緑色", "薄緑"
			RETURNF "緑色"
		CASE "黄色", "橙色", "豹纹"
			RETURNF "黄色"
		ENDSELECT
	ENDSELECT
	RETURNF ""

CASE "胖次", "内衣", "ぱんつ"
	IF CHECK_PANTIES("泳装", ARG)
		LOCALS = 泳装
	ELSEIF CHECK_PANTIES("布鲁马", ARG)
		LOCALS = 布鲁马
	ELSE
		LOCALS = %ARGS%
	ENDIF
CASE "略"
	IF CHECK_PANTIES("貞操帯", ARG)
		LOCALS = 貞操帯
	ELSEIF CHECK_PANTIES("布鲁马", ARG)
		LOCALS = 布鲁马
	ELSEIF CHECK_PANTIES("鬥士胖次", ARG)
		LOCALS = 鬥士胖次
	ELSEIF CHECK_PANTIES("简约胖次", ARG)
		LOCALS = 简约胖次
	ELSEIF CHECK_PANTIES("四角褲", ARG)
		LOCALS = 四角褲
	ELSEIF CHECK_PANTIES("兜襠布", ARG)
		LOCALS = 兜襠布
	;ビキニって呼称は上下合わせないとちょっと微妙かも
	ELSEIF CHECK_PANTIES("比基尼", ARG)
		IF CHECK_PANTIES("淚珠泳衣", ARG)
			LOCALS = 淚珠泳衣比基尼
		ELSEIF CHECK_PANTIES("Micro", ARG)
			LOCALS = Micro比基尼
		ELSE
			LOCALS = 比基尼胖次
		ENDIF
	;ELSEIF CHECK_PANTIES("スパッツ", ARG)
	;	LOCALS = スパッツ
	ELSEIF CHECK_PANTIES("襯褲", ARG)
		LOCALS = 襯褲
	ELSEIF CHECK_PANTIES("Ｃ胖次", ARG)
		LOCALS = Ｃ胖次
	ELSEIF CHECK_PANTIES("前貼", ARG)
		LOCALS = 前貼
	;ELSEIF CHECK_PANTIES("絆創膏", ARG)
	;	LOCALS = 絆創膏
	ELSE
		LOCALS = 胖次
	ENDIF
CASE "種類"
	;LOCALSに名称が入る
	SPLIT CSTR:ARG:胖次, "/", LOCALS
CASEELSE
	;LOCALSに名称が入る
	SPLIT CSTR:ARG:胖次, "/", LOCALS
	COLOR_ABOUT = %CHECK_CLOCOLOR(CSTR:ARG:胖次)%

	IF CHECK_PANTIES("男物", ARG)
		LOCALS = \@ TALENT:ARG:男性 == 0 ? 男式 # \@%LOCALS%
	;名称と色が被ってる場合は省略
	ELSEIF STRCOUNT(LOCALS, COLOR_ABOUT)
		RETURNF LOCALS
	;一部の柄物
	ELSEIF STRCOUNTS(CSTR:ARG:胖次, "條紋", "水玉模様", "豹纹")
		SELECTCASE COLOR_ABOUT
		CASE ""
		CASE "赤", "青", "黒", "白"
			LOCALS = %COLOR_ABOUT%い%LOCALS%
		CASEELSE
			LOCALS = %COLOR_ABOUT%の%LOCALS%
		ENDSELECT
	ELSE
		LOCALS = %CLOCOLOR(CSTR:ARG:胖次, "形容詞")%%LOCALS%
	ENDIF
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;ARG番のパンツの名称。着せ替えなどで表を作る際に使う
;-------------------------------------------------
@NAME_PANTIES(ARG)
#FUNCTIONS
VARSET LOCALS

SELECTCASE ARG
CASE 0
	LOCALS = 內褲
CASE 1
	LOCALS = 三角胖次
CASE 2
	LOCALS = 全後衛胖次
CASE 3
	LOCALS = 低腰胖次
CASE 4
	LOCALS = 超低腰胖次
CASE 5
	LOCALS = 高開衩胖次
CASE 6
	LOCALS = Ｔ字褲
CASE 7
	LOCALS = 丁字褲
CASE 8
	LOCALS = Ｃ胖次
CASE 9
	LOCALS = 四角内褲
CASE 10
	LOCALS = 開放型開洞胖次
CASE 11
	LOCALS = 後開型
CASE 12
	LOCALS = Micro內褲

CASE 15
	LOCALS = 條紋胖次
CASE 16
	LOCALS = 水玉模様の胖次
CASE 17
	LOCALS = 豹纹胖次

CASE 20
	LOCALS = 蕾丝的胖次
CASE 21
	LOCALS = 透明胖次
CASE 22
	LOCALS = 褶边の胖次
CASE 23
	LOCALS = 繩系胖次
CASE 24
	LOCALS = 皮革胖次
CASE 25
	LOCALS = 兜襠布
CASE 26
	LOCALS = 貞操帯
CASE 27
	LOCALS = 前貼
CASE 28
	LOCALS = 儿童胖次

CASE 30
	LOCALS = 比基尼
CASE 31
	LOCALS = Micro比基尼
CASE 32
	LOCALS = 淚珠泳衣比基尼

CASE 35
	LOCALS = 四角褲
CASE 36
	LOCALS = 鬥士胖次
CASE 37
	LOCALS = 高開衩布鲁马

CASE 39
	LOCALS = Ｈ的内衣
ENDSELECT

RETURNF LOCALS


;-------------------------------------------------
;キャラARGにパンツARGSを着せた場合のCSTR処理
;名前/色/属性/…/属性/ というルールで記述される(一番左は名前、次は色で固定)
;後で属性を追加したい際には CSTR:パンツ += "Ｖ露出/" のように単純に追加すればよい。同じ属性が複数あっても問題ない
;-------------------------------------------------
@PALAM_PANTIES, ARGS, ARG
#DIM MEMO_TARGET
;追加で記述された属性を一時的に保管する
#DIMS STR_ADD

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

STR_ADD = %EXTSTR(ARGS, "/")%
ARGS = %EXTSTR(ARGS, "", "/")%

MEMO_TARGET = TARGET
TFLAG:自分の内衣に着替えた = 0

SELECTCASE ARGS
CASE "", "没穿内褲"
	CSTR:ARG:胖次 = 
;キャラ衣装
CASE "自分の内衣"
	TARGET = ARG

	TRYCALLFORM CLOTHES_K{NO:TARGET}, "内衣"

	TFLAG:自分の内衣に着替えた = 1
	TARGET = MEMO_TARGET
CASE "しまった内衣"
	VARSET LOCALS
	SPLIT CSTR:ARG:しまった内衣, "_", LOCALS
	CSTR:ARG:胖次 = %LOCALS:0%
	CSTR:ARG:胸罩 = %LOCALS:1%
	CSTR:ARG:しまった内衣 = 

CASE "內褲"
	CSTR:ARG:胖次 = 內褲/淡色/简约/
CASE "三角胖次"
	CSTR:ARG:胖次 = 三角胖次/淡色/正面蝴蝶結/
CASE "全後衛胖次"
	CSTR:ARG:胖次 = 全後衛胖次/淡色/
CASE "低腰胖次"
	CSTR:ARG:胖次 = 低腰胖次/淡色/
;本作ではローレグ＆ローライズの小さいパンツをローレグパンツとする。not子供パンツ
CASE "超低腰胖次"
	CSTR:ARG:胖次 = 超低腰胖次/淡色/
CASE "高開衩胖次"
	CSTR:ARG:胖次 = 高開衩胖次/隨機顏色/
CASE "Ｔ字褲"
	CSTR:ARG:胖次 = Ｔ字褲/辣妹/
CASE "丁字褲"
	CSTR:ARG:胖次 = 丁字褲/辣妹/高開衩/Ｔ字褲/
CASE "Ｃ胖次"
	CSTR:ARG:胖次 = Ｃ胖次/隨機顏色/Ｔ字褲/不可移開/
CASE "四角内褲"
	CSTR:ARG:胖次 = 四角内褲/淡色/
CASE "開放型開洞胖次"
	CSTR:ARG:胖次 = 開放型開洞胖次/Ｈ的颜色/Ｖ露出/Ａ露出/
CASE "後開型"
	CSTR:ARG:胖次 = 後開型/辣妹/Ａ露出/超低腰/
CASE "Micro內褲"
	CSTR:ARG:胖次 = Micro內褲/隨機顏色/
CASE "襯褲"
	CSTR:ARG:胖次 = 襯褲/白/不可移開/

CASE "條紋胖次"
	CSTR:ARG:胖次 = 條紋胖次/條紋色/
CASE "水玉模様の胖次"
	CSTR:ARG:胖次 = 水玉模様の胖次/水玉色/
CASE "水玉模様のＴ字褲"
	CSTR:ARG:胖次 = 水玉模様のＴ字褲/水玉色/
CASE "豹纹胖次"
	CSTR:ARG:胖次 = 豹纹胖次/豹纹/Ｔ字褲/
CASE "豹纹のＴ字褲"
	CSTR:ARG:胖次 = 豹纹のＴ字褲/豹纹/
CASE "肉球印花胖次"
	CSTR:ARG:胖次 = 肉球印花胖次/黒/紐/

CASE "蕾丝的胖次"
	CSTR:ARG:胖次 = 蕾丝的胖次/Ｈ的颜色/Ｔ字褲/透明/ランジェリー/
CASE "透明胖次"
	CSTR:ARG:胖次 = 透明胖次/淡色/ランジェリー/
CASE "褶边の胖次"
	CSTR:ARG:胖次 = 褶边の胖次/淡色/正面蝴蝶結紐/ランジェリー/
CASE "繩系胖次"
	CSTR:ARG:胖次 = 繩系胖次/淡色/
CASE "皮革胖次"
	CSTR:ARG:胖次 = 皮革胖次/黒/高開衩/Ｔ字褲/
CASE "叶子胖次"
	CSTR:ARG:胖次 = 叶子胖次/緑色/超低腰/

CASE "比基尼"
	CSTR:ARG:胖次 = 比基尼/隨機顏色/高開衩/紐/
CASE "白色比基尼"
	CSTR:ARG:胖次 = 白色比基尼/白/
CASE "黑色比基尼"
	CSTR:ARG:胖次 = 黑色比基尼/黒/Ｔ字褲/紐/
CASE "Micro比基尼"
	CSTR:ARG:胖次 = Micro比基尼/隨機顏色/紐/
CASE "淚珠泳衣比基尼"
	CSTR:ARG:胖次 = 淚珠泳衣比基尼/隨機顏色/紐/Ｔ字褲/Micro/

CASE "男式比基尼"
	CSTR:ARG:胖次 = 男式比基尼/黒/泳装/
CASE "衝浪胖次"
	CSTR:ARG:胖次 = 衝浪胖次/黒/泳装/

CASE "兜襠布"
	CSTR:ARG:胖次 = 兜襠布/白/高開衩/Ｔ字褲/
CASE "貞操帯"
	CSTR:ARG:胖次 = 貞操帯/黒/皮革/不可移開/脱衣不可/
CASE "前貼"
	CSTR:ARG:胖次 = 前貼/白/Ａ露出/不可移開/
CASE "儿童胖次"
	CSTR:ARG:胖次 = 儿童胖次/儿童胖次颜色/

CASE "四角褲"
	CSTR:ARG:胖次 = 四角褲/男式色/男物/
CASE "鬥士胖次"
	CSTR:ARG:胖次 = 鬥士胖次/男式色/男物/

CASE "高開衩布鲁马"
	CSTR:ARG:胖次 = 高開衩布鲁马/%CLOCOLOR_MAIDFACE("布鲁马", "", ARG)%/
CASE "Ｈ的内衣"
	CSTR:ARG:胖次 = Ｈ的内衣/Ｈ的颜色/

CASEELSE
	CSTR:ARG:胖次 = %ARGS%/隨機顏色/
ENDSELECT

CSTR:ARG:胖次 = %MERGE_STR(CSTR:ARG:胖次, STR_ADD)%


;-------------------------------------------------
;ARGの穿いているパンツの詳細
;-------------------------------------------------
@DETAIL_PANTIES(ARG, ARGS)
#FUNCTIONS
;種類名：下着かパンツかビキニかブルマ
#DIMS TYPE_PANTU
SIF ARG == 0 && TARGET
	ARG = TARGET

IF CHECK_PANTIES("布鲁马", ARG)
	TYPE_PANTU = 布鲁马
ELSEIF CHECK_PANTIES("比基尼", ARG)
	TYPE_PANTU = 比基尼
ELSE
	TYPE_PANTU = %TEXTR("胖次/内衣")%
ENDIF
VARSET LOCALS
SELECTCASE ARGS
;触っただけでパンツを類推する場合
CASE "感触"
	IF CHECK_PANTIES("Ｖ露出", ARG)
		IF CHECK_PANTIES("Ａ露出", ARG)
			LOCALS += "股間的前面到後側都完全露出了"
		ELSE
			LOCALS += "陰部並沒有被布料遮擋著"
		ENDIF
	ELSEIF CHECK_PANTIES("Ａ露出", ARG)
		LOCALS += "菊門周圍完全沒有布料"
	ELSEIF CHECK_PANTIES("開衩", ARG)
		LOCALS += "内衣部分遮掩着窄缝"
	;マイクロ（Ｔバックでもある）
	ELSEIF CHECK_PANTIES("Micro", ARG)
		LOCALS += "布料面積異常的少"
	ELSEIF CHECK_PANTIES("带拉链的", ARG)
		LOCALS += "內衣部分隱藏著拉鏈"
	ELSEIF CHECK_PANTIES("繩系胖次", ARG)
		LOCALS += "側面由絲帶繫結來固定"
	ELSEIF CHECK_PANTIES("蕾丝", ARG)
		LOCALS += "帶著彩色的蕾絲花邊的"
	ENDIF

	IF CHECK_PANTIES("前貼", ARG)
		RETURNF "前貼"
	ELSEIF CHECK_PANTIES("兜襠布", ARG)
		RETURNF "兜襠布"
	ELSEIF CHECK_PANTIES("淚珠泳衣", ARG)
		LOCALS += "所謂淚珠泳衣の"
	ELSEIF CHECK_PANTIES("Ｃ胖次", ARG)
		LOCALS += "Ｃ胖次的"
	ELSEIF CHECK_PANTIES("丁字褲", ARG)
		LOCALS += "丁字褲的"
	ELSEIF CHECK_PANTIES("Ｔ字褲", ARG)
		LOCALS += "Ｔ字褲的"
	;ローorハイ
	ELSEIF CHECK_PANTIES("超低腰", ARG)
		LOCALS += "低腰胖次的"
	ELSEIF CHECK_PANTIES("高開衩", ARG)
		LOCALS += "高衩胖次的"
	ELSEIF LOCALS != ""
		LOCALS += "後衛胖次的"
	ENDIF
	SIF LOCALS == ""
		LOCALS = %TEXTR("標準的/普通的/帶來安定感的")%後衛胖次的
	LOCALS = %LOCALS%%TYPE_PANTU%

CASEELSE
	;Ｈな下着
	IF CHECK_PANTIES("Ｈ的内衣", ARG) || CHECK_PANTIES("Ｖ露出", ARG) || CHECK_PANTIES("Ａ露出", ARG)
		SIF CHECK_PANTIES("蕾丝", ARG)
			LOCALS += "帶著彩色的蕾絲花邊的"

		IF CHECK_PANTIES("透明", ARG)
			LOCALS += "透明素材的"
		ELSEIF CHECK_PANTIES("絲綢", ARG)
			LOCALS += "輕薄的絲制的"
		ENDIF

		IF CHECK_PANTIES("Ｖ露出", ARG)
			IF CHECK_PANTIES("Ａ露出", ARG)
				LOCALS += "股間的前面到後側都完全露出了"
			ELSE
				LOCALS += "陰部並沒有被布料遮擋著"
			ENDIF
		ELSEIF CHECK_PANTIES("Ａ露出", ARG)
			LOCALS += "菊門周圍完全沒有布料"
		;マイクロ（Ｔバックでもある）
		ELSEIF CHECK_PANTIES("Micro", ARG)
			LOCALS += "布料面積異常的少"
		ELSEIF CHECK_PANTIES("带拉链的", ARG)
			LOCALS += "內衣部分隱藏著拉鏈"
		ELSEIF CHECK_PANTIES("開衩", ARG)
			LOCALS += "内衣部分遮掩着窄缝"
		ENDIF

		SIF LOCALS == ""
			LOCALS = 洋溢著情色氣息的

		IF CHECK_PANTIES("丁字褲", ARG)
			LOCALS += "丁字褲"
			TYPE_PANTU = 
		ELSEIF CHECK_PANTIES("Ｔ字褲", ARG)
			LOCALS += "Ｔ字褲"
			TYPE_PANTU = 
		;ローorハイ
		ELSEIF CHECK_PANTIES("超低腰", ARG)
			LOCALS += "超低腰"
		ELSEIF CHECK_PANTIES("高開衩", ARG)
			LOCALS += "高開衩"
		ENDIF
		;紐
		IF CHECK_PANTIES("紐", ARG)
			LOCALS += "紐"
			SIF CHECK_PANTIES("比基尼", ARG) == 0
				TYPE_PANTU = 胖次
		ENDIF
		LOCALS = %LOCALS%%TYPE_PANTU%
	ELSEIF CHECK_PANTIES("Micro", ARG)
		IF RAND:2
			LOCALS = 用碎布來形容會更恰當一些的、用料非常緊張的\@ CHECK_PANTIES("比基尼", ARG) ? 比基尼 # 胖次 \@
		ELSE
			LOCALS = 重要的地方完全无法遮掩住的用料極少的\@ CHECK_PANTIES("比基尼", ARG) ? 泳装 # 内衣 \@
		ENDIF
	ELSEIF CHECK_PANTIES("比基尼", ARG)
		LOCALS = 風格大膽的比基尼
	ELSEIF CHECK_PANTIES("Ｃ胖次", ARG)
		LOCALS = 讓人迷惑與是否能稱為胖次的逸品、C胖次
	;前貼り
	ELSEIF CHECK_PANTIES("前貼", ARG)
		LOCALS = %TEXTR("完美贴合/紧紧贴住")%股間遮住%TEXTR("重要地方/阴部")%的前貼
	;ELSEIF CHECK_PANTIES("褌", ARG)

	;ELSEIF CHECK_PANTIES("貞操帯", ARG)

	ELSEIF CHECK_PANTIES("肉球印花胖次", ARG)
		LOCALS = 正面印有小小猫耳的胖次
	ELSEIF CHECK_PANTIES("條紋", ARG)
		IF RAND:2
			LOCALS = 股間的%TEXTR("陰影/線條")%被%TEXTR("柔和的/保守的")%%TEXTR("顯示著/主張著")%的條紋%TEXTR("胖次/内衣")%
		ELSE
			LOCALS = 条纹沿着%TEXTR("臀部/股間")%稍稍弯曲的条纹胖次
		ENDIF
	ELSEIF CHECK_PANTIES("水玉模様", ARG)
		LOCALS = 可愛的水玉模様の%TEXTR("胖次/の内衣")%
	ELSEIF CHECK_PANTIES("豹纹", ARG)
		SELECTCASE AGE(ARG)
		CASE IS >= 4
			LOCALS = %TEXTR("風格性感的/洋溢著情色氣息的/飄蕩著成熟氣息的")%
		CASE 3
			LOCALS = %TEXTR("風格性感的/帶著挑起慾望的暗示的/作為辣妹這一群體的象徵物一樣的")%
		CASE 2
			LOCALS = %TEXTR("具備成熟風韻的/帶著微妙可愛感的/帶著挑起慾望的暗示的/")%
		CASE 1
			LOCALS = %TEXTR("與現在的氣氛十分相稱的/帶著微妙可愛感的/帶著挑起慾望的暗示的")%
		ENDSELECT
		LOCALS += @"%PANTIES(ARG)%"
	ELSEIF CHECK_PANTIES("蕾丝的胖次", ARG)
		IF RAND:4 == 0
			LOCALS = 彰顯女性魅力的%CLOCOLOR(CSTR:胸罩, "形容詞")%蕾絲質地的
		ELSEIF RAND:3 == 0
			LOCALS = 飄蕩著成熟氣息的%CLOCOLOR(CSTR:ARG:胸罩, "形容詞")%蕾絲花邊的
		ELSEIF RAND:2
			LOCALS = 風格典雅的美麗的蕾絲質地的
		ELSE
			LOCALS = 具備成熟風韻而不失可愛的蕾絲花邊的
		ENDIF
		LOCALS = %LOCALS%%TYPE_PANTU%
	ELSEIF CHECK_PANTIES("褶边の胖次", ARG)
		LOCALS = 有%TEXTR("可愛的/清純的")%褶邊%TEXTR("装飾著的/附著的")%%TYPE_PANTU%
	ELSEIF CHECK_PANTIES("繩系胖次", ARG)
		IF RAND:2
			LOCALS = 用十分容易解下的絲帶繫住的繩系胖次
		ELSE
			LOCALS = %TEXTR("両侧的打结/三角形的布料部分")%十分显眼的繩系胖次
		ENDIF
	ELSEIF CHECK_PANTIES("丁字褲", ARG)
		LOCALS = 連接的帶子深陷在股溝中的大膽的丁字褲
	ELSEIF CHECK_PANTIES("Ｔ字褲", ARG)
		LOCALS = 將可愛的尻部的線條勾畫出來的T字褲
	ELSEIF CHECK_PANTIES("低腰", ARG)
		LOCALS = 在大腿上側用淺色橡膠套緊的低腰褲
	ELSEIF CHECK_PANTIES("透明胖次", ARG)
		IF RAND:3 == 0
			LOCALS = 隐约露出%CALLNAME:ARG%的%TEXTR("下腹部/臀部")%的内衣
		ELSEIF RAND:2 == 0
			LOCALS = 大膽地透明質地的內衣
		ELSE
			LOCALS = 氣氛淫靡、能看的通透的內褲
		ENDIF
	ELSEIF CHECK_PANTIES("高開衩布鲁马", ARG)
		LOCALS = %TEXTR("突显柔软恥丘的/讓能被一覽無遺的腹股沟更加引人注意的/潇洒精致的/深深切入股間的")%高開衩布鲁马
	ELSEIF CHECK_PANTIES("高開衩胖次", ARG)
		LOCALS = %TEXTR("將柔軟的恥丘明顯的凸顯出來的/讓能被一覽無遺的鼠蹊部更加引人注意的/讓美麗的身體更加誘人的/深深切入股間的")%高衩內褲
	ELSEIF CHECK_PANTIES("皮革胖次", ARG)
		LOCALS = 在股間附有隱蔽的拉鏈的、緊繃的皮革高衩內褲
	ELSEIF CHECK_PANTIES("超低腰", ARG)
		SELECTCASE COND("陰毛", ARG)
		CASE 1, 2
			LOCALS = 剝殼雞蛋一樣光潔的恥丘
		CASE 3
			LOCALS = 被完全剔除陰毛的恥丘
		CASE 4
			IF CONDS("陰毛", ARG) == "剃り跡"
				LOCALS = 生長著微小毛茬的恥丘
			ELSE
				LOCALS = 稍微生长了的陰毛
			ENDIF
		CASE 5
			IF CEVENTS("陰毛の名称", ARG) != "" && RAND:2
				LOCALS = 被整齊修剪成%CEVENTS("陰毛の名称", ARG)%的陰毛
			ELSE
				LOCALS = 被漂亮的整理過的陰毛
			ENDIF
		CASE IS >= 6
			LOCALS = 十分茂密的陰毛
		ENDSELECT
		IF LOCALS != ""
			IF RAND:3 == 0
				LOCALS = 就像是將%LOCALS%特意展示出來一樣的設計
			ELSEIF RAND:2
				IF AGE(ARG) >= 3
					LOCALS = %LOCALS%姑且不論、甚至于被包裹的小小陰蒂也能看見
				ELSE
					LOCALS = %LOCALS%姑且不論、甚至于縱筋的上端也能看見
				ENDIF
			ELSE
				LOCALS = %LOCALS%\@ ABL:ARG:露出癖 >= 3 ? 正以最為適合展示的姿態被 # 清楚地彰顯著 \@
			ENDIF
		ELSE
			LOCALS = 股間的布料異常的薄的
		ENDIF
		LOCALS = %LOCALS%低腰胖次
	ELSEIF CHECK_PANTIES("正面蝴蝶結", ARG)
		LOCALS = %TEXTR("被小小的蝴蝶結玩笑似的點綴著的/在前方附著小小的蝴蝶結的/附著可愛的蝴蝶結的")%%CLOCOLOR(CSTR:ARG:胖次, "形容詞")%%TYPE_PANTU%
	ELSEIF CHECK_PANTIES("简约", ARG)
		LOCALS = 装飾感較低的%CLOCOLOR(CSTR:ARG:胖次, "形容詞")%%TYPE_PANTU%
	ELSE
		LOCALS = %PANTIES(ARG)%
	ENDIF
ENDSELECT

RETURNF LOCALS




;-------------------------------------------------
;ARGがARGSのパンツを好きor嫌いかチェックする
;-------------------------------------------------
@FAV_PANTIES(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
RETURNF STRCOUNT(CSTR:ARG:喜欢的胖次, ARGS)

@DISLIKE_PANTIES(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
RETURNF STRCOUNT(CSTR:ARG:讨厌的胖次, ARGS)

;-------------------------------------------------
;ARGのパンツが条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_PANTIES(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF CSTR:ARG:胖次 == ""
	RETURNF 0

SELECTCASE ARGS
CASE "はみ胖次可能"
	SIF STRCOUNTS(CSTR:ARG:胖次, "高開衩", "Ｔ字褲", "Ｃ胖次", "Micro", "前貼", "貞操帯")
		RETURNF 0
	RETURNF 1
CASE "半脱ぎ時太もも保持可能"
	SIF STRCOUNTS(CSTR:ARG:胖次, "Ｃ胖次", "兜襠布", "貞操帯", "前貼")
		RETURNF 0
	RETURNF 1
CASE "ワイド開洞"
	SIF STRCOUNTS(CSTR:ARG:胖次, "鬥士胖次", "简约胖次", "四角褲", "四角内褲")
		RETURNF 1
;主に"アソコのチラ見せ撮影"の判定で使う
CASE "めくり可能"
	;男物パンツは股間の布地が横幅あり過ぎてめくれない
	SIF STRCOUNTS(CSTR:ARG:胖次, "鬥士胖次", "简约胖次", "四角褲", "四角内褲")
		RETURNF 0
	;めくれなさそうなもの
	SIF STRCOUNTS(CSTR:ARG:胖次, "Ｃ胖次", "兜襠布", "貞操帯", "前貼", "Ｖ露出")
		RETURNF 0
	RETURNF 1
;ずれ不可でなく、ずらしていない
CASE "ずらし可", "ずらせる"
	IF STRCOUNTS(CSTR:ARG:胖次, "带拉链的")
		SIF STRCOUNTS(CSTR:ARG:胖次, "带拉链的開")
			RETURNF 0
		SIF STRCOUNTS(CSTR:ARG:胖次, "带拉链的閉")
			RETURNF 1
	ENDIF
	SIF STRCOUNTS(CSTR:ARG:胖次, "移開的")
		RETURNF 0
	SIF STRCOUNTS(CSTR:ARG:胖次, "不可移開")
		RETURNF 0
	RETURNF 1
;ファスナーがあり、あけていない
CASE "带拉链的閉"
	SIF STRCOUNTS(CSTR:ARG:胖次, "带拉链的開")
		RETURNF 0
	SIF STRCOUNTS(CSTR:ARG:胖次, "带拉链的")
		RETURNF 1
CASE "ＶＡ露出"
	SIF STRCOUNT(CSTR:ARG:胖次, "Ｖ露出") && STRCOUNT(CSTR:ARG:胖次, "Ａ露出")
		RETURNF 1
CASE "Ａのみ露出"
	SIF STRCOUNT(CSTR:ARG:胖次, "Ｖ露出") == 0 && STRCOUNT(CSTR:ARG:胖次, "Ａ露出")
		RETURNF 1
CASE "開洞", "Ｖ開洞", "尿開洞"
	SIF STRCOUNTS(CSTR:ARG:胖次, "带拉链的開", "移開的", "破開的", "Ｖ露出", "開衩")
		RETURNF 0
	RETURNF 1
CASE "Ａ開洞"
	SIF STRCOUNTS(CSTR:ARG:胖次, "带拉链的開", "移開的", "破開的", "Ａ露出", "開衩")
		RETURNF 0
	RETURNF 1
CASE "勒紧的"
	SIF STRCOUNTS(CSTR:ARG:胖次, "移開的", "带拉链的開", "Ｖ露出", "開衩", "破開的")
		RETURNF 0
	SIF STRCOUNTS(CSTR:ARG:胖次, "高開衩", "淚珠泳衣", "Micro")
		RETURNF 1
CASE "Ｈ"
	SIF STRCOUNTS(CSTR:ARG:胖次, "Ｈ", "透明", "高開衩", "Ｔ字褲", "带拉链的", "開衩", "開放型", "後開型")
		RETURNF 1
CASE "性感"
	SIF STRCOUNTS(CSTR:ARG:胖次, "Ｈ", "透明", "高開衩", "Ｔ字褲", "带拉链的", "開衩")
		RETURNF 1
	SIF STRCOUNTS(CSTR:ARG:胖次, "ストリング", "Micro", "Ｖ露出", "Ａ露出")
		RETURNF 1
CASE "Micro"
	SIF STRCOUNT(CSTR:ARG:胖次, "Micro")
		RETURNF 1
	SIF STRCOUNT(CSTR:ARG:胖次, "高開衩") && STRCOUNT(CSTR:ARG:胖次, "Ｔ字褲") && STRCOUNT(CSTR:ARG:胖次, "超低腰")
		RETURNF 1
CASE "闷热的"
	SIF STRCOUNTS(CSTR:ARG:胖次, "移開的", "带拉链的開", "Ｖ露出", "開衩", "破開的")
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:胖次, "闷热的")
		RETURNF 1
CASE "带污迹的"
	SIF STRCOUNTS(CSTR:ARG:胖次, "移開的", "带拉链的開", "Ｖ露出", "開衩", "破開的", "通気性没有")
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:胖次, "湿的")
		RETURNF 1
CASE "男物"
	SIF STRCOUNTS(CSTR:ARG:胖次, "鬥士胖次", "简约胖次", "四角褲")
		RETURNF 1
CASEELSE
	SIF STRCOUNT(CSTR:ARG:胖次, ARGS)
		RETURNF 1
ENDSELECT
RETURNF 0
