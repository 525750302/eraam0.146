;-------------------------------------------------
;写真撮影の際に使う関数や写真閲覧用の関数など
;製作者:hexagon
;-------------------------------------------------
;パンツのクローズアップ写真
;-------------------------------------------------
@PHOTO_PANTIES
STR:写真 = 
STR:補足 = 

IF CHECK_CLO("连褲襪露出")
	STR:写真 += @"%OUTER(TARGET)%"
	IF PENIS(TARGET)
		IF CHECK_PANTIES("開洞") == 0
			STR:写真 += @"透过%OUTER(TARGET)%阴茎露出の%CALLNAME:TARGET%の%PANTIES(TARGET)%的特写镜头"
		ELSEIF CHECK_PANTIES("Ａのみ露出")
			STR:写真 += @"透过%OUTER(TARGET)%屁股全部露出の%CALLNAME:TARGET%の%PANTIES(TARGET)%的特写镜头"
		ELSE
			STR:写真 += @"膨胀鼓起の%CALLNAME:TARGET%の%PANTIES(TARGET)%的特写"
		ENDIF
	ELSE
		IF CHECK_PANTIES("開洞") == 0
			STR:写真 += @"阴部透过连裤袜%CALLNAME:TARGET%の%PANTIES(TARGET)%的特写镜头"
		ELSEIF CHECK_PANTIES("Ａのみ露出")
			STR:写真 += @"透过%OUTER(TARGET)%屁股全部露出の%CALLNAME:TARGET%の%PANTIES(TARGET)%的特写镜头"
		ELSE
			STR:写真 += @"能看到%CALLNAME:TARGET%の%PANTIES(TARGET)%的特写"
		ENDIF
	ENDIF
	STR:補足 += "_胖次_特写镜头"
;サービス
ELSEIF COND("阴部のチラ見せ撮影可能") && CHECK_PANTIES("めくり可能") && (CHOSE_DEVENT(STR:日常イベント名) || RAND:3 == 0)
	IF COND("自撮り中")
		STR:写真 += @"翻開%PANTIES(TARGET)%、能看到%TEXTS("割れ目")%の"
	ELSE
		STR:写真 += @"主动翻開%PANTIES(TARGET, "胖次")%、能看见阴部の"
		STR:日常イベント名 = 阴部のチラ見せ撮影
	ENDIF
	SIF CHECK_CLO("上下が揃ってない") == 0
		STR:写真 += @"%PHOTO_COSTUME(TARGET)%"
	STR:写真 += @"%CALLNAME:TARGET%"
	STR:補足 += "_阴部_胖次_特写镜头"
ELSE
	IF COND("自撮り中")
		STR:写真 += @"充满画面的特写镜头：%PANTIES(TARGET)%\@ CHECK_PANTIES("勒紧的") ? 陷入肉中 # 穿着 \@の%CALLNAME:TARGET%の股間"
	ELSE
		IF CHECK_PANTIES("開洞") == 0 || CHECK_PANTIES("Ａ開洞") == 0
			STR:写真 += @"充满画面的特写镜头：穿着%PANTIES(TARGET)%の%CALLNAME:TARGET%の股間"
		ELSE
			STR:写真 += @"充满画面的特写镜头：被%PANTIES(TARGET)%\@ CHECK_PANTIES("勒紧的") ? 陷入肉中 # 包着 \@の%CALLNAME:TARGET%の股間"
		ENDIF
	ENDIF
	STR:補足 += "_胖次_特写镜头"
ENDIF

;-------------------------------------------------
;アソコのクローズアップ写真
;-------------------------------------------------
@PHOTO_PUSSY
#DIMS EVENT_TABLE
STR:写真 = 
STR:補足 = 

;睡眠姦後
IF EXP_GAIN("睡眠姦経験")
	IF CHOSE_DEVENT("睡眠姦Ｖ中出し")
		STR:写真 += @"%TEXTR("被精液弄得黏黏糊糊/精液正在缓缓滴落/到处粘着精液")%"
	ELSE
		STR:写真 += @"%TEXTR("充满愛液的/分泌着愛液的/愛液滑溜溜地流出来")%"
	ENDIF
	STR:写真 += @"の%TEXTR("使用済の/＿")%%CALLNAME:TARGET%の阴部"
;サービス
ELSEIF COND("阴部の見せつけ撮影可能") && (COND("身嗜み：Ｖ拡張") || CHOSE_DEVENT(STR:日常イベント名) || STRCOUNTS(CSTR:DAILYCOM履歴, "Ｖ性交性処理", "Ｖ中出し") || RAND:3 == 0 || CFLAG:躁动难忍|| CFLAG:Ｖ已中出)
	EVENT_TABLE = 汎用/
	IF COND("身嗜み：尿道拡張")
		EVENT_TABLE += "尿道拡張塞子/尿道拡張塞子/"
	ELSEIF ABL:尿道拡張 >= 1
		EVENT_TABLE += "拡張尿道/拡張尿道/"
		SIF EXP:尿道性交経験
			EVENT_TABLE += "拡張尿道/"
	ENDIF
	SIF COND("陰核肥大")
		EVENT_TABLE += "陰核肥大/陰核肥大/"
	SIF TALENT:陰蒂環
		EVENT_TABLE += "陰蒂環/阴蒂穿環/"
	SIF COND("身嗜み：Ｖ拡張")
		EVENT_TABLE += "Ｖ拡張振動棒引き抜き/Ｖ拡張振動棒引き抜き/Ｖ拡張振動棒引き抜き/"
	SIF ABL:Ｖ拡張 >= 5
		EVENT_TABLE += "Ｖ拡張/Ｖ拡張/"

	IF STRCOUNT(CSTR:DAILYCOM履歴, "Ｖ中出し")
		EVENT_TABLE = Ｖ中出し直後
	ELSEIF STRCOUNT(CSTR:DAILYCOM履歴, "Ｖ性交性処理")
		EVENT_TABLE = Ｖ使用直後
	ENDIF

	;ルート確定
	EVENT_TABLE = %TEXTR(EVENT_TABLE)%

	;開くところ
	SELECTCASE EVENT_TABLE
	CASE "Ｖ拡張振動棒引き抜き"
		STR:写真 += @"用手指拨開%TEXTR("陰唇/阴部")%、"
	CASEELSE
		STR:写真 += @"主动打開%TEXTR("重要的地方/阴部")%、"
	ENDSELECT
	;本編
	SELECTCASE EVENT_TABLE
	CASE "Ｖ拡張振動棒引き抜き"
		IF COND("身嗜み：Ｖ拡張") >= 3
			STR:写真 += @"%TEXTR("大口张開的膣穴/大大広张開的膣口/扩张完畢的膣口毫不检点地/能看到里面複雑粘膜的膣口")%敞開著"
		ELSE
			STR:写真 += @"%TEXTR("振動棒拔出之后紧闭的膣穴/未闭合的膣穴/準備万端的膣口")%等待著"
		ENDIF
		STR:補足 += "_膣"
	CASE "Ｖ中出し直後"
		STR:写真 += @"%TEXTR("中出性交之后的膣口/愛液混着精液滴垂的膣口/注入完精液快要溢出的膣口")%敞開著"
		STR:補足 += "_膣"
	CASE "Ｖ使用直後"
		STR:写真 += @"%TEXTR("刚使用过的膣口/刚刚完成工作的膣口")%敞開著"
		STR:補足 += "_膣"
	CASE "Ｖ拡張"
		STR:写真 += @"%TEXTR("大口打開的膣穴/大大拡張開的膣口")%敞開著"
		STR:補足 += "_膣"
	CASE "尿道拡張塞子"
		STR:写真 += "张開用拡張用塞子挿入的尿道口"
		STR:補足 += "_尿道口"
	CASE "拡張尿道"
		IF EXP:尿道性交経験
			STR:写真 += @"打開%TEXTR("拡張到可以性交的/非常松弛的/拡張到可以混淆阴道程度的")%尿道口"
		ELSE
			STR:写真 += @"打開%TEXTR("拡張的/有点松的/大号")%尿道口"
		ENDIF
		STR:補足 += "_尿道口"
	CASE "陰核肥大"
		STR:写真 += @"拨開%TEXTR("肥大的陰核/勃起的陰核/自慢的大栗子")%"
		STR:補足 += "_陰核"
	CASE "陰蒂環"
		IF RAND:2
			STR:写真 += @"拨開%TEXTR("貫穿陰核的/装飾陰核的")%%TEXTS("陰蒂環")%"
		ELSE
			STR:写真 += @"拨開%TEXTS("陰蒂環")%%TEXTR("装飾着的/反光的")%陰核"
		ENDIF
		STR:補足 += "_阴蒂ピ"
	CASEELSE
		IF CHOSE_DEVENT("睡眠姦Ｖ中出し")
			STR:写真 += @"%TEXTR("在睡眠中/不知不觉中")%%TEXTR("注入的/中出的")%精液在无意识张開的膣口上缓慢垂落"
			STR:補足 += "_膣"
		;ELSEIF CFLAG:Ｖ中出し済
		;	STR:写真 += @"%TEXTR("僅かに精液汚れの付いた膣口/精液の残滓が伺える膣口")%を"
		;	STR:補足 += "_膣"
		ELSE
			STR:写真 += @"%TEXTR("内部各个角落/里面細节一览无余的/毫不迟疑地")%"
		ENDIF
	ENDSELECT
	;態度（あっさりと）
	IF TALENT:開朗 && CFLAG:夸奖
		STR:写真 += @"%TEXTR("很平静地/眨着眼")%地展示の"
	ELSEIF TALENT:温柔 && CFLAG:夸奖
		STR:写真 += "露出温柔微笑地展示の"
	ELSEIF TALENT:不知羞恥 && TALENT:強気 + TALENT:高慢 && CFLAG:夸奖
		STR:写真 += @"暴露出来的同时%TEXTR("滲出了優越感/从容笑着/微笑着")%の"
	ELSEIF TALENT:強気 && CFLAG:夸奖
		STR:写真 += "開心笑着地展示の"
	ELSEIF TALENT:害羞 && CFLAG:夸奖
		STR:写真 += "展示时羞涩地微笑着の"
	ELSE
		STR:写真 += "展示给你看の"
	ENDIF
	SIF CHECK_CLO("上下が揃ってない") == 0
		STR:写真 += @"%PHOTO_COSTUME(TARGET)%"
	STR:写真 += @"%CALLNAME:TARGET%"
	STR:日常イベント名 = 阴部の見せつけ撮影
	STR:補足 += "_くぱぁ"

ELSEIF COND("身嗜み：Ｖ拡張") || COND("身嗜み：Ａ拡張") || COND("身嗜み：尿道拡張") 
	;プラグはいくつ入ってる？
	SELECTCASE (COND("身嗜み：Ｖ拡張") > 0) + (COND("身嗜み：Ａ拡張") > 0) + (COND("身嗜み：尿道拡張") > 0)
	CASE 3
		STR:写真 += "所有的洞里全部"
	CASE 2
		IF COND("身嗜み：Ｖ拡張")
			STR:写真 += "膣内和"
			IF COND("身嗜み：Ａ拡張")
				STR:写真 += "肛門内都"
			ELSE
				STR:写真 += "尿道内都"
			ENDIF
		ELSE
			STR:写真 += "肛門和尿道内都"
		ENDIF
	CASE 1
		SIF COND("身嗜み：Ｖ拡張")
			STR:写真 += "膣内"
		SIF COND("身嗜み：Ａ拡張")
			STR:写真 += "肛門内都"
		SIF COND("身嗜み：尿道拡張")
			STR:写真 += "尿道内都"
	ENDSELECT
	STR:写真 += @"被拡張用玩具塞得滿滿の%CALLNAME:TARGET%の股間"
;それ以外
ELSE
	STR:写真 += "充满画面的特写镜头："
	;汗かピアスか陰毛のどれに焦点当てるかみたいな分岐にしてみる
	IF CHECK_STAIN(STAIN:胖次, "湿的", "闷热的") && RAND:3 == 0
		STR:写真 += @"%CALLNAME:TARGET%の闷热的闷热的の"
	ELSEIF CHECK_STAIN(STAIN:Ｖ, "汗") && RAND:3 == 0
		STR:写真 += @"%CALLNAME:TARGET%の汗ばんだ"
	ELSEIF (TALENT:陰蒂環 || TALENT:陰唇環) && RAND:2
		IF COND("陰毛") >= 6 && RAND:2
			STR:写真 += "陰毛中的穿環正在发光"
		ELSE
			IF TALENT:男性
				STR:写真 += "穿環穿透的"
			ELSE
				IF TALENT:陰蒂環
					IF TALENT:扶她
						STR:写真 += "阴茎"
					ELSE
						STR:写真 += "陰核"
					ENDIF
					SIF TALENT:陰唇環
						STR:写真 += "和小陰唇"
				ELSE
					STR:写真 += "小陰唇"
				ENDIF
				STR:写真 += "上被穿環穿透"
			ENDIF
		ENDIF
		STR:写真 += @"%CALLNAME:TARGET%の"
		STR:補足 += "_性器穿環"
	;陰毛表現　既存の箇所をだいぶ参考にしてます…
	ELSE
		SELECTCASE COND("陰毛")
		CASE 1
			STR:写真 += @"%TEXTR("毛の生えていない/僅かな産毛しか生えていない")%"
		CASE 2
			IF COND("黑木耳")
				STR:写真 += @"%TEXTR("一切毛の生えていない/隅々まで脱毛済みの")%"
			ELSE
				STR:写真 += @"%TEXTR("一切毛の生えていない/隅々まで脱毛済みの/脱毛されてスベスベの")%"
			ENDIF
		CASE 3
			STR:写真 += @"%TEXTR("光滑无毛に剃り上げられている/綺麗に剃り上げられた/産毛も残さず剃り上げられている")%"
		CASE 4
			IF CONDS("陰毛") == "無髭"
				STR:写真 += @"%TEXTR("陰毛有些长了/没有剃到的陰毛有些显眼")%"
			ELSEIF CONDS("陰毛") == "剃り跡"
				STR:写真 += @"陰毛剃过的痕迹%TEXTR("很显眼/很有风味")%"
			ELSE
				STR:写真 += @"陰毛%TEXTR("稀疏生长着/不齐地生长着")%"
			ENDIF
		CASE 5
			IF CEVENTS("陰毛の名称") != "" && RAND:2
				STR:写真 += @"陰毛%CEVENTS("陰毛の名称")%剃得很整洁"
			ELSE
				STR:写真 += @"陰毛%TEXTR("经过精心打理过/修剪得很分明")%"
			ENDIF
		CASE IS >= 6
			IF COND("黑木耳")
				STR:写真 += @"%TEXTR("だらしなく陰毛に覆われている/モジャモジャの陰毛に縁どられた")%"
			ELSE
				STR:写真 += @"陰毛%TEXTR("软绵绵地生长着/茂密生长着")%"
			ENDIF
		ENDSELECT
		STR:写真 += @"%CALLNAME:TARGET%の"
	ENDIF
	IF COND("黑木耳")
		IF STRCOUNT(STR:写真, "んだ") == 0
			STR:写真 += @"%TEXTR("黑木耳/黒ずんだ阴部")%"
		ELSE
			STR:写真 += "黑木耳"
		ENDIF
	ELSEIF COND("美穴")
		IF STRCOUNT(STR:補足, "性器穿環")
			STR:写真 += "美穴"
		ELSE
			STR:写真 += @"%TEXTR("美穴/縦スジ")%"
		ENDIF
	ELSEIF COND("萝莉小穴")
		IF STRCOUNT(STR:補足, "性器穿環")
			STR:写真 += @"%TEXTR("萝莉小穴/幼い阴部")%"
		ELSE
			STR:写真 += @"%TEXTR("萝莉小穴/幼い割れ目/縦スジ")%"
		ENDIF
	ELSEIF TALENT:扶她
		STR:写真 += "股間"
	ELSEIF PENIS(TARGET)
		STR:写真 += @"%TEXTS("阴茎：客観表現")%"
	ELSE
		STR:写真 += "阴部"
	ENDIF
ENDIF
SIF PENIS(TARGET)
	STR:補足 += "_阴茎"
SIF TALENT:男性 == 0
	STR:補足 += "_阴部"
STR:補足 += "_特写镜头"


;-------------------------------------------------
;お尻の穴のクローズアップ写真
;-------------------------------------------------
@PHOTO_ANAL
#DIMS EVENT_TABLE
STR:写真 = 
STR:補足 = 

;サービス
IF COND("肛門の見せつけ撮影可能") && (COND("身嗜み：Ａ拡張") || CHOSE_DEVENT(STR:日常イベント名) || STRCOUNTS(CSTR:DAILYCOM履歴, "Ａ性交性処理", "Ａ中出し") || RAND:3 == 0 || CFLAG:躁动难忍|| CFLAG:Ａ已中出)
	EVENT_TABLE = 汎用/
	SIF COND("身嗜み：Ａ拡張")
		EVENT_TABLE += "Ａ拡張振動棒引き抜き/"
	IF STRCOUNT(CSTR:DAILYCOM履歴, "Ａ中出し")
		EVENT_TABLE = Ａ中出し直後
	ELSEIF STRCOUNT(CSTR:DAILYCOM履歴, "Ａ性交性処理")
		EVENT_TABLE = Ａ使用直後
	ENDIF

	;ルート確定
	EVENT_TABLE = %TEXTR(EVENT_TABLE)%

	;開くところ
	STR:写真 += @"主动把屁股扒開、将"
	;本編
	SELECTCASE EVENT_TABLE
	CASE "Ａ拡張振動棒引き抜き"
		IF COND("身嗜み：Ａ拡張") >= 3
			STR:写真 += @"%TEXTR("大口张開的肛門/大大広张開的肛門/淫荡的肛門/直腸内部の色泽新鲜的粘膜")%"
		ELSE
			STR:写真 += @"%TEXTR("肛塞拔出後尚未閉合的肛門/因期待而张開的肛門/準備万端的肛門")%"
		ENDIF
		STR:補足 += "_肛門"
	CASE "Ａ中出し直後"
		STR:写真 += @"%TEXTR("中出性交之後的肛門/汚濁的精液滴落的肛門/刚刚注入的精液溢出的肛門")%"
		STR:補足 += "_肛門"
	CASE "Ａ使用直後"
		STR:写真 += @"%TEXTR("使用之後肛門/刚做完一件大事的肛門")%"
		STR:補足 += "_肛門"
	CASEELSE
		SIF TALENT:縦深菊穴
			STR:写真 += "縦向打開"
		STR:写真 += @"肛門%TEXTR("内部的各个角落/内部的細节一览无余地/毫不吝啬地")%"
	ENDSELECT
	;態度（あっさりと）
	IF TALENT:開朗 && CFLAG:夸奖
		STR:写真 += @"%TEXTR("很平静地/眨着眼")%地展示の"
	ELSEIF TALENT:温柔 && CFLAG:夸奖
		STR:写真 += "露出温柔微笑地展示の"
	ELSEIF TALENT:不知羞恥 && TALENT:強気 + TALENT:高慢 && CFLAG:夸奖
		STR:写真 += "像是在炫耀一样の"
	ELSEIF TALENT:強気 && CFLAG:夸奖
		STR:写真 += "開心笑着地展示の"
	ELSEIF TALENT:害羞 && CFLAG:夸奖
		STR:写真 += "展示时羞涩地微笑着の"
	ELSE
		STR:写真 += "展示给你看の"
	ENDIF
	SIF CHECK_CLO("上下が揃ってない") == 0
		STR:写真 += @"%PHOTO_COSTUME(TARGET)%"
	STR:写真 += @"%CALLNAME:TARGET%"
	STR:日常イベント名 = 肛門の見せつけ撮影
	STR:補足 += "_Ａくぱぁ"
ELSEIF COND("身嗜み：Ａ拡張")
	STR:写真 += @"被拡張用玩具塞得滿滿の%CALLNAME:TARGET%の肛門"
;それ以外
ELSE
	STR:写真 += "充满画面的特写镜头："
	IF STRCOUNT(CSTR:DAILYCOM履歴, "Ａ性交性処理")
		STR:写真 += "使われたばかりで緩み気味の"
	ELSEIF CHECK_STAIN(STAIN:胖次, "湿的", "闷热的")
		STR:写真 += "闷热的闷热的"
	ELSEIF CHECK_STAIN(STAIN:Ｖ, "汗")
		STR:写真 += "汗湿的"
	ELSEIF AGE(TARGET) >= 3
		SELECTCASE COND("陰毛")
		;もさもさ
		CASE 7
			IF RAND:2 && AGE(TARGET) >= 4
				LOCALS = 濃い陰毛に覆われている
			ELSE
				LOCALS = 陰毛で縁取られている
			ENDIF
		CASE 6
			LOCALS = 陰毛で縁取られている
		;綺麗に剃り整えられている
		CASE 5, 3
			LOCALS = %TEXTR("光滑无毛に剃り上げられている/ムダ毛を完璧に処理されている/綺麗に剃りあげられている")%
		;生えかけ
		CASE 4
			IF RAND:2 && AGE(TARGET) >= 4
				LOCALS = 陰毛%TEXTR("が少し伸びてきている/の剃り残しが目立つ")%
			ELSE
				LOCALS = 陰毛%TEXTR("まばらに生えている/生えそろっていない")%
			ENDIF
		;脱毛
		CASE 2
			LOCALS = %TEXTR("脱毛済みの/綺麗に除毛されている/完璧にお手入れされている")%
		ENDSELECT
	ENDIF
	IF TALENT:縦深菊穴
		STR:写真 += @"%CALLNAME:TARGET%の縦深菊穴"
	ELSE
		STR:写真 += @"%CALLNAME:TARGET%の肛門"
	ENDIF
ENDIF
STR:補足 += "_肛門"
STR:補足 += "_特写镜头"

;-------------------------------------------------
;ポーズ撮影。同じポーズは基本的には撮らない
;-------------------------------------------------
@PHOTO_POSING
TFLAG:撮影おしまい = 0

SELECTCASE CSTR:PREV_DAILYCOM
CASE STR:日常イベント名
	SELECTCASE CHOSE_DEVENT("POSE撮影")
	CASE 4
		IF CHARA_NUM("室内の邪魔者") == 0 && COND("POSE撮影中脱衣可能")
			PRINTFORML …差不多可以让%CALLNAME:TARGET%脱掉衣服吧？
			CALL PRINT_SELECT, "拜托试试/还是算了"
			IF RESULT == 0
				CALL PHOTO_DATUI
			ELSE
				PRINTFORML 那么就继续这样写真撮影了…
				PRINTFORML %CALLNAME:TARGET%好像还在继续配合摄影。
			ENDIF
		ELSE
			PRINTFORML %CALLNAME:TARGET%似乎想参与写真撮影。
		ENDIF
	CASE IS >= 2
		PRINTFORML %CALLNAME:TARGET%似乎想参与写真撮影。
	CASEELSE
		PRINTFORML %CALLNAME:TARGET%は似乎想继续参与写真撮影。
	ENDSELECT
	PRINTFORM %CALLNAME:TARGET%
CASEELSE
	PRINTFORML ……%CALLNAME:TARGET%似乎响应了撮影。
	PRINTFORM 镜头对准、%CALLNAME:TARGET%
ENDSELECT

CALL PHOTO_POSING_MAIN

IF TFLAG:撮影おしまい
	PRINTL 
	CLEARLINE 1
	PRINTFORMW ……但是、似乎想不到一个新的好姿势、所以姿势和以前一样。摄影结束。
ELSE
	PRINTW 
ENDIF

;-------------------------------------------------
;二人きりかつ全裸化の合意があれば撮影時に服を脱いでくれる
;状況によっては断られるますが褒めてごり押しも可能
;-------------------------------------------------
@PHOTO_DATUI
#DIMS EVENT_TABLE

;脱ぐ服を決める
PRINTL 
PRINTFORML …要穿成什么样呢？

EVENT_TABLE = %MENU_PHOTO_DATUI(TARGET)%
VARSET LOCALS
SPLIT EVENT_TABLE, "/", LOCALS
CALL PRINT_SELECT, EVENT_TABLE

SELECTCASE LOCALS:RESULT
CASE "全裸"
	;CALL EMOTION_PLUS, "小悪魔", TARGET
	CALL EVENT_DATUI, "裸体"
	CALL SETFLAG, "全裸化", TARGET
CASE  "内衣姿"
	CALL EVENT_DATUI, "衣服"
CASE "拜托脱掉裙子"
	PRINTFORMW %CALLNAME:TARGET%脱下了%SKIRT(TARGET)%。
	CALL SET_SKIRT, ""
	SIF COND("胖次形状確認") == 0
		CALL TEXT_PANTU_CHECK_SHORT
CASE "拜托脱掉胖次", "布鲁马を脱いでもらう", "泳装を脱いでもらう"
	SIF COND("胖次形状確認") == 0
		CALL TEXT_PANTU_CHECK_SHORT
	CALL EVENT_DATUI, "胖次"
CASEELSE
	PRINTFORMW 就以现在的衣装继续拍摄吧。
ENDSELECT
PRINTL 
PRINTFORM 转向手机的

@MENU_PHOTO_DATUI(ARG)
#FUNCTIONS
#DIMS EVENT_TABLE

SIF COND("合意：Ｈ的写真", ARG) == 0
	RETURNF ""

EVENT_TABLE = 
SIF CHECK_CLO("裸体", ARG) == 0
	EVENT_TABLE += "全裸/"
SIF EQUIP:ARG:胸罩 && EQUIP:ARG:胖次 && CHECK_CLO("内衣姿", ARG) == 0
	EVENT_TABLE += "内衣姿/"
SIF CHECK_CLO("裙脱衣可能", ARG)
	EVENT_TABLE += "拜托脱掉裙子/"
SIF CHECK_CLO("胖次露出", ARG) && EQUIP:ARG:胖次 && CHECK_CLO("泳装姿", ARG) == 0
	EVENT_TABLE += @"%PANTIES(ARG, "胖次")%を脱いでもらう/"

SIF EVENT_TABLE != ""
	EVENT_TABLE += "还是这样就好/"
RETURNF EVENT_TABLE

;-------------------------------------------------
;各ポーズは STR:補足 でジャンル分け
;既にしたポーズはとらない＆新たなポーズ思い浮かばない場合には TFLAG:撮影おしまい = 1 として終わり！
;-------------------------------------------------
@PHOTO_POSING_MAIN
#DIM LCOUNT
#DIMS EVENT_TABLE
#DIMS MEMO_EVENT_TABLE
#DIMS EVENT_CHOICE
;これがONだとコス描写を出さない
#DIM IGNORE_COSTUME
STR:写真 = 
STR:補足 = 

IGNORE_COSTUME = 0
EVENT_TABLE = 
;ポーズは徐々にエロくなっていくみたいな
;特殊な衣装のポーズは起きやすくする
IF COND("POSE撮影進行度") >= 3
	;股間強調
	IF CHECK_CLO("Ｖ露出") && ABL:露出癖 >= 5 && EQUIP:裙 == 0
		SIF PENIS(TARGET)
			EVENT_TABLE += "阴茎見せつけ/"
		SIF TALENT:男性 == 0
			EVENT_TABLE += "立ちくぱぁ/"
	ENDIF

	;日焼け跡と水着の形状が同じ
	SIF CHECK_CLO("泳装姿") && CSTR:晒痕 != "" && (TEXTS("晒痕") == @"%SWIM(CSTR:贴身制服, "種類")%" || TEXTS("晒痕") == @"%SWIM(CSTR:胖次, "種類")%")
		EVENT_TABLE += "晒痕上/晒痕下/"

	SIF CHECK_CLO("裸体") || CHECK_CLO("泳装姿") || CHECK_CLO("内衣姿")
		EVENT_TABLE += "全身見せつけ/"
	SIF CHECK_CLO("乳頭露出") == 0 && EQUIP:贴身制服 + EQUIP:服 + EQUIP:胸罩
		EVENT_TABLE += "胸部見せつけ/"
	SIF CHECK_CLO("乳頭露出")
		EVENT_TABLE += "手胸罩/"
	
	;パンチラ
	IF EQUIP:裙 && CHECK_CLO("胖次視認可能")
		EVENT_TABLE += "たくし上げ胖次チラ/"
		;Ｔバックだと後ろを向く
		SIF CHECK_PANTIES("Ｔ字褲")
			EVENT_TABLE += "Ｔ字褲胖次チラ/"
		SIF COND("脚が無い") == 0
			EVENT_TABLE += "両脚持ち上げ胖次チラ/"
		SIF COND("脚が無い") == 0 && CHECK_SKIRT("迷你")
			EVENT_TABLE += "ハイキック胖次チラ/うつ伏せ胖次チラ/"
		SIF COND("脚が無い") == 0 && CHECK_SKIRT("長") == 0 && CHECK_SHIRT("旗袍") == 0
			EVENT_TABLE += "片膝立て胖次チラ/"
	ENDIF

	SIF (TALENT:好色 || TALENT:淫乱) && BASE:欲求不満 >= 50
		EVENT_TABLE += "まんぐり返し/"
	SIF TALENT:淫壷
		EVENT_TABLE += "お腹爱心/"
	SIF ABL:舌靈活 >= 3 && (TALENT:淫乱 || TALENT:好色)
		EVENT_TABLE += "指口交/"
ENDIF
;進行度２以上なら１以下の進行度のものは行わない
IF COND("POSE撮影進行度") >= 2
	EVENT_TABLE += "お尻突き出し/"
	SIF TALENT:巨乳
		EVENT_TABLE += "巨乳強調/"
	SIF CHECK_CLO("腋露出")
		EVENT_TABLE += "腋見せつけ/腋見せつけ/腋見せつけ/"
	SIF CHECK_CLO("低腰臀圍")
		EVENT_TABLE += "低腰臀圍/低腰臀圍/低腰臀圍/"
	SIF CHECK_CLO("泳装姿") || CHECK_CLO("内衣姿") || CHECK_CLO("裸体") || CHECK_BODYS("谷間露出")
		EVENT_TABLE += "胸部強調/"
	SIF TALENT:女性 && EQUIP:裙 == 0 && CHECK_OUTER("長褲") == 0 && CHECK_CLO("Ｖ露出") == 0
		EVENT_TABLE += "恥丘強調/"
	SIF CHECK_SHIRT("和服")
		EVENT_TABLE += "和服半脱/和服半脱/"
	;脚主体
	IF COND("脚が無い") == 0
		SIF (CHECK_OUTER("连褲襪") || CHECK_OUTER("热褲") || EQUIP:外套 == 0) && (CHECK_SKIRT("迷你") || EQUIP:裙 == 0)
			EVENT_TABLE += "脚見せつけ/"
		SIF CHECK_SHIRT("体操服")
			EVENT_TABLE += "開脚ストレッチ/開脚ストレッチ/開脚ストレッチ/"
		;チャイナ　膝立ての場合ぱんつ隠れそうな予感
		SIF CHECK_SHIRT("旗袍")
			EVENT_TABLE += "チャイナ片膝立て/チャイナ開衩/チャイナ開衩/チャイナカンフー/チャイナカンフー/"
		SIF CHECK_SHIRT("和服")
			EVENT_TABLE += "和服脚見せつけ/和服脚見せつけ/"
		;袴　足無い人の袴はほぼロングスカートだろう、たぶん
		SIF CHECK_SKIRT("袴")
			EVENT_TABLE += "袴脚見せつけ/袴脚見せつけ/"
	ENDIF
ENDIF
IF COND("POSE撮影進行度")
	EVENT_TABLE += "寝転がる/座る/"
	SIF CHECK_BRA("猫型胸罩") && CHECK_PANTIES("肉球印花胖次")
		EVENT_TABLE += "猫内衣/ネコランジェリー/猫内衣/"
	SIF CHECK_SHIRT("水手夏服")
		EVENT_TABLE += "水手夏服/水手夏服/水手夏服/"
	SIF CHECK_SHIRT("体操服")
		EVENT_TABLE += "肩ストレッチ/肩ストレッチ/肩ストレッチ/"
	IF CHECK_SHIRT("啦啦队制服")
		EVENT_TABLE += "チアコス/チアコス/チアコス/"
		SIF TALENT:巨乳
			EVENT_TABLE += "チアコス胸部/チアコス胸部/チアコス胸部/"
	ENDIF
	SIF COND("脚が無い") == 0
		EVENT_TABLE += "女豹のポーズ/"
;前段階のエロくないポーズ？
ELSE
	;マタニティヌード的な
	IF COND("妊娠中：臨月") && (CHECK_CLO("裸体") || CHECK_CLO("内衣姿"))
		EVENT_TABLE += "妊婦写真"
	ELSE
		EVENT_TABLE += "腰を下ろす/振り返る/ピース/"
		SIF CHECK_CLO("裸体")
			EVENT_TABLE += "体を隠す/"
		SIF CHECK_SKIRT("裙")
			EVENT_TABLE += "裙つまみ/"
		SIF TALENT:害羞 || TALENT:不知羞恥 || TALENT:小悪魔 || TALENT:好色
			EVENT_TABLE += "恥じらう/"
	ENDIF
ENDIF

;とってくれるポーズ内容の調整
MEMO_EVENT_TABLE = %EVENT_TABLE%
VARSET LOCALS
SPLIT EVENT_TABLE, "/", LOCALS
EVENT_TABLE = 

FOR LCOUNT, 0, 100
	SIF LOCALS:LCOUNT == ""
		BREAK
	;同じポーズはとらない
	SIF STRCOUNT(SAVESTR:この時間のPOSE撮影履歴, @"%CALLNAME:TARGET%_%LOCALS:LCOUNT%\/")
		CONTINUE
	EVENT_TABLE += @"%LOCALS:LCOUNT%\/"
NEXT

;ポーズをやりつくした場合
IF EVENT_TABLE == ""
	EVENT_TABLE = %MEMO_EVENT_TABLE%
	TFLAG:撮影おしまい = 1
ENDIF

EVENT_CHOICE = %TEXTR(EVENT_TABLE)%

;ポーズ描写
SELECTCASE EVENT_CHOICE
;ポーズ撮影進行度3
CASE "阴茎見せつけ"
	PRINTFORM %PHOTO_SHOWING()%的腰伸了过来…
	STR:写真 +="面朝这边"
	SIF BASE:欲求不満 >= 50 && ABL:露出癖 >= 3
		STR:写真 += "興奮で硬くなっている"
	SELECTCASE SIZE("阴茎", TARGET)
	CASE 0
		STR:写真 += @"%TEXTR("小小的/小粒的/可爱的")%"
	CASE 1
		STR:写真 += ""
	CASE IS >= 2
		STR:写真 += @"%TEXTR("立派的/大号的")%"
	CASE IS >= 4
		STR:写真 += @"%TEXTR("巨大的/凶悪的")%"
	ENDSELECT
	SIF TALENT:扶她
		STR:写真 += "扶她"
	STR:写真 += "阴茎显露出来の"
	;ポーズ解説が脱衣前提なので格好次第でコス描写を省く
	SIF CHECK_CLO("上下が揃ってない")
		IGNORE_COSTUME = 1
CASE "立ちくぱぁ"
	IF RAND:2 && !TALENT:扶她
		PRINTFORM %PHOTO_SHOWING()%向阴部伸出手…
		STR:写真 += "站着用手指打開自己的阴部の"
	ELSE
		PRINTFORM 向后看并把屁股面向这边…
		STR:写真 += "回头看向这边、一边抬起屁股一边用手指打開阴部の"
	ENDIF
	;ポーズ解説が脱衣前提なので格好次第でコス描写を省く
	SIF CHECK_CLO("上下が揃ってない")
		IGNORE_COSTUME = 1

CASE "晒痕上"
	IF CHECK_BODYS("競泳泳装")
		PRINTFORM 的手搭在競泳泳装的肩带上…
		STR:写真 += "肩带"
	ELSEIF CHECK_CLO("比基尼姿")
		PRINTFORM 的手搭在%BRA(TARGET)%的肩带上…
		STR:写真 += "胸罩紐带"
	ELSE
		PRINTFORM 的手搭在泳装附近、移開布料…
		STR:写真 += "泳装布地"
	ENDIF
	STR:写真 += "移開后晒痕暴露出来の"
	;ポーズ解説が脱衣前提なので格好次第でコス描写を省く
	SIF CHECK_CLO("上下が揃ってない")
		IGNORE_COSTUME = 1
CASE "晒痕下"
	IF CHECK_CLO("比基尼姿")
		PRINTFORM 的拇指放在%PANTIES(TARGET)%上…
		STR:写真 += "胖次"
		SIF CHECK_PANTIES("Micro")
			STR:写真 += "紐带"
		STR:写真 += "移開后腰间的"
	ELSE
		PRINTFORM 向下半身伸出手、移開泳装…
		STR:写真 += @"布料挪開后%TEXTR("鼠径部上/腹股沟上")%的"
	ENDIF
	STR:写真 += "晒痕暴露出来の"
	;ポーズ解説が脱衣前提なので格好次第でコス描写を省く
	SIF CHECK_CLO("上下が揃ってない")
		IGNORE_COSTUME = 1
CASE "全身見せつけ"
	;ハダカかつ日焼け跡あり
	IF CHECK_CLO("裸体") && CSTR:晒痕 != ""
		PRINTFORM 摆出\@ TALENT:好色 || TALENT:小悪魔 ? 展示 # 能看到 \@肢体上的晒痕的姿势…
		STR:写真 += @"身体上%TEXTS("晒痕")%的晒痕清楚地残留着の"
	ELSEIF RAND:2
		PRINTFORM %PHOTO_SHOWING()%両手広開…
		STR:写真 += "双手张開、身体暴露着の"
	ELSE
		PRINTFORM %PHOTO_SHOWING()%摆出双手叉腰的姿势…
		STR:写真 += "双手撑腰、露出身体の"
	ENDIF
CASE "胸部見せつけ"
	IF EQUIP:贴身制服
		IF CHECK_CLO("泳装姿")
			IF RAND:2 && TALENT:巨乳
				PRINTFORM 的手搭在泳装两边的部分、把布料向中间移動…
				STR:写真 += "胸上两边的布料挤进乳沟、"
			ELSE
				PRINTFORM 的手搭在泳装肩紐帶上…
				STR:写真 += "肩带解開、布料滑落后"
			ENDIF
		;多分バニーとかはここ
		ELSE
			PRINTFORM 把手掛在%BODYS(TARGET)%的胸前…
			STR:写真 += "衣服滑落、"
		ENDIF
		STR:写真 += @"\@ TALENT:男性 ? 胸 # 胸部 \@全部露出の"
		;ポーズ解説が脱衣前提なので格好次第でコス描写を省く
		SIF CHECK_CLO("上下が揃ってない")
			IGNORE_COSTUME = 1
	ELSEIF EQUIP:服
		;下からまくるorボタンを外すor胸元を広げる
		IF CHECK_SHIRT("提起裙角")
			PRINTFORM 把手移到%PHOTO_SHOWING()%%SHIRT(TARGET)%的下摆…
			;服めくってキャミソール見せるだけってのもなぁ、と考えた結果
			SIF CHECK_BRA("吊帯衫")
				STR:写真 += @"%BRA(TARGET)%和"
			STR:写真 += "衣服卷起"
			IF CHECK_BRA("吊帯衫") || EQUIP:胸罩 == 0
				STR:写真 += "胸部完全露出、"
			ELSEIF CHECK_BRA("乳頭露出")
				STR:写真 = @"%SHIRT(TARGET)%向上卷起"
				IF CHECK_BRA("１／４杯胸罩")
					STR:写真 += @"可以透过%BRA(TARGET)%看到胸部の"
				ELSE
					STR:写真 += @"穿着%BRA(TARGET)%乳頭露出の"
				ENDIF
				;ポーズ解説にコス描写が含まれるのでキャラ解説の方のコス描写は省く
				IGNORE_COSTUME = 1
			ELSE
				STR:写真 += @"%BRA(TARGET)%露出の"
			ENDIF
			;ポーズ解説が脱衣前提なので格好次第でコス描写を省く
			SIF CHECK_CLO("上下が揃ってない")
				IGNORE_COSTUME = 1
		ELSEIF CHECK_SHIRT("宽大的Ｔ恤") || CHECK_SHIRT("%CALLNAME:MASTER%の衬衫")
			IF CHECK_SHIRT("宽大的Ｔ恤")
				PRINTFORM 抓住%SHIRT(TARGET)%的下摆、一口气抬了上去…
				STR:写真 += @"%SHIRT(TARGET)%翻到胸部、"
			ELSEIF CHECK_SHIRT("%CALLNAME:MASTER%の衬衫")
				PRINTFORM 手搭在%SHIRT(TARGET)%的扣子上…
				STR:写真 += @"解開%SHIRT(TARGET)%扣子"
				IF ABL:露出癖 >= 5
					STR:写真 += "左右大大张開、"
				ELSE
					STR:写真 += "向前打開、"
				ENDIF
			ENDIF
			IF EQUIP:胸罩 && EQUIP:胖次
				STR:写真 += "内衣姿"
			ELSEIF EQUIP:胖次
				STR:写真 += @"只穿着一枚%PANTIES(TARGET, "胖次")%的姿态"
			ELSEIF CHECK_CLO("裸体")
				STR:写真 += "衣服下的裸体"
			ENDIF
			STR:写真 += "表露出来的%CALLNAME:TARGET%"
			IGNORE_COSTUME = 1
		ELSEIF CHECK_SHIRT("女式衬衫")
			PRINTFORM %PHOTO_SHOWING()%%SHIRT(TARGET)%的紐扣用手解開…
			STR:写真 += @"解開%SHIRT(TARGET)%的扣子、"
			IF TALENT:巨乳
				STR:写真 += "乳沟"
			ELSE
				STR:写真 += "胸部大胆地"
			ENDIF
			STR:写真 += "暴露在外面の"
			;ポーズ解説にコス描写が含まれるのでキャラ解説の方のコス描写は省く
			IGNORE_COSTUME = 1
		ELSE
			PRINTFORM %PHOTO_SHOWING()%的手搭在%SHIRT(TARGET)%的胸前…
			STR:写真 += @"%SHIRT(TARGET)%半脱、"
			IF EQUIP:胸罩 == 0
				STR:写真 += "胸部完全露出、"
			ELSEIF CHECK_BRA("乳頭露出")
				IF CHECK_BRA("１／４杯胸罩")
					STR:写真 += @"可以透过%BRA(TARGET)%看到胸部の"
				ELSE
					STR:写真 += @"穿着%BRA(TARGET)%乳頭露出の"
				ENDIF
			ELSE
				STR:写真 += @"%BRA(TARGET)%露出の"
			ENDIF
			;ポーズ解説にコス描写が含まれるのでキャラ解説の方のコス描写は省く
			IGNORE_COSTUME = 1
		ENDIF
	;ブラあり
	ELSEIF EQUIP:胸罩
		PRINTFORM %PHOTO_SHOWING()%把%BRA(TARGET)%
		IF CHECK_BRA("Micro") && CHECK_BRA("乳頭露出") == 0 && RAND:2
			PRINTFORM 少量移開…
			STR:写真 += "移開胸罩、"
			IF COND("陥没乳頭")
				STR:写真 += "陥没"
			ELSE
				SIF TALENT:乳环
					STR:写真 += "带着乳環的"
				SIF COND("乳頭肥大")
					STR:写真 += "大号的"
			ENDIF
			STR:写真 += "乳頭"
		ELSE
			IF CHECK_BRA("乳頭露出")
				PRINTFORM 挺起胸…
				STR:写真 += @"身穿%BRA(TARGET)%、"
				;ポーズ解説にコス描写が含まれるのでキャラ解説の方のコス描写は省く
				IGNORE_COSTUME = 1
			ELSE
				PRINTFORM 向上梳起…
				STR:写真 += @"胸罩向上移開、"
			ENDIF
			IF TALENT:巨乳 >= 2
				STR:写真 += @"%TEXTR("像硕果一样/漂亮形状的")%"
			ELSEIF TALENT:巨乳 == 1
				STR:写真 += @"%TEXTR("大号的/柔软的")%"
			ELSEIF TALENT:貧乳
				STR:写真 += @"%TEXTR("小不点/大小谨慎的")%"
			ELSE
				STR:写真 += @"%TEXTR("形状优美的/曲线优美的")%"
			ENDIF
			STR:写真 += @"\@ TALENT:男性 ? 胸 # 胸部 \@"
		ENDIF
		STR:写真 += @"を%TEXTR("露出/被看到/暴露在外")%"
	ENDIF
	STR:補足 += "_胸罩"
CASE "手胸罩"
	PRINTFORM 羞恥地用両只手遮住胸…
	STR:写真 += @"双手交叉遮住両边%TEXTR("胸/乳頭")%"
	;ポーズ解説が脱衣前提なので格好次第でコス描写を省く
	SIF CHECK_BRA("１／４杯胸罩")
		IGNORE_COSTUME = 1
	STR:補足 += "_胸罩"
CASE "Ｔ字褲胖次チラ"
	;ロング以外なら尻尾で持ち上げる？
	IF RAND:3 == 0 && TALENT:尾巴 && TALENT:尾巴 != 4 && CHECK_SKIRT("長") == 0
		PRINTFORM 看向後方把尾巴立起…
		STR:写真 += @"尾巴把%SKIRT(TARGET, "略")%撩起、"
	;うつ伏せ
	ELSEIF RAND:2 && CHECK_SKIRT("迷你")
		PRINTFORM 习惯性地趴倒在%NAMES("床")%上…
		STR:写真 += @"趴在%NAMES("床")%上把屁股朝向这边、"
	ELSE
		PRINTFORM 看向後方用手把%SKIRT(TARGET, "略")%掀開…
		STR:写真 += @"%SKIRT(TARGET, "略")%翻起、"
	ENDIF
	;パンスト描写
	IF CHECK_CLO("连褲襪露出")
		IF COND("身嗜み：Ａ拡張")
			STR:写真 += @"%OUTER(TARGET)%和"
		ELSE
			STR:写真 += @"隔著%OUTER(TARGET)%露出"
		ENDIF
	ENDIF
	;アナルプラグはあるか？
	IF COND("身嗜み：Ａ拡張")
		STR:写真 += @"%PANTIES(TARGET)%挿着塞子的"
	ELSE
		STR:写真 += @"将%PANTIES(TARGET)%吞入的"
	ENDIF
	STR:写真 += "尻暴露在外面"
	STR:補足 += "_胖次"
CASE "たくし上げ胖次チラ", "ハイキック胖次チラ", "うつ伏せ胖次チラ", "片膝立て胖次チラ", "両脚持ち上げ胖次チラ"
	SELECTCASE EVENT_CHOICE
	CASE "たくし上げ胖次チラ"
		PRINTFORM %PHOTO_SHOWING()%開始把%SKIRT(TARGET, "略")%向上抬起…
		;ワンピースはお腹のとこまでめくる？
		SIF CHECK_SHIRT("連衣裙") && RAND:2
			STR:写真 += @"到能看见%TEXTR("小腹/肚脐")%的程度"
		STR:写真 += @"%SKIRT(TARGET, "略")%向上卷起"
	CASE "ハイキック胖次チラ"
		PRINTFORM 確認周囲后单腿大幅度摇動著…
		STR:写真 += "一条腿高高抬起、"
	CASE "うつ伏せ胖次チラ"
		PRINTFORM 习惯性地趴倒在%NAMES("床")%上…
		STR:写真 += @"趴在%NAMES("床")%上"
		IF CHECK_CLO("Ｖ露出") || CHECK_PANTIES("前貼")
			STR:写真 += "屁股高抬、"
		ELSE
			STR:写真 += "屁股朝向这边、"
		ENDIF
	CASE "片膝立て胖次チラ"
		PRINTFORM 在%NAMES("床")%上坐下、抬起膝盖将%CLO("掀起裙子")%撩起…
		STR:写真 += "单膝坐下、"
	CASE "両脚持ち上げ胖次チラ"
		PRINTFORM 躺倒在%NAMES("床")%上抬起了両足…
		STR:写真 += "并拢的両腿抬起、"
	ENDSELECT
	;パンスト描写
	SIF CHECK_CLO("连褲襪露出")
		STR:写真 += @"隔著%OUTER(TARGET)%露出"
	IF EQUIP:胖次 == 0
		IF COND("身嗜み：Ｖ拡張")
			STR:写真 += "拡張用振動棒挿入的"
		ELSEIF CHECK_CLO("Ｖ露出")
			STR:写真 += "赤裸的"
		ENDIF
		IF PENIS(TARGET) && !(TALENT:扶她 && COND("身嗜み：Ｖ拡張"))
			;なんか見える箇所が変わるイメージで
			IF EVENT_CHOICE == "両脚持ち上げ胖次チラ"
				IF TALENT:男性
					SIF SIZE("阴茎", TARGET) == 0
						STR:写真 += "可爱的"
					STR:写真 += "睾丸"
				ELSE
					STR:写真 += "阴部"
					STR:補足 += "_阴部"
				ENDIF	
			ELSE
				STR:写真 += "阴茎"
				STR:補足 += "_阴茎"
			ENDIF
		ELSE
			STR:写真 += "阴部"
			STR:補足 += "_阴部"
		ENDIF
	ELSE
		STR:写真 += @"%PANTIES(TARGET)%"
		STR:補足 += "_胖次"
	ENDIF
	STR:写真 += "暴露在外"
CASE "まんぐり返し"
	PRINTFORM 躺在%NAMES("床")%上抱住両足…
	STR:写真 += @"在%NAMES("床")%上翻滚的姿勢"
	IF (EQUIP:裙 && CHECK_CLO("胖次視認可能")) || CHECK_CLO("Ｖ露出")
		STR:写真 += "、"
		IF EQUIP:胖次
			STR:写真 += @"%PANTIES(TARGET)%"
		ELSE
			IF PENIS(TARGET)
				STR:写真 += "阴茎"
			ELSE
				SIF COND("陰毛") <= 3
					STR:写真 += "光滑的"
				STR:写真 += "阴部"
			ENDIF
		ENDIF
		STR:写真 += "露出着"
		SIF CHECK_CLO("裸体") == 0
			IGNORE_COSTUME = 1
	ELSE
		STR:写真 += ""
	ENDIF
CASE "お腹爱心"
	PRINTFORM %PHOTO_SHOWING()%摆好了姿势……
	STR:写真 += @"用手比作心型贴在腹部\@ CHECK_CLO("淫紋露出") ? 雕刻的淫紋上 # \@"
CASE "指口交"
	PRINTFORM %PHOTO_SHOWING()%摆好了姿势……
	STR:写真 += @"%TEXTR("自己的手指伸进嘴里和舌头纠缠/手指比作环状、放在伸出的舌头前")%"

;ポーズ撮影進行度2
CASE "お尻突き出し"
	PRINTFORM %PHOTO_SHOWING()%摆好了姿势……
	IF CHECK_SKIRT("超短迷你")
		STR:写真 += @"朝这边撅起臀部、能从%SKIRT(TARGET,"略")%中看到%CLOCOLOR(CSTR:胖次, "形容詞")%%PANTIES(TARGET, "胖次")%胖次"
		IGNORE_COSTUME = 1
	ELSE
		IF TALENT:好色 || (TALENT:淫乱 && RAND:2)
			STR:写真 += @"像是为了%TEXTR("誘惑/挑発")%的"
		ELSE
			STR:写真 += "朝向这边"
		ENDIF
		IF CHECK_CLO("体操服姿") && CHECK_OUTER("布鲁马")
			STR:写真 += "被布鲁马包住的"
			IGNORE_COSTUME = 1
		ELSEIF EQUIP:胖次 && CHECK_CLO("胖次が見える格好") && CHECK_PANTIES("前貼") == 0
			STR:写真 += @"%PANTIES(TARGET)%"
			IF CHECK_PANTIES("Ｔ字褲")
				STR:写真 += "勒入的"
			ELSE
				STR:写真 += "包住的"
			ENDIF
			IGNORE_COSTUME = 1
		ENDIF
		IF TALENT:巨尻 && RAND:2
			STR:写真 += @"%TEXTR("大/安産型")%"
		ELSEIF TALENT:小尻 && RAND:2
			STR:写真 += @"%TEXTR("小巧的/可愛的")%"
		ENDIF
		STR:写真 += "屁股伸出"
	ENDIF
CASE "巨乳強調"
	IF RAND:2
		PRINTFORM %PHOTO_SHOWING()%摆好了姿势……
		;のせる
		IF PLACE("部室") && RAND:3 == 0
			STR:写真 += "将沉重的胸部放在桌子上坐着"
		ELSE
			STR:写真 += @"正在%TEXTR("揉搓/用手托着")%自己丰硕的胸"
		ENDIF
	ELSE
		IF RAND:2
			PRINTFORM 向前傾做出用手戳膝盖的姿势…
			STR:写真 += "弯下腰"
		ELSE
			PRINTFORM 做出抱着胳膊抬着胸部的姿势…
			STR:写真 += "抱着胳膊"
		ENDIF
		STR:写真 += @"紧紧抓住%TEXTR("胸/胸部")%、\@ CHECK_BRA("猫型胸罩") ? 从猫型的镂空中 # \@強調乳沟"
	ENDIF
CASE "恥丘強調"
	IF CHECK_CLO("体操服姿") && TALENT:大力士
		PRINTFORM 仰面躺下后突然做出挺腰的動作…
		STR:写真 += "以拱桥的体勢"
	ELSEIF RAND:2
		PRINTFORM 坐在椅子上両膝并立…
		STR:写真 += "以Ｍ字開脚的姿勢"
	ELSE
		PRINTFORM 的手放在桌子上、将体重向后寄托伸出腰部…
		STR:写真 += "上半身像弓一样拉伸"
	ENDIF
	STR:写真 += "展示着恥丘的盛况"
CASE "腋見せつけ"
	PRINTFORM 両手大幅度举起、向頭的後方移动…
	STR:写真 += @"双手抱在脑后%TEXTS("腋：視覚")%被看到"
CASE "低腰臀圍"
	PRINTFORM 转身、蹲了下去…
	STR:写真 += @"转身蹲下、%OUTER(TARGET)%包裹着的"
	IF EQUIP:胖次 == 0 || CHECK_PANTIES("超低腰") || CHECK_PANTIES("Ｃ胖次") || CHECK_PANTIES("前貼")
		SIF TALENT:巨尻
			STR:写真 += "肥美的"
		STR:写真 += "臀部一小部分"
		CALL SETFLAG_DAILY, "胖次おぼろげ確認"
	ELSE
		SIF CHECK_PANTIES("低腰")
			STR:写真 += "一小部分"
		STR:写真 += @"%PANTIES(TARGET)%"
		CALL SETFLAG_DAILY, "胖次色確認"
	ENDIF
	STR:写真 += "暴露在外の"
CASE "胸部強調"
	PRINTFORM 调整到横向、
	IF RAND:2
		PRINTFORM 把手搭在腰上…
		STR:写真 += "手臂向后拉伸"
	ELSE
		PRINTFORM 上半身后仰…
		STR:写真 += "大大仰起身体"
	ENDIF
	STR:写真 += "強調横乳线条"
CASE "和服半脱"
	PRINTFORM 的手搭在%SHIRT(TARGET)%的衣领上…
	STR:写真 += @"%SHIRT(TARGET)%的领口打開到%TEXTR("最大极限/能看到胸口的极限")%"
	;ポーズ解説にコス描写が含まれるのでキャラ解説の方のコス描写は省く
	IGNORE_COSTUME = 1
CASE "脚見せつけ"
	PRINTFORM 迈出一步并决定好了姿势…
	;スカート
	SIF EQUIP:裙
		STR:写真 += @"从%SKIRT(TARGET, "略")%里伸出"
	;なんか穿いてる
	IF CHECK_SOCKS("直到大腿")
		STR:写真 += @"%SOCKS(TARGET)%包裹住的"
	ELSEIF CHECK_OUTER("连褲襪")
		STR:写真 += @"%OUTER(TARGET)%包裹住的"
	;日焼け
	ELSEIF CSTR:晒痕 != ""
		IF CFLAG:晒痕強度 >= 20
			STR:写真 += "晒得黝黑的"
		ELSE
			STR:写真 += "拥有健康晒痕的"
		ENDIF
	;ここから生足
	ELSE
		;脚の太さ
		IF TALENT:娇小 || AGE(TARGET) == 1
			;とりあえずウェストで判定？
			IF CONDS("腰圍") == "太め"
				STR:写真 += @"%TEXTR("婴儿肥/胖乎乎")%的"
			ELSE
				STR:写真 += @"%TEXTR("纤细而奢华的/柔弱的")%"
			ENDIF
		ELSEIF STRCOUNTS(CONDS("体型"), "苗条", "高挑")
			STR:写真 += @"%TEXTR("苗条/流线型")%的"
		ELSEIF STRCOUNTS(CONDS("体型"), "S型", "豊満", "肉感", "豐腴")
			STR:写真 += @"%TEXTR("丰满/肉感")%的"
		ELSE
			STR:写真 += @"%TEXTR("胖瘦刚刚好的/綺麗的")%"
		ENDIF
		;脚の色
		SELECTCASE CONDS("肤色")
		CASE "褐色肌"
			STR:写真 += "小麦色的"
		CASE "色白"
			STR:写真 += "白色的"
		CASE "青肌"
			STR:写真 += "青色的"
		ENDSELECT
	ENDIF
	STR:写真 += "腿交叉站着"
CASE "開脚ストレッチ"
	IF TALENT:大力士 && RAND:2
		PRINTFORM 站立着高高抬起一只脚、用手抓着固定住了…
		STR:写真 += "脚高高抬起、展示Ｙ字平衡"
	ELSE
		PRINTFORM 坐在地板上打開双腿…
		STR:写真 += "大腿张開、做着股関节伸展运动"
	ENDIF
CASE "チャイナ片膝立て"
	PRINTFORM 在%NAMES("床")%上坐下、单膝抬起動作著…
	STR:写真 += @"单膝跪坐、以差一点就能看到内衣的体勢下坐下"
CASE "チャイナ開衩"
	IF RAND:2 || CHECK_PANTIES("紐")
		PRINTFORM 侧向站在镜头前、用手将開衩处稍微上提…
		STR:写真 += "面向侧面、从開衩处露出来"
		IF CHECK_PANTIES("紐")
			STR:写真 += @"%PANTIES(TARGET)%的绳结"
		ELSE
			STR:写真 += "綺麗的大腿"
		ENDIF
		STR:写真 += "露了出来の"
	ELSE
		PRINTFORM 迈出一步并决定好了姿势…
		STR:写真 += "从開衩处轻轻踏出一条腿"
	ENDIF
CASE "チャイナカンフー"
	PRINTFORM 单足站起、摆出一副拳法的架势…
	STR:写真 += "单足站立、另一条腿大幅度抬高、大胆地从開衩处露出"
CASE "和服脚見せつけ"
	PRINTFORM 坐在地板上、手伸到%SHIRT(TARGET)%的下摆…
	STR:写真 += @"静坐时%SHIRT(TARGET)%的下摆被撩起、露出赤足"
	;ポーズ解説にコス描写が含まれるのでキャラ解説の方のコス描写は省く
	IGNORE_COSTUME = 1
CASE "袴脚見せつけ"
	PRINTFORM 的手伸到%SKIRT(TARGET)%的下摆…
	STR:写真 += @"掀起%SKIRT(TARGET)%、大腿裸露"

;ポーズ撮影進行度1
CASE "寝転がる"
	PRINTFORM %PHOTO_SHOWING()%摆好了姿势……
	STR:写真 += @"%NAMES("床")%上"
	IF RAND:3 == 0
		STR:写真 += "躺着托住脸頬"
	ELSEIF RAND:2
		STR:写真 += "侧躺着正在翻滚"
	ELSE
		STR:写真 += @"%TEXTR("仰面/無防備地")%躺着"
	ENDIF

CASE "座る"
	PRINTFORM %PHOTO_SHOWING()%摆好了姿势……
	;現状六華ちゃん専用。脚が無い人が増えたら調整
	IF COND("脚が無い")
		IF COND("種族：拉米亚") && RAND:2
			STR:写真 += "優雅地卷在一起"
		ELSE
			STR:写真 += "下半身横着、摆出一副人魚姿态"
		ENDIF
	ELSE
		STR:写真 += @"%TEXTR(@"%NAMES("床")%上盘腿坐着/屁股坐在地上/抱着腿坐着、歪头看向这边")%"
	ENDIF
CASE "猫内衣"
	PRINTFORM 両手轻握作出猫拳…
	STR:写真 += "弯着手摆出像猫一様的姿势"
CASE "水手夏服"
	PRINTFORM 伸展身体、摆出舒服点姿势…
	STR:写真 += @"大幅度伸展身体、从水手服的下摆能看到%TEXTR("肚脐/腹部/侧腹")%"
	;ポーズ解説にコス描写が含まれるのでキャラ解説の方のコス描写は省く
	IGNORE_COSTUME = 1
CASE "肩ストレッチ"
	PRINTFORM 的一只手抓住另一只手的手腕全力向上伸展…
	STR:写真 += "手臂交叉做着肩膀伸展运动"
CASE "チアコス", "チアコス胸部"
	IF PLACE("出先") && !PLACE("情人旅館")
		PRINTFORM 声音不大地
	ELSEIF TALENT:害羞 || TALENT:内気 || TALENT:感情缺乏
		PRINTFORM 用保守的声音
	ELSEIF TALENT:開朗
		PRINTFORM 用元気的声音
	ELSE
		PRINTFORM 与呼喊声一起
	ENDIF
	PRINTFORM 開始给%CALLNAME:MASTER%进行応援…

	SELECTCASE EVENT_CHOICE
	CASE "チアコス"
		STR:写真 += @"%TEXTR(@"裙摆飘起/单手高举露出%TEXTS("腋：視覚")%/清楚地露着肚脐")%"
	CASE "チアコス胸部"
		STR:写真 += @"%TEXTR("大号的胸部上下弾动/跃动着巨乳")%"
	ENDSELECT
	STR:写真 += @"给%CALLNAME:MASTER%进行応援"
CASE "女豹のポーズ"
	PRINTFORM %PHOTO_SHOWING()%摆好了姿势……
	STR:写真 += @"在%NAMES("床")%上趴着模仿「女豹の姿勢」"

;ポーズ撮影進行度0
CASE "妊婦写真"
	PRINTFORM 我が子を慈しむようにポーズをとった…
	STR:写真 += "大きく膨らんだお腹を両手で抱えている"
CASE "腰を下ろす"
	PRINTFORM 在%NAMES("床")%上慢慢地坐下…
	STR:写真 += @"坐在%NAMES("床")%上慢慢看了过来"
CASE "振り返る"
	PRINTFORM 慢慢地转过身来…
	STR:写真 += "扭动身体、摆出一副回头看的姿势"
CASE "ピース"
	PRINTFORM 摆出不显眼的姿势…
	STR:写真 += "面对照相机"
	IF TALENT:叛逆 || TALENT:感情缺乏
		STR:写真 += "看起来没有干劲地"
	ELSEIF TALENT:開朗 || TALENT:喜歡引人注目
		STR:写真 += "笑容满面地"
	ENDIF
	STR:写真 += "比着胜利手势の"
CASE "体を隠す"
	PRINTFORM 雙手把身体遮起来…
	IF TALENT:感情缺乏
		STR:写真 += "無表情的"
	ELSEIF TALENT:害羞 || TALENT:清楚 || TALENT:内気 || TALENT:謙虚
		STR:写真 += "满脸通红"
	ELSEIF TALENT:不知羞恥 || TALENT:小悪魔 || TALENT:好色
		STR:写真 += "一边笑着一边"
	ELSE
		STR:写真 += "有点害羞的微笑着"
	ENDIF
	STR:写真 += @"把胸%TEXTR("和重要的地方/＿")%遮挡起来"
CASE "裙つまみ"
	PRINTFORM 摆出炫耀\@ CHECK_SKIRT("长款") ? 長 # \@裙子的姿势…
	IF COND("脚が無い") == 0
		STR:写真 += @"单手把%SKIRT(TARGET, "略")%脱下露出股间"
	ELSE
		STR:写真 += @"正在单手把%SKIRT(TARGET, "略")%捏起来穿上"
	ENDIF
CASE "恥じらう"
	PRINTFORM 不知怎么開始畏缩起来了…
	IF TALENT:不知羞恥 || TALENT:小悪魔 || TALENT:好色
		STR:写真 += " 一副腼腆的样子移開视线 "
	ELSE
		STR:写真 += "害羞地移開视线"
	ENDIF
ENDSELECT

;ここで服と名前
SIF IGNORE_COSTUME == 0
	STR:写真 += @"%PHOTO_COSTUME(TARGET)%"
STR:写真 += @"%CALLNAME:TARGET%"

STR:補足 += "_摆POSE"
SAVESTR:この時間のPOSE撮影履歴 += @"%CALLNAME:TARGET%_%EVENT_CHOICE%\/"

;ポーズによって見えた下着の確認フラグを立てる
SIF STRCOUNT(STR:補足, "_胸罩")
	CALL SETFLAG_DAILY, "胸罩形状確認"
SIF STRCOUNT(STR:補足, "_胖次")
	CALL SETFLAG_DAILY, "胖次形状確認"

;-------------------------------------------------
;写真撮影時のARGの服装を描写する
;全部は対応しきれてないし多分もっと追加します
;-------------------------------------------------
@PHOTO_COSTUME(ARG)
#FUNCTIONS

VARSET LOCALS

IF CHECK_CLO("裸体", ARG)
	IF EQUIP:ARG:襪子 && EQUIP:ARG:靴 + EQUIP:ARG:手袋 == 0
		LOCALS = %TEXTR("全裸に襪子の/襪子しか身に着けていない")%
	ELSEIF EQUIP:ARG:靴 && EQUIP:ARG:襪子 + EQUIP:ARG:手袋 == 0
		LOCALS = %TEXTR("全裸に靴のみの/靴しか身に着けていない")%
	ELSEIF EQUIP:ARG:手袋 && EQUIP:ARG:靴 + EQUIP:ARG:襪子 == 0
		LOCALS = %TEXTR("全裸に手袋の/手袋しか身に着けていない")%
	ELSEIF EQUIP:ARG:手袋 + EQUIP:ARG:靴 + EQUIP:ARG:襪子
		LOCALS = %TEXTR("殆ど裸の/ほぼ裸体の")%
	ELSE
		LOCALS = %TEXTR("一糸不挂の/裸体の")%
	ENDIF
;ELSEIF RAND:2
;	LOCALS = %CLO_ABOUT(ARG, "姿")%の
;対応してみたけど微妙　もっとスマートな書き方あるかも…
;下半身下着
ELSEIF COND("調教中") == 0 && EQUIP:ARG:服 && EQUIP:ARG:胖次 && CHECK_CLO("胖次が見える格好", ARG)
	IF CHECK_PANTIES("布鲁马", ARG)
		LOCALS = %PANTIES(ARG, "種類")%姿の
	ELSE
		LOCALS = 胖次一覽無遺の
	ENDIF
;上半身下着
ELSEIF COND("調教中") == 0 && EQUIP:ARG:服 == 0 && EQUIP:ARG:胸罩 && EQUIP:ARG:裙 + EQUIP:ARG:外套
	LOCALS = 胸罩一覽無遺の
;下半身裸
ELSEIF CHECK_CLO("没穿下衣", ARG)
	LOCALS = 下半身裸の
;上半身裸
ELSEIF COND("調教中") == 0 && CHECK_CLO("没穿外衣", ARG) && TALENT:ARG:男性 == 0
	LOCALS = 上半身裸の
ELSEIF CHECK_CLO("比基尼姿", ARG)
	IF CHECK_PANTIES("淚珠泳衣", ARG)
		LOCALS = 淚珠泳衣比基尼姿の
	ELSEIF CHECK_BRA("Micro", ARG) && CHECK_PANTIES("Micro", ARG)
		LOCALS = 微型比基尼姿の
	ELSE
		LOCALS = 比基尼姿の
	ENDIF
ELSEIF CHECK_BODYS("死库水", ARG)
	LOCALS = 死库水姿の
ELSEIF CHECK_BODYS("競泳泳装", ARG)
	LOCALS = 競泳泳装姿の
ELSEIF CHECK_CLO("内衣姿", ARG)
	IF CHECK_BRA("猫型胸罩", ARG) && CHECK_PANTIES("肉球印花胖次", ARG)
		LOCALS = 貓女內衣姿の
	ELSE
		LOCALS = 内衣姿の
	ENDIF
ELSEIF CHECK_CLO("ランジェリー姿")
	LOCALS = ランジェリー姿の
ELSEIF CHECK_CLO("体操服姿", ARG) && CHECK_OUTER("布鲁马", ARG) && EQUIP:ARG:裙 == 0
	LOCALS = 布鲁马姿の
ELSEIF CHECK_SHIRT("体操服", ARG)
	LOCALS = 体操服姿の
ELSEIF CHECK_SHIRT("运动衫", ARG)
	LOCALS = 运动衫姿の
ELSEIF STRCOUNT(CSTR:ARG:服, "水手")
	LOCALS = 水手服姿の
ELSEIF STRCOUNT(CSTR:ARG:服, "西装外衣")
	LOCALS = 西装外衣姿の
ELSEIF STRCOUNT(CSTR:ARG:服, "美式校服")
	LOCALS = 外衣打结の
ELSEIF CHECK_CLO("学生服姿", ARG)
	LOCALS = 制服姿の
ELSEIF CHECK_SHIRT("女教師の服", ARG)
	LOCALS = 女教師姿の
ELSEIF CHECK_SHIRT("振袖", ARG)
	LOCALS = 振袖姿の
ELSEIF CHECK_SHIRT("和服", ARG)
	LOCALS = 和服姿の
ELSEIF CHECK_SHIRT("和服", ARG)
	LOCALS = 和服姿の
ELSEIF CHECK_SHIRT("女仆装", ARG)
	LOCALS = 女仆装姿の
ELSEIF CHECK_SHIRT("护士服", ARG)
	LOCALS = 护士姿の
ELSEIF CHECK_SHIRT("旗袍", ARG)
	LOCALS = 旗袍姿の
ELSEIF CHECK_SHIRT("巫女服", ARG)
	LOCALS = 巫女服姿の
ELSEIF CHECK_SHIRT("婚纱礼服", ARG)
	LOCALS = 婚纱姿の
ELSEIF CHECK_SHIRT("啦啦队制服", ARG)
	LOCALS = 啦啦队姿の
ELSEIF CHECK_BODYS("拘束衣", ARG)
	LOCALS = 绷带姿の
ELSEIF CHECK_BODYS("兔", ARG)
	LOCALS = 兔女郎姿の
ELSEIF CHECK_SHIRT(@"%CALLNAME:MASTER%の衬衫", ARG)
	LOCALS = %CALLNAME:MASTER%的衬衫姿の
ELSEIF COND("居家服に着替えた", ARG)
	LOCALS = 穿的很随意の
ELSE
	LOCALS = %CLO("現在的格好＆の", ARG)%
ENDIF

RETURNF LOCALS

;-------------------------------------------------
;いろいろ見せてくれる時のARGの反応を描写する
;性格素質によっていろいろ変わる。たぶん調整必要
;-------------------------------------------------
@PHOTO_SHOWING(ARGS, ARG)
#FUNCTIONS
#DIMS ACT_TABLE

SIF ARG == 0 && TARGET
	ARG = TARGET

VARSET ACT_TABLE
VARSET LOCALS

SELECTCASE ARGS
CASE "見せつけ"
	IF TEQUIP:ARG:甜言蜜语 || CFLAG:ARG:夸奖 || TCVAR:ARG:干劲 > 20
		IF TALENT:ARG:開朗
			ACT_TABLE += @"暴露出来的同时%TEXTR("看起来笑得很開心/眨着眼睛")%の/"
		ELSEIF TALENT:ARG:不知羞恥 && TALENT:ARG:強気 + TALENT:ARG:高慢
			ACT_TABLE += @"暴露出来的同时%TEXTR("滲出了優越感/从容笑着/微笑着/露出自信的微笑")%の/"
		ELSEIF TALENT:ARG:強気
			ACT_TABLE += @"暴露出来的同时%TEXTR("微笑着看向摄像机/看起来笑得很開心")%の/"
		ELSEIF TALENT:ARG:冷静
			ACT_TABLE += @"暴露出来的同时%TEXTR("保守地/冷冷地")%笑着の/"
		ENDIF

		SIF TALENT:ARG:温柔 + TALENT:ARG:献身
			ACT_TABLE += @"暴露出来的同时%TEXTR("平稳地/温柔地/眯起眼睛")%笑着の/"
		SIF COND("陶酔", ARG) && PREVEX("絶頂数合計", ARG)
			ACT_TABLE += @"見せつけながら%TEXTR("快楽に染まった顔で/絶頂の余韻を滲ませつつ")%微笑んでいる/"
	ENDIF
	IF TALENT:ARG:害羞 && (TEQUIP:ARG:甜言蜜语 || CFLAG:ARG:夸奖 || TCVAR:ARG:干劲 > 20)
		ACT_TABLE += @"暴露出来的同时%TEXTR("保守地/羞恥地")%微笑着の/"
	ELSEIF TALENT:ARG:害羞 || (PREVPLAY("排泄") && PREVCOM == TNUM("扒開菊穴") )
		ACT_TABLE += @"暴露出来的同时%TEXTR("移開了视线/满脸通红/背过头去")%の/"
	ENDIF




	SIF ACT_TABLE == ""
		ACT_TABLE += "暴露了出来の/"

	LOCALS = %TEXTR(ACT_TABLE)%

CASEELSE
	;ためしにRANDIF使ったら横長になって見づらくなったのでどうするか迷い中
	IF CFLAG:ARG:躁动难忍 && ABL:ARG:欲望 >= 3 && RAND:3 == 0 
		LOCALS = 浮现出扇情的表情并
	ELSEIF (TALENT:ARG:小悪魔 || TALENT:ARG:好色) && RAND:3 == 0
		LOCALS = 一边嘻嘻笑着一边
	ELSEIF TALENT:ARG:感情缺乏 && RAND:3 == 0
		LOCALS = 無表情的
	ELSEIF (TALENT:ARG:開朗 || TALENT:ARG:喜歡引人注目) && RAND:3 == 0
		LOCALS = 開心地微笑着并
	ELSEIF (TALENT:ARG:害羞 || TALENT:ARG:清楚) && RAND:3 == 0
		LOCALS = 稍微有点害臊地
	ELSEIF (TALENT:ARG:高慢 || TALENT:ARG:強気) && RAND:3 == 0
		LOCALS = 浮现出充满自信表情并
	ELSEIF (TALENT:ARG:献身 || TALENT:ARG:温柔) && RAND:3 == 0
		LOCALS = 温柔的微笑着并
	ELSEIF (TALENT:ARG:謙虚 || TALENT:ARG:内気) && RAND:3 == 0
		LOCALS = 有些踌躇但
	ELSEIF (TALENT:ARG:冷静 || TALENT:ARG:不知羞恥 || TALENT:ARG:粗野) && RAND:3 == 0
		LOCALS = 一点不害羞地
	ELSE
		LOCALS = %TEXTR("以慢吞吞的动作/慢慢地")%
	ENDIF
ENDSELECT
RETURNF LOCALS


;-------------------------------------------------
;写真を管理する画面
;-------------------------------------------------
@VIEW_PHOTO
IF SAVESTR:待ち受け画像 != ""
	PRINTFORML %CALLNAME:MASTER%设定的屏幕保护图像
	PRINTFORML 『%SAVESTR:待ち受け画像%』
	PRINTFORMW 看了一会后、打開了手机的照片文件夹。
ENDIF
FLAG:写真閲覧画面 = 1
CALL VIEW_PHOTO_MAIN

@VIEW_PHOTO_MAIN
#DIM LCOUNT
#DIM LCOUNT2
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT2
#DIM CHOICE
#DIMS PHOTOS, 301
#DIM MAX_PAGE
#DIMS SHOW_FILTER
#DIMS NAME_FILTER
#DIM NUM_PHOTOCOUNT
#DIM IS_DELPHOTO
#DIMS BUTTON_PHOTO

VARSET PHOTOS
VARSET LOCALS
VARSET LOCAL
IS_DELPHOTO = 0

MAX_PAGE = 1
NUM_PHOTOCOUNT = 0
FOR LCOUNT, 70, 100
	VARSET LOCALS
	SPLIT SAVESTR:LCOUNT, "/", LOCALS
	FOR LCOUNT2, 0, 10
		SELECTCASE LOCALS:LCOUNT2
		CASE ""
			CONTINUE
		CASEELSE
			;フィルタで弾く
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "对象") && STRCOUNT(LOCALS:LCOUNT2, CALLNAME:TARGET) == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "胖次") && STRCOUNT(LOCALS:LCOUNT2, "胖次") == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "阴部") && STRCOUNT(LOCALS:LCOUNT2, "阴部") == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "阴茎") && STRCOUNT(LOCALS:LCOUNT2, "阴茎") == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "特写镜头") && STRCOUNT(LOCALS:LCOUNT2, "特写镜头") == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "摆POSE") && STRCOUNT(LOCALS:LCOUNT2, "摆POSE") == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "睡眠中") && STRCOUNT(LOCALS:LCOUNT2, "睡眠中") == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "自撮り") && STRCOUNT(LOCALS:LCOUNT2, "自撮り") == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "事後") && STRCOUNT(LOCALS:LCOUNT2, "事後") == 0
				CONTINUE

			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "学生服") && STRCOUNT(LOCALS:LCOUNT2, "学生服") == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "泳装") && STRCOUNT(LOCALS:LCOUNT2, "泳装") == 0
				CONTINUE
			SIF STRCOUNT(SAVESTR:写真検索絞り込み, "体操服") && STRCOUNT(LOCALS:LCOUNT2, "体操服") == 0
				CONTINUE

			PHOTOS:NUM_PHOTOCOUNT = %LOCALS:LCOUNT2%

			NUM_PHOTOCOUNT += 1
			SELECTCASE NUM_PHOTOCOUNT%30
			CASE 0
				MAX_PAGE += 1
			ENDSELECT
		ENDSELECT
	NEXT
NEXT

MAX_PAGE = LIMIT(MAX_PAGE, 1, 10)

SIF FLAG:写真閲覧画面 > MAX_PAGE
	FLAG:写真閲覧画面 = MAX_PAGE

MEMO_LINECOUNT = LINECOUNT

SIF TFLAG:NEXTPHOTO
	GOTO INPUT_END

DRAWLINE
PRINTFORML 保存着{NUM("写真")}枚照片。要看哪一张呢？
;クローズアップ写真を選別している場合には表記を短縮
IF STRCOUNT(SAVESTR:写真検索絞り込み, "特写镜头")
	CALL PRINT_STRL, "黄色_（※为了方便选择特写镜头的照片、把表述缩短了※）"
ELSE
	PRINTFORML 
ENDIF

FOR LCOUNT, 30*(FLAG:写真閲覧画面 - 1), 30*FLAG:写真閲覧画面
	SELECTCASE PHOTOS:LCOUNT
	CASE ""
		SETCOLOR DEF_COLOR("灰色")
		PRINTFORML [×]（没有照片数据）
		RESETCOLOR
	CASEELSE
		PRINTFORM [{LCOUNT, 2}]

		VARSET LOCALS
		SPLIT PHOTOS:LCOUNT, "_", LOCALS
		;クローズアップ写真を選別している場合には表記を短縮
		SIF STRCOUNT(SAVESTR:写真検索絞り込み, "特写镜头") && EXTSTR(LOCALS, "特写镜头された") != ""
			LOCALS = %EXTSTR(LOCALS, "特写镜头された")%
		;文章を省略して一部のみ表示してみる。とりあえず30文字目までRESULTS:0に入れる
		SUBSTRINGU LOCALS, 0, 30
		;30文字以下の場合もあるのでここで判定
		STRLENSU RESULTS:0
		;省略されていないとみなされた場合、末尾の...はつかない
		PRINTFORML  『%RESULTS:0%\@ LOCALS == RESULTS:0 || RESULT:0 < 30 ? # ... \@』
	ENDSELECT
NEXT
PRINTFORML 

VARSET SHOW_FILTER
SIF STRCOUNT(SAVESTR:写真検索絞り込み, "对象")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "对象_500/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "胖次")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "胖次_501/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "阴部")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "阴部_502/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "阴茎")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "阴茎_503/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "特写镜头")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "特写镜头_505/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "摆POSE")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "摆POSE_506/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "睡眠中")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "睡眠中_507/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "自撮り")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "自撮り_508/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "事後")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "事後_509/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "学生服")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "学生服_520/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "泳装")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "泳装_521/"

SIF STRCOUNT(SAVESTR:写真検索絞り込み, "体操服")
	SHOW_FILTER += "黄色/"
SHOW_FILTER += "体操服_522/"

CALL PRINT_LIST, SHOW_FILTER
PRINTL 

BUTTON_PHOTO = [999]返回 [888]重複・類似写真の整理

IF MAX_PAGE > FLAG:写真閲覧画面 && FLAG:写真閲覧画面 > 1
	BUTTON_PHOTO += " [777]察看前一頁 [666]察看下一頁"
ELSEIF MAX_PAGE > FLAG:写真閲覧画面
	BUTTON_PHOTO += " _灰色_NOBUTTON_[777]察看前一頁_ [666]察看下一頁"
ELSEIF FLAG:写真閲覧画面 > 1
	BUTTON_PHOTO += " [777]察看前一頁 _灰色_NOBUTTON_[666]察看下一頁_"
ENDIF
SIF NUM("写真")
	BUTTON_PHOTO += " [555]古い方から30枚消去"

CALL PRINT_STRL, BUTTON_PHOTO

$INPUT_LOOP

INPUT
CHOICE = RESULT

$INPUT_END

SELECTCASE CHOICE
CASE 999
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RETURN 0

CASE 500 TO 549
	SELECTCASE CHOICE
	CASE 500
		NAME_FILTER = 对象
	CASE 501
		NAME_FILTER = 胖次
	CASE 502
		NAME_FILTER = 阴部
	CASE 503
		NAME_FILTER = 阴茎

	CASE 505
		NAME_FILTER = 特写镜头
	CASE 506
		NAME_FILTER = 摆POSE
	CASE 507
		NAME_FILTER = 睡眠中
	CASE 508
		NAME_FILTER = 自撮り
	CASE 509
		NAME_FILTER = 事後

	CASE 520
		NAME_FILTER = 学生服
	CASE 521
		NAME_FILTER = 泳装
	CASE 522
		NAME_FILTER = 体操服

	CASEELSE
		NAME_FILTER = 
	ENDSELECT

	IF NAME_FILTER != ""
		IF STRCOUNT(SAVESTR:写真検索絞り込み, NAME_FILTER)
			VARSET LOCALS
			SPLIT SAVESTR:写真検索絞り込み, "/", LOCALS
			SAVESTR:写真検索絞り込み = 
			FOR LCOUNT, 0, 10
				SELECTCASE LOCALS:LCOUNT
				CASE NAME_FILTER, ""
					CONTINUE
				CASEELSE
					SAVESTR:写真検索絞り込み += @"%LOCALS:LCOUNT%\/"
				ENDSELECT
			NEXT
		ELSE
			SAVESTR:写真検索絞り込み += @"%NAME_FILTER%\/"
		ENDIF
		FLAG:写真閲覧画面 = 1
	ENDIF
CASE 888
	PRINTFORML 标题相同的写真只保留最新的、其他都削除可以吗？
	CALL PRINT_SELECT, "是/否"
	IF RESULT == 0
		LOCAL = NUM("写真")
		CALL DEL_SAME_PHOTO
		IF NUM("写真") == LOCAL
			PRINTFORMW 重複的写真一枚都没有了。
		ELSE
			PRINTFORMW 共削除了{LOCAL - NUM("写真")}枚重複的写真。
		ENDIF
	ENDIF
	PRINTL 
	PRINTFORML 内容７成以上相同的写真、也顺便削除一下吗？
	CALL PRINT_SELECT, "是/否"
	IF RESULT == 0
		REDRAW 1
		PRINTFORML 搜索＆削除中……请稍等一下
		REDRAW 0
		LOCAL = NUM("写真")
		CALL DEL_SIMILAR_PHOTO
		IF NUM("写真") == LOCAL
			PRINTFORMW 相似内容的写真一枚都没有了。
		ELSE
			PRINTFORMW 共削除了{LOCAL - NUM("写真")}枚相似内容的写真。
		ENDIF
	ENDIF
CASE 555
	SIF NUM("写真") == 0
		GOTO INPUT_LOOP

	CALL DEL_OLD_PHOTO, 30
	PRINTFORMW 将最旧的30枚照片削除了。

CASE 777
	SIF FLAG:写真閲覧画面 == 1
		GOTO INPUT_LOOP
	FLAG:写真閲覧画面 -= 1

CASE 666
	SIF FLAG:写真閲覧画面 == MAX_PAGE
		GOTO INPUT_LOOP

	FLAG:写真閲覧画面 += 1

CASE 0 TO 299
	SIF PHOTOS:CHOICE == ""
		GOTO INPUT_LOOP

	TFLAG:NEXTPHOTO = 0

	DRAWLINE
	MEMO_LINECOUNT = LINECOUNT

	VARSET LOCALS
	SPLIT PHOTOS:CHOICE, "_", LOCALS

	PRINTFORML 『%LOCALS%』写入了。
	PRINTFORM NO.{CHOICE}
	SIF STRCOUNT(PHOTOS:CHOICE, "日期")
		PRINTFORML  撮影日：%TO_DATE(EXTSTR(PHOTOS:CHOICE, "日期", "_") )%

	MEMO_LINECOUNT2 = LINECOUNT

	PRINTFORML 
	PRINTFORML [0] 设置为屏幕保护图像
	PRINTFORML [1] 削除
	IF PHOTOS:(CHOICE + 1) != ""
		PRINTFORML [2] 返回
		PRINTFORML [3] 下一个
		CALL ONEINPUT_SELECT, 4
	ELSE
		PRINTFORML [2] 返回
		CALL ONEINPUT_SELECT, 3
	ENDIF

	SELECTCASE RESULT
	CASE 0
		SAVESTR:待ち受け画像 = %LOCALS%
		CALL PRINT_STRW, @"手机的屏幕保护图片_L_黄色_『%SAVESTR:待ち受け画像%』_L_决定了"
	CASE 1
		SIF SAVESTR:待ち受け画像 == LOCALS
			SAVESTR:待ち受け画像 = 
		CALL DEL_PHOTO, PHOTOS:CHOICE
		IS_DELPHOTO = 1
		SIF PHOTOS:(CHOICE + 1) != ""
			TFLAG:NEXTPHOTO = 1
	CASE 3
		IF PHOTOS:(CHOICE + 1) != ""
			TFLAG:NEXTPHOTO = 1
			CHOICE += 1
		ENDIF
	ENDSELECT
ENDSELECT

;CLEARLINE LINECOUNT - MEMO_LINECOUNT
IF IS_DELPHOTO
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	CALL PRINT_STRL, @"青_『%LOCALS%』を消しました"
ELSEIF TFLAG:NEXTPHOTO
	CLEARLINE LINECOUNT - MEMO_LINECOUNT2
ENDIF
RESTART


;-------------------------------------------------
;写真ARGSを消去する
;-------------------------------------------------
@DEL_PHOTO, ARGS
#DIM LCOUNT
#DIM LCOUNT2
#DIMS MEMO_SAVESTR, 100
#DIM NUM_SAVESTR
#DIM NUM_PHOTOCOUNT

VARSET MEMO_SAVESTR

FOR LCOUNT, 70, 100
	MEMO_SAVESTR:LCOUNT = %SAVESTR:LCOUNT%
NEXT
VARSET SAVESTR, "", 70, 100

NUM_PHOTOCOUNT = 0
NUM_SAVESTR = 70
FOR LCOUNT, 70, 100
	VARSET LOCALS
	SPLIT MEMO_SAVESTR:LCOUNT, "/", LOCALS
	FOR LCOUNT2, 0, 10
		SELECTCASE LOCALS:LCOUNT2
		CASE "", ARGS
			CONTINUE
		CASEELSE
			SAVESTR:NUM_SAVESTR += @"%LOCALS:LCOUNT2%\/"
			NUM_PHOTOCOUNT += 1

			SELECTCASE NUM_PHOTOCOUNT
			CASE 10
				NUM_PHOTOCOUNT = 0
				NUM_SAVESTR += 1
			ENDSELECT
		ENDSELECT
	NEXT
NEXT

;FOR LCOUNT, 70, 100
;	SELECTCASE SAVESTR:LCOUNT
;	CASE ""
;	CASEELSE
;		PRINTFORML SAVESTR:{LCOUNT} = %SAVESTR:LCOUNT%
;	ENDSELECT
;NEXT


;-------------------------------------------------
;写真ARGSを記録する。すでに撮ったものと全く同じ写真は記録しない
;記録にはSAVESTRの70～99番を割り当てる
;-------------------------------------------------
@SAVE_PHOTO, ARGS
#DIM LCOUNT
#DIM LCOUNT2
#DIMS MEMO_SAVESTR, 100
#DIM NUM_SAVESTR
#DIM NUM_PHOTOCOUNT

;すでに撮ったものと全く同じ写真は記録しない
FOR LCOUNT, 70, 100
	SIF STRCOUNT(@"\/%SAVESTR:LCOUNT%\/", @"\/%ARGS%\/")
		RETURN 0
NEXT

VARSET MEMO_SAVESTR

;今回撮った写真
MEMO_SAVESTR:69 = %ARGS%

;SAVESTRの70～99番を記録
FOR LCOUNT, 70, 100
	MEMO_SAVESTR:LCOUNT = %SAVESTR:LCOUNT%
NEXT
VARSET SAVESTR, "", 70, 100

NUM_PHOTOCOUNT = 0
NUM_SAVESTR = 70

;69から始まるのは今回撮った写真がMEMO_SAVESTR:69としてあるため
FOR LCOUNT, 69, 100
	VARSET LOCALS
	SPLIT MEMO_SAVESTR:LCOUNT, "/", LOCALS
	FOR LCOUNT2, 0, 10
		SELECTCASE LOCALS:LCOUNT2
		CASE ""
			CONTINUE
		CASEELSE
			SAVESTR:NUM_SAVESTR += @"%LOCALS:LCOUNT2%\/"
			NUM_PHOTOCOUNT += 1

			SELECTCASE NUM_PHOTOCOUNT
			CASE 10
				NUM_PHOTOCOUNT = 0
				NUM_SAVESTR += 1
			ENDSELECT
		ENDSELECT
	NEXT
NEXT


;-------------------------------------------------
;表題が同じ写真を最新の物を除いて消去する
;-------------------------------------------------
@DEL_SAME_PHOTO
#DIM LCOUNT
#DIM LCOUNT2
#DIMS PHOTOS, 300
#DIM PHOTOCOUNT
#DIMS PHOTOTITLE
#DIM NUM_SAVESTR

VARSET PHOTOS
PHOTOCOUNT = 0

;PHOTOS:0～PHOTOS:299に写真文字列を入れる
FOR LCOUNT, 70, 100
	VARSET LOCALS
	SPLIT SAVESTR:LCOUNT, "/", LOCALS
	FOR LCOUNT2, 0, 10
		SELECTCASE LOCALS:LCOUNT2
		CASE ""
			BREAK
		CASEELSE
			PHOTOS:PHOTOCOUNT = %LOCALS:LCOUNT2%
			PHOTOCOUNT += 1
		ENDSELECT
	NEXT
NEXT

;表題が同じものを最新除き全消去
FOR LCOUNT, 0, 300
	;すでに消されている場合はスキップ
	SIF PHOTOS:LCOUNT == ""
		CONTINUE

	VARSET LOCALS
	SPLIT PHOTOS:LCOUNT, "_", LOCALS
	;LOCALSに写真の表題が入っている
	PHOTOTITLE = %LOCALS%

	FOR LCOUNT2, LCOUNT+1, 300
		VARSET LOCALS
		SPLIT PHOTOS:LCOUNT2, "_", LOCALS

		SELECTCASE LOCALS
		;待ち受けと同じなら整理しない
		CASE SAVESTR:待ち受け画像

		CASE PHOTOTITLE
			PHOTOS:LCOUNT2 = 
		ENDSELECT
	NEXT
NEXT

VARSET SAVESTR, "", 70, 100
PHOTOCOUNT = 0
NUM_SAVESTR = 70

;文字列が残っているもので再構成
FOR LCOUNT, 0, 300
	SELECTCASE PHOTOS:LCOUNT
	CASE ""
		CONTINUE
	CASEELSE
		SAVESTR:NUM_SAVESTR += @"%PHOTOS:LCOUNT%\/"
		PHOTOCOUNT += 1
		IF PHOTOCOUNT == 10
			PHOTOCOUNT = 0
			NUM_SAVESTR += 1
		ENDIF
	ENDSELECT
NEXT

;-------------------------------------------------
;似た写真を最新の物を除いて消去する
;-------------------------------------------------
@DEL_SIMILAR_PHOTO
#DIM LCOUNT
#DIM LCOUNT2
#DIMS PHOTOS, 300
#DIM PHOTOCOUNT
#DIMS PHOTOTITLE
#DIM NUM_SAVESTR

VARSET PHOTOS
PHOTOCOUNT = 0

;PHOTOS:0～PHOTOS:299に写真文字列を入れる
FOR LCOUNT, 70, 100
	VARSET LOCALS
	SPLIT SAVESTR:LCOUNT, "/", LOCALS
	FOR LCOUNT2, 0, 10
		SELECTCASE LOCALS:LCOUNT2
		CASE ""
			BREAK
		CASEELSE
			PHOTOS:PHOTOCOUNT = %LOCALS:LCOUNT2%
			PHOTOCOUNT += 1
		ENDSELECT
	NEXT
NEXT

;７割同じ内容のものを最新除き全消去
FOR LCOUNT, 0, 300
	;すでに消されている場合はスキップ
	SIF PHOTOS:LCOUNT == ""
		CONTINUE

	VARSET LOCALS
	SPLIT PHOTOS:LCOUNT, "_", LOCALS
	;LOCALSに写真の表題が入っている
	PHOTOTITLE = %LOCALS%

	FOR LCOUNT2, LCOUNT+1, 300
		VARSET LOCALS
		SPLIT PHOTOS:LCOUNT2, "_", LOCALS

		;待ち受けと同じなら整理しない
		SIF LOCALS == SAVESTR:待ち受け画像
			CONTINUE
		;同一人物のものでないなら消さない
		SIF WHOSE_PHOTO(PHOTOTITLE) != WHOSE_PHOTO(LOCALS)
			CONTINUE
		;７割類似なら消す
		SIF CHECK_HIGHMATCH(PHOTOTITLE, LOCALS) == 0
			CONTINUE
		PHOTOS:LCOUNT2 = 
	NEXT
	SIF LCOUNT%10 == 0
		TWAIT 1, 1
NEXT

VARSET SAVESTR, "", 70, 100
PHOTOCOUNT = 0
NUM_SAVESTR = 70

;文字列が残っているもので再構成
FOR LCOUNT, 0, 300
	SELECTCASE PHOTOS:LCOUNT
	CASE ""
		CONTINUE
	CASEELSE
		SAVESTR:NUM_SAVESTR += @"%PHOTOS:LCOUNT%\/"
		PHOTOCOUNT += 1
		IF PHOTOCOUNT == 10
			PHOTOCOUNT = 0
			NUM_SAVESTR += 1
		ENDIF
	ENDSELECT
NEXT

;-------------------------------------------------
;誰の写真かを返す
;-------------------------------------------------
@WHOSE_PHOTO(ARGS)
#FUNCTION
#DIM LCOUNT

FOR LCOUNT, 2, CHARANUM
	;名前が最後の場合
	SIF SUBSTRINGU(ARGS, STRLENSU(ARGS) - STRLENSU(CALLNAME:LCOUNT), STRLENSU(CALLNAME:LCOUNT) ) == CALLNAME:LCOUNT
		RETURNF LCOUNT
	;名前が途中の場合
	SIF STRCOUNT(ARGS, @"%CALLNAME:LCOUNT%の")
		RETURNF LCOUNT
NEXT
RETURNF 0
;-------------------------------------------------
;古いものからARG枚消す
;-------------------------------------------------
@DEL_OLD_PHOTO, ARG
#DIM LCOUNT
#DIM LCOUNT2
#DIM NUM_SAVESTR
#DIM PHOTOCOUNT
#DIMS PHOTOS, 300
#DIM MEMO_PHOTONUM

MEMO_PHOTONUM = NUM("写真")

VARSET PHOTOS
PHOTOCOUNT = 0

;PHOTOS:0～PHOTOS:299に写真文字列を入れる
FOR LCOUNT, 70, 100
	VARSET LOCALS
	SPLIT SAVESTR:LCOUNT, "/", LOCALS
	FOR LCOUNT2, 0, 10
		SELECTCASE LOCALS:LCOUNT2
		CASE ""
			BREAK
		CASEELSE
			SIF PHOTOCOUNT >= MEMO_PHOTONUM - ARG
				BREAK
			PHOTOS:PHOTOCOUNT = %LOCALS:LCOUNT2%
			PHOTOCOUNT += 1
		ENDSELECT
	NEXT
NEXT

VARSET SAVESTR, "", 70, 100
PHOTOCOUNT = 0
NUM_SAVESTR = 70

;文字列が残っているもので再構成
FOR LCOUNT, 0, 300
	SELECTCASE PHOTOS:LCOUNT
	CASE ""
		CONTINUE
	CASEELSE
		SAVESTR:NUM_SAVESTR += @"%PHOTOS:LCOUNT%\/"
		PHOTOCOUNT += 1
		IF PHOTOCOUNT == 10
			PHOTOCOUNT = 0
			NUM_SAVESTR += 1
		ENDIF
	ENDSELECT
NEXT


;-------------------------------------------------
;キャラARGが補足ARGSを満たす自撮り写真を用意しているかをチェックする関数
;-------------------------------------------------
@CHECK_SELFIE(ARG, ARGS, ARGS:1, ARGS:2, ARGS:3)
#FUNCTION
#DIM LCOUNT

VARSET LOCALS
SPLIT SAVESTR:自撮り写真, "/", LOCALS
FOR LCOUNT, 0, 10
	SIF LOCALS:LCOUNT == ""
		BREAK
	;ARGの自撮りを探す
	SIF STRCOUNT(LOCALS:LCOUNT, @"_%CALLNAME:ARG%") == 0
		CONTINUE
	;探す写真かをチェック
	SIF STRCOUNT(LOCALS:LCOUNT, @"_%ARGS%")
		RETURNF 1
	SIF ARGS:1 != "" && STRCOUNT(LOCALS:LCOUNT, @"_%ARGS:1%")
		RETURNF 1
	SIF ARGS:2 != "" && STRCOUNT(LOCALS:LCOUNT, @"_%ARGS:2%")
		RETURNF 1
	SIF ARGS:3 != "" && STRCOUNT(LOCALS:LCOUNT, @"_%ARGS:3%")
		RETURNF 1
NEXT
