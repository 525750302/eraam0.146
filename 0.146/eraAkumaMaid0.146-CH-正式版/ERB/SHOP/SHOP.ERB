;-------------------------------------------------
;時間ごとに一度だけ起きるイベントはここで記述
;-------------------------------------------------
@EVENTSHOP
#PRI
#DIM LCOUNT
#DIM LCOUNT2
#DIM MEMO_LINECOUNT
#DIM MEMO_TARGET
#DIMS MEMOS_PANTU
#DIMS MEMOS_BRA
#DIMS MEMOS, 10
#DIM CHARACOUNT

;オートちょっかい関連フラグの初期化
FLAG:なんかして欲しい = 0

;安全危険日判定(誕生日依存でいってみる)
CALL SETFLAG, "安全危険日判定"

;早朝のＨイベント、朝の挨拶イベント
IF COND("朝")
	;朝の髪型変更はここ。昼と夜は下の方にある
	CALL SETFLAG, "ＴＰＯによる外見変更"

	CALL SETFLAG, "要受精確率"
	;これは一日に一回だけ
	FLAG:避孕套取り出し = 0
	;中出しと精飲のリセット
	CVARSET CFLAG, GETNUM(CFLAG, "Ｖ已中出")
	CVARSET CFLAG, GETNUM(CFLAG, "Ａ已中出")
	CVARSET CFLAG, GETNUM(CFLAG, "尿道已中出")
	CVARSET CFLAG, GETNUM(CFLAG, "Ｂ已中出")
	CVARSET CFLAG, GETNUM(CFLAG, "精飲済")
	;没収した下着のリセット
	CVARSET CSTR, GETNUM(CSTR, "没収済胖次")
	CVARSET CFLAG, GETNUM(CFLAG, "胖次没収")
	;汗のリセットは早朝と入浴のみ
	CVARSET CFLAG, GETNUM(CFLAG, "汗")
	CVARSET CFLAG, GETNUM(CFLAG, "淋浴済")
	;水泳フラグ…塩素消毒フラグの方が近いか？
	CVARSET CFLAG, GETNUM(CFLAG, "水泳")
	CVARSET CFLAG, GETNUM(CFLAG, "体育")
	;帰り道で一緒だったフラグ
	CVARSET CFLAG, GETNUM(CFLAG, "一緒に帰宅")
	;プレゼントも一日一回
	CVARSET CFLAG, GETNUM(CFLAG, "送禮渡し済")


	;早朝Ｈ
	CALL YOBAI_ASAGAKE
	DRAWLINE

	;挨拶リセット
	CVARSET CFLAG, GETNUM(CFLAG, "打招呼した")
	CVARSET CFLAG, GETNUM(CFLAG, "打招呼された")

	MEMO_TARGET = TARGET
	;キャラごとに時間遷移で弄りたい事が有るならここで。時間帯はCOND("朝")
	FOR LCOUNT, 2 , CHARANUM
		TARGET = LCOUNT
		DRAWLINE
		MEMO_LINECOUNT = LINECOUNT
		TRYCALLFORM TIMEEVENT_K{NO:TARGET}
		SIF LINECOUNT == MEMO_LINECOUNT
			CLEARLINE 1
	NEXT
	TARGET = MEMO_TARGET

	;時間を昼に
	TIME = 1

	MEMO_LINECOUNT = LINECOUNT
	FOR LCOUNT, 2, CHARANUM
		SIF COND("同居：一周間", LCOUNT)
			CALL SETFLAG, "同居：日数減らし", LCOUNT
	NEXT
	SIF LINECOUNT != MEMO_LINECOUNT
		DRAWLINE

	;朝拘束発生の際はパートナーが拘束相手になる
	IF COND("朝拘束発生")
		SIF COND("朝袭相手") == 0
			CALL SETFLAG, "対象指定", FIND_COND("朝袭相手")
		RETURN 1
	ELSEIF TO_NUMBER(FLAG:昼の相手) > 0
		CALL SETFLAG, "対象指定", TO_NUMBER(FLAG:昼の相手)
	ENDIF
ELSEIF COND("夜")
	SIF TO_NUMBER(FLAG:夜の相手) > 0
		CALL SETFLAG, "対象指定", TO_NUMBER(FLAG:夜の相手)
ENDIF


;PALAMとCUPなどの初期化
FOR LCOUNT, 1, CHARANUM
	VARSET PALAM:LCOUNT:0
	VARSET CUP:LCOUNT:0
	;おやつの感想フラグの初期化
	CALL DEL_DEVENT, "点心の感想", LCOUNT
NEXT

CALL SHOP_BONUS_ADVISER

SAVESTR:胖次ドーピング = 

CVARSET EQUIP, GETNUM(EQUIP, "Ｈ的奖励")
CVARSET EQUIP, GETNUM(EQUIP, "お浴室でＨ")
;やる気リセット(脱衣などで使用)
CVARSET TCVAR, GETNUM(TCVAR, "干劲")
;標語実践フラグをOFF
CVARSET CFLAG, GETNUM(CFLAG, "標語実践")

CVARSET CFLAG, GETNUM(CFLAG, "调戏数")
;パンツは謎にする
CVARSET CFLAG, GETNUM(CFLAG, "内衣確認済")
CVARSET CFLAG, GETNUM(CFLAG, "股間確認済")
CVARSET CFLAG, GETNUM(CFLAG, "检查阴部")
CVARSET CFLAG, GETNUM(CFLAG, "肛門チェック")
CVARSET CFLAG, GETNUM(CFLAG, "掀起裙子")
CVARSET CFLAG, GETNUM(CFLAG, "甜言蜜语")
CVARSET CFLAG, GETNUM(CFLAG, "夸奖")
CVARSET CFLAG, GETNUM(CFLAG, "心情差")
CVARSET CFLAG, GETNUM(CFLAG, "憤怒")
CVARSET CFLAG, GETNUM(CFLAG, "害羞")
CVARSET CFLAG, GETNUM(CFLAG, "睡眠")
CVARSET CFLAG, GETNUM(CFLAG, "湿透")
CVARSET CFLAG, GETNUM(CFLAG, "隙あり")
CVARSET CFLAG, GETNUM(CFLAG, "刚睡醒")
CVARSET CFLAG, GETNUM(CFLAG, "廁所済")

CVARSET CFLAG, GETNUM(CFLAG, "不在")
CVARSET CFLAG, GETNUM(CFLAG, "入室までの所要時間")
CVARSET CFLAG, GETNUM(CFLAG, "淋浴中")
CVARSET CFLAG, GETNUM(CFLAG, "暇を持て余す")
CVARSET CFLAG, GETNUM(CFLAG, "電源BOX")
CVARSET CFLAG, GETNUM(CFLAG, "POSE结束摄影")
CVARSET CFLAG, GETNUM(CFLAG, "躁动难忍")
CVARSET CFLAG, GETNUM(CFLAG, "薄着变成了")
CVARSET CFLAG, GETNUM(CFLAG, "Cosplay中")
CVARSET CFLAG, GETNUM(CFLAG, "淫紋の様子")
CVARSET CFLAG, GETNUM(CFLAG, "陰毛描写")
CVARSET CFLAG, GETNUM(CFLAG, "呼び出された")
CVARSET CFLAG, GETNUM(CFLAG, "性処理済")
CVARSET CFLAG, GETNUM(CFLAG, "性欲解消済")
CVARSET CFLAG, GETNUM(CFLAG, "交流中")
CVARSET CFLAG, GETNUM(CFLAG, "ＣＩＰ発生済")
CVARSET CFLAG, GETNUM(CFLAG, "オート胖次交易発生済")
CVARSET CFLAG, GETNUM(CFLAG, "レ阴蒂エーション中")
SAVESTR:レ阴蒂エーション = 

;お店での買い物履歴は時間ごとにリセット
CVARSET CSTR, GETNUM(CSTR, "SHOP_BUY履歴"), "/"

CVARSET CSTR, GETNUM(CSTR, "PREV_DAILYCOM")
CVARSET CSTR, GETNUM(CSTR, "DAILYCOM履歴")

;移動履歴をリセット
CVARSET CSTR, GETNUM(CSTR, "移動履歴")
;脱いだ服は時間ごとにリセット
CVARSET CSTR, GETNUM(CSTR, "脱掉的靴")
CVARSET CSTR, GETNUM(CSTR, "脱掉的胖次")
CVARSET CSTR, GETNUM(CSTR, "脱掉的衣服一览")
CVARSET CSTR, GETNUM(CSTR, "しまった内衣")
CVARSET CSTR, GETNUM(CSTR, "帰宅時の格好")
;汚れのリセット(CSTR:Ｐ汚れ もリセットもされる)
FOR LCOUNT, 1, CHARANUM
	CALL RESET_STAIN_EX, "老廃物以外全部", LCOUNT
NEXT


;日常イベント名をリセット
STR:日常イベント名 = 
SAVESTR:PREV_日常イベント名 = 
CVARSET CFLAG, GETNUM(CFLAG, "PREV_DAILYCATE")
SAVESTR:PREV_直前行動 = 

SAVESTR:归途のゲスト = 

SAVESTR:PREV_PHOTO = 
;名前で自撮り送信してくるかを判定するので、昼夜に最高で各一回づつ
SAVESTR:送信済み自撮り写真 = 
SAVESTR:送信済み自撮り胖次 = 
SAVESTR:この時間のPOSE撮影履歴 = 

FLAG:対複数 = 0
FLAG:叫出来Ｈ中 = 0
FLAG:便当制作者 = 0

;おやつフラグリセット
FLAG:点心当番 = 0
SAVESTR:点心 = 
SAVESTR:点心詳細 = 
;お茶フラグリセット
CVARSET CFLAG, GETNUM(CFLAG, "茶")
SAVESTR:茶 = 

;お昼に見たパンツフラグは次の昼(つまり今)消す
SIF COND("昼")
	CVARSET CSTR, GETNUM(CSTR, "PREV_胖次")

;あなたが居る初期位置なSAVESTR:現在位置 を決める
SELECTCASE TEXTS("曜日")
CASE "日"
	;IF COND("昼") && FIND_COND("朝駆け相手") && CSTR:FIND_COND("朝駆け相手"):朝駆け == "朝添い寝"
	;	SAVESTR:現在位置 = %CALLNAME:MASTER%の部屋
	;ELSE
		SAVESTR:現在位置 = 居間
	;ENDIF
CASEELSE
	IF COND("昼")
		SAVESTR:現在位置 = 部室
	ELSE
		SAVESTR:現在位置 = 居間
	ENDIF
ENDSELECT

;時間経過ごとの移動テキストと着替え
IF FLAG:オープニング
	SAVESTR:現在位置 = %CALLNAME:MASTER%的房間
ELSEIF COND("昼")
	;昼の髪型変更はここ。朝は上に、夜は下の方にある（家を出入りするタイミングで変更するため）
	CALL SETFLAG, "ＴＰＯによる外見変更"

	MEMO_LINECOUNT = LINECOUNT
	SELECTCASE TEXTS("日期対応イベント")
	CASE "", "情人節翌日", "圣诞节前夕", "圣诞节翌日"
	CASE "换衣季(夏)"
		CALL PRINT_STRL, "从今日開始换装！"
		CALL PRINT_STRW, "黄色_(※制服变成了夏季样式、汗湿的时候会变透明能看到胸罩哦)"
	CASE "换衣季(冬)"
		CALL PRINT_STRL, "……从今日開始换装。"
		CALL PRINT_STRW, "黄色_(※制服变成了常规样式、或者是冬季样式)"
	CASE "即将到来文化祭"
		IF DAYEV("周一", "游戏内")
			CALL PRINT_STRL, "本週末的周日日……是_黄色_文化祭_！"
			CALL PRINT_STRW, "黄色_(※周日日时，目的地会是文化祭现场)"
		ENDIF
	CASE "周考"
		IF DAYEV("周一", "游戏内")
			CALL PRINT_STRL, "……这週末的周六有_黄色_定期試験_。"
			CALL PRINT_STRW, "黄色_(※互动指令で一起学习事が出来ます。頑張りましょう)"
		ENDIF
	CASE "女仆の日"
		CALL PRINT_STRW, @"今日是_黄色_%TEXTS("日期対応イベント")%_。_L_大家一定会换上女仆装的……！"
	CASE "比基尼の日"
		CALL PRINT_STRW, @"今日是_黄色_%TEXTS("日期対応イベント")%_。_L_大家一定会换上比基尼的……！"
	CASE "兔女郎の日"
		CALL PRINT_STRW, @"今日是_黄色_%TEXTS("日期対応イベント")%_。_L_大家一定会换上兔女郎装的……！"
	CASE "新年"
		CALL PRINT_STRW, @"今日是_黄色_%TEXTS("日期対応イベント")%_呢。_L_祝贺您新年快乐！_HH"
	CASEELSE
		CALL PRINT_STRW, @"今日是_黄色_%TEXTS("日期対応イベント")%_！"
	ENDSELECT
	SIF LINECOUNT > MEMO_LINECOUNT
		DRAWLINE

	IF COND("日")
		SIF TEXTS("日期対応イベント") == ""
			PRINTFORMW 今日休息。睡到中午吧。
		;普段着に着替える
		FOR LCOUNT, 2, CHARANUM
			CALL SET_COORDINATE, "便装", LCOUNT

			;いつもの格好を設定
			CSTR:LCOUNT:いつもの格好 = %CLO_ABOUT(LCOUNT)%

			;日曜朝のお相手はあなたのＹシャツ姿に
			IF TALENT:MASTER:男性 && COND("朝袭相手", LCOUNT)
				CALL MEMO_DRESS, "脱掉的衣服一览", LCOUNT
				CALL SET_COORDINATE, @"%CALLNAME:MASTER%の衬衫", LCOUNT
			ENDIF
		NEXT
	ELSE
		;お弁当の約束してる？
		FLAG:便当制作者 = COND("便当相手", MASTER)
		IF FLAG:便当制作者
			IF COND("朝一緒", FLAG:便当制作者)
				PRINTFORML %CALLNAME:MASTER%从%CALLNAME:(FLAG:便当制作者)%的手中接过了便当、向学園出发了。
				PRINTFORM ……
			ELSE
				PRINTFORM %CALLNAME:MASTER%向学校出发了……
			ENDIF
		ELSE
			PRINTFORM %CALLNAME:MASTER%向学校出发了……
		ENDIF
		;制服に着替える
		FOR LCOUNT, 2, CHARANUM
			SIF CFLAG:LCOUNT:夜型
				CONTINUE
			CALL SET_COORDINATE, "制服", LCOUNT
		NEXT

		IF DAYEV("試験当日")
			CALL EVENT_ACADEMY, "試験終了"
		ELSEIF DAYEV("試験返却日")
			CALL EVENT_ACADEMY, "試験結果発表"
		ELSEIF COND("体育")
			CALL PRINT_STRW, "今日的午休前是_黄色_体育_の授業。"
			;体操服に着替える
			FOR LCOUNT, 2, CHARANUM
				SIF CFLAG:LCOUNT:夜型
					CONTINUE
				;妊娠していたら見学
				SIF COND("妊娠中：妊娠発覚済み", LCOUNT)
					CONTINUE
				;75%で体操服で現れる
				SIF RAND:4
					CALL SET_COORDINATE, "体操服", LCOUNT
				IF COND("教員", LCOUNT)
					;体育教師ではない＆体操服姿でもない
					SIF STRCOUNT(CSTR:LCOUNT:学年, "体育") == 0 && CHECK_CLO("体操服姿", LCOUNT) == 0
						CONTINUE
				ENDIF
				CFLAG:LCOUNT:体育 = 1
			NEXT
			;PRINTFORMW 体操服の%CALLNAME:TARGET%を眺めていたら、ブルマを太ももまでずり下げて腰を突き出してきた
		ELSEIF COND("水泳")
			CALL PRINT_STRW, "今日的午休前是_水色_水泳_の授業。"
			;水着に着替える
			FOR LCOUNT, 2, CHARANUM
				SIF CFLAG:LCOUNT:夜型
					CONTINUE
				;妊娠していたら見学
				SIF COND("妊娠中：妊娠発覚済み", LCOUNT)
					CONTINUE

				CALL SET_COORDINATE, "泳装", LCOUNT
				;水着姿でない
				SIF CHECK_CLO("泳装姿", LCOUNT) == 0
					CONTINUE
				;泳いだ人は水泳フラグON
				CFLAG:LCOUNT:水泳 = 1
				;75%で水着で現れる
				IF RAND:4 == 0
					CALL SET_COORDINATE, "制服", LCOUNT
				ELSEIF COND("冬") + COND("秋")
					CALL SET_SHIRT, "运动衫", LCOUNT
				ENDIF
			NEXT
		ELSE
			PRINTFORMW 今天是正常上课的日子。
		ENDIF
		;いつもの格好を設定
		FOR LCOUNT, 2, CHARANUM
			CSTR:LCOUNT:いつもの格好 = %CLO_ABOUT(LCOUNT)%
		NEXT

		;標語を実践してくれるかなどを判定
		CALL FOLLOW_RULES

		;日焼け対策をしていないなら日焼け(日焼け標語があるので標語の次)
		SELECTCASE TEXTS("季節")
		CASE "春", "夏"
			FOR LCOUNT, 2, CHARANUM
				SIF CFLAG:LCOUNT:水泳 && COND("晒痕対策", LCOUNT) == 0
					CALL SETFLAG, "晒痕", LCOUNT
			NEXT
		ENDSELECT

		;標語を貼りだす
		CALL POST_RULES

		;自撮りを行う
		CALL PHOTO_SELFY
		CALL SEND_SELFY

		CALL EVENT_ACADEMY, "学園での日常"
		CALL EVENT_ACADEMY, "お仕事"
		CALL EVENT_ACADEMY, "性処理"
		CVARSET CFLAG, GETNUM(CFLAG, "性処理済")
		CVARSET CFLAG, GETNUM(CFLAG, "性欲解消済")
		;お弁当orその他で主に２パターン
		CALL EVENT_ACADEMY, "お昼ご飯"

		;学園のお昼は性処理係の証を付ける
		CALL SETFLAG, "性処理係の証"
	ENDIF

	;特殊な衣装
	SELECTCASE TEXTS("日期対応イベント")
	CASE "女仆の日"
		FOR LCOUNT, 2, CHARANUM
			SIF CFLAG:LCOUNT:夜型
				CONTINUE
			;オトコは男の娘のみ
			;SIF TALENT:LCOUNT:オトコ && COND("男の娘", LCOUNT) == 0
			;	CONTINUE
			SIF TALENT:LCOUNT:叛逆 || ABL:LCOUNT:信頼 < 3
				CONTINUE
			SIF (ABL:LCOUNT:露出癖 < 3 && TALENT:LCOUNT:不知羞恥 <= 0) && ABL:LCOUNT:奉仕精神 < 5
				CONTINUE
			;メイド服に着替える
			CALL SET_COORDINATE, TEXTS("日期対応イベント"), LCOUNT

			CFLAG:LCOUNT:Cosplay中 = 1
		NEXT

	CASE "比基尼の日"
		FOR LCOUNT, 2, CHARANUM
			SIF CFLAG:LCOUNT:夜型
				CONTINUE
			;オトコは男の娘のみ
			;SIF TALENT:LCOUNT:オトコ && COND("男の娘", LCOUNT) == 0
			;	CONTINUE
			SIF TALENT:LCOUNT:叛逆 || ABL:LCOUNT:信頼 < 3
				CONTINUE
			SIF (ABL:LCOUNT:露出癖 < 3 && TALENT:LCOUNT:不知羞恥 <= 0) && ABL:LCOUNT:奉仕精神 < 5
				CONTINUE
			;今年の水着が選んであるならそれに着替えるかも
			IF CSTR:LCOUNT:今年の泳装 != "" && (ABL:LCOUNT:露出癖 >= 5 || TALENT:LCOUNT:不知羞恥)
				CALL SET_COORDINATE, "今年の泳装", LCOUNT
			ELSE
				CALL SET_COORDINATE, TEXTS("日期対応イベント"), LCOUNT
			ENDIF
			CFLAG:LCOUNT:Cosplay中 = 1
		NEXT
	CASE "兔女郎の日"
		FOR LCOUNT, 2, CHARANUM
			SIF CFLAG:LCOUNT:夜型
				CONTINUE
			;オトコは男の娘のみ
			;SIF TALENT:LCOUNT:オトコ && COND("男の娘", LCOUNT) == 0
			;	CONTINUE
			SIF TALENT:LCOUNT:叛逆 || ABL:LCOUNT:信頼 < 3
				CONTINUE
			SIF (ABL:LCOUNT:露出癖 < 3 && TALENT:LCOUNT:不知羞恥 <= 0) && ABL:LCOUNT:奉仕精神 < 5
				CONTINUE
			;バニーに着替える
			CALL SET_COORDINATE, TEXTS("日期対応イベント"), LCOUNT

			CFLAG:LCOUNT:Cosplay中 = 1
		NEXT
	CASE "新年"
		FOR LCOUNT, 2, CHARANUM
			;SIF TALENT:LCOUNT:オトコ
			;	CONTINUE
			SIF TALENT:LCOUNT:叛逆
				CONTINUE
			;振袖に着替える
			CALL SET_COORDINATE, TEXTS("日期対応イベント"), LCOUNT
		NEXT
	CASE "猫の日"
		FOR LCOUNT, 2, CHARANUM
			SIF TALENT:LCOUNT:叛逆
				CONTINUE
			;コスプレがあるかチェック
			CALL SET_COORDINATE, TEXTS("日期対応イベント"), LCOUNT
		NEXT
	CASEELSE
		;あなた好みの服装が分かっている場合には着替えてくれるかも
		FOR LCOUNT, 2, CHARANUM

			;確率は低い
			SIF RAND:10
				CONTINUE
			SIF COND("同伴可能", LCOUNT)
				CONTINUE
			SIF COND("自発Cosplay可能", LCOUNT) == 0
				CONTINUE
			;どれに着替えるか
			VARSET LOCALS
			SPLIT CSTR:LCOUNT:希望着装履歴, "/", LOCALS
			;50％で最近希望された衣装に着替える
			IF RAND:2
				CALL SET_COORDINATE, EXTSTR(LOCALS:0, "＠"), LCOUNT
			ELSE
				CALL SET_COORDINATE, EXTSTR(TEXTR(CSTR:LCOUNT:希望着装履歴), "＠"), LCOUNT
			ENDIF
			CFLAG:LCOUNT:Cosplay中 = 1
		NEXT
	ENDSELECT
ELSEIF COND("夜")
	SELECTCASE TEXTS("曜日")
	CASE "日"
	CASEELSE
		;制服に着替える
		FOR LCOUNT, 2, CHARANUM
			SIF CFLAG:LCOUNT:夜型
				CONTINUE
			CALL SET_COORDINATE, "制服", LCOUNT
		NEXT
	ENDSELECT
	;普段着に着替える
	FOR LCOUNT, 2, CHARANUM
		;帰宅時の格好を記録
		CALL MEMO_DRESS, "帰宅時の格好", LCOUNT
		CALL SET_COORDINATE, "便装", LCOUNT
		;いつもの格好を設定
		CSTR:LCOUNT:いつもの格好 = %CLO_ABOUT(LCOUNT)%
	NEXT
ENDIF
;標語で衣装を変えた後にコスプレで上書きした場合の処理
FOR LCOUNT, 2, CHARANUM
	SIF CFLAG:LCOUNT:Cosplay中 && CFLAG:LCOUNT:標語実践 == 2
		CFLAG:LCOUNT:標語実践 = 0
NEXT

;着替えたタイミングで自撮りを行う
CALL PHOTO_SELFY

;自宅の場合にはキャラごとの居る基本の場所をCSTR:現在位置 に入れておく
IF PLACE("自宅")
	FOR LCOUNT, 2, CHARANUM
		IF CSTR:LCOUNT:喜欢的房间 != "" && RAND:4
			CALL MOVE_PLACE, TEXTR(CSTR:LCOUNT:喜欢的房间), LCOUNT
		ELSE
			CALL MOVE_PLACE, @"%CSVCALLNAME(NO:LCOUNT)%的房間", LCOUNT
		ENDIF
	NEXT
ELSEIF PLACE("部室")
	;本日のおやつは？
	CALL SETFLAG, "本日の点心"

	FOR LCOUNT, 2, CHARANUM
		CALL MOVE_PLACE, "部室", LCOUNT
	NEXT
ENDIF

SELECTCASE TEXTS("季節詳細")
;真夏は全員うっすら汗状態がデフォ
CASE "夏"
	CVARSET CFLAG, GETNUM(CFLAG, "汗"), 1
	FOR LCOUNT, 1, CHARANUM
		SIF RAND:3 == 0
			CFLAG:LCOUNT:汗 += 1 + RAND:3
		SIF TALENT:LCOUNT:開朗 && RAND:3
			CFLAG:LCOUNT:汗 += RAND:4
		SIF TALENT:LCOUNT:粗野 && RAND:3
			CFLAG:LCOUNT:汗 += RAND:4
		SIF TALENT:LCOUNT:易出汗
			CFLAG:LCOUNT:汗 += 3 + RAND:5
	NEXT
CASE "初夏"
	FOR LCOUNT, 1, CHARANUM
		SIF RAND:3 == 0
			CFLAG:LCOUNT:汗 += 1 + RAND:2
		SIF TALENT:LCOUNT:開朗 && RAND:3
			CFLAG:LCOUNT:汗 += RAND:3
		SIF TALENT:LCOUNT:粗野 && RAND:3
			CFLAG:LCOUNT:汗 += RAND:3
		SIF TALENT:LCOUNT:易出汗
			CFLAG:LCOUNT:汗 += 2 + RAND:4
	NEXT
CASEELSE
	FOR LCOUNT, 1, CHARANUM
		SIF TALENT:LCOUNT:易出汗 == 0
			CONTINUE

		IF COND("冬") && RAND:6
			CONTINUE
		ELSEIF RAND:3
			CONTINUE
		ENDIF

		CFLAG:LCOUNT:汗 += 1 + RAND:6
	NEXT
ENDSELECT
;体を動かす講義の直後だと汗かき状態
IF COND("体育") && COND("昼")
	FOR LCOUNT, 1, CHARANUM
		;汗かきにならない条件など
		IF COND("妊娠中：妊娠発覚済み", LCOUNT)
		;教師かつ体育担当ではない＆体操服姿でもない
		ELSEIF COND("教員", LCOUNT) && STRCOUNT(CSTR:LCOUNT:学年, "体育") == 0 && CHECK_CLO("体操服姿", LCOUNT) == 0
		ELSE
			SIF CFLAG:LCOUNT:汗 < 6
				CFLAG:LCOUNT:汗 = 6
			SIF COND("冬") && RAND:2
				CFLAG:LCOUNT:汗 = 3
			;性器に汚れを付ける？
			IF COND("夏") && CONFIG("夏天的運動汗流浃背")
				SIF PENIS(LCOUNT)
					SETBIT BASE:LCOUNT:老廃物, 0
				SIF TALENT:LCOUNT:男性 == 0
					SETBIT BASE:LCOUNT:老廃物, 1
			ENDIF
		ENDIF

		;着替え済みの場合には汗だくではなくす
		SIF LCOUNT != MASTER && CHECK_CLO("体操服姿", LCOUNT) == 0
			CALL SETFLAG_DAILY, "拭汗", LCOUNT
	NEXT
ENDIF
;汗汚れを付ける
FOR LCOUNT, 1, CHARANUM
	SIF CFLAG:LCOUNT:汗 == 0
		CONTINUE

	;雪女は汗をかかない
	SIF COND("種族：雪女", LCOUNT)
		CFLAG:LCOUNT:汗 = 0

	;夜は汗だく状態の人は汗状態にする
	SIF COND("夜")
		CFLAG:LCOUNT:汗 = 1

	CALL SETFLAG_DAILY, "汗汚れ", LCOUNT
NEXT

MEMO_TARGET = TARGET
TARGET = 0
;入室にかかる時間フラグ設定…目当ての相手がなかなか来ないのは面白くないので基本的には３Ｔで全員集合させる
IF TEXTS("曜日") != "日"
	;これは = 1 の時に入室イベントが発生する
	FOR LCOUNT, 1, CHARANUM
		;最初からいない人は不在 = 1。場所移動ではぶられた人は = 2となる
		IF COND("部室出現可能", LCOUNT) == 0
			IF COND("昼")
				CFLAG:LCOUNT:不在 = 1
				CFLAG:LCOUNT:入室までの所要時間 = 0
				CONTINUE
			ENDIF
		ENDIF
		SELECTCASE LCOUNT
		CASE MASTER
			CFLAG:LCOUNT:入室までの所要時間 = 2
		CASEELSE
			;顔出し頻度が低い人
			IF CFLAG:LCOUNT:顔出し頻度 && COND("発情中", LCOUNT) == 0
				CFLAG:LCOUNT:入室までの所要時間 = 2 + RAND:100
			ELSEIF RAND:2
				CFLAG:LCOUNT:入室までの所要時間 = 2
			ELSEIF RAND:2
				CFLAG:LCOUNT:入室までの所要時間 = 3 + RAND:2
			ELSE
				CFLAG:LCOUNT:入室までの所要時間 = 5 + RAND:2
			ENDIF
		ENDSELECT
	NEXT

	;新しく入部する人々はCFLAG:入室までの所要時間の3から一人づつ順々に現れる
	IF CHARA_NUM("入部前")
		LOCAL = CHARA_NUM("入部前")
		CHARACOUNT = 0

		FOR LCOUNT, 2, CHARANUM
			SIF CFLAG:LCOUNT:入室までの所要時間 == 0
				CONTINUE

			;加入前の人を3から一人づつ並べる
			IF CFLAG:LCOUNT:加入日 == 0
				CFLAG:LCOUNT:入室までの所要時間 = 3 + CHARACOUNT
				CHARACOUNT += 1
			;値が3以上の人全員、CHARA_NUM("入部前")だけ後にずらす
			ELSEIF CFLAG:LCOUNT:入室までの所要時間 >= 3
				CFLAG:LCOUNT:入室までの所要時間 += LOCAL
			ENDIF
		NEXT
	ENDIF
ENDIF

IF COND("夜") || TEXTS("曜日") == "日"
	;一部キャラは不在。呼び出せば来てくれるかも
	FOR LCOUNT, 2, CHARANUM
		;最初からいない人は不在 = 1。場所移動ではぶられた人は = 2となる
		IF COND("自宅出現可能", LCOUNT) == 0
			CFLAG:LCOUNT:不在 = 1
			CFLAG:LCOUNT:入室までの所要時間 = 0
			CONTINUE
		ENDIF
		CALL SETFLAG_DAILY, "打招呼", LCOUNT
	NEXT
ENDIF

;寝てたりシャワってたりの判定。これについては例外的に全キャラの中から選び、イベント発生キャラがパートナーになる
;汗＆服姿だとたまにシャワー
IF RAND:4 == 0 && COND("Cosplay日") == 0
	TARGET = FIND_COND("淋浴可能")
	CFLAG:淋浴中 = 1
;たまに寝てる…条件はまだ決まってないのでテキトー
ELSEIF RAND:10 == 0
	TARGET = FIND_COND("居睡眠可能")
	CFLAG:睡眠 = 4 + RAND:4 + RAND:4
ENDIF
SIF TARGET == 0
	TARGET = MEMO_TARGET

;パートナーが不在の場合
SIF CFLAG:不在
	CALL SETFLAG, "対象指定", FINDCHARA(CFLAG:不在, 0, 2)
;何らかの処理でTARGETが変更されて優先順位が整えられてない場合
SIF CFLAG:優先順位 != 1
	CALL SETFLAG, "優先順位", TARGET
;パートナーの入室時間はMASTERに合わせる
CFLAG:入室までの所要時間 = CFLAG:MASTER:入室までの所要時間


;各種パラの調整
FOR LCOUNT, 1, CHARANUM
	CALL SETFLAG, "ＳＨＯＰ画面開始前", LCOUNT
NEXT

;現実時間フラグ取得
CALL GET_REALTIMEFORFLAG

;自宅での処理
IF PLACE("自宅")
	;自宅にいる人が居間に居るのか、それぞれの部屋に居るのかチェック
	FOR LCOUNT, 2, CHARANUM
		SELECTCASE LCOUNT
		CASE TARGET
			CALL MOVE_PLACE, SAVESTR:現在位置, LCOUNT
		CASEELSE
			IF RAND:3 == 0
				CALL MOVE_PLACE, SAVESTR:現在位置, LCOUNT
			ELSE
				;自室にいる場合はたまに睡眠中
				SIF RAND:10 == 0
					CFLAG:LCOUNT:睡眠 = 4 + RAND:4 + RAND:4
			ENDIF
		ENDSELECT
		SIF CFLAG:LCOUNT:不在
			CFLAG:LCOUNT:睡眠 = 0
	NEXT

	;自室にいる場合には部屋着
	FOR LCOUNT, 2, CHARANUM
		SIF COND("居家服着替え可能", LCOUNT) == 0
			CONTINUE

		CALL SET_COORDINATE, "居家服", LCOUNT
	NEXT
	;特殊な衣装
	SELECTCASE TEXTS("日期対応イベント")
	CASE "七夕"
		FOR LCOUNT, 2, CHARANUM
			SIF COND("夜") == 0
				CONTINUE
			;SIF TALENT:LCOUNT:オトコ
			;	CONTINUE
			SIF TALENT:LCOUNT:叛逆 || ABL:LCOUNT:信頼 < 3
				CONTINUE

			;浴衣に着替える
			CALL SET_COORDINATE, TEXTS("日期対応イベント"), LCOUNT
		NEXT
	CASE "猫の日", "万圣节", "圣诞节"
		FOR LCOUNT, 2, CHARANUM
			SIF TALENT:LCOUNT:叛逆
				CONTINUE
			;コスプレがあるかチェック
			CALL SET_COORDINATE, TEXTS("日期対応イベント"), LCOUNT
		NEXT
	ENDSELECT
ENDIF

;常時Ｖバイブな人が電源ボックスを繋いでいるかの判定
FOR LCOUNT, 2, CHARANUM
	SIF TALENT:LCOUNT:常時Ｖ振動棒 == 0 || ABL:LCOUNT:欲望 < 3 || ABL:LCOUNT:露出癖 == 0 || ABL:LCOUNT:自慰中毒 == 0 || CHECK_CLO("電源BOX保持可能", LCOUNT) == 0
		CONTINUE
	SIF COND("欲求不満", LCOUNT) == 0 && ABL:LCOUNT:自慰中毒 < 5
		CONTINUE
	;スカート履いてない時にやっちゃうのは痴女なので現状不可能
	SIF CHECK_SKIRT("裙", LCOUNT) == 0
		CONTINUE
	SIF MAX(ABL:LCOUNT:露出癖, ABL:LCOUNT:自慰中毒) >= 3 && RAND:3 == 0
		CALL SETFLAG, "電源BOX装着", LCOUNT
NEXT

SELECTCASE TEXTS("曜日")
CASE "日"
CASEELSE

	CALL BLANK_DRAWLINE
	DRAWLINE

	;入室イベント
	CALL EVENT_ENTER_ROOM

	;おやつイベント
	CALL EVENT_ACADEMY, "部活の点心"
ENDSELECT
;夜の髪型変更はここ。朝と昼は上の方にある
SIF COND("夜")
	CALL SETFLAG, "ＴＰＯによる外見変更"

;-------------------------------------------------
;時間ごとに変化するフラグのチェック
;-------------------------------------------------
@CHECK_FLAG_CHARA
#DIM LCOUNT
#DIM NUM_MEMBER

;現在この場所にいるキャラ数をカウントし、二人きりかを見る
NUM_MEMBER = 1
FLAG:二人きり = 0
CVARSET CSTR, GETNUM(CSTR, "お誘い")

FOR LCOUNT, 1, CHARANUM
	VARSET NOWEX:LCOUNT:0
	;服のずれなどを直す
	CALL RESETFLAG_CLO, LCOUNT, "汚れは就这様"
	;自宅だと靴を脱ぐ設定なら脱がせる
	CALL SET_SHOES_MEMO, LCOUNT
	SIF COND("外套脱衣発生", LCOUNT)
		CALL RESET_OTHER, "外套", LCOUNT
	CALL VARSET_MAIDFACE, LCOUNT

	CALL SETFLAG, "学力の設定", LCOUNT
	CALL SETFLAG, "属性", LCOUNT

	SELECTCASE COND("欲求不満", LCOUNT)
	CASE IS >= 2
		CFLAG:LCOUNT:躁动难忍 = 1
	CASE 1
		IF COND("発情期", LCOUNT)
			CFLAG:LCOUNT:躁动难忍 = 1
		ELSE
			CFLAG:LCOUNT:躁动难忍 = 0
		ENDIF
	CASE IS <= 0
		CFLAG:LCOUNT:躁动难忍 = 0
	ENDSELECT

	;オトコなら胎内の精液を消去。
	IF TALENT:LCOUNT:男性 && TALENT:LCOUNT:妊娠可能 == 0
		BASE:LCOUNT:胎内精子量 = 0
		BASE:LCOUNT:胎内精液量 = 0
	ELSE
		;妊娠などの理由でバイオリズムを一旦リセットされていた人の処理
		SIF BASE:LCOUNT:バイオリズム == 0
			BASE:LCOUNT:バイオリズム = (ABS(BASE:LCOUNT:誕生日%10 + FLAG:現実時間%10) % 10) + 1
	ENDIF

	SIF COND("現在存在", LCOUNT) == 0 || LCOUNT == MASTER
		CONTINUE

	NUM_MEMBER += 1

	IF DEVENT("初回台詞", LCOUNT) == 0
		CFLAG:LCOUNT:打招呼した = 1
		CFLAG:LCOUNT:打招呼された = 1
	ENDIF

	IF SAVESTR:現在位置 == CSTR:LCOUNT:現在位置
		SIF CMOVE(SAVESTR:現在位置, LCOUNT) == 0
			CSTR:LCOUNT:移動履歴 = %SAVESTR:現在位置%\/%CSTR:LCOUNT:移動履歴%
		;パンツ＆ブラの確認チェック
		SIF CHECK_CLO("胖次露出", LCOUNT) && CHECK_CLO("胖次未確認", LCOUNT) == 0
			CALL SETFLAG_DAILY, "胖次形状確認", LCOUNT
		SIF (CHECK_CLO("胸罩露出", LCOUNT) || COND("胸罩透け", LCOUNT) ) && CHECK_CLO("胸罩未確認", LCOUNT) == 0
			CALL SETFLAG_DAILY, "胸罩形状確認", LCOUNT
		;電源ボックスチェック
		SIF CFLAG:LCOUNT:電源BOX && (CFLAG:LCOUNT:掀起裙子 || CHECK_SKIRT("裙", LCOUNT) == 0)
			CALL SETFLAG, "電源BOX発覚", LCOUNT
	ENDIF

	SIF CFLAG:LCOUNT:加入日 == 0
		CFLAG:LCOUNT:加入日 = DAY

	IF CFLAG:LCOUNT:優先順位 == 0
		;優先順位の調整
		IF FINDCHARA(CFLAG:優先順位, 1, 2) <= 0
			CFLAG:LCOUNT:優先順位 = 1
		ELSEIF FINDCHARA(CFLAG:優先順位, 2, 2) <= 0
			CFLAG:LCOUNT:優先順位 = 2
		ELSEIF FINDCHARA(CFLAG:優先順位, 3, 2) <= 0
			CFLAG:LCOUNT:優先順位 = 3
		ENDIF
	ENDIF
NEXT

SIF NUM_MEMBER == 2
	FLAG:二人きり = 1


@SETFLAG_SHOP
#DIM LCOUNT
;季節、年表示のフラグ
CALL SETFLAG, "年月日"

MONEY = MAX(MONEY, 0)
;TFLAGをリセット
VARSET TFLAG
;オープニング終了
FLAG:オープニング = 0
;助手画面をデフォに戻す
FLAG:助手画面 = 0
;能力表示画面の３サイズ表示を初期化
FLAG:能力画面 = 0
;パートナー切り替え画面の設定を初期化
FLAG:対象変更画面 = 0
;調教中等を判定するフラグOFF
FLAG:状況判定 = 0
;立位モードOFF
CLEARBIT FLAG:調教过滤器, 10
FLAG:放課後Ｈ = 0
;非表示コマンドの表示状態をリセット
CLEARBIT FLAG:調教过滤器, 21
;PLAYERをMASTERに戻す（念のため）
MASTER = 1
PLAYER = MASTER
;複数キャラを使うイベント用の対象フラグリセット
VARSET TARGET, 0, 1, 4
VARSET PLAYER, 0, 1, 4
;TARGETとASSIが-1となることを禁止
TARGET = MAX(TARGET, 0)
ASSI:1 = MAX(ASSI:1, 0)
;調教助手は日常はアドバイザーと同じ
ASSI = MAX(ASSI:1, 0)
SELECTCOM = -1

;TARGETをパートナーにする
CALL SET_PARTNER, TARGET

IF CHARA_NUM("室内の邪魔者")
	FOR LCOUNT, 2, CHARANUM
		SIF CFLAG:LCOUNT:睡眠 && COND("現在存在", LCOUNT)
			CALL SETFLAG_DAILY, "叫醒", LCOUNT
	NEXT
ENDIF
;0夜に妊婦と過ごした, 3同衾イベント
CVARSET CFLAG, GETNUM(CFLAG, "行動")
CVARSET EQUIP, GETNUM(EQUIP, "Ｈ的奖励")
;イベントフラグをリセット
CVARSET TCVAR, GETNUM(TCVAR, "イベント")
;あなたの居場所を再設定
CSTR:MASTER:現在位置 = %SAVESTR:現在位置%

STR:日常イベント名 = 
STR:日常イベント名２ = 

;アニメーション用変数のリセット
VARSET ANIME_CHARA
VARSET ANIME_FCOUNT

;-------------------------------------------------
;幾つかのフラグをここで設定（主に新規キャラ参入のときに働く）
;-------------------------------------------------
CALL CHECK_FLAG_CHARA

;精力のセット
CALL SET_COUNTER_BASE
;調教コマンド実行前のパラメータの記録
CALL SAVE_PALAM_BEFORECOM

;-------------------------------------------------
;SHOP画面
;-------------------------------------------------
@SHOW_SHOP
#DIM MEMO_TARGET
#DIM MEMO_LINECOUNT

;-------------------------------------------------
;バージョンアップ
;-------------------------------------------------
CALL VERUP_eraAkumaMaid, TARGET
;ゲーム起動時にチェックする基本的なVERUP
SIF FLAG:VER_UPチェック == 0
	CALL CHECK_VER_UP

;-------------------------------------------------
;ロード時台詞
;-------------------------------------------------
;CALL KOJO_LOAD, TARGET

CALL SHOW_SHOP_MAIN

@SHOW_SHOP_MAIN
IF SAVESTR:進行中Ｈイベント != ""
	CALL EVENT_ACADEMY, SAVESTR:進行中Ｈイベント
	SAVESTR:進行中Ｈイベント = 
	RETURN 1
ENDIF

;-------------------------------------------------
;メイン画面の表示前にフラグの整理
;-------------------------------------------------
CALL SETFLAG_SHOP
;-------------------------------------------------
;メイン画面を表示する
;-------------------------------------------------
CALL PRINT_SHOP
;-------------------------------------------------
;毎日のイチャラブイベント
;-------------------------------------------------
CALL KOJO_DAILY


;これがONだと@USERSHOPに移動する
FLAG:SELECT_USERSHOP = 0

;@USERSHOP_PREはこの中で呼ばれる
IF CONFIG("颜绘动画動作中")
	CALL FLIRT_AND_ANIMATION
ELSE
	CALL FLIRT
ENDIF

;アニメーション用変数のリセット
VARSET ANIME_CHARA
VARSET ANIME_FCOUNT

;これがONだと@USERSHOPに移動する
SIF FLAG:SELECT_USERSHOP
	RETURN 1

RESTART

@REPRINT_SHOP_ANIME
TFLAG:颜绘动画中 = 1
CLEARLINE LINECOUNT - TFLAG:LINECOUNT_SHOP
CALL PRINT_SHOP
CALL KOJO_DAILY
TFLAG:颜绘动画中 = 0

;-------------------------------------------------
;SHOP画面の表示し直し。フラグが表示中に変更された場合などに使う
;-------------------------------------------------
@REPRINT_SHOP
CLEARLINE LINECOUNT - TFLAG:LINECOUNT_SHOP
CALL PRINT_SHOP

@REPRINT_SHOP_NAME
CLEARLINE LINECOUNT - TFLAG:LINECOUNT_SHOP
CALL PRINT_SHOP
;名前とだいたいの服装を表示
CALL PRINT_NAME_CLO

@REPRINT_SHOP_SELECT
#DIM MEMO_LINECOUNT
MEMO_LINECOUNT = LINECOUNT
CALL PRINT_SHOP
CLEARLINE LINECOUNT - MEMO_LINECOUNT


;-------------------------------------------------
;SHOP画面の表示
;-------------------------------------------------
@PRINT_SHOP
#LOCALSIZE 1000
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM NEXT_LINE
#DIM MEMO_TARGET
#DIM MEMO_ASSI
#DIM CHOICE
#DIMS MENU_SHOP
#DIMS WISH_CLO
#DIMS ITEMS
#DIMS GET_PANTU
#DIMS MEMO_PANTU

REDRAW 0

;再描画に備えて記録
TFLAG:LINECOUNT_SHOP = LINECOUNT

CALL BLANK_DRAWLINE
DRAWLINE

PRINTFORM %TEXTS("日期表示")% 
PRINTFORM -%TEXTS("現在位置")%- 
IF SAVESTR:現在位置 == "部室"
	SIF SAVESTR:標語 != ""
		CALL PRINT_STR, @"『_黄色_%SAVESTR:標語%_』"
ENDIF
IF TEXTS("日期対応イベント") != ""
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORM %TEXTS("日期対応イベント")%
	SIF DAYEV("周考") && TEXTS("日期対応イベント") != "周考"
		PRINTFORM  周考
	;クリスマスツリーチェック
	IF DAYEV("圣诞节期間") && NUM("圣诞节ツリー")
		SETCOLOR DEF_COLOR("爱心粉紅")
		FOR LCOUNT, 0, NUM("圣诞节ツリー")
			PRINTFORM %UNICODE(0xD83C)%%UNICODE(0xDF84)%
		NEXT
	ENDIF
	RESETCOLOR
ENDIF
PRINTL 
IF SAVESTR:点心 != ""
	PRINTFORM 本日の点心:%SAVESTR:点心詳細%
	SIF SAVESTR:茶 != ""
		PRINTFORM ＆%SAVESTR:茶%
	PRINTL 
ELSEIF SAVESTR:茶 != ""
	PRINTFORML 现在的饮料:%SAVESTR:茶%
ENDIF

;この時間帯に居る人たちを並べる
CALL PRINT_MEMBER

CALL PRINT_STR, @"青_[0]_ %NAME:MASTER%"
;PRINTFORM [0] %NAME:MASTER%
IF TALENT:MASTER:扶她
	PRINTPLAINFORM %UNICODE(0x26A5)%[%TALENT_NAME(GETNUM(TALENT, "阴茎"), TALENT:MASTER:阴茎, MASTER)%] 
ELSEIF TALENT:MASTER:男性
	PRINTPLAINFORM ♂[%TALENT_NAME(GETNUM(TALENT, "阴茎"), TALENT:MASTER:阴茎, MASTER)%] 
ELSE
	PRINTPLAINFORM ♀ 
ENDIF
PRINTPLAIN 魂
CALL PRINT_BAR, "魂ゲージ", BASE:MASTER:魂, MAXBASE:MASTER:魂, 5
PRINTFORM %TEXTS("0.01", BASE:MASTER:魂)% 
PRINTFORM 精力
CALL PRINT_BAR, "減少ゲージ", BASE:MASTER:精力, MAXBASE:MASTER:精力, 5
PRINTFORM {BASE:MASTER:精力}/{MAXBASE:MASTER:精力} 
CALL TEXT, "学力", MASTER
CALL TEXT, "学力評価", MASTER
CALL TEXT, "生理周期", MASTER
CALL TEXT, "状態", MASTER
PRINTL 
PRINTFORM  所持金:%MONEY_C(MONEY)% 

ITEMS = 
;消耗品。クリックで購入可能
FOR LCOUNT, 50, 60
	SIF ITEMNAME:LCOUNT == ""
		CONTINUE
	ITEMS = %ITEMS%%ITEMNAME:LCOUNT% ×{ITEM:LCOUNT}_{9000 + LCOUNT}/
NEXT
CALL PRINT_LIST, ITEMS
;没収中のパンツはここに並べる
FOR LCOUNT, 2, CHARANUM
	GET_PANTU = 
	SIF CSTR:LCOUNT:没収済胖次 == ""
		CONTINUE

	SWAP CSTR:LCOUNT:胖次, CSTR:LCOUNT:没収済胖次

	IF COND("胖次ドーピング", LCOUNT)
		GET_PANTU = 灰色\/%PANTIES(LCOUNT)%（%CALLNAME:LCOUNT%）\/
	ELSE
		GET_PANTU = %PANTIES(LCOUNT)%（%CALLNAME:LCOUNT%）_{4000 + LCOUNT}\/
	ENDIF

	CALL PRINT_LIST, GET_PANTU

	SWAP CSTR:LCOUNT:胖次, CSTR:LCOUNT:没収済胖次
NEXT
PRINTL 

;優先順位の整合性をとる
CALL CHECK_POSITION

NUM_ANIME = 0

;優先順位上位５名を表示する
FOR LCOUNT, 1, 6
	CHOICE = FINDCHARA(CFLAG:優先順位, LCOUNT, 2)
	SIF CHOICE <= 0
		BREAK
	SIF CFLAG:CHOICE:不在
		CONTINUE

	;PRINTL 
	;PRINTFORM [{LCOUNT}]
	CALL PRINT_STR, @"青_[{LCOUNT}]"

	IF COND("現在不在", CHOICE)
		IF PLACE("自宅")
			SIF CHOICE != TARGET
				PRINTFORM [{LCOUNT + 10}]
			SELECTCASE CSTR:CHOICE:現在位置
			CASE @"%CSVCALLNAME(NO:CHOICE)%的房間"
				CALL PRINT_STR, "灰色_(自室)_"
			CASEELSE
				CALL PRINT_STR, @"灰色_(%CSTR:CHOICE:現在位置%)_"
			ENDSELECT
		ELSE
			SIF CHOICE != TARGET && CFLAG:睡眠 == 0
				PRINTFORM [{LCOUNT + 10}]
			CALL PRINT_STR, "灰色_(不在)_"
		ENDIF
	ELSE
		IF COND("颜绘表示中", CHOICE)
			ANIME_CHARA:NUM_ANIME = CHOICE
			NUM_ANIME += 1
		ENDIF

		SIF CHOICE != TARGET
			PRINTFORM [{LCOUNT + 10}]

		;SELECTCASE CHOICE
		;CASE TARGET
		;	SETCOLOR DEF_COLOR("黄色")
		;CASE ASSI:1
		;	SETCOLOR DEF_COLOR("緑")
		;ENDSELECT

		IF CFLAG:CHOICE:行動参加中
			SETCOLOR DEF_COLOR("黄色")
		ELSEIF CFLAG:CHOICE:レ阴蒂エーション中
			SETCOLOR DEF_COLOR("緑")
		ENDIF
	ENDIF
	PRINTFORM %NAME:CHOICE%
	RESETCOLOR
	IF TALENT:CHOICE:扶她
		PRINTPLAINFORM %UNICODE(0x26A5)%[%TALENT_NAME(GETNUM(TALENT, "阴茎"), TALENT:CHOICE:阴茎, CHOICE)%]
	ELSEIF TALENT:CHOICE:男性
		PRINTPLAINFORM ♂[%TALENT_NAME(GETNUM(TALENT, "阴茎"), TALENT:CHOICE:阴茎, CHOICE)%]
	ENDIF
	SIF CFLAG:CHOICE:便当
		PRINTFORM %UNI("便当")% 
	IF TALENT:CHOICE:男性
		PRINTPLAINFORM [%CONDS("体型", CHOICE)%]
	ELSE
		PRINTPLAINFORM [%CONDS("体型", CHOICE)%(%CONDS("罩杯", CHOICE)%)]
	ENDIF
	;日焼け有り？
	IF TEXTS("晒痕", CHOICE) != ""
		CALL PRINT_STR, @"[_茶色_%TEXTS("晒痕", CHOICE)%_]"
	ELSEIF TALENT:CHOICE:肤色
		SIF COND("颜绘表示中", CHOICE) == 0
			PRINTFORM [%CONDS("肤色", CHOICE)%]
	ENDIF
	CALL TEXT, "関係", CHOICE
	CALL TEXT, "学力評価", CHOICE
	CALL TEXT, "生理周期", CHOICE
	CALL TEXT, "状態", CHOICE

	PRINTL 
	SELECTCASE CFLAG:CHOICE:好感度
	CASE IS >= 2500
		CALL PRINT_STR, @" 好感度_爱心粉紅_{CFLAG:CHOICE:好感度, 4}_ "
	CASE IS < 0
		CALL PRINT_STR, @" 好感度_青_{CFLAG:CHOICE:好感度, 4}_ "
	CASEELSE
		CALL PRINT_STR, @" 好感度{CFLAG:CHOICE:好感度, 4}_ "
	ENDSELECT
	PRINTFORM 体力{BASE:CHOICE:体力} 

	PRINTFORM 精力
	;CALL PRINT_BAR, "減少ゲージ", BASE:CHOICE:精力, MAXBASE:CHOICE:精力, 6
	;PRINTFORM {BASE:CHOICE:精力}/{MAXBASE:CHOICE:精力} 
	SELECTCASE BASE:CHOICE:精力*100/MAXBASE:CHOICE:精力
	CASE IS >= 90
		CALL PRINT_STR, @"緑_%TOSTR(BASE:CHOICE:精力)%_ "
	CASE IS >= 75
		CALL PRINT_STR, @"%TOSTR(BASE:CHOICE:精力)% "
	CASEELSE
		CALL PRINT_STR, @"青_%TOSTR(BASE:CHOICE:精力)%_ "
	ENDSELECT

	CALL TEXT, "学力", CHOICE

	PRINTFORM 欲求
	CALL PRINT_BAR, "欲求ゲージ", BASE:CHOICE:欲求不満, 100, 6
	;PRINTFORM {BASE:CHOICE:欲求不満}\% 
	PRINT  

	IF TALENT:CHOICE:男性 == 0
		CALL TEXT, "胎内精液量", CHOICE
		IF CFLAG:CHOICE:創口貼装着
			PRINTFORM %UNI("創口貼")% 
		ELSE
			PRINT  
		ENDIF
	ENDIF

	CALL TEXT, "尿意", CHOICE
	PRINTL 
	;不在でないなら衣装の表示
	IF COND("現在不在", CHOICE) == 0
		PRINT  
		CALL PRINT_DRESS, CHOICE, "短縮表示"

		;着せ替えのお願い表示
		;3000-パンツ没収, 3100-全裸化, 3200-下着化
		IF COND("現在不在", CHOICE) == 0 && CFLAG:CHOICE:睡眠 == 0
			MEMO_TARGET = TARGET

			TARGET = CHOICE
			PRINT  
			WISH_CLO = 

			IF COND("胖次没収実行可能") && COND("合意：胖次交易")
				WISH_CLO = %WISH_CLO%胖次交易_{3000 + LCOUNT}\/
			;返されても困るか…
			;ELSEIF EQUIP:パンツ == 0 && CSTR:没収済パンツ != ""
			;	WISH_CLO = %WISH_CLO%パンツ返却_{3000 + LCOUNT}\/
			ENDIF

			SIF CHECK_CLO("裸体") == 0 && COND("合意：全裸化") && CHOSE_DEVENT("变成裸体") == 0
				WISH_CLO = %WISH_CLO%全裸化_{3100 + LCOUNT}\/

			;IF EQUIP:服 + EQUIP:スカート
			;	;下着化判定
			;	SELECTCASE TCVAR:やる気 - (CHARA_NUM("集合予定") - 2 > 0)*20
			;	CASE IS <= 0
			;		WISH_CLO = %WISH_CLO%灰色/下着姿_{3200 + CHOICE}\/
			;	CASE IS < 30
			;		WISH_CLO = %WISH_CLO%下着姿_{3200 + CHOICE}\/
			;	CASEELSE
			;		WISH_CLO = %WISH_CLO%ハートピンク/下着姿_{3200 + CHOICE}\/
			;	ENDSELECT
			;ENDIF

			CALL PRINT_LIST, WISH_CLO

			TARGET = MEMO_TARGET
		ENDIF
		PRINTL 
	ENDIF
	;指示されているならそれも表示
	IF CHOICE == ASSI && ABL:CHOICE:助手 && CFLAG:CHOICE:助手の指示
		PRINT  指示：
		CALL ASSI_LIST_INDICATE, CHOICE
		PRINTL 
	ENDIF
NEXT

;優先順位上位５名の顔グラを表示する
IF NUM_ANIME
	DRAWLINE
	;あなたの顔グラ出す？
	IF CONFIG("MASTER颜绘") && CFLAG:MASTER:颜绘
		CALL PRINT_MAIDFACE, 1, ANIME_CHARA:0, ANIME_CHARA:1, ANIME_CHARA:2, ANIME_CHARA:3
	ELSE
		CALL PRINT_MAIDFACE, ANIME_CHARA:0, ANIME_CHARA:1, ANIME_CHARA:2, ANIME_CHARA:3, ANIME_CHARA:4
	ENDIF
ENDIF
DRAWLINE


;基本的にはこんな感じ
;[100]ベッドに誘う		[101]デートに誘う		[102]ラブホに呼び出す	[103]能力表示と役割変更
;-------------------------------------------------
;[200]トーク			[201]もてなす			[202]隙を見せる			[203]パンツチェック
;[204]スマホで写真撮影	[205]性処理を頼む
;-------------------------------------------------
;[300]取引する			[301]人脈を広げる		[302]買い物する			[303]抜け出して密会
;[333]セーブ			[444]ロード				[555]コンフィグ			[666]写真の閲覧
;[777]エンディングを迎える						[888]季節イベント		[999]休憩


VARSET MENU_SHOP

SIF CFLAG:睡眠 || COND("约会の予定あり")
	MENU_SHOP = %MENU_SHOP%灰色/
IF CFLAG:ご褒美権
	MENU_SHOP = %MENU_SHOP%[100]ご褒美を頂く/
ELSE
	MENU_SHOP = %MENU_SHOP%[100]邀请上%NAMES("床")%/
ENDIF

;只今デートのお時間(日曜昼)
IF DAYEV("约会領帯ム")
	IF PLACE("自宅") == 0
		MENU_SHOP = %MENU_SHOP%灰色/[101]和誰一起约会/
	ELSEIF COND("约会の予定あり")
		MENU_SHOP = %MENU_SHOP%爱心粉紅/[101]去约会/
	ELSEIF CHARA_NUM("约会可能")
		MENU_SHOP = %MENU_SHOP%[101]和誰一起约会/
	ELSE
		MENU_SHOP = %MENU_SHOP%灰色/[101]和誰一起约会/
	ENDIF
;休日のデートに誰かを誘う？
ELSE
	IF CFLAG:睡眠
		MENU_SHOP = %MENU_SHOP%灰色/[101]邀请去约会/
	ELSEIF COND("约会可能") && CHARA_NUM("约会的约定") == 0
		IF COND("约会お誘い済み")
			MENU_SHOP = %MENU_SHOP%[101]接受约会邀请/
		ELSE
			MENU_SHOP = %MENU_SHOP%[101]邀请去约会/
		ENDIF
	ELSE
		MENU_SHOP = %MENU_SHOP%灰色/[101]邀请去约会/
	ENDIF
ENDIF

;この場にいない人をＨに誘う(居間or部室のみ選択可能)
IF PLACE("居間") + PLACE("部室") == 0 || FIND_COND("叫出来Ｈ可能") == 0 || COND("约会の予定あり")
	MENU_SHOP = %MENU_SHOP%灰色/[102]把部員叫过来/
ELSEIF PLACE("居間")
	MENU_SHOP = %MENU_SHOP%[102]叫到情人酒店来/
ELSEIF PLACE("部室")
	MENU_SHOP = %MENU_SHOP%[102]叫到做爱房来/
ENDIF

MENU_SHOP = %MENU_SHOP%[103]能力表示与职责変更/

;MENU_SHOP = %MENU_SHOP%[110]身嗜み変更/
;MENU_SHOP = %MENU_SHOP%[111]弱点・性癖の変更/
;MENU_SHOP = %MENU_SHOP%[112]好みの服装の変更/


MENU_SHOP = %MENU_SHOP%DRAWLINE/

;性処理できるかチェック
CALL CHECK_COM_ABLE, TARGET
;日常イベントのコマンド表示
FOR LCOUNT, 200, 208
	SELECTCASE DAILYCOMNAME_SELECT(DAILYCOMNAME(LCOUNT) )
	CASE ""
		CONTINUE
	;性処理は合意が無いとダメ
	CASE "拜托処理性欲", "进行性処理"
		IF COND("性処理意欲") == 0 || CHARA_NUM("性処理合意没有")
			MENU_SHOP = %MENU_SHOP%灰色/
		ELSEIF COND("干劲性処理") == 0 && COND("进行性処理") == 0
			MENU_SHOP = %MENU_SHOP%灰色/
		ENDIF
	CASE "性欲解消を手伝う"
		SIF COND("性欲解消を手伝う可能") == 0
			MENU_SHOP = %MENU_SHOP%灰色/
	;ちょっと隠ぺいしてみる
	;CASE "股間に見入る"
	;	MENU_SHOP = %MENU_SHOP%[{LCOUNT}]%DAILYCOMNAME(LCOUNT)%/
	;	CONTINUE
	;CASE "余韻に浸る", "股間に見入る"
	;	MENU_SHOP = %MENU_SHOP%ハートピンク/
	;CASE "お誘いに乗る"
	;	MENU_SHOP = %MENU_SHOP%ハートピンク/
	CASEELSE
		STR:選択肢 = 
		TRYCALLFORM DAILY{DAILYNUM(DAILYCOMNAME(LCOUNT))}_K{NO:TARGET}_ADDSELECT
		SIF STR:選択肢 != ""
			MENU_SHOP = %MENU_SHOP%緑/
	ENDSELECT

	MENU_SHOP = %MENU_SHOP%[{LCOUNT}]%DAILYCOMNAME_SELECT(DAILYCOMNAME(LCOUNT) )%/
NEXT

MENU_SHOP = %MENU_SHOP%DRAWLINE/

SIF TARGET == 0 || CFLAG:睡眠
	MENU_SHOP = %MENU_SHOP%灰色/
IF TARGET && COND("魂の取引可能")
	MENU_SHOP = %MENU_SHOP%[300]进行交易/
ELSE
	MENU_SHOP = %MENU_SHOP%[300]拜托了/
ENDIF
IF FLAG:勧誘可能 == 0 || CHARA_NUM("勧誘可能") == 0 || CFLAG:睡眠
	MENU_SHOP = %MENU_SHOP%灰色/
ELSE
	SIF PLACE("出先")
		MENU_SHOP = %MENU_SHOP%灰色/
ENDIF
MENU_SHOP = %MENU_SHOP%[301]勧誘同伴/

SIF CEVENT("槞彁：バイト探し", MASTER) && !CEVENT("槞彁：就労ビザ取得命令", MASTER)
	MENU_SHOP = %MENU_SHOP%太字/黄色/
SIF CFLAG:睡眠
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[302]買东西/

IF COND("場所移動可能") == 0 || COND("约会の予定あり")
	MENU_SHOP = %MENU_SHOP%灰色/
;槞彁はイベントあるかもなのでお知らせ
ELSEIF NO:TARGET == 15 && CEVENT("槞彁：ビザ取得约会のお誘い", MASTER) && !CEVENT("槞彁：ビザ発行不可", MASTER) && TEXTS("曜日") == "日"
	MENU_SHOP = %MENU_SHOP%太字/黄色/
ENDIF
IF PLACE("部室")
	IF CHARA_NUM("集合予定") >= 3
		MENU_SHOP = %MENU_SHOP%[303]偷偷密会/
	ELSE
		MENU_SHOP = %MENU_SHOP%[303]换場所/
	ENDIF
ELSE
	IF PLACE("野外")
		MENU_SHOP = %MENU_SHOP%[303]返回/
	ELSE
		MENU_SHOP = %MENU_SHOP%[303]换場所/
	ENDIF
ENDIF

MENU_SHOP = %MENU_SHOP%改行/[333]SAVE/

MENU_SHOP = %MENU_SHOP%[444]LOAD/

MENU_SHOP = %MENU_SHOP%[555]Config/
SIF NUM("写真") == 0
	MENU_SHOP = %MENU_SHOP%灰色/
MENU_SHOP = %MENU_SHOP%[666]浏览写真/

STR:ＥＮＤ标题 = 
STR:エンディング = 
STR:ＥＮＤカラー = 

SIF CFLAG:睡眠 == 0 || CFLAG:睡眠時エンディング
	TRYCALLFORM MENU_SINGLE_ENDING_K{NO:TARGET}

SELECTCASE STR:エンディング
CASE ""
	STR:ＥＮＤ标题 = 
CASEELSE
	;@MENU_SINGLE_ENDING_K{NO:TARGET}で文字色が決められていない場合
	IF STR:ＥＮＤカラー == ""
		SIF COND("未読ＥＮＤ選択可能")
			STR:ＥＮＤカラー = 爱心粉紅
	ENDIF
	SIF STR:ＥＮＤカラー != ""
		MENU_SHOP = %MENU_SHOP%%STR:ＥＮＤカラー%/
ENDSELECT

SELECTCASE STRLENS(STR:ＥＮＤ标题)
CASE 0
	MENU_SHOP = %MENU_SHOP%NOBUTTON/NOBUTTON/
CASE IS >= 19
	MENU_SHOP = %MENU_SHOP%[777]%STR:ＥＮＤ标题% %BL(24 - STRLENS(STR:ＥＮＤ标题) + 18)%/
CASEELSE
	MENU_SHOP = %MENU_SHOP%[777]%STR:ＥＮＤ标题%/NOBUTTON/
ENDSELECT

;マリーちゃんの中の人さん作のキャラ固定季節イベント
;メイドが健在である
;IF FINDCHARA(NO, 1) > 0
;	MENU_SHOP = %MENU_SHOP%[884]お菓子材料店/
;	MENU_SHOP = %MENU_SHOP%[885]チョコレート練成陣/DRAWLINE/
;ENDIF

SELECTCASE TEXTS("季節")
CASE "春"
	FLAG:季節イベントフラグ = CLEARBITS(FLAG:季節イベントフラグ, 10, 39)

	MENU_SHOP = %MENU_SHOP%NOBUTTON/
CASE "夏"
	FLAG:季節イベントフラグ = CLEARBITS(FLAG:季節イベントフラグ, 0, 9)
	FLAG:季節イベントフラグ = CLEARBITS(FLAG:季節イベントフラグ, 20, 39)

	IF GETBIT(FLAG:季節イベントフラグ, 10) == 0 && COND("昼") && TEXTS("曜日") == "土"
		MENU_SHOP = %MENU_SHOP%[888]部員全員で海へ/
	ELSEIF GETBIT(FLAG:季節イベントフラグ, 11) == 0 && COND("昼") && TEXTS("曜日") == "土"
		MENU_SHOP = %MENU_SHOP%[888]海イベントおまけ/
	ELSE
		MENU_SHOP = %MENU_SHOP%NOBUTTON/
	ENDIF
CASE "秋"
	FLAG:季節イベントフラグ = CLEARBITS(FLAG:季節イベントフラグ, 0, 19)
	FLAG:季節イベントフラグ = CLEARBITS(FLAG:季節イベントフラグ, 30, 39)

	MENU_SHOP = %MENU_SHOP%NOBUTTON/
CASE "冬"
	FLAG:季節イベントフラグ = CLEARBITS(FLAG:季節イベントフラグ, 0, 29)

	IF GETBIT(FLAG:季節イベントフラグ, 30) == 0 && COND("夜") && TEXTS("日期対応イベント") == "圣诞节"
		MENU_SHOP = %MENU_SHOP%[888]皆で圣诞节/
	ELSE
		MENU_SHOP = %MENU_SHOP%NOBUTTON/
	ENDIF
CASEELSE
	MENU_SHOP = %MENU_SHOP%NOBUTTON/
ENDSELECT

SIF COND("约会の予定あり")
	MENU_SHOP = %MENU_SHOP%灰色/
;休憩
IF PLACE("自宅")
	SELECTCASE TEXTS("時間")
	CASE "昼"
		MENU_SHOP = %MENU_SHOP%[999]休息到晚上/
	CASE "夜"
		IF PLACE("居間")
			MENU_SHOP = %MENU_SHOP%[999]洗个澡然后睡觉/
		ELSE
			MENU_SHOP = %MENU_SHOP%[999]就寝/
		ENDIF
	CASEELSE
		MENU_SHOP = %MENU_SHOP%[999]休息到晚上/
	ENDSELECT
ELSE
	SELECTCASE TEXTS("時間")
	CASE "夜"
		MENU_SHOP = %MENU_SHOP%[999]回去睡觉/
	CASEELSE
		MENU_SHOP = %MENU_SHOP%[999]打发時间/
	ENDSELECT
ENDIF

MENU_SHOP = %MENU_SHOP%DRAWLINE/

;六華が居る？
IF FINDCHARA(NO, 5) > 0
	IF TEXTS("日期対応イベント") == "新年" && DEVENT("社修繕完了", FINDCHARA(NO, 5))
		MENU_SHOP = %MENU_SHOP%[886]抽神签/DRAWLINE/
	ELSE
		SIF COND("昼") && TEXTS("曜日") != "日"
			MENU_SHOP = %MENU_SHOP%灰色/
		MENU_SHOP = %MENU_SHOP%[886]访问白蛇神社/DRAWLINE/
	ENDIF
ENDIF

VARSET LOCALS
SPLIT MENU_SHOP, "/", LOCALS

NEXT_LINE = 0

;SHOP画面の表示
FOR LCOUNT, 0, 100
	;まずは色だけチェック
	SELECTCASE LOCALS:LCOUNT
	CASE "灰色", "水色", "爱心粉紅", "青", "緑", "緑色", "黄色"
		;すでに指定色になっている場合は色変換を止める
		IF GETCOLOR() != DEF_COLOR(LOCALS:LCOUNT)
			SETCOLOR DEF_COLOR(LOCALS:LCOUNT)
			CONTINUE
		ENDIF
	ENDSELECT

	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	CASE "太字"
		FONTSTYLE 1
		CONTINUE
	CASE "改行"
		IF NEXT_LINE%4
			NEXT_LINE = 0
			PRINTL 
		ENDIF
		CONTINUE
	CASE "強制改行"
		NEXT_LINE = 0
		PRINTL 
		CONTINUE
	CASE "DRAWLINE"
		IF NEXT_LINE%4
			NEXT_LINE = 0
			PRINTL 
		ENDIF
		DRAWLINE
		CONTINUE
	ENDSELECT

	;太字にした？
	IF GETBIT(GETSTYLE(), 0)
		SELECTCASE LOCALS:LCOUNT
		CASE "NOBUTTON"
			PRINTPLAINFORM %TEXT_LJ("", 21)%
		CASEELSE
			PRINTFORM %TEXT_LJ(LOCALS:LCOUNT, 21)%
		ENDSELECT
	ELSE
		SELECTCASE LOCALS:LCOUNT
		CASE "NOBUTTON"
			PRINTPLAINFORM %TEXT_LJ("", 24)%
		CASEELSE
			PRINTFORM %TEXT_LJ(LOCALS:LCOUNT, 24)%
		ENDSELECT
	ENDIF
	FONTREGULAR
	RESETCOLOR

	NEXT_LINE += 1
	SIF NEXT_LINE%4 == 0
		PRINTL 
NEXT
SIF NEXT_LINE%4
	PRINTL 


;時間が経過しそうなものを選んだ際にはFLAG:現実時間を再取得する
@USERSHOP
#DIM LCOUNT
#DIM CHOICE
#DIM ACTOR
#DIM MEMO_TARGET
#DIM MEMO_ASSI
#DIM MEMO_ITEM
#DIM MEMO_DOPING
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT2
;起きうるイベントの判定に使う
#DIMS EVENT_TABLE
;これがONだとBEGIN TRAINする
#DIM GO_BEGIN_TRAIN
#DIM GO_BEGIN_TURNEND

GO_BEGIN_TRAIN = 0
GO_BEGIN_TURNEND = 0

CHOICE = RESULT
;PRINTFORML CHOICE = {CHOICE}

SELECTCASE FLAG:SELECT_USERSHOP
;[999]休憩
CASE 999
	SELECTCASE CHOICE
	CASE 0
		;現実時間フラグ取得
		CALL GET_REALTIMEFORFLAG
		IF CFLAG:睡眠 == 0
			IF RAND:10 == 0 && DAYEV("周日", "游戏内") && COND("昼") && TALENT:恋慕 && EXP:约会経験
				PRINTFORMW %CALLNAME:MASTER%和%CALLNAME:TARGET%、在考虑要做什么。
				PRINTFORMW 是去买东西呢、和朋友一起去玩呢、还是去教会做弥撒呢……
				PRINTFORMW Ｈ的话、%CALLNAME:MASTER%和%CALLNAME:TARGET%现在都没有那种心情。
				PRINTFORMW 所以呀、决定在家里无所事事地度过。
				PRINTFORMW 反正、最近一直过着充满刺激的日子。
				PRINTFORMW 偶尔、就这么度过悠闲的一天也不错。
				PRINTFORMW 一起坐在沙发上、无所事事的%CALLNAME:MASTER%旁边坐着的%CALLNAME:TARGET%
				PRINTFORMW 咚咚咚地敲着%CALLNAME:MASTER%的肩、表达想做膝枕的想法。
				PRINTFORMW 没有理由拒绝的%CALLNAME:MASTER%、就这样被%CALLNAME:TARGET%放到了膝枕上。
				PRINTFORMW 就这样、時間慢慢流逝。
				PRINTFORMW 不知不觉中%CALLNAME:MASTER%被撫摸着、梳起了头发。
				PRINTFORMW 彼此都没有说话、空気中十分的安静。
				PRINTFORMW 不过、是心情舒畅的安静。
				PRINTFORMW 原来、偶尔有这样一天也不错……
			ELSE
				CALL KOJO_DAILYCOM, "休憩"
			ENDIF
		ENDIF
		IF TFLAG:お浴室でＨ
			CALL BLANK_DRAWLINE
			PRINTFORMW ……决定和%TEXTS("間柄の")%%CALLNAME:TARGET%在浴室場Ｈ。
			GO_BEGIN_TRAIN = 1
		ELSEIF TFLAG:床イン
			CALL BLANK_DRAWLINE
			PRINTFORMW ……和%TEXTS("間柄の")%%CALLNAME:TARGET%一起睡觉了。
			GO_BEGIN_TRAIN = 1
		ELSE
			GO_BEGIN_TURNEND = 1
		ENDIF
	CASE 1

	CASEELSE
		CLEARLINE 1
		INPUT
		RESTART
	ENDSELECT
;[100]ベッドに誘う
CASE 100
	SELECTCASE CHOICE
	CASE 0
		GO_BEGIN_TRAIN = 1
		IF CFLAG:ご褒美権
			CFLAG:ご褒美権 -= 1
			EQUIP:Ｈ的奖励 = 1
		ENDIF
	CASE 1

	CASEELSE
		CLEARLINE 1
		INPUT
		RESTART
	ENDSELECT
;[101]デートに誘う
CASE 101
	;デート
	SELECTCASE CHOICE
	;ラブホテルに誘う
	CASE 0
		IF STR:Ｈする場所 != ""
			SAVESTR:現在位置 = %STR:Ｈする場所%
		ELSEIF TFLAG:文化祭约会
			SAVESTR:現在位置 = 人気のない教室
		ELSE
			SAVESTR:現在位置 = 情人旅館
		ENDIF
		TFLAG:床イン = 1
		CALL KOJO_DATE, "约会終了"
	;帰る
	CASE 1
		CALL KOJO_DATE, "约会終了"
		IF TFLAG:文化祭约会
			PRINTFORML %CALLNAME:MASTER%和%CALLNAME:TARGET%、差不多是该回家的时间了。
			PRINTFORMW 两人似乎获得了很宝贵的快乐回忆……
		ELSEIF COND("同居中")
			PRINTFORMW 结束了约会的%CALLNAME:MASTER%们一边回味着今天的经历、一边靠近着对方的身体踏上了帰路……
		ELSE
			PRINTFORMW %CALLNAME:MASTER%们对今天的约会发表了各自的感想之後、虽然有些依依不舍但还是各自踏上了回家的道路……
		ENDIF
		PRINTL 
	CASEELSE
		CLEARLINE 1
		INPUT
		RESTART
	ENDSELECT

	CFLAG:好感度 += 100
	PRINTFORML 好感度+100

	EXP:MASTER:约会経験 += 1
	EXP:约会経験 += 1
	PRINTFORMW 约会経験+1（二人とも）

	;デートの約束リセット
	CVARSET CFLAG, GETNUM(CFLAG, "约会的约定")

	;フラグが立っているならＨ！
	IF TFLAG:床イン || TFLAG:お浴室でＨ
		PRINTL 
		IF TFLAG:文化祭约会
			PRINTFORMW %TEXTS("間柄の")%%CALLNAME:TARGET%邀请用Ｈ来缓解祭典的压力。
		ELSEIF SAVESTR:現在位置 == "情人旅館"
			PRINTFORMW %TEXTS("間柄の")%%CALLNAME:TARGET%邀请去情人旅馆解决约会后的压力。
		ELSE
			PRINTFORMW %TEXTS("間柄の")%%CALLNAME:TARGET%邀请用Ｈ来缓解约会后的压力。
		ENDIF
		CALL MOVE_PLACE, SAVESTR:現在位置, TARGET
		GO_BEGIN_TRAIN = 1
	ELSE
		GO_BEGIN_TURNEND = 1
	ENDIF
;[202]隙を見せる。でのＨのお誘いとか押し倒しとかお風呂とか
CASE 202
	SELECTCASE CHOICE
	CASE 0
		SELECTCASE CSTR:お誘い
		CASE "邀请一起入浴"
			STR:日常イベント名 = 一緒にお浴室
			CALL BLANK_DRAWLINE
			PRINTL ………
			PRINTL ……
			PRINTL …
			PRINTL 
			CALL KOJO_DAILYCOM, "一緒にお浴室"
			;フラグが立っているならＨ！
			IF TFLAG:床イン || TFLAG:お浴室でＨ
				GO_BEGIN_TRAIN = 1
			ELSE
				PRINTFORMW %CALLNAME:MASTER%和%TEXTS("間柄の")%%CALLNAME:TARGET%进了浴室。
				GO_BEGIN_TURNEND = 1
			ENDIF
		CASE "推倒"
			SIF FLAG:叫出来Ｈ中 == 0
				PRINTFORMW %CALLNAME:MASTER%接受了%TEXTS("間柄の")%%CALLNAME:TARGET%。
			GO_BEGIN_TRAIN = 1
		CASEELSE
			SIF FLAG:叫出来Ｈ中 == 0
				PRINTFORMW %CALLNAME:MASTER%接受了%TEXTS("間柄の")%%CALLNAME:TARGET%的邀请。
			GO_BEGIN_TRAIN = 1
		ENDSELECT
		SIF STR:Ｈする場所 != ""
			SAVESTR:現在位置 = %STR:Ｈする場所%
	CASE 1
		;後半部分を再生
		CALL EVENT_DAILY, DAILYNUM("卖个破绽"), "卖个破绽"
		;パートナーを元の人に戻す
		TARGET = FINDCHARA(CFLAG:優先順位, 1, 2)
	CASEELSE
		CLEARLINE 1
		INPUT
		RESTART
	ENDSELECT
;特殊なイベントで来た場合
CASE 250
	SELECTCASE CHOICE
	CASE 0
		GO_BEGIN_TRAIN = 1
	CASE 1

	CASE 2
		IF COND("自発胖次送禮可能")
			GO_BEGIN_TRAIN = 1
		ELSE
			CLEARLINE 1
			INPUT
			RESTART
		ENDIF
	CASEELSE
		CLEARLINE 1
		INPUT
		RESTART
	ENDSELECT

	TFLAG:選択番号 = CHOICE
	CALL EVENT_ACADEMY, "二人きり：Ｈ邀请後半"
ENDSELECT

IF GO_BEGIN_TRAIN
	SIF TFLAG:お浴室でＨ
		EQUIP:お浴室でＨ = 1

	IF CFLAG:ご褒美権
		PRINTFORML 
		PRINTFORML ……从%CALLNAME:TARGET%那里得到Ｈ的奖励吗？
		CALL PRINT_STRL, "黄色_※実行値に+15＆初期气氛+_HH_HH_HH_黄色_＆危険日以外の中出し＆写真撮影許可、之类的奖励哦"
		CALL PRINT_SELECT, "要/还是保留吧"
		IF RESULT == 0
			CFLAG:ご褒美権 -= 1
			EQUIP:Ｈ的奖励 = 1
		ENDIF
	ENDIF

	;他の人の反応があるなら一人だけ出す（喋るのは優先順位が上の人から）
	IF FLAG:二人きり == 0
		PRINTL 
		MEMO_LINECOUNT = LINECOUNT
		FOR LCOUNT, 1, CHARANUM
			ACTOR = FINDCHARA(CFLAG:優先順位, LCOUNT, 2)
			SELECTCASE ACTOR
			CASE IS <= 0, TARGET, ASSI
				CONTINUE
			CASEELSE
				SIF CFLAG:ACTOR:睡眠 || COND("現在不在", ACTOR)
					CONTINUE
			ENDSELECT

			TFLAG:退出する = 0
			;名前とだいたいの服装を表示
			SIF COND("颜绘表示中", ACTOR) == 0
				CALL PRINT_NAME_CLO, ACTOR
			MEMO_LINECOUNT2 = LINECOUNT
			CALL KOJO_GREETING, "他の人がＨ開始", ACTOR
			SIF TFLAG:退出する
				CFLAG:ACTOR:不在 = 1

			SELECTCASE LINECOUNT
			CASE IS > MEMO_LINECOUNT2
				BREAK
			CASEELSE
				SIF COND("颜绘表示中", ACTOR) == 0
					CLEARLINE 1
			ENDSELECT
		NEXT
		SIF LINECOUNT == MEMO_LINECOUNT
			CLEARLINE 1
	ENDIF

	;時間帯を進める場合には交流している部員を選出する
	CALL SETFLAG, "部員同士の交流：角色選出"

	REDRAW 1
	CALL SETFLAG, "PALAMの記録"

	;BEGIN前に顔グラ変化を一旦リセット
	CVARSET CSTR, GETNUM(CSTR, "颜绘変化")

	BEGIN TRAIN
ELSEIF GO_BEGIN_TURNEND
	;休憩フラグを立てる
	FLAG:休憩 = 1
	;時間帯を進める場合には交流している部員を選出する
	CALL SETFLAG, "部員同士の交流：角色選出"

	;TRYCALL EVENT_REST_PRE

	;BEGIN前に顔グラ変化を一旦リセット
	CVARSET CSTR, GETNUM(CSTR, "颜绘変化")

	BEGIN TURNEND
ENDIF

RETURN 1


@USERSHOP_PRE, ARG
#DIM LCOUNT
#DIM CHOICE
#DIMS CHOICES
#DIM MEMO_TARGET
#DIM MEMO_ASSI
#DIM MEMO_ITEM
#DIMS MEMO_DOPING
#DIM MEMO_LINECOUNT
;起きうるイベントの判定に使う
#DIMS EVENT_TABLE

FLAG:SELECT_USERSHOP = 0
CHOICE = ARG

SELECTCASE CHOICE
;選択されたキャラをTARGETに
CASE 1002 TO 999+CHARANUM
	CHOICE = CHOICE - 1000
	SIF CFLAG:CHOICE:不在
		RETURN 0
	SELECTCASE CHOICE
	CASE TARGET
	CASEELSE
		IF COND("現在不在", CHOICE) == 0
			CALL SETFLAG, "対象指定", CHOICE
		;今居ないキャラをクリックした場合は、可能なら呼び寄せる
		ELSEIF CFLAG:睡眠 == 0
			IF PLACE("自宅")
				SIF PLACE("居間") == 0
					RETURN 0
				PRINTFORMW %CALLNAME:MASTER%在房间里喊了%CALLNAME:CHOICE%一声。
				IF CFLAG:CHOICE:睡眠
					PRINTFORMW ……但是没有反応。是睡着了吗？
					RETURN 0
				ENDIF
			ELSEIF PLACE("部室")
				PRINTFORMW %CALLNAME:MASTER%在部室里喊了%CALLNAME:CHOICE%一声。
			ELSE
				RETURN 0
			ENDIF
			CFLAG:CHOICE:呼び出された = 1
			;入室イベント
			CALL EVENT_ENTER_ROOM
			CFLAG:CHOICE:呼び出された = 0
			CALL SETFLAG, "対象指定", CHOICE
		ENDIF
	ENDSELECT
;選択されたキャラをアドバイザーに
CASE 2002 TO 1999+CHARANUM
	SELECTCASE CHOICE - 2000
	;アドバイザーをクリックしたらアドバイザーを外す
	CASE ASSI, ASSI:1
		CALL SETFLAG, "助手指定", 0
	;アドバイザーでない人をクリックしたら、可能ならその人をアドバイザーにする
	CASEELSE
		SIF COND("助手可能", CHOICE - 2000)
			CALL SETFLAG, "助手指定", CHOICE - 2000
	ENDSELECT
;選択されたキャラに衣装チェンジしてもらう
;パンツ取引
CASE 3001 TO 3005
	CHOICE = FINDCHARA(CFLAG:優先順位, CHOICE - 3000, 2)
	SIF CHOICE <= 0 || CFLAG:CHOICE:睡眠 || COND("現在不在", CHOICE) || COND("胖次没収実行可能", CHOICE) == 0 || COND("合意：胖次交易", CHOICE) == 0
		RETURN 0

	MEMO_TARGET = TARGET
	TARGET = CHOICE

	CALL EVENT_WISH_PANTU
	;直前行動として記録
	SIF STR:日常イベント名 != ""
		CALL SET_PREV_DAILYCOM

	TARGET = MEMO_TARGET
;全裸化
CASE 3101 TO 3105
	CHOICE = FINDCHARA(CFLAG:優先順位, CHOICE - 3100, 2)
	SIF CHOICE <= 0 || CFLAG:CHOICE:睡眠 || COND("現在不在", CHOICE) || CHECK_CLO("裸体", CHOICE) || COND("合意：全裸化", CHOICE) == 0 || CHOSE_DEVENT("变成裸体", CHOICE)
		RETURN 0

	MEMO_TARGET = TARGET
	TARGET = CHOICE

	CALL EVENT_WISH_HADAKA
	;直前行動として記録
	SIF STR:日常イベント名 != ""
		CALL SET_PREV_DAILYCOM

	TARGET = MEMO_TARGET

;没収済パンツを楽しむ
CASE 4002 TO 3999+CHARANUM
	CHOICE = CHOICE - 4000
	SIF CSTR:CHOICE:没収済胖次 == "" || COND("胖次ドーピング", CHOICE)
		RETURN 0

	SWAP CSTR:CHOICE:胖次, CSTR:CHOICE:没収済胖次
	EQUIP:CHOICE:胖次 = STRLENS(CSTR:CHOICE:胖次)

	;CHICEのパンツで楽しんでいるあなたに対するTARGETの対応を見るための処理
	MEMO_DOPING = %SAVESTR:胖次ドーピング%
	SAVESTR:胖次ドーピング = %CALLNAME:CHOICE%\/

	SIF CFLAG:睡眠 == 0
		CALL KOJO_DAILYCOM, "胖次ドーピング：地の文前"
	IF TFLAG:地の文スキップ == 0
		IF CFLAG:CHOICE:睡眠 && COND("現在存在", CHOICE)
			IF INSERT("挿入")
				PRINTFORM %CALLNAME:MASTER%緩緩地動着腰、刚才脱下来的%CALLNAME:CHOICE%的
			ELSE
				PRINTFORM %CALLNAME:MASTER%看着%CALLNAME:CHOICE%的寝顔、没収了
			ENDIF
			;SIF CHECK_PANTIES("クロッチ染み", CHOICE)
			;	PRINTFORM 染み付き
			PRINTFORMW 内衣%TEXTR("放在鼻子前做了一次深呼吸/用手摊開来仔细观察/张開后用舌头舔了舔污渍较深的地方")%。
		ELSE
			PRINTFORM %CALLNAME:MASTER%从口袋里掏出%CALLNAME:CHOICE%的%PANTIES(CHOICE, "略")%、
			PRINTFORMW %TEXTR("放在鼻子前做了一次深呼吸/用手摊開来仔细观察/用舌头舔了舔污渍较深的地方")%。
		ENDIF
		IF CFLAG:睡眠 == 0
			PRINTL 
			CALL KOJO_DAILYCOM, "胖次ドーピング", "改行没有"
		ENDIF
	ENDIF

	IF BASE:MASTER:精力 < MAXBASE:MASTER:精力
		PRINTFORMW 精力+{MAX(MAXBASE:MASTER:精力/4, 5)}（%CALLNAME:MASTER%）
		BASE:MASTER:精力 = MIN(BASE:MASTER:精力 + MAX(MAXBASE:MASTER:精力/4, 5), MAXBASE:MASTER:精力)
	ENDIF

	SWAP CSTR:CHOICE:胖次, CSTR:CHOICE:没収済胖次
	EQUIP:CHOICE:胖次 = STRLENS(CSTR:CHOICE:胖次)

	SAVESTR:胖次ドーピング += @"%MEMO_DOPING%"
;選択したアイテムを購入
CASE 9050 TO 9059
	IF ITEMNAME:(CHOICE - 9000) != "" && ITEM:(CHOICE - 9000) < 99 && MONEY >= ITEMPRICE:(CHOICE - 9000)
		PRINTFORMW %ITEMNAME:(CHOICE - 9000)%买下了。所持金-%MONEY_C(ITEMPRICE:(CHOICE - 9000) )%
		ITEM:(CHOICE - 9000) += 1
		MONEY -= ITEMPRICE:(CHOICE - 9000)
		CLEARLINE LINECOUNT - TFLAG:LINECOUNT_SHOP
		PRINTFORML %ITEMNAME:(CHOICE - 9000)%买下了。所持金-%MONEY_C(ITEMPRICE:(CHOICE - 9000) )%
	ENDIF
CASE 0
	CALL NEW_PRINT_STATE, MASTER, "通常"
CASE 1 TO 5
	CHOICE = FINDCHARA(CFLAG:優先順位, CHOICE, 2)
	;この時間帯には会えないキャラの場合には何もできない
	SIF CHOICE <= 0 || CFLAG:CHOICE:不在
		RETURN 0
	;能力表示をする
	CALL NEW_PRINT_STATE, CHOICE, "通常"
CASE 12 TO 15
	CHOICE = FINDCHARA(CFLAG:優先順位, CHOICE - 10, 2)
	;この時間帯には会えないキャラの場合には何もできない
	SIF CHOICE <= 0 || CFLAG:CHOICE:不在
		RETURN 0
	IF CHOICE != TARGET && COND("現在不在", CHOICE) == 0
		CALL SETFLAG, "対象指定", CHOICE
	;自宅＆現在位置にそのキャラが居ない、ならばその人の部屋に行く
	ELSEIF PLACE("自宅")
		SIF COND("現在不在", CHOICE)
			CALL MOVE_ROOM, CSTR:CHOICE:現在位置
	;部室＆現在位置にそのキャラが居ない、ならばその人を呼び出す
	ELSEIF PLACE("部室")
		IF CFLAG:睡眠 == 0
			PRINTFORMW %CALLNAME:MASTER%在部室里喊了%CALLNAME:CHOICE%一声。
			CFLAG:CHOICE:呼び出された = 1
			;入室イベント
			CALL EVENT_ENTER_ROOM
			CFLAG:CHOICE:呼び出された = 0
			CALL SETFLAG, "対象指定", CHOICE
		ENDIF
	ENDIF
;[999]休憩
CASE 999
	IF TARGET
		IF COND("约会の予定あり")
			PRINTFORMW 今日有和%NAMES("约会相手")%约会的约定。
		;この分岐はUSERSHOPに続く
		ELSE
			PRINTFORML 時間会向前推進%BL(CONFIG("９９９インデント補正") )%、没関系吗？
			PRINTFORML %BL(CONFIG("９９９インデント補正") )%[0]ＯＫ！
			PRINTFORML %BL(CONFIG("９９９インデント補正") )%[1]果然还是算了
			FLAG:SELECT_USERSHOP = CHOICE
			RETURN 1
		ENDIF
	ELSEIF TARGET == 0
		PRINTW 无法选择该对象
	ENDIF
;[100]ベッドに誘う
CASE 100
	IF TARGET
		IF COND("约会の予定あり")
			PRINTFORMW 今日有和%NAMES("约会相手")%约会的约定。
		ELSEIF CFLAG:睡眠
			PRINTW 对方正在睡觉
		;この分岐はUSERSHOPに続く
		ELSE
			IF CFLAG:ご褒美権
				PRINTFORML %CALLNAME:TARGET%からＨ的奖励を貰いますか？
				CALL PRINT_STRL, "黄色_※実行値に+15＆初期气氛+_HH_HH_HH_黄色_＆危険日以外の中出し＆写真撮影許可、之类的奖励哦"
			ELSEIF FLAG:叫出来Ｈ中
				IF CFLAG:调戏数
					PRINTFORML 差不多要和%CALLNAME:TARGET%Ｈ了吗？
				ELSE
					PRINTFORML 那么干脆利落地要和%CALLNAME:TARGET%Ｈ吗？
				ENDIF
			ELSEIF CFLAG:Cosplay中
				PRINTFORML 邀请%CLO("現在的格好＆の")%%CALLNAME:TARGET%来H。
			ELSE
				PRINTFORML 邀请%TEXTS("間柄の")%%CALLNAME:TARGET%来H。
			ENDIF
			IF CFLAG:ご褒美権
				PRINTFORML [0]要\@ DAYEV("文化祭") ? (如果选择的话文化祭会在Ｈ期间内迎来閉幕式) # \@
				PRINTFORML [1]まだいいや
			ELSEIF FLAG:叫出来Ｈ中
				PRINTFORML [0]要做\@ DAYEV("文化祭") ? (如果选择的话文化祭会在Ｈ期间内迎来閉幕式) # \@
				PRINTFORML [1]再亲热一会
			ELSE
				PRINTFORML [0]ＯＫ！\@ DAYEV("文化祭") ? (如果选择的话文化祭会在Ｈ期间内迎来閉幕式) # \@
				PRINTFORML [1]果然还是算了
			ENDIF
			FLAG:SELECT_USERSHOP = CHOICE
			RETURN 1
		ENDIF
	ELSE
		PRINTW 对方不在
	ENDIF
;[101]デートに誘う
CASE 101
	;只今デートのお時間
	IF DAYEV("约会領帯ム")
		SIF PLACE("自宅") == 0
			RETURN 0
		CALL EVENT_DATE_MAIN
		;この分岐はUSERSHOPに続く
		IF COND("约会中")
			IF STR:Ｈする場所 != ""
				PRINTFORML  [ 0] 一起去%STR:Ｈする場所%吧
			ELSEIF TFLAG:文化祭约会
				PRINTFORML  [ 0] 邀请去没有人的教室
			ELSE
				PRINTFORML  [ 0] 邀请去情人酒店
			ENDIF
			IF COND("同居中")
				PRINTFORML  [ 1] 一起回家
			ELSE
				PRINTFORML  [ 1] 虽然有些不舍、还是结束了约会
			ENDIF
			FLAG:SELECT_USERSHOP = CHOICE
			RETURN 1
		ENDIF
	;休日のデートに誰かを誘う？
	ELSE
		IF TARGET == 0
			PRINTW 对方不在。
		ELSEIF CFLAG:睡眠
			PRINTW 对方正在睡觉。
		ELSE
			IF CHARA_NUM("约会的约定")
				PRINTFORMW 已经邀请%NAMES("约会相手")%约会了。
			ELSEIF COND("约会可能")
				CALL KOJO_DATE, "约会に誘われた"
				IF COND("约会お誘い済み")
					PRINTFORMW 接受了%CALLNAME:TARGET%提出的约会的邀请。
				ELSE
					PRINTFORMW %CALLNAME:TARGET%接受了约会的邀请。
				ENDIF
				;お弁当頼む？
				CALL SETFLAG, "约会の便当"
				PRINTFORMW 那么就期待着下一个星期天的昼时吧。
				CALL SETFLAG, "约会的约定", TARGET
			ELSE
				PRINTFORMW ……虽然很遗憾、但是%CALLNAME:TARGET%没有接受约会的邀请。
				CALL PRINT_STRW, "(※想要对方接受邀请的话需要_黄色_[信頼LV3]or[恋慕]or[屈服刻印LV1]_)"
			ENDIF
		ENDIF
	ENDIF

;[102]ラブホに呼び出す
CASE 102
	;この場にいない人をＨに誘う(居間or部室のみ選択可能)
	SIF PLACE("居間") + PLACE("部室") == 0 || FIND_COND("叫出来Ｈ可能") == 0 || COND("约会の予定あり")
		RETURN 0
	CALL SELECT_CALLGIRL

;能力の表示
CASE 103
	CALL SHOW_CHARADATA

;パートナーの変更
CASE 104
	MEMO_TARGET = TARGET
	IF CHARA_NUM("控え角色") || TARGET <= 0
		CALL CHANGE_TARGET
	ENDIF
	;パートナーを変えた＆一度は相手をしたことがある
	SIF TARGET && TARGET != MEMO_TARGET && EXP:Ｈ回数
		CALL KOJO_GUEST, "調教相手に選ばれた", TARGET
;アドバイザー変更
CASE 105
	MEMO_ASSI = ASSI
	IF CHARA_NUM("助手可能")
		CALL SELECT_ASSI
	ELSEIF CHARA_NUM("助手可能") == 0
		PRINTFORMW %TEXTS("助手の名称")%不在
	ENDIF
	IF ASSI && ASSI != MEMO_ASSI
		CALL KOJO_GUEST, "助手に抜擢", ASSI
		SIF EXP:ASSI:助手経験 == 0
			EXP:ASSI:助手経験 += 1
	ENDIF

;[110]身嗜み変更
CASE 110
	CALL SET_MIDASHINAMI, MASTER
CASE 111
	INVERTBIT FLAG:自動進行停止, 0
	;アニメーション用変数のリセット
	VARSET ANIME_CHARA
	VARSET ANIME_FCOUNT
	CLEARLINE LINECOUNT - TFLAG:LINECOUNT_SHOP
CASE 112

;トーク等のデイリーイベント
CASE 200 TO 207
	SELECTCASE DAILYCOMNAME(CHOICE)
	CASE ""
	;性処理は合意が無いとダメ
	CASE "拜托処理性欲"
		CALL EVENT_DAILY, DAILYNUM(DAILYCOMNAME(CHOICE) ), DAILYCOMNAME(CHOICE)
	CASEELSE
		CALL EVENT_DAILY, DAILYNUM(DAILYCOMNAME(CHOICE) ), DAILYCOMNAME(CHOICE)
	ENDSELECT
;特殊なイベント用
CASE 250
	RETURN 0
;取引する
CASE 300
	IF TARGET && CFLAG:睡眠 == 0
		;現実時間フラグ取得
		CALL GET_REALTIMEFORFLAG
		LOCAL = FLAG:現実時間

		CALL EVENT_WISH
		;取引したら時間経過
		IF FLAG:現実時間 != LOCAL
			;入室イベント
			CALL EVENT_ENTER_ROOM
		ENDIF
	ENDIF
;キャラを増やす
CASE 301
	IF FLAG:勧誘可能 && CHARA_NUM("勧誘可能") && CFLAG:睡眠 == 0 && PLACE("出先") == 0
		;現実時間フラグ取得
		CALL GET_REALTIMEFORFLAG
		TFLAG:过滤器 = 0
		CALL SHOP_CHARA
	ENDIF
;買い物する
CASE 302
	IF CFLAG:睡眠 == 0
		MEMO_ITEM = NUM("所持道具")

		;現実時間フラグ取得
		CALL GET_REALTIMEFORFLAG
		CALL EVENT_SHOP

		;お買い物したら時間経過
		IF NUM("所持道具") != MEMO_ITEM
			CALL SETFLAG_DAILY, "拭汗", MASTER
			CALL SETFLAG_DAILY, "拭汗"
			;入室イベント
			CALL EVENT_ENTER_ROOM
		ENDIF
	ENDIF
;密会する
CASE 303
	IF COND("约会の予定あり")
		PRINTFORMW 如果要这样做的话、和%NAMES("约会相手")%的约会就会迟到了。
	ELSEIF COND("場所移動可能")
		IF PLACE("野外")
			CALL MOVE_ROOM, "居間"
		ELSE
			TFLAG:指导者同行 = 0
			;パートナーorアドバイザーが反応
			IF ASSI && COND("現在存在", ASSI) && RAND:2
				CALL KOJO_GREETING, "場所を変える", ASSI
			ELSE
				CALL KOJO_GREETING, "場所を変える", TARGET
			ENDIF
			CALL MOVE_ROOM
		ENDIF
	ENDIF
;[333]セーブ
CASE 333
	LOADGLOBAL
	SELECTCASE GLOBAL:最新ＳＬ番号
	CASE 0 TO 98
		CALL SAVEGAME_EX, GLOBAL:最新ＳＬ番号/20
	CASEELSE
		CALL SAVEGAME_EX
	ENDSELECT
;[444]ロード
CASE 444
	LOADGLOBAL
	SELECTCASE GLOBAL:最新ＳＬ番号
	CASE 0 TO 98
		CALL LOADGAME_EX, GLOBAL:最新ＳＬ番号/20
	CASEELSE
		CALL LOADGAME_EX
	ENDSELECT
;[555]コンフィグ
CASE 555
	CALL SET_CONFIGURE
;[666]写真の閲覧
CASE 666
	SIF NUM("写真")
		CALL VIEW_PHOTO

;[777]エンディングを迎える
CASE 777
	SIF STR:エンディング == ""
		RETURN 0

	CALL SELECT_ENDING

;[888]マリーちゃんの中の人さんイベントなど
CASE 888
	SELECTCASE TEXTS("季節")
	CASE "夏"
		IF GETBIT(FLAG:季節イベントフラグ, 10) == 0 && COND("昼") && TEXTS("曜日") == "土"
			PRINTFORML 这个是、就算没有勧誘过的角色也会登場然后大家一起玩的事件！
			PRINTFORMW ……真的要看吗？
			CALL PRINT_SELECT, "看/不看"
			IF RESULT == 0
				TRYCALL EVENT_TEST
				SETBIT FLAG:季節イベントフラグ, 10
			ENDIF
		ELSEIF GETBIT(FLAG:季節イベントフラグ, 11) == 0 && COND("昼") && TEXTS("曜日") == "土"
			PRINTFORML 接着上次的、这个也是就算没有勧誘过的角色也会登場然后大家一起玩的事件！
			PRINTFORMW ……真的要看吗？
			CALL PRINT_SELECT, "看/不看"
			IF RESULT == 0
				TRYCALL EVENT_TESTNISE
				SETBIT FLAG:季節イベントフラグ, 11
			ENDIF
		ELSE
			RETURN 0
		ENDIF
	CASE "冬"
		IF GETBIT(FLAG:季節イベントフラグ, 30) == 0 && COND("夜") && TEXTS("日期対応イベント") == "圣诞节"
			PRINTFORML 这个是、就算没有勧誘过的角色也会登場然后大家一起玩的事件！
			PRINTFORMW ……真的要看吗？
			CALL PRINT_SELECT, "看/不看"
			IF RESULT == 0
				TRYCALL EVENT_TESTCHRISTMAS
				SETBIT FLAG:季節イベントフラグ, 30
			ENDIF
		ELSE
			RETURN 0
		ENDIF
	CASEELSE
		RETURN 0
	ENDSELECT

;イベントテスト
CASE 884
	SIF FINDCHARA(NO, 1) <= 0
		RETURN 0
	IF 1 || DAYEV("情人節翌日", "リアル日時") || DAYEV("情人節", "リアル日時")
		CALL EVENT_TESTVALENTINE1
	ELSE
		IF COND("昼") && TEXTS("曜日") != "日"
			CALL PRINT_STR, "黄色_- 弗理希艾尔（制服） -_L_"
		ELSE
			CALL PRINT_STR, "黄色_- 弗理希艾尔（女仆装） -_L_"
		ENDIF
		PRINTFORMW 「请等到情人节、主人様」
	ENDIF
CASE 885
	SIF FINDCHARA(NO, 1) <= 0
		RETURN 0
	IF 1 || DAYEV("情人節翌日", "リアル日時") || DAYEV("情人節", "リアル日時")
		CALL EVENT_TESTVALENTINE2
	ELSE
		IF COND("昼") && TEXTS("曜日") != "日"
			CALL PRINT_STR, "黄色_- 弗理希艾尔（制服） -_L_"
		ELSE
			CALL PRINT_STR, "黄色_- 弗理希艾尔（女仆装） -_L_"
		ENDIF
		PRINTFORMW 「情人节的時候就会给你用的、主人様」
	ENDIF
CASE 886
	SIF FINDCHARA(NO, 5) <= 0
		RETURN 0

	IF TEXTS("日期対応イベント") == "新年" && DEVENT("社修繕完了", FINDCHARA(NO, 5))
		TRYCALL EVENT_TESTOMIKUJI
	ELSEIF COND("昼") && TEXTS("曜日") != "日"
		;20%でフェリシエル
		IF RAND:5 == 0 && (FIND_COND("弗理希艾尔", "現在存在") || NO:TARGET == 1)
			CALL PRINT_MAIDFACE, FINDCHARA(NO, 1)
			CALL PRINT_STR, @"『%TEXTR("那么、你的/听得见吗…听得见吗……/呼呼呼、有破绽、看招")%_"
			PRINTFORMW 这里是弗理希艾尔。你现在就在主人様的旁边对吧。不允许翘课。如果无论如何都要翘课的话请带着我一起」
			IF NO:TARGET == 1
				TRYCALL EVENT_TESTOMIKUJI
			ENDIF
		;80%で六華
		ELSE
			CALL PRINT_MAIDFACE, FINDCHARA(NO, 5)
			IF RAND:3 == 0
				PRINTFORMW 「妾身是六華……用神通力直接传話过来的……如果翘课了的话、主様的妹様会担心的……」
			ELSEIF RAND:2 == 0
				PRINTFORMW 「妾身是六華…现在直接向您的心里传的话……白天不要翘课了乖乖来上学吧……」
			ELSE
				PRINTFORMW 「白天请好好上学……如果不想被我向実母殿告状的话就老老实实去上课吧……」
			ENDIF
		ENDIF
	ELSE
		TRYCALL EVENT_TESTOMIKUJI
	ENDIF

;[88224646]デバッグ
CASE 88224646
	SIF TARGET <= 0
		TARGET = 1
	TRYCALL DEBUG_MODE

	TARGET = FINDCHARA(CFLAG:優先順位, 1, 2)
	;3サイズと身長
	FOR LOCAL, 1, CHARANUM
		CALL SET_3SIZE, LOCAL
	NEXT
ENDSELECT

RETURN 0

;移動場所は今のところヤリ部屋、体育倉庫、更衣室、保健室
;ARGSに文字列が入っている場合は、選択肢無しでそこへの移動となる（主に、今居ない人の部屋への移動）
@MOVE_ROOM, ARGS
#DIM LCOUNT
#DIM MEMO_TARGET
#DIM MEMO_LINECOUNT
;起きうるイベントの判定に使う
#DIMS EVENT_TABLE
#DIMS SELECT_TARGET
#DIMS SELECT_ASSI
#DIMS SELECT_NOBUTTON
#DIMS CHOICES

SELECT_TARGET = 
SELECT_ASSI = 
SELECT_NOBUTTON = 
EVENT_TABLE = 
STR:選択肢 = 

MEMO_LINECOUNT = LINECOUNT

;場所指定なしなら選択肢
IF ARGS == ""
	IF PLACE("自宅")
		;パートナーが寝ている場合
		IF CFLAG:睡眠
			EVENT_TABLE += "居間/"
			SELECT_NOBUTTON = -%CALLNAME:MASTER%一个人移動-_NOBUTTON
		ELSE
			IF TFLAG:指导者同行
				PRINTFORML -%CALLNAME:TARGET%带着%CALLNAME:ASSI%一起移動-
				SELECT_NOBUTTON = -只带着%CALLNAME:ASSI%移動-_NOBUTTON
			ELSE
				PRINTFORML -和%CALLNAME:TARGET%一起移動-
				SELECT_NOBUTTON = -%CALLNAME:MASTER%一个人移動-_NOBUTTON
			ENDIF

			STR:選択肢 = 
			SIF MONEY >= 700
				STR:選択肢 += @"情人旅館（需要%MONEY_C(700)%。有実行値和情绪的加成）\/"

			STR:選択肢 += "公園/"
			SIF PLACE("居間") == 0
				STR:選択肢 += "居間/"
			;MASTER,TARGET,ASSIの３人の部屋を上位に表示
			SIF PLACE(@"%CSVCALLNAME(NO:MASTER)%的房間") == 0
				STR:選択肢 += @"%CSVCALLNAME(NO:MASTER)%的房間/"
			SIF PLACE(@"%CSVCALLNAME(NO:TARGET)%的房間") == 0
				STR:選択肢 += @"%CSVCALLNAME(NO:TARGET)%的房間/"
			IF PLACE(@"%CSVCALLNAME(NO:ASSI)%的房間") == 0
				SIF TFLAG:指导者同行
					STR:選択肢 += @"%CSVCALLNAME(NO:ASSI)%的房間/"
			ENDIF
		ENDIF
		;口上側で選択肢を自由に作ることができる
		TRYCALLFORM MOVE_K{NO:TARGET}_ADDSELECT

		;それ以外の人の部屋を下位に表示
		FOR LCOUNT, 2, CHARANUM
			SIF CFLAG:LCOUNT:不在
				CONTINUE
			SELECTCASE LCOUNT
			CASE MASTER, TARGET
				CONTINUE
			CASEELSE
				SIF PLACE(@"%CSVCALLNAME(NO:LCOUNT)%的房間") == 0 && CSTR:LCOUNT:現在位置 == @"%CSVCALLNAME(NO:LCOUNT)%的房間"
					EVENT_TABLE += @"%CSVCALLNAME(NO:LCOUNT)%的房間/"
			ENDSELECT
		NEXT

		SIF EVENT_TABLE != ""
			STR:選択肢 += @"%SELECT_NOBUTTON%\/%EVENT_TABLE%"
	ELSEIF PLACE("野外")
		IF COND("夜") || TEXTS("曜日") == "日"
			CHOICES = 居間
		ELSE
			CHOICES = 部室
		ENDIF
	ELSE
		IF TFLAG:指导者同行
			PRINTFORML 要把%CALLNAME:TARGET%和%CALLNAME:ASSI%带去哪里？
		ELSE
			PRINTFORML 要把%CALLNAME:TARGET%带去哪里？
		ENDIF
		STR:選択肢 = 做爱房/体育倉庫/更衣室/保健室/
	ENDIF

	IF ASSI && ASSI != TARGET && COND("現在不在", ASSI) == 0 && CHARA_NUM("集合予定") != 2
		IF TFLAG:指导者同行 == 0
			SELECT_ASSI = 带着%CALLNAME:ASSI%一起去
		ELSE
			SELECT_ASSI = 不带%CALLNAME:ASSI%一起去
		ENDIF
		STR:選択肢 = %STR:選択肢%%SELECT_ASSI%\/
	ENDIF

	SIF CEVENT("槞彁：ビザ取得约会のお誘い", MASTER) && !CEVENT("槞彁：ビザ発行不可", MASTER) && TEXTS("曜日") == "日" && NO:TARGET == 15
		STR:選択肢 += "_太字_斜体_黄色_去申請就业签证_色変更解除_斜体解除_太字解除_\/"

	STR:選択肢 += "果然还是算了/"
	VARSET LOCALS
	SPLIT STR:選択肢, "/", LOCALS

	CALL PRINT_SELECT_EX, STR:選択肢

	CHOICES = %EXTSTR(LOCALS:RESULT, "", "（")%
ELSE
	CHOICES = %ARGS%
ENDIF

SELECTCASE CHOICES
CASE "_太字_斜体_黄色_去申請就业签证_色変更解除_斜体解除_太字解除_"
	CALL CALL_LIB("入界管理局イベント：移動中")
	CALL PRINT_SELECT("牵着手/不牵手")
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW 悄悄地握住了槞彁的手。
			PRINTFORMW 手小小的很温暖。
			PRINTFORMW 「诶嘿嘿...」
		CASE 1
			PRINTFORMW %CALLNAME:TARGET%无精打采地往前走。
	ENDSELECT
	CALL CALL_LIB("入界管理局イベント：佐藤田中：選択まで")
	CALL PRINT_SELECT("拜托了/拒绝")
	SELECTCASE RESULT
		CASE 0
			CALL ENDING_K15("HappyEnd")
		CASE 1
			CALL CALL_LIB("入界管理局イベント：佐藤田中：選択後")
	ENDSELECT
	
	CALL SET_CEVENT, "槞彁：ビザ発行不可", MASTER
	;本当ならここに追加する予定じゃなかったんだけどとりあえず先行公開するために入れておくですわ
	CALL SET_CEVENT, "槞彁：Demsolaviya20XX呼び出し可", MASTER
	HTML_PRINT @"<p align='left'><font color = 'Yellow'><b>迷你游戏：Demoslaviya20XX被解放了</b></font></p>"
	WAIT
	HTML_PRINT @"<p align='left'><font color = 'Yellow'>可以请求%CALLNAME:TARGET%从最開始重新開始了。</font></p>"
	RETURN 0
CASE "果然还是算了"
	TFLAG:指导者同行 = 0
	RETURN 0
CASE SELECT_NOBUTTON
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE SELECT_ASSI
	INVERTBIT TFLAG:指导者同行, 0
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASEELSE
	MEMO_TARGET = TARGET

	IF PLACE("野外")
		STR:日常イベント名 = 外から戻った
		SAVESTR:現在位置 = %CHOICES%
	ELSE
		SAVESTR:現在位置 = %CHOICES%

		SELECTCASE SAVESTR:現在位置
		CASE @"%CSVCALLNAME(NO:MASTER)%的房間"
			STR:日常イベント名 = 主人公的房間に連れ込まれた
		CASE "居間"
			IF CFLAG:睡眠
				STR:日常イベント名 = 居間へ
			ELSE
				STR:日常イベント名 = 帰還
			ENDIF
		CASEELSE
			STR:日常イベント名 = 摆脱
		ENDSELECT
	ENDIF

	FOR LCOUNT, 2, CHARANUM
		SELECTCASE CFLAG:LCOUNT:不在
		CASE 2
			SIF STR:日常イベント名 == "外から戻った"
				CFLAG:LCOUNT:不在 = 0
			CONTINUE
		CASE IS >= 1
			CONTINUE
		ENDSELECT

		SELECTCASE LCOUNT
		CASE TARGET
			;移動先が自分の部屋？
			SELECTCASE SAVESTR:現在位置
			CASE @"%CSVCALLNAME(NO:TARGET)%的房間"
				STR:日常イベント名 = 自室に招待
				CALL SET_DEVENT, "自室訪問"
			CASE @"%CSVCALLNAME(NO:TARGET)%的家"
				STR:日常イベント名 = 自宅に招待
				CALL SET_DEVENT, "自宅訪問"
			;移動先が同行するアドバイザーの部屋？
			CASE @"%CSVCALLNAME(NO:ASSI)%的房間"
				IF TFLAG:指导者同行
					STR:日常イベント名 = 指导者部屋訪問
					CALL SET_DEVENT, "自室訪問", ASSI
				ENDIF
			ENDSELECT
		CASE ASSI
			SIF ASSI == TARGET
				CONTINUE
			IF TFLAG:指导者同行 == 0
				IF PLACE("自宅")
					;移動先が自分の部屋ならパートナーになる
					IF SAVESTR:現在位置 == @"%CSVCALLNAME(NO:ASSI)%的房間"
						CALL SETFLAG, "対象指定", ASSI
						IF CSTR:ASSI:現在位置 == @"%CSVCALLNAME(NO:ASSI)%的房間"
							STR:日常イベント名 = 自室に訪問された
						ELSE
							STR:日常イベント名 = 自室に招待
						ENDIF
						CALL SET_DEVENT, "自室訪問", ASSI
					ENDIF
				ELSEIF COND("現在存在", LCOUNT)
					CFLAG:LCOUNT:不在 = 2
				ELSE
					CFLAG:LCOUNT:不在 = 1
				ENDIF
				CONTINUE
			ENDIF
		CASEELSE
			IF PLACE("自宅")
				;移動先が自分の部屋ならパートナーになる
				IF SAVESTR:現在位置 == @"%CSVCALLNAME(NO:LCOUNT)%的房間" && CSTR:LCOUNT:現在位置 == @"%CSVCALLNAME(NO:LCOUNT)%的房間"
					CALL SETFLAG, "対象指定", LCOUNT
					STR:日常イベント名 = 自室に訪問された
					CALL SET_DEVENT, "自室訪問", LCOUNT
				ENDIF
			ELSEIF COND("現在存在", LCOUNT)
				CFLAG:LCOUNT:不在 = 2
			ELSE
				CFLAG:LCOUNT:不在 = 1
			ENDIF
		ENDSELECT
	NEXT

	;場所を変えた人たちのフラグを弄る
	CALL SETFLAG_DAILY, "場所を変える", MASTER
	;パートナーのフラグなど
	IF CFLAG:睡眠
		TARGET = 0
		CALL SETFLAG, "対象指定", FIND_COND("現在存在")
	ELSE
		CALL SETFLAG_DAILY, "場所を変える", TARGET
	ENDIF
	;同行したアドバイザーのフラグなど
	SIF TFLAG:指导者同行
		CALL SETFLAG_DAILY, "場所を変える", ASSI

	SELECTCASE STR:日常イベント名
	CASE "自宅に招待"
		CALL BLANK_DRAWLINE
		IF TFLAG:指导者同行
			PRINTFORMW %CALLNAME:MASTER%和%CALLNAME:ASSI%访问了%CALLNAME:TARGET%的家。
		ELSE
			PRINTFORMW %CALLNAME:MASTER%前往了%SAVESTR:現在位置%。
		ENDIF
		;部屋の描写
		MEMO_LINECOUNT = LINECOUNT
;		IF STR:日常イベント名 == "アドバイザーの自宅に招待
;			SIF DEVENT("自宅訪問", ASSI) == 0
;				CALL EVENT_PROFILE, "自宅描写", ASSI
;		ELSEIF STR:日常イベント名 == "自宅に招待
			SIF DEVENT("自室訪問") == 0
				CALL EVENT_PROFILE, "自宅描写", TARGET
;		ENDIF
		SIF LINECOUNT > MEMO_LINECOUNT
			WAIT
	CASE "自室に招待", "自室に訪問された", "指导者部屋訪問"
		CALL BLANK_DRAWLINE
		IF CFLAG:睡眠
			SIF CHECK_CLO("ＣＩＰ発生")
				CALL PRINT_CUTIN, "外套", TARGET
			PRINTFORMW %CALLNAME:MASTER%一进去%SAVESTR:現在位置%就发现、房间的主人%CALLNAME:TARGET%正
			IF CHECK_CLO("ＣＩＰ発生")
				IF CHECK_CLO("胖次が見える格好")
					IF CHECK_PANTIES("布鲁马")
						PRINTFORM 体操服姿で
					ELSE
						PRINTFORM 只穿着胖次
					ENDIF
				ELSEIF CHECK_CLO("泳装姿")
					PRINTFORM 就那樣穿著泳衣
				ELSE
					PRINTFORM 衣冠不整地
				ENDIF
			ENDIF
			PRINTFORMW 睡眠着。
		ELSE
			PRINTFORMW %CALLNAME:MASTER%前往了%SAVESTR:現在位置%。
		ENDIF

		;部屋の描写
		MEMO_LINECOUNT = LINECOUNT
		IF STR:日常イベント名 == "指导者部屋訪問"
			SIF DEVENT("自室訪問", ASSI) == 0
				CALL EVENT_PROFILE, "自室描写", ASSI
		ELSE
			SIF DEVENT("自室訪問") == 0
				CALL EVENT_PROFILE, "自室描写", TARGET
		ENDIF
		SIF LINECOUNT > MEMO_LINECOUNT
			WAIT

		;服装の解説
		IF COND("居家服に着替えた") && DEVENT("居家服解説") == 0 && STR:日常イベント名 == "自室に訪問された" && CSTR:移動履歴 == @"\/%CSVCALLNAME(NO:TARGET)%的房間\/"
			MEMO_LINECOUNT = LINECOUNT
			IF CHECK_CLO("裸体")
				PRINTFORMW ……一进去第一眼看到的就是%CALLNAME:TARGET%的裸体！
				PRINTFORMW 看起来%CALLNAME:TARGET%在在自己的房间里什么都不穿主義的
			ELSEIF CHECK_CLO("没穿下衣")
				PRINTFORMW ……令人震惊的是%CALLNAME:TARGET%下半身连内裤都没穿
				PRINTFORMW 就算是在自己的房间是不是也有点開放过头了？
			ELSEIF CHECK_CLO("没穿外衣")
				PRINTFORMW ……令人震惊的是%CALLNAME:TARGET%上半身什么都没穿
				PRINTFORMW 就算是在自己的房间是不是也有点開放过头了？
			ELSEIF CHECK_CLO("内衣姿")
				PRINTFORMW ……%CALLNAME:TARGET%在自己的房间里装扮非常随意、除了内衣以外全部脱掉了
			ELSEIF CHECK_CLO("胖次が見える格好")
				PRINTFORMW %CALLNAME:TARGET%在自己的房间里装扮非常随意、裙子和裤子都脱掉了、下半身只穿着一条内裤……
			ELSEIF CHECK_CLO("胸罩が見える格好")
				PRINTFORMW %CALLNAME:TARGET%在自己的房间里装扮非常随意、衣服全都脱掉了、只穿着内衣
			ENDIF
			SIF LINECOUNT > MEMO_LINECOUNT
				CALL SET_DEVENT, "居家服解説", TARGET
		;ELSEIF STR:日常イベント名 == "自室に招待"
		;	CALL EVENT_ACADEMY, "秘密のお願い"
		ENDIF

		CALL TEXT, "出会いざまの格好所感", TARGET
	CASE "外から戻った"
		CALL BLANK_DRAWLINE
		PRINTFORMW %CALLNAME:MASTER%回到了%SAVESTR:現在位置%
	CASEELSE
		;移動前の場所に居た人の中から代表で一人が台詞
		SIF FIND_COND("抜け出された")
			CALL KOJO_GREETING, "抜け出された", FIND_COND("抜け出された")

		CALL BLANK_DRAWLINE
		IF PLACE("自室")
			PRINTFORMW %CALLNAME:MASTER%邀请%CALLNAME:TARGET%\@ TFLAG:指导者同行 ? 達 # \@去自己的房间。
		ELSEIF PLACE("野外")
			CALL SETFLAG, "ＴＰＯによる外見変更"
			CALL SET_COORDINATE, "外出着"
			PRINTFORMW %CALLNAME:MASTER%带着%CALLNAME:TARGET%\@ TFLAG:指导者同行 ? 達 # \@去往了%SAVESTR:現在位置%。
		ELSEIF PLACE("情人旅館")
			CALL SETFLAG, "ＴＰＯによる外見変更"
			CALL SET_COORDINATE, "外出着"
			MONEY -= 700
			ITEM:潤滑液 += 1
			ITEM:避孕套 += 5
			PRINTFORMW %CALLNAME:MASTER%说着要不要去%SAVESTR:現在位置%、对着%CALLNAME:TARGET%\@ TFLAG:指导者同行 ? 達 # \@发出了邀请。
			PRINTFORML ＜%CALLNAME:MASTER%获得了房间里备好的一瓶润滑液、五个避孕套＞
			PRINTFORMW 所持金‐%MONEY_C(700)%
		ELSEIF PLACE("個室")
			PRINTFORMW %CALLNAME:MASTER%说着要不要去%SAVESTR:現在位置%、对着%CALLNAME:TARGET%\@ TFLAG:指导者同行 ? 達 # \@发出了邀请。
		ELSE
			PRINTFORMW %CALLNAME:MASTER%回到了%SAVESTR:現在位置%
		ENDIF

		;邪魔されない場所に連れて来た
		;SIF PLACE("個室")
		;	CALL EVENT_ACADEMY, "秘密のお願い"
	ENDSELECT

	CALL KOJO_DAILYCOM, "場所を変える", "改行没有"

	;前回行動フラグを一回リセット
	CVARSET CSTR, GETNUM(CSTR, "PREV_DAILYCOM")
	CVARSET CFLAG, GETNUM(CFLAG, "PREV_DAILYCATE")

	CALL SET_PREV_DAILYCOM

	SIF PLACE("学園内")
		CALL TALK_OTHER

	RETURN 1
ENDSELECT

@SELECT_CALLGIRL
#DIM LCOUNT
#DIM CHOICE

IF PLACE("居間")
	PRINTFORML 要把誰叫到情人酒店呢？（需要%MONEY_C(700)%。有実行値与气氛的加成奖励）
ELSE
	PRINTFORML 要把誰叫到做爱房来Ｈ呢？
ENDIF
CALL SHOW_CHARA_LIST, "叫出来Ｈ"

$INPUT_LOOP
INPUT

CHOICE = RESULT

SELECTCASE CHOICE
CASE 100
	RETURN 0
CASE IS < MASTER, IS >= CHARANUM
	GOTO INPUT_LOOP
CASEELSE
	SIF COND("叫出来Ｈ可能", CHOICE) == 0
		GOTO INPUT_LOOP
ENDSELECT

;現実時間フラグ取得
CALL GET_REALTIMEFORFLAG

;前回行動フラグを一回リセット
CVARSET CSTR, GETNUM(CSTR, "PREV_DAILYCOM")
CVARSET CFLAG, GETNUM(CFLAG, "PREV_DAILYCATE")

FLAG:叫出来Ｈ中 = 1
CALL SETFLAG, "対象指定", CHOICE
;トイレに行ってから来ます
BASE:尿意 /= 5
;呼び出しＨはパンツを穿き変える
SIF CFLAG:不在
	CALL RAND_PANTU, TARGET
CFLAG:不在 = 0
CFLAG:入室までの所要時間 = 0

IF PLACE("居間")
	SAVESTR:現在位置 = 情人旅館
ELSE
	SAVESTR:現在位置 = 做爱房
ENDIF
CALL MOVE_PLACE, SAVESTR:現在位置, TARGET

;服装はデートや日常の衣装が良いかも？あとＨな下着とかどうしよ
SIF PLACE("情人旅館")
	CALL SET_COORDINATE, "外出着"

;アドバイザーは…いない方が自然か
FOR LCOUNT, 2, CHARANUM
	SIF LCOUNT == TARGET
		CONTINUE
	IF COND("現在存在", LCOUNT)
		CFLAG:LCOUNT:不在 = 2
	ELSE
		CFLAG:LCOUNT:不在 = 1
	ENDIF
NEXT

TFLAG:地の文スキップ = 0
STR:日常イベント名 = Ｈ目的で呼び出された
CALL KOJO_DAILYCOM, STR:日常イベント名, "改行没有"

IF TFLAG:地の文スキップ == 0
	;移動前の場所に居た人の中から代表で一人が台詞
	SIF PLACE("做爱房") && FIND_COND("抜け出された")
		CALL KOJO_GREETING, "抜け出された", FIND_COND("抜け出された")
	CALL BLANK_DRAWLINE
	IF PLACE("情人旅館")
		PRINTFORMW %CALLNAME:MASTER%在%SAVESTR:現在位置%旁边的小巷稍微等了一会、%CLO_ABOUT(TARGET, "姿")%的%CALLNAME:TARGET%出現了。
	;ヤリ部屋
	ELSE
		PRINTFORMW %CALLNAME:MASTER%在%SAVESTR:現在位置%稍微等了一会、%CLO("現在的格好＆の")%的%CALLNAME:TARGET%一边打招呼一边走过来了。
	ENDIF
ENDIF
CFLAG:打招呼した = 1

;二人きりイベント
CALL EVENT_ACADEMY, "二人きり"

IF PLACE("情人旅館")
	MONEY -= 700
	ITEM:潤滑液 += 1
	ITEM:避孕套 += 5
	CALL BLANK_DRAWLINE
	PRINTFORML ＜%CALLNAME:MASTER%获得了房间里备好的一瓶润滑液、五个避孕套＞
	PRINTFORMW 所持金‐%MONEY_C(700)%
ENDIF

CALL SET_PREV_DAILYCOM

RETURN 1
