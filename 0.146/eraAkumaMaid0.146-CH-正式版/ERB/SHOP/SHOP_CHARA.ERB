;-------------------------------------------------
;相手を紹介してもらったりします
;-------------------------------------------------
@SHOP_CHARA
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM MEMO_TARGET
#DIM CHOICE
#DIMS CHOICES
#DIM MEMO_BASE, 100

REDRAW 0
;キャラメイク中フラグON
SETBIT FLAG:状況判定, 13
;あなたの血縁フラグ等を記憶
FOR LCOUNT, 0, 100
	MEMO_BASE:LCOUNT = BASE:MASTER:LCOUNT
NEXT
SIF CONFIG("素質的固定与除外")
	CALL SETFLAG, "素質的固定与除外の初期化"

MEMO_LINECOUNT = LINECOUNT

PRINTL 
CALL SHOP_CHARA_SHOW

$INPUT_LOOP
INPUTS

CHOICES = %RESULTS%
CHOICE = TOINT(CHOICES)

SELECTCASE CHOICE
CASE 999
	RETURN 0
;[200]中等部以下　[201]１年生　[202]２年生　[203]３年生　[204]教師　[205]その他
CASE 200 TO 205
	IF TFLAG:过滤器 == CHOICE
		TFLAG:过滤器 = 0
	ELSE
		TFLAG:过滤器 = CHOICE
	ENDIF
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
CASE IS >= 1
	SIF EXISTCSV_EX(CHOICE) == 0
		GOTO INPUT_LOOP

	;出現条件がある場合は個別に判定
	STR:実行条件 = 
	TRYCALLFORM ADDCOND_K{CHOICE}("勧誘")

	;ダメだった場合
	IF STR:実行条件 != ""
		CALL PRINT_STRW, STR:実行条件
		[IF_DEBUG]
			PRINTL 
			PRINTFORML デバッグモードの特権で条件を無視して勧誘しますか？
			CALL PRINT_SELECT, "是/ズルはしない"
			SIF RESULT == 0
				STR:実行条件 = 
		[ENDIF]
		IF STR:実行条件 != ""
			CLEARLINE LINECOUNT - MEMO_LINECOUNT
			RESTART
		ENDIF
	ENDIF
	;紹介してもらえるか判定。そのキャラが既にいるかで判定する
	IF FINDCHARA(NO, CHOICE) >= 0
		SIF CFLAG:FINDCHARA(NO, CHOICE):初期仲間 == 0
			PRINTW 已经勧誘过了呦
		CLEARLINE LINECOUNT - MEMO_LINECOUNT
		RESTART
	ENDIF
;それ以外はだいたい文字列
CASEELSE
	IF COMMENT_ADVISER(CHOICES) != ""
		CLEARLINE 1
		PRINTL 
		PRINTFORML ☆%CHOICES%☆
		PRINTFORMW %COMMENT_ADVISER(CHOICES)%
	ENDIF

	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDSELECT

MEMO_TARGET = TARGET

;キャラ解説を入れる
CALL CHARA_MANUAL, CHOICE
;いいえを選ぶと再入力
IF RESULT == 0
	;表示されていたキャラを白紙に戻す
	DELCHARA CHARANUM-1
	TARGET = MEMO_TARGET

	;あなたのフラグの変化を元に戻す（現在だと血縁のみ）
	FOR LCOUNT, 0, 100
		BASE:MASTER:LCOUNT = MEMO_BASE:LCOUNT
	NEXT

	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

;もしもアドバイザーが居ない状態ならば、現在のTARGETを（可能なら）アドバイザーにし、選んだキャラをTARGETにする
SIF ASSI == 0 && TARGET && COND("助手可能")
	ASSI = TARGET

TARGET = CHARANUM - 1

;式神などの特例
IF COND("種族：式神")
	PRINTFORMW ★失去了力量的原鬼神or原妖。现在是学園的%CSTR:学年%。其名为[%CSTR:称号%]%NAME:TARGET%。现在进行了仪式★
ELSEIF CSTR:学年 == "不法侵入"
	PRINTFORM ★在学園里发现的不法侵入\@ COND("種族：悪魔") ? 悪魔 # 者 \@、
	IF CSTR:関係 == "借住"
		PRINTFORMW 把[%CSTR:称号%]%NAME:TARGET%带回了家★
	ELSE
		PRINTFORMW 勧誘了[%CSTR:称号%]%NAME:TARGET%★
	ENDIF
ELSE
	PRINTFORMW ★勧誘了学園的%CSTR:学年%、[%CSTR:称号%]%NAME:TARGET%★
ENDIF
CALL SETFLAG, "合意", TARGET
PRINTL 
MEMO_LINECOUNT = LINECOUNT
;加入後台詞（なくても大丈夫）
CALL EVENT_PROFILE, "勧誘直後", TARGET
SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

TFLAG:地の文スキップ = 0
MEMO_LINECOUNT = LINECOUNT
;出現条件の出力。特殊な場合以外は書かなくて良いです
CALL EVENT_PROFILE, "出現条件"
SIF COND("部室出現可能") && TFLAG:地の文スキップ == 0 && LINECOUNT == MEMO_LINECOUNT
	CALL PRINT_STRW, @"%NAME:TARGET%和%CALLNAME:MASTER%的関系为_黄色_%CSTR:関係%_。因此、会在(昼)的部室里露露面。"

;顔グラの設定
IF CFLAG:颜绘 == 0 && CONFIG("颜绘ＯＮ")
	DRAWLINE
	PRINTFORML %NAME:TARGET%还没有設定头像。要設定吗？
	CALL PRINT_SELECT, "設定/不設定"
	IF RESULT == 0
		CALL SIMULATE_MAIDFACE, TARGET
	ELSE
		PRINTFORMW 今後想要設定的时候、请在设置中调整。
	ENDIF
ENDIF

IF PLACE("部室")
	IF COND("部室出現可能")
		DRAWLINE
		CALL SETFLAG, "優先順位", TARGET
		TFLAG:地の文スキップ = 0
		MEMO_LINECOUNT = LINECOUNT
		CALL EVENT_PROFILE, "初訪問"
		IF TFLAG:地の文スキップ == 0 && LINECOUNT == MEMO_LINECOUNT
			PRINTFORMW ……总而言之现在%CALLNAME:TARGET%会到部室里来了。
		ENDIF
	ELSE
		CFLAG:不在 = 1
		CFLAG:優先順位 = CHARANUM - 2
		TARGET = MEMO_TARGET
	ENDIF
ELSEIF PLACE("自宅")
	IF COND("自宅出現可能")
		CALL SETFLAG, "優先順位", TARGET
	ELSE
		CFLAG:不在 = 1
		CFLAG:優先順位 = CHARANUM - 2
		TARGET = MEMO_TARGET
	ENDIF
ENDIF


@SHOP_CHARA_SHOW
DRAWLINE
CALL PRINT_STRL, @"勧誘对%CALLNAME:MASTER%的体質有兴趣的对象作为部員。详细情报请查看各个角色的_緑_□ 特記事項 □_"
CALL PRINT_STRL, "黄色_（魂的授受的话性交是最簡単的方式、因此如果没有特殊注释的话那么所有人都是作为性伴侣的）"
CALL PRINT_STRL, "関于特技的情报可以通过按下名字按钮来进行确认"
DRAWLINE
CALL PRINT_STRL, "爱心粉紅_H_已经写好了个人结局的角色会用特殊颜色标注出来_爱心粉紅_H"

DRAWLINE
CALL SET_SHOP_CHARA_SHOW, "ＥＮＤチェック"
DRAWLINE

SELECTCASE TFLAG:过滤器
CASE 200
	CALL PRINT_STRL, "[999]返回　_黄色_[200]中等部以下_　[201]１年生　[202]２年生　[203]３年生　[204]教師　[205]其他"
CASE 201
	CALL PRINT_STRL, "[999]返回　[200]中等部以下　_黄色_[201]１年生_　[202]２年生　[203]３年生　[204]教師　[205]其他"
CASE 202
	CALL PRINT_STRL, "[999]返回　[200]中等部以下　[201]１年生　_黄色_[202]２年生_　[203]３年生　[204]教師　[205]其他"
CASE 203
	CALL PRINT_STRL, "[999]返回　[200]中等部以下　[201]１年生　[202]２年生　_黄色_[203]３年生_　[204]教師　[205]其他"
CASE 204
	CALL PRINT_STRL, "[999]返回　[200]中等部以下　[201]１年生　[202]２年生　[203]３年生　_黄色_[204]教師_　[205]其他"
CASE 205
	CALL PRINT_STRL, "[999]返回　[200]中等部以下　[201]１年生　[202]２年生　[203]３年生　[204]教師　_黄色_[205]其他"
CASEELSE
	PRINTFORML [999]返回　[200]中等部以下　[201]１年生　[202]２年生　[203]３年生　[204]教師　[205]其他
	PRINTFORML 备注：汉化版从100号開始为天朝自制口上
ENDSELECT



;-------------------------------------------------
;勧誘するキャラの一覧表示
;ARGSに"ＥＮＤチェック"って入れるとエンディングが書かれているキャラの色が変わる
;-------------------------------------------------
@SET_SHOP_CHARA_SHOW, ARGS
#DIM LCOUNT
#DIM LCOUNT2

PRINTFORML 　　　%TEXT_LJ("名前", 16)%　%TEXT_LJ("学年",14)%　%TEXT_LJ("称号",22)%　特技
DRAWLINE
;実際にキャラを表示
FOR LCOUNT, 1, 200
	;存在するキャラなのか判定
	SIF EXISTCSV_EX(LCOUNT) == 0
		CONTINUE

	SELECTCASE CSVCSTR(LCOUNT, GETNUM(CSTR, "学年") )
	CASE "初等部", "中等部"
		SELECTCASE TFLAG:过滤器
		CASE 201 TO 205
			CONTINUE
		ENDSELECT
	CASE "１年生"
		SELECTCASE TFLAG:过滤器
		CASE 200, 202 TO 205
			CONTINUE
		ENDSELECT
	CASE "２年生"
		SELECTCASE TFLAG:过滤器
		CASE 200 TO 201, 203 TO 205
			CONTINUE
		ENDSELECT
	CASE "３年生"
		SELECTCASE TFLAG:过滤器
		CASE 200 TO 202, 204 TO 205
			CONTINUE
		ENDSELECT
	CASEELSE
		;教員
		IF STRCOUNTS(CSVCSTR(LCOUNT, GETNUM(CSTR, "学年") ), "教師", "司書")
			SELECTCASE TFLAG:过滤器
			CASE 200 TO 203, 205
				CONTINUE
			ENDSELECT
		ELSE
			SELECTCASE TFLAG:过滤器
			CASE 200 TO 204
				CONTINUE
			ENDSELECT
		ENDIF
	ENDSELECT

	;そのキャラが既にいるかで判定
	IF FINDCHARA(NO, LCOUNT) >= 0
		SETCOLOR DEF_COLOR("灰色")
		FONTSTYLE 4
	ENDIF

	;エンディングが書かれているかチェック（TRYCALLなのでRESULTに0を代入しておく）
	IF ARGS == "ＥＮＤチェック"
		RESULT = 0
		TRYCALLFORM IS_SINGLE_ENDING_K{LCOUNT}
		SIF RESULT
			SETCOLOR DEF_COLOR("爱心粉紅")
	ENDIF
	;名前、肩書、特技
	IF LCOUNT >= 100
		PRINTFORM [{LCOUNT}] 
	ELSE
		PRINTFORM  [{LCOUNT, 2}] 
	ENDIF
	PRINTFORM %TEXT_LJ(CSVNAME(LCOUNT), 16)%　

	IF CSVTALENT(LCOUNT, GETNUM(TALENT, "処女") )
		PRINTFORM %TEXT_LJ(@"%CSVCSTR(LCOUNT, GETNUM(CSTR, "学年") )%(処女)", 14)%　
	ELSE
		PRINTFORM %TEXT_LJ(CSVCSTR(LCOUNT, GETNUM(CSTR, "学年") ), 14)%　
	ENDIF
	PRINTFORM %TEXT_LJ(CSVCSTR(LCOUNT, GETNUM(CSTR, "称号") ), 22)%　

	VARSET LOCALS
	SPLIT CSVCSTR(LCOUNT, GETNUM(CSTR, "建议") ), "＆", LOCALS
	FOR LCOUNT2, 0, 10
		SELECTCASE LOCALS:LCOUNT2
		CASE ""
			BREAK
		CASEELSE
			PRINTBUTTON LOCALS:LCOUNT2, LOCALS:LCOUNT2
			SIF LOCALS:(LCOUNT2 + 1) != ""
				PRINT ＆
		ENDSELECT
	NEXT
	PRINTL 
	RESETCOLOR
	FONTSTYLE 0
NEXT

