@EVENTFIRST
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;Verを最新にする
FLAG:VER_eraAkumaMaid = GAMEBASE_VERSION
;フラグに使う現実時間を取得
CALL GET_REALTIMEFORFLAG

ASSI = 0
;これは仮の日付
DAY = 1
;顔グラをデフォに
FLAG:颜绘 = 1

DELCHARA 0
;[0]番はドッペルあなた
;この人の変数を対応した変数の一時保存用に使いまくるので重要な人です
ADDCHARA 99

;[1]番はあなた
ADDCHARA 0
MASTER = 1

MONEY = 5000

;グローバル変数の読み込み
LOADGLOBAL

$SELECT_MODE

REDRAW 0
MEMO_LINECOUNT = LINECOUNT

PRINTL 
;モード選択
PRINTL ★★選択模式与設定★★
PRINTL 
PRINTFORML  [ 0] NORMAL
PRINTFORML  [ 1] BILLIONAIRE (所持金100倍。除此之外没有区别) 
PRINTFORML  [10] Config
SIF GLOBAL > 0 && FLAG:VER_Config != GLOBAL
	PRINTFORML  [11] Config設定 加载（記録日:{GLOBAL/10000}年{(GLOBAL%10000)/100}月{GLOBAL%100}日）
PRINTFORML [100] 返回

$INPUT_LOOP
INPUT

IF RESULT == 100
	REDRAW 1
	BEGIN TITLE
ELSEIF RESULT == 11 && GLOBAL > 0
	CALL LOAD_CONFIGURE
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	GOTO SELECT_MODE
ELSEIF RESULT == 10
	CALL SET_CONFIGURE
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	GOTO SELECT_MODE
ELSEIF RESULT == 1
	MONEY *= 100
	PRINTW 以BILLIONAIRE模式開始
ELSEIF RESULT == 0
ELSE
	GOTO INPUT_LOOP
ENDIF

REDRAW 1
FLAG:オープニング = 1
FLAG:項目OFF = 0
;バージョンチェックはしたものと扱う
FLAG:VER_UPチェック = 1

;キャラメイク中フラグON
SETBIT FLAG:状況判定, 13

CALL START_CHARA_SELECT

PLAYER = MASTER
SAVESTR:現在位置 = %CSVCALLNAME(NO:MASTER)%的房間

CALL EVENT_OPENING

ASSI:1 = TARGET
ASSI = TARGET

CALL SETFLAG, "同居：一直", TARGET

DAY = 1
TIME = 2

BEGIN SHOP

@EVENT_OPENING
;オープニングのはじまりはじまり
PRINTL 
DRAWLINE
PRINTFORMW 「…………来。……起来。请起来、主人様」
PRINTFORMW 在床上睡觉的%CALLNAME:MASTER%被摇晃着睁開了眼睛、映入眼中的是正看着自己的女仆
PRINTFORMW 带着头巾、身着整洁的女仆装、还穿着吊带袜……不这个就不太女仆了吧
PRINTFORMW 在眼前站立着的有点焦躁的她……

CALL PRINT_STR, "黄色_※突然的就開始女仆的角色定制了※_W"

CALL CHARA_MANUAL, 1
DRAWLINE
TARGET = 2

PRINTL 
DRAWLINE
PRINTFORMW 「早上好。一直到我叫您起床之前都在熟睡什么的、还真是够散漫呢」
PRINTFORMW 「………………莫非、您忘了吗？」
PRINTL 
SETCOLOR DEF_COLOR("粉紅")
FONTSTYLE 1
PRINTFORMW 「今天可是、%CNAME("二人称")%来袭击还没有醒来的%CNAME("一人称")%的日子呦」
FONTSTYLE 0
RESETCOLOR
PRINTL 
PRINTFORMW 時间稍微的向前回退
DRAWLINE
PRINTFORMW 眼前的这个女仆模様的女性其实是……以人的魂为食粮的悪魔！
PRINTFORMW 一切的起因、差不多要从在長期休暇马上要结束、新学期就要開始的数日前
PRINTFORMW %CALLNAME:TARGET%用甜言蜜语让%CALLNAME:MASTER%签订了契約这件事開始
PRINTFORMW ……一般来说、现在应该变成了『因为没几天可活了所以干脆就对眼前的女性自由地做各种各様的事情好了』之类的情况
PRINTL 
PRINTFORMW 一般来说的话。
PRINTL 
CALL PRINT_STR, @"然而%CALLNAME:MASTER%、拥有着非常稀少的、可以回復魂的损傷的特殊異能_黄色_[復活]_！_W_"
PRINTFORMW 于是注意到这件事的%CALLNAME:TARGET%和%CALLNAME:MASTER%签订了新的契约
PRINTL 
PRINTFORMW 「如果%CNAME("二人称")%愿意的话……要不要和%CNAME("一人称")%换成長期的魂的交易契约？」
PRINTL 
;一番最初の仲間はシステムメッセージ役も担う
CFLAG:初期仲間 = 1
;セフレ契約
TALENT:炮友 = 2
ABL:信頼 += 1
CALL PRINT_STRW, @"于是%CALLNAME:MASTER%就和%CALLNAME:TARGET%签订了_黄色_[炮友契約]"
CALL SETFLAG, "合意", TARGET
PRINTL 
PRINTFORMW 就这様、我们之间的関係以一个新的起点開始了
PRINTL 
PRINTFORMW ……诶？你问什么是魂的交易？
PRINTFORML %CALLNAME:TARGET%曾曰道：
PRINTFORMW 「%CNAME("二人称")%和%CNAME("一人称")%一起变得舒服起来的時候、以及进行交易仪式的時候就可以交易魂了」
PRINTFORMW 大概是这个意思
;PRINTFORMW あとは分かるな！？
DRAWLINE
PRINTFORMW 那么、就请享受和悪魔女仆的快乐故事吧
PRINTFORMW ……要是有余裕的话可以去看看ReadMeの"该同人作独自的Rule等"
DRAWLINE


;-------------------------------------------------
;PCを選択式にしてみる
;-------------------------------------------------
@START_CHARA_SELECT
;周回フラグがある場合
IF FLAG:周回数
	PRINTL 请选择玩家情况
	PRINTL 
	PRINTL [ 0]继承本周目的玩家数据
	PRINTL [ 1]重建新的

	CALL INPUT_SELECT, 2

	SELECTCASE RESULT
	CASE 0
		PRINTFORMW 继承了、%CALLNAME:MASTER%的档案
		RETURN 1
	ENDSELECT
ENDIF

CALL CREATE_MASTER
RETURN 1


@CREATE_MASTER
PRINTL 

;IF FLAG:周回数 == 0
;	;CALL INPUT_SELECT, 2
;	RESULT = 0
;ELSE
	CALL RENEWAL_ABL, MASTER
	CALL PRINT_STR, "请选择性別（♀和Futanari会自動的变成_黄色_[兩面通吃]_）_L"
	PRINTL [ 0] ♂
	PRINTL [ 1] 伪娘(看上去就像女性一様可愛、行为也非常女性化的少年)
	PRINTL [ 2] ♀
	PRINTL [ 3] 扶她
	CALL INPUT_SELECT, 4
;ENDIF

SELECTCASE RESULT
CASE 0
	TALENT:MASTER:高大 = 1
CASE 1
	TALENT:MASTER:伪娘 = 1
CASE 2
	TALENT:MASTER:男性 = 0
	TALENT:MASTER:女性 = 1
	TALENT:MASTER:兩面通吃 = 1
CASE 3
	TALENT:MASTER:男性 = 0
	TALENT:MASTER:扶她 = 1
	TALENT:MASTER:兩面通吃 = 1
ENDSELECT

TALENT:MASTER:外見年齢 = 3
;初期ちんこは並＆仮性にしておく
TALENT:MASTER:阴茎 = 1
TALENT:MASTER:包茎 = 1

;こいつはサービスだぜ
TALENT:MASTER:性豪 = 1
TALENT:MASTER:復活 = 100

;MASTERの感度は全て3LV。尿道感覚以外はね
ABL:MASTER:Ｃ感覚 = 3
ABL:MASTER:Ｖ感覚 = 3
ABL:MASTER:Ａ感覚 = 3
ABL:MASTER:Ｂ感覚 = 3
ABL:MASTER:Ｍ感覚 = 3
ABL:MASTER:技巧 = 2

CALL SET_DETAIL, MASTER

CALL SETCONFIG, "弱点"
CALL SETCONFIG, "性癖"

;調教知識の選択
CALL SETFLAG, "調教知識の選択", MASTER

CALL CREATE_CHARA, MASTER
SIF RESULT == 1
	RESTART

;初期パラをここで記録
CALL SETFLAG, "初期变量記録", MASTER


@SET_DETAIL, ARG
#LOCALSIZE 1000
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

;色々設定
EXP:ARG:Ｖ経験 = MAX(EXP:ARG:Ｖ経験, EXP:ARG:Ｖ性交経験, EXP:ARG:Ｖ拡張経験)
IF EXP:ARG:Ｖ経験
	TALENT:ARG:処女 = 0
ELSEIF TALENT:ARG:男性 == 0
	TALENT:ARG:処女 = 1
ENDIF

EXP:ARG:Ａ経験 = MAX(EXP:ARG:Ａ経験, EXP:ARG:Ａ性交経験, EXP:ARG:Ａ拡張経験)
IF EXP:ARG:Ａ性交経験
	TALENT:ARG:Ａ処女 = 0
ELSE
	TALENT:ARG:Ａ処女 = 1
ENDIF

TALENT:ARG:尿道処女 = 1

IF EXP:ARG:Ｖ挿入経験
	TALENT:ARG:童貞 = 0
ELSEIF TALENT:ARG:男性
	TALENT:ARG:童貞 = 1
ENDIF

SIF CONFIG("陰毛") && COND("陰毛", ARG) == 0
	CALL SETFLAG, "陰毛と腋毛", ARG

CFLAG:ARG:身嗜み = 0

SELECTCASE COND("陰毛", ARG)
CASE 3
	SETBIT CFLAG:ARG:身嗜み, 1
CASE 5
	SETBIT CFLAG:ARG:身嗜み, 2
CASE 7
	SETBIT CFLAG:ARG:身嗜み, 3
ENDSELECT

SELECTCASE COND("腋毛", ARG)
CASE 3
	SETBIT CFLAG:ARG:身嗜み, 6
CASE 7
	SETBIT CFLAG:ARG:身嗜み, 7
ENDSELECT

VARSET LOCAL
PRINTL 
PRINTFORML 请決定素質和経験
SIF COND("ガチ男性", ARG)
	PRINTFORML "身材"是女性视角的、对于男性[高大]应该是最适合的
SIF FLAG:オープニング
	CALL PRINT_STR, "顺便一提_黄色_Ｖ挿入経験_、_黄色_阴茎尺寸_、_黄色_包皮_、_黄色_性技術_的重要度比较高_L_"
IF TALENT:ARG:男性 == 0
	PRINT Ｖ経験　　　　…
	SELECTCASE EXP:ARG:Ｖ経験
	CASE IS >= 50
		CALL PRINT_STR, "粉紅_NOBUTTON_豊富"
	CASE IS >= 20
		CALL PRINT_STR, "爱心粉紅_NOBUTTON_很多"
	CASE IS >= 1
		CALL PRINT_STR, "黄色_NOBUTTON_有些"
	CASEELSE
		PRINTPLAIN 処女
	ENDSELECT
	PRINTL 　　[  0]処女　[  1]有些　[  2]很多　[  3]豊富
	LOCAL:0 = 1
	IF EXP:ARG:Ｖ経験
		PRINT Ｖ性交経験　　…
		SELECTCASE EXP:ARG:Ｖ性交経験
		CASE IS >= 50
			CALL PRINT_STR, "粉紅_NOBUTTON_豊富"
		CASE IS >= 20
			CALL PRINT_STR, "爱心粉紅_NOBUTTON_很多"
		CASE IS >= 1
			CALL PRINT_STR, "黄色_NOBUTTON_有些"
		CASEELSE
			PRINTPLAIN 没有
		ENDSELECT
		PRINTL 　　[  5]没有　[  6]有些　[  7]很多　[  8]豊富
		LOCAL:5 = 1
		PRINT Ｖ拡張経験　　…
		SELECTCASE EXP:ARG:Ｖ拡張経験
		CASE IS >= 20
			CALL PRINT_STR, "粉紅_NOBUTTON_豊富"
		CASE IS >= 5
			CALL PRINT_STR, "爱心粉紅_NOBUTTON_很多"
		CASE IS >= 1
			CALL PRINT_STR, "黄色_NOBUTTON_有些"
		CASEELSE
			PRINTPLAIN 没有
		ENDSELECT
		PRINTL 　　[ 10]没有　[ 11]有些　[ 12]很多　[ 13]豊富
		LOCAL:10 = 1
	ENDIF
ENDIF
PRINT Ａ経験　　　　…
SELECTCASE EXP:ARG:Ａ経験
CASE IS >= 50
	CALL PRINT_STR, "粉紅_NOBUTTON_豊富"
CASE IS >= 20
	CALL PRINT_STR, "爱心粉紅_NOBUTTON_很多"
CASE IS >= 1
	CALL PRINT_STR, "黄色_NOBUTTON_有些"
CASEELSE
	PRINTPLAIN 没有
ENDSELECT

PRINTL 　　[ 15]没有　[ 16]有些　[ 17]很多　[ 18]豊富
LOCAL:15 = 1

PRINT Ａ性交経験　　…
SELECTCASE EXP:ARG:Ａ性交経験
CASE IS >= 50
	CALL PRINT_STR, "粉紅_NOBUTTON_豊富"
CASE IS >= 20
	CALL PRINT_STR, "爱心粉紅_NOBUTTON_很多"
CASE IS >= 1
	CALL PRINT_STR, "黄色_NOBUTTON_有些"
CASEELSE
	PRINTPLAIN 没有
ENDSELECT
PRINTL 　　[ 20]没有　[ 21]有些　[ 22]很多　[ 23]豊富
LOCAL:20 = 1
PRINT Ａ拡張経験　　…
SELECTCASE EXP:ARG:Ａ拡張経験
CASE IS >= 20
	CALL PRINT_STR, "粉紅_NOBUTTON_豊富"
CASE IS >= 5
	CALL PRINT_STR, "爱心粉紅_NOBUTTON_很多"
CASE IS >= 1
	CALL PRINT_STR, "黄色_NOBUTTON_有些"
CASEELSE
	PRINTPLAIN 没有
ENDSELECT
PRINTL 　　[ 25]没有　[ 26]有些　[ 27]很多　[ 28]豊富
LOCAL:25 = 1

IF PENIS(ARG)
	CALL PRINT_STR, "黄色_Ｖ挿入経験_　　…_"
	SELECTCASE EXP:ARG:Ｖ挿入経験
	CASE IS >= 50
		CALL PRINT_STR, "粉紅_NOBUTTON_豊富"
	CASE IS >= 20
		CALL PRINT_STR, "爱心粉紅_NOBUTTON_很多"
	CASE IS >= 1
		CALL PRINT_STR, "黄色_NOBUTTON_有些"
	CASEELSE
		PRINTPLAIN 童貞
	ENDSELECT
	PRINTL 　　[ 30]童貞　[ 31]有些　[ 32]很多　[ 33]豊富
	LOCAL:30 = 1
	PRINT Ａ挿入経験　　…
	SELECTCASE EXP:ARG:Ａ挿入経験
	CASE IS >= 50
		CALL PRINT_STR, "粉紅_NOBUTTON_豊富"
	CASE IS >= 20
		CALL PRINT_STR, "爱心粉紅_NOBUTTON_很多"
	CASE IS >= 1
		CALL PRINT_STR, "黄色_NOBUTTON_有些"
	CASEELSE
		PRINTPLAIN 没有
	ENDSELECT
	PRINTL 　　[ 35]没有　[ 36]有些　[ 37]很多　[ 38]豊富
	LOCAL:35 = 1
ENDIF

PRINT 身材　　　　　…
IF TALENT:ARG:高大
	CALL PRINT_STR, "黄色_NOBUTTON_高大"
ELSEIF TALENT:ARG:娇小
	CALL PRINT_STR, "黄色_NOBUTTON_娇小"
ELSE
	PRINTPLAIN 普通
ENDIF
PRINTFORML 　　[100]娇小　[101]普通　\@ TALENT:ARG:伪娘 == 0 ? [102]高大 # \@
LOCAL:100 = 1
IF TALENT:ARG:男性 == 0
	PRINT 胸部的大小　　…
	IF TALENT:ARG:巨乳 >= 2
		CALL PRINT_STR, "粉紅_NOBUTTON_爆乳"
	ELSEIF TALENT:ARG:巨乳
		CALL PRINT_STR, "爱心粉紅_NOBUTTON_巨乳"
	ELSEIF TALENT:ARG:貧乳
		CALL PRINT_STR, "青_NOBUTTON_貧乳"
	ELSE
		PRINTPLAIN 普通
	ENDIF
	PRINTL 　　[105]貧乳　[106]普通　[107]巨乳　[108]爆乳
	LOCAL:105 = 1
ENDIF
IF PENIS(ARG)
	CALL PRINT_STR, "黄色_阴茎尺寸_　　　…_"
	SELECTCASE TALENT:ARG:阴茎
	CASE IS >= 3
		CALL PRINT_STR, "粉紅_NOBUTTON_馬根"
	CASE 2
		CALL PRINT_STR, "爱心粉紅_NOBUTTON_巨根"
	CASE 1
		PRINTPLAIN 普通
	CASE 0
		CALL PRINT_STR, "青_NOBUTTON_短小"
	ENDSELECT
	PRINTFORML 　　[110]短小　[111]普通　[112]巨根　[113]马根
	LOCAL:110 = 1
	CALL PRINT_STR, "黄色_包皮_　　　　　…_"
	IF COND("真性包茎", ARG)
		CALL PRINT_STR, "青_NOBUTTON_真性"
	ELSEIF COND("假性包茎", ARG)
		PRINTPLAIN 假性
	ELSE
		CALL PRINT_STR, "黄色_NOBUTTON_没有"
	ENDIF
	PRINTFORML 　　[115]真性　[116]假性　[117]没有
	LOCAL:115 = 1
ENDIF
IF CONFIG("陰毛")
	PRINT 陰毛　　　　　…
	SELECTCASE COND("陰毛", ARG)
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_無毛"
	CASE 2, 3
		CALL PRINT_STR, "黄色_NOBUTTON_剃毛"
	CASE 4, 6
		PRINTPLAIN 有些
	CASE 5
		CALL PRINT_STR, "黄色_NOBUTTON_剃整"
	CASE 7
		CALL PRINT_STR, "黄色_NOBUTTON_密林"
	ENDSELECT
	PRINTL 　　[120]無毛　[121]剃毛　[122]有些　[123]剃整　[124]密林
	LOCAL:120 = 1
ENDIF
IF CONFIG("腋毛")
	PRINTFORM %TEXTS("腋")%毛　　　　　…
	SELECTCASE COND("腋毛", ARG)
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_無毛"
	CASE 2
		CALL PRINT_STR, "黄色_NOBUTTON_脱毛"
	CASE 3
		CALL PRINT_STR, "黄色_NOBUTTON_剃毛"
	CASE 4
		PRINTPLAIN 有些
	CASE 7
		CALL PRINT_STR, "黄色_NOBUTTON_密林"
	ENDSELECT
	PRINTL 　　[125]無毛　[126]剃毛　[127]有些　[128]密林
	LOCAL:125 = 1
ENDIF
PRINT 肤色　　　　　…
SELECTCASE TALENT:ARG:肤色
CASE 0
	CALL PRINT_STR, "NOBUTTON_普通"
CASE 1
	CALL PRINT_STR, "黄色_NOBUTTON_褐色"
CASE 2
	CALL PRINT_STR, "黄色_NOBUTTON_色白"
CASE 3
	CALL PRINT_STR, "黄色_NOBUTTON_青肌"
ENDSELECT
;PRINTPLAIN 　　
PRINTL 　　[130]普通　[131]褐色　[132]色白
LOCAL:130 = 1

PRINT 外見年齢　　　…
SELECTCASE AGE(ARG)
CASE 1
	CALL PRINT_STR, "黄色_NOBUTTON_兒童"
CASE 2
	IF TALENT:ARG:男性
		CALL PRINT_STR, "黄色_NOBUTTON_少年"
	ELSE
		CALL PRINT_STR, "黄色_NOBUTTON_ＪＫ"
	ENDIF
CASE 3
	IF TALENT:ARG:男性
		CALL PRINT_STR, "黄色_NOBUTTON_青年"
	ELSE
		CALL PRINT_STR, "黄色_NOBUTTON_ＪＤ"
	ENDIF
CASE 4
	CALL PRINT_STR, "黄色_NOBUTTON_大人"
ENDSELECT
IF TALENT:ARG:伪娘
	PRINTFORML 　　[135]兒童　[136]少年
ELSE
	PRINTFORML 　　[135]兒童　[136]\@ TALENT:ARG:男性 ? 少年 # ＪＫ \@　[137]\@ TALENT:ARG:男性 ? 青年 # ＪＤ \@　[138]大人
ENDIF
LOCAL:135 = 1

IF TALENT:ARG:男性 == 0 && CONFIG("乳頭肥大")
	PRINT 乳頭肥大　　　…
	SELECTCASE COND("乳頭肥大", ARG)
	CASE IS >= 3
		CALL PRINT_STR, "粉紅_NOBUTTON_LV３"
	CASE 2
		CALL PRINT_STR, "爱心粉紅_NOBUTTON_LV２"
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_LV１"
	CASEELSE
		PRINTPLAIN 没有
	ENDSELECT
	PRINTL 　　[140]没有　[141]LV１　[142]LV２　[143]LV３
	LOCAL:140 = 1
ENDIF
IF TALENT:ARG:男性 == 0 && CONFIG("陥没乳頭")
	PRINT 陥没乳頭　　　…
	SELECTCASE COND("陥没乳頭", ARG)
	CASE 2
		CALL PRINT_STR, "爱心粉紅_NOBUTTON_真性"
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_假性"
	CASEELSE
		PRINTPLAIN 没有
	ENDSELECT
	PRINTL 　　[145]没有　[146]假性　[147]真性
	LOCAL:145 = 1
ENDIF
IF TALENT:ARG:男性 == 0 && CONFIG("巨乳輪")
	PRINT 巨乳輪　　　　…
	SELECTCASE COND("巨乳輪", ARG)
	CASE IS >= 3
		CALL PRINT_STR, "粉紅_NOBUTTON_LV３"
	CASE 2
		CALL PRINT_STR, "爱心粉紅_NOBUTTON_LV２"
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_LV１"
	CASEELSE
		PRINTPLAIN 没有
	ENDSELECT
	PRINTL 　　[150]没有　[151]LV１　[152]LV２　[153]LV３
	LOCAL:150 = 1
ENDIF
IF TALENT:ARG:男性 == 0 && CONFIG("陰核肥大")
	PRINT 陰核肥大　　　…
	SELECTCASE COND("陰核肥大", ARG)
	CASE IS >= 3
		CALL PRINT_STR, "粉紅_NOBUTTON_LV３"
	CASE 2
		CALL PRINT_STR, "爱心粉紅_NOBUTTON_LV２"
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_LV１"
	CASEELSE
		PRINTPLAIN 没有
	ENDSELECT
	PRINTL 　　[155]没有　[156]LV１　[157]LV２　[158]LV３
	LOCAL:155 = 1
ENDIF
IF TALENT:ARG:男性 == 0 && CONFIG("陰核包茎")
	PRINT 陰核包茎　　　…
	IF COND("陰核包茎", ARG)
		CALL PRINT_STR, "黄色_NOBUTTON_包茎"
	ELSE
		PRINTPLAIN 没有
	ENDIF
	PRINTL 　　[160]没有　[161]包茎
	LOCAL:160 = 1
ENDIF

PRINT 施虐属性　　　…
SELECTCASE ABL:ARG:施虐属性
CASE 0
	PRINTPLAIN 没有
CASE IS <= 1
	CALL PRINT_STR, "黄色_NOBUTTON_少し"
CASE IS <= 3
	CALL PRINT_STR, "黄色_NOBUTTON_有些"
CASEELSE
	CALL PRINT_STR, "粉紅_NOBUTTON_很强"
ENDSELECT
PRINTL 　　[225]没有　[226]少许　[227]有些　[228]很强
LOCAL:225 = 1

PRINT 受虐属性　　　…
SELECTCASE ABL:ARG:受虐属性
CASE 0
	PRINTPLAIN 没有
CASE IS <= 1
	CALL PRINT_STR, "黄色_NOBUTTON_少し"
CASE IS <= 3
	CALL PRINT_STR, "黄色_NOBUTTON_有些"
CASEELSE
	CALL PRINT_STR, "粉紅_NOBUTTON_很强"
ENDSELECT
PRINTL 　　[230]没有　[231]少许　[232]有些　[233]很强
LOCAL:230 = 1

IF FLAG:周回数
	IF TALENT:ARG:男性
		PRINT 性交相手の好み…
		IF TALENT:ARG:兩面通吃
			CALL PRINT_STR, "爱心粉紅_NOBUTTON_両方"
		ELSEIF TALENT:ARG:討厭女人
			CALL PRINT_STR, "黄色_NOBUTTON_男性"
		ELSE
			PRINTPLAIN 女性
		ENDIF
		PRINTL 　　[235]女性　[236]男性　[237]両方
		LOCAL:235 = 1
	ELSE
		PRINT 性交相手の好み…
		IF TALENT:ARG:兩面通吃
			CALL PRINT_STR, "爱心粉紅_NOBUTTON_両方"
		ELSEIF TALENT:ARG:討厭男人
			CALL PRINT_STR, "黄色_NOBUTTON_女性"
		ELSE
			PRINTPLAIN 男性
		ENDIF
		PRINTL 　　[240]男性　[241]女性　[242]両方
		LOCAL:240 = 1
	ENDIF
ENDIF

CALL PRINT_STR, "黄色_性技術_　　　　…_"
SELECTCASE ABL:ARG:技巧
CASE IS >= 5
	CALL PRINT_STR, "粉紅_NOBUTTON_巧み"
CASE IS >= 3
	CALL PRINT_STR, "爱心粉紅_NOBUTTON_擅長"
CASE IS >= 1
	PRINTPLAIN 有些
CASEELSE
	CALL PRINT_STR, "青_NOBUTTON_没有"
ENDSELECT
;技巧で巧みを選択できるのは２週目以降
PRINTFORML 　　[245]没有　[246]有些　[247]擅長　\@ FLAG:周回数 ? [248]巧み # \@
LOCAL:245 = 1

PRINTFORML [900] 展開与角色定制有関的Config
PRINTFORML [999] 決定

$INPUT_LOOP
INPUT

CHOICE = RESULT

SELECTCASE CHOICE
CASE 900, 999
CASEELSE
	IF LOCAL:(CHOICE/5*5) == 0
		PRINTL 無効値
		GOTO INPUT_LOOP
	ENDIF
ENDSELECT

SELECTCASE CHOICE
CASE 999
	REDRAW 1
	RETURN 1
CASE 900
	CALL SET_CONFIGURE, "角色定制関連"
CASE 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
CASE 1
	EXP:ARG:Ｖ経験 = 1 + RAND:20
	EXP:ARG:Ｖ性交経験 = MIN(EXP:ARG:Ｖ性交経験, EXP:ARG:Ｖ経験)
	EXP:ARG:Ｖ拡張経験 = MIN(EXP:ARG:Ｖ拡張経験, EXP:ARG:Ｖ経験)
CASE 2
	EXP:ARG:Ｖ経験 = 20 + RAND:30
	EXP:ARG:Ｖ性交経験 = MIN(EXP:ARG:Ｖ性交経験, EXP:ARG:Ｖ経験)
	EXP:ARG:Ｖ拡張経験 = MIN(EXP:ARG:Ｖ拡張経験, EXP:ARG:Ｖ経験)
CASE 3
	EXP:ARG:Ｖ経験 = 50 + RAND:50
	EXP:ARG:Ｖ性交経験 = MIN(EXP:ARG:Ｖ性交経験, EXP:ARG:Ｖ経験)
	EXP:ARG:Ｖ拡張経験 = MIN(EXP:ARG:Ｖ拡張経験, EXP:ARG:Ｖ経験)
CASE 5
	EXP:ARG:Ｖ性交経験 = 0
CASE 6
	EXP:ARG:Ｖ性交経験 = 1 + RAND:20
CASE 7
	EXP:ARG:Ｖ性交経験 = 20 + RAND:30
CASE 8
	EXP:ARG:Ｖ性交経験 = 50 + RAND:50
CASE 10
	EXP:ARG:Ｖ拡張経験 = 0
CASE 11
	EXP:ARG:Ｖ拡張経験 = 1 + RAND:5
CASE 12
	EXP:ARG:Ｖ拡張経験 = 5 + RAND:15
CASE 13
	EXP:ARG:Ｖ拡張経験 = 20 + RAND:20

CASE 15
	EXP:ARG:Ａ経験 = 0
	EXP:ARG:Ａ性交経験 = 0
	EXP:ARG:Ａ拡張経験 = 0
CASE 16
	EXP:ARG:Ａ経験 = 1 + RAND:20
	EXP:ARG:Ａ性交経験 = MIN(EXP:ARG:Ａ性交経験, EXP:ARG:Ａ経験)
	EXP:ARG:Ａ拡張経験 = MIN(EXP:ARG:Ａ拡張経験, EXP:ARG:Ａ経験)
CASE 17
	EXP:ARG:Ａ経験 = 20 + RAND:30
	EXP:ARG:Ａ性交経験 = MIN(EXP:ARG:Ａ性交経験, EXP:ARG:Ａ経験)
	EXP:ARG:Ａ拡張経験 = MIN(EXP:ARG:Ａ拡張経験, EXP:ARG:Ａ経験)
CASE 18
	EXP:ARG:Ａ経験 = 50 + RAND:50
	EXP:ARG:Ａ性交経験 = MIN(EXP:ARG:Ａ性交経験, EXP:ARG:Ａ経験)
	EXP:ARG:Ａ拡張経験 = MIN(EXP:ARG:Ａ拡張経験, EXP:ARG:Ａ経験)
CASE 20
	EXP:ARG:Ａ性交経験 = 0
CASE 21
	EXP:ARG:Ａ性交経験 = 1 + RAND:20
CASE 22
	EXP:ARG:Ａ性交経験 = 20 + RAND:30
CASE 23
	EXP:ARG:Ａ性交経験 = 50 + RAND:50
CASE 25
	EXP:ARG:Ａ拡張経験 = 0
CASE 26
	EXP:ARG:Ａ拡張経験 = 1 + RAND:5
CASE 27
	EXP:ARG:Ａ拡張経験 = 5 + RAND:15
CASE 28
	EXP:ARG:Ａ拡張経験 = 20 + RAND:20
CASE 30
	EXP:ARG:Ｖ挿入経験 = 0
CASE 31
	EXP:ARG:Ｖ挿入経験 = 1 + RAND:20
CASE 32
	EXP:ARG:Ｖ挿入経験 = 20 + RAND:30
CASE 33
	EXP:ARG:Ｖ挿入経験 = 50 + RAND:50
CASE 35
	EXP:ARG:Ａ挿入経験 = 0
CASE 36
	EXP:ARG:Ａ挿入経験 = 1 + RAND:20
CASE 37
	EXP:ARG:Ａ挿入経験 = 20 + RAND:30
CASE 38
	EXP:ARG:Ａ挿入経験 = 50 + RAND:50

CASE 100
	TALENT:ARG:娇小 = 1
	TALENT:ARG:高大 = 0
CASE 101
	TALENT:ARG:娇小 = 0
	TALENT:ARG:高大 = 0
CASE 102
	IF TALENT:ARG:伪娘 == 0
		TALENT:ARG:娇小 = 0
		TALENT:ARG:高大 = 1
	ENDIF
CASE 105
	TALENT:ARG:貧乳 = 1
	TALENT:ARG:巨乳 = 0
CASE 106
	TALENT:ARG:貧乳 = 0
	TALENT:ARG:巨乳 = 0
CASE 107
	TALENT:ARG:貧乳 = 0
	TALENT:ARG:巨乳 = 1
CASE 108
	TALENT:ARG:貧乳 = 0
	TALENT:ARG:巨乳 = 2
CASE 110
	TALENT:ARG:阴茎 = 0
CASE 111
	TALENT:ARG:阴茎 = 1
CASE 112
	TALENT:ARG:阴茎 = 2
CASE 113
	TALENT:ARG:阴茎 = 3
CASE 115
	TALENT:ARG:包茎 = 2
CASE 116
	TALENT:ARG:包茎 = 1
CASE 117
	TALENT:ARG:包茎 = 0
CASE 120
	BASE:ARG:陰毛 = 100
CASE 121
	BASE:ARG:陰毛 = 300
CASE 122
	BASE:ARG:陰毛 = 600
CASE 123
	BASE:ARG:陰毛 = 500
CASE 124
	BASE:ARG:陰毛 = 700
CASE 125
	BASE:ARG:腋毛 = 100
CASE 126
	BASE:ARG:腋毛 = 300
CASE 127
	BASE:ARG:腋毛 = 400
CASE 128
	BASE:ARG:腋毛 = 700
CASE 130 TO 132
	TALENT:ARG:肤色 = CHOICE - 130
;男の娘はこっちだけ
CASE 135 TO 136
	TALENT:ARG:外見年齢 = CHOICE - 134
CASE 137 TO 138
	IF TALENT:ARG:伪娘
		GOTO INPUT_LOOP
	ELSE
		TALENT:ARG:外見年齢 = CHOICE - 134
	ENDIF
CASE 140 TO 143
	TALENT:ARG:乳頭肥大 = CHOICE - 140
CASE 145 TO 147
	TALENT:ARG:陥没乳頭 = CHOICE - 145
CASE 150 TO 153
	TALENT:ARG:巨乳輪 = CHOICE - 150

CASE 155 TO 158
	TALENT:ARG:陰核肥大 = CHOICE - 155
CASE 160 TO 161
	TALENT:ARG:陰核包茎 = CHOICE - 160

CASE 225
	ABL:ARG:施虐属性 = 0
CASE 226
	ABL:ARG:施虐属性 = 1
CASE 227
	ABL:ARG:施虐属性 = 3
CASE 228
	ABL:ARG:施虐属性 = 5

CASE 230
	ABL:ARG:受虐属性 = 0
CASE 231
	ABL:ARG:受虐属性 = 1
CASE 232
	ABL:ARG:受虐属性 = 3
CASE 233
	ABL:ARG:受虐属性 = 5

CASE 235
	TALENT:ARG:討厭女人 = 0
	TALENT:ARG:兩面通吃 = 0
CASE 236
	TALENT:ARG:討厭女人 = 1
	TALENT:ARG:兩面通吃 = 0
CASE 237
	TALENT:ARG:討厭女人 = 0
	TALENT:ARG:兩面通吃 = 1

CASE 240
	TALENT:ARG:討厭男人 = 0
	TALENT:ARG:兩面通吃 = 0
CASE 241
	TALENT:ARG:討厭男人 = 1
	TALENT:ARG:兩面通吃 = 0
CASE 242
	TALENT:ARG:討厭男人 = 0
	TALENT:ARG:兩面通吃 = 1

CASE 245
	ABL:ARG:技巧 = 0
CASE 246
	ABL:ARG:技巧 = 1 + RAND:2
CASE 247
	ABL:ARG:技巧 = 3 + RAND:2
CASE 248
	ABL:ARG:技巧 = 5
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@CREATE_CHARA, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT

REDRAW 0

MEMO_LINECOUNT = LINECOUNT
PRINTL 

;MASTERの複製作成
ADDCOPYCHARA ARG

;男にも濡れやすさの変化をつける試み
IF RAND:3 == 0
	IF RAND:2
		TALENT:ARG:容易濕 = 1
		TALENT:ARG:不易濕 = 0
	ELSE
		TALENT:ARG:容易濕 = 0
		TALENT:ARG:不易濕 = 1
	ENDIF
ENDIF
;IF RAND:3 == 0
;	IF RAND:2
;		TALENT:ARG:賢い = 1
;	ELSE
;		TALENT:ARG:愚か = 1
;	ENDIF
;ENDIF
IF RAND:3 == 0 && PENIS(ARG)
	IF RAND:2
		TALENT:ARG:早漏 = 1
		TALENT:ARG:遅漏 = 0
	ELSE
		TALENT:ARG:早漏 = 0
		TALENT:ARG:遅漏 = 1
	ENDIF
ENDIF

FOR LCOUNT, 0, 4
	SELECTCASE RAND:10 + ABL:ARG:技巧
	CASE 0 TO 2
		LOCAL = 0
	CASE 3 TO 5
		LOCAL = 1
	CASE 6 TO 8
		LOCAL = 2
	CASEELSE
		LOCAL = 3
	ENDSELECT

	SELECTCASE LCOUNT
	CASE 0
		TALENT:ARG:天性の舌靈活 = LOCAL
	CASE 1
		TALENT:ARG:天性の指靈活 = LOCAL
	CASE 2
		TALENT:ARG:天性の腰靈活 = LOCAL
	CASE 3
		SIF TALENT:ARG:男性 == 0
			TALENT:ARG:天性の魔乳 = LOCAL
	ENDSELECT
NEXT

FOR LCOUNT, 0, 100
	SIF EXP:ARG:LCOUNT
		EXP:ARG:LCOUNT = MAX(MULTIPLY(EXP:ARG:LCOUNT, 50 + (RAND:6 + RAND:6)*10), 1)
NEXT

IF COND("角色メイク中")
	SIF STR:髪記録 != ""
		CALL SETFLAG, "髪記録からの復元", ARG
	SIF STR:表情記録 != ""
		CALL SETFLAG, "表情記録からの復元", ARG
ENDIF
SIF STR:髪記録 != "" || STR:表情記録 != ""
	CFLAG:ARG:颜绘 = 1

CALL SETFLAG, "識別番号", ARG
CALL RESET_EXP_GAIN, ARG
CALL GET_EXABL, ARG
CALL GET_EXTALENT, ARG
CALL SET_3SIZE, ARG
CALL SETFLAG, "乳頭の詳細設定", ARG
CALL SETFLAG, "陰核の詳細設定", ARG
CALL SETFLAG, "属性", ARG
CALL CHECK_FAV_POSITION, ARG
CALL SET_COUNTER_BASE
CALL VARSET_MAIDFACE, ARG

CALL NEW_PRINT_STATE, ARG, "角色メイク"
PRINTL 
PRINTFORML 这様可以了吗？（[ 3]在变更完後也会随机属性）
IF CONFIG("颜绘ＯＮ")
	PRINTFORML [ 0]是的　[ 1]从头開始重新設定　[ 2]随机属性　[ 3]名字変更　[ 4]Config　[ 5]颜绘を\@ CFLAG:ARG:颜绘 ? 変更 # 設定 \@する
	CALL INPUT_SELECT, 6
ELSE
	PRINTFORML [ 0]是的　[ 1]从头開始重新設定　[ 2]随机属性　[ 3]名字変更　[ 4]Config
	CALL INPUT_SELECT, 5
ENDIF

SELECTCASE RESULT
CASE 0, 1
	REDRAW 1
	DELCHARA CHARANUM-1
	RETURN RESULT
CASE 2
	COPYCHARA CHARANUM-1, ARG
	DELCHARA CHARANUM-1
CASE 3
	COPYCHARA CHARANUM-1, ARG
	DELCHARA CHARANUM-1

	CALL NAME_CONVERT, MASTER
CASE 4
	CALL SET_CONFIGURE, "角色定制関連"
	COPYCHARA CHARANUM-1, ARG
	DELCHARA CHARANUM-1
CASE 5
	CALL SIMULATE_MAIDFACE, ARG
	IF CFLAG:ARG:颜绘
		CALL SETFLAG, "髪の記録", ARG
		CALL SETFLAG, "表情の記録", ARG
	ELSE
		STR:髪記録 = 
		STR:表情記録 = 
	ENDIF
	COPYCHARA CHARANUM-1, ARG
	DELCHARA CHARANUM-1
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@RENEWAL_ABL, ARG
VARSET TALENT:ARG:0, 0
VARSET CFLAG:ARG:0, 0
VARSET ABL:ARG:0, 0
VARSET EXP:ARG:0, 0
VARSET MARK:ARG:0, 0
VARSET JUEL:ARG:0, 0
SIF FLAG:周回数
	VARSET CSTR:ARG:0, ""

ABL:ARG:技巧 = 0
TALENT:ARG:男性 = 1
;妊娠イベントなど、ベースフラグのリセット
VARSET BASE:ARG:0, 0, 20, 60
VARSET TCVAR:ARG:0


@GAME_RENEWAL
#DIM LCOUNT
#DIM MEMO_MASTER
