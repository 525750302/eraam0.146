;-------------------------------------------------
;キャラARGにアクセサリARGSをつける。ARGS == "" なら全部外す
;これだけは / で区切って10個程度まで装備可能
;他のものと異なり各装備に追加の属性などは無く、記号的な意味が非常に強い
;既に着けているアクセサリに区分が被った物がある場合には上書きする
;-------------------------------------------------
@SET_OTHER, ARGS, ARG
#DIM LCOUNT

SIF ARG == 0 && TARGET
	ARG = TARGET

SELECTCASE ARGS
CASE ""
	CSTR:ARG:アクセサリ = 
CASEELSE
	VARSET LOCALS
	SPLIT ARGS, "/", LOCALS

	;すでに装備している物から区分被りを消す
	FOR LCOUNT, 0, 10
		SELECTCASE LOCALS:LCOUNT
		CASE ""
			CONTINUE
		CASEELSE
			CALL RESET_OTHER, LOCALS:LCOUNT, ARG
		ENDSELECT
	NEXT
	CSTR:ARG:アクセサリ += @"%ARGS%\/"
	;念のため "/" の整理
	CALL RESET_OTHER, "", ARG
ENDSELECT

EQUIP:ARG:アクセサリ = STRLENS(CSTR:ARG:アクセサリ)
TFLAG:着替えた = 1
RETURN 1


;-------------------------------------------------
;ARGが身に着けているARGSをはずし、順番を区分依存で揃える
;身に着けていない場合は何も起きない…のでARGSに""を入れる事で不要な "/" の整理も出来る
;-------------------------------------------------
@RESET_OTHER, ARGS, ARG
#DIM LCOUNT
#DIMS ORDER_OTHER, 10

SIF ARG == 0 && TARGET
	ARG = TARGET

VARSET LOCALS
SPLIT CSTR:ARG:アクセサリ, "/", LOCALS
CSTR:ARG:アクセサリ = 

VARSET ORDER_OTHER
FOR LCOUNT, 0, 10

	SELECTCASE LOCALS:LCOUNT
	;ARGSと一致する場合は飛ばす
	CASE "", ARGS
		CONTINUE
	;ARGSが"その他"以外の区分ならばARGSと同じ区分のものを飛ばす
	CASEELSE
		SIF TYPE_OTHER(ARGS) != "" && TYPE_OTHER(LOCALS:LCOUNT) == TYPE_OTHER(ARGS)
			CONTINUE
	ENDSELECT

	SELECTCASE TYPE_OTHER(LOCALS:LCOUNT)
	CASE "首"
		ORDER_OTHER:0 += @"%LOCALS:LCOUNT%\/"
	CASE "眼鏡"
		ORDER_OTHER:1 += @"%LOCALS:LCOUNT%\/"
	CASE "帽子"
		ORDER_OTHER:2 += @"%LOCALS:LCOUNT%\/"
	CASE "つけ耳"
		ORDER_OTHER:3 += @"%LOCALS:LCOUNT%\/"
	CASE "髪飾り"
		ORDER_OTHER:4 += @"%LOCALS:LCOUNT%\/"
	CASE "外套"
		ORDER_OTHER:5 += @"%LOCALS:LCOUNT%\/"
	CASEELSE
		SIF LOCALS:LCOUNT != ""
			ORDER_OTHER:6 += @"%LOCALS:LCOUNT%\/"
	ENDSELECT
NEXT

FOR LCOUNT, 0, 10
	SELECTCASE ORDER_OTHER:LCOUNT
	CASE ""
		CONTINUE
	CASEELSE
		CSTR:ARG:アクセサリ += @"%ORDER_OTHER:LCOUNT%"
	ENDSELECT
NEXT

EQUIP:ARG:アクセサリ = STRLENS(CSTR:ARG:アクセサリ)

;-------------------------------------------------
;CSTR:ARG:アクセサリにARGSと完全一致するものがある場合には外し、そうでないなら装備させる
;-------------------------------------------------
@INVERTSET_OTHER, ARGS, ARG
SIF ARG == 0 && TARGET
	ARG = TARGET
IF STRCOUNT(@"\/%CSTR:ARG:アクセサリ%", @"\/%ARGS%\/")
	CALL RESET_OTHER, ARGS, ARG
ELSE
	CALL SET_OTHER, ARGS, ARG
ENDIF

;-------------------------------------------------
;アクセサリARGSの区分を返す。その他など特定区分に属さないものの場合は空文字が返る
;-------------------------------------------------
@TYPE_OTHER(ARGS)
#FUNCTIONS
#DIMS CHECK_TABLE
#DIM LCOUNT

CHECK_TABLE = 首/眼鏡/帽子/つけ耳/髪飾り/外套/サスペンダー
VARSET LOCALS
SPLIT CHECK_TABLE, "/", LOCALS

FOR LCOUNT, 0, 10
	SIF LOCALS:LCOUNT == ""
		BREAK
	SIF OTHER(ARGS, LOCALS:LCOUNT) != ""
		RETURNF LOCALS:LCOUNT
NEXT
RETURNF ""

;-------------------------------------------------
;アクセサリARGS内の、ARGS:1区分のアクセサリの名前。基本的に一つしか身に着けないものが対象
;ARGS:1区分のものを身に着けているか否かの判定にも使える。
;-------------------------------------------------
@OTHER(ARGS, ARGS:1)
#FUNCTIONS
#DIM LCOUNT

VARSET LOCALS
SPLIT ARGS, "/", LOCALS

FOR LCOUNT, 0, 10
	SELECTCASE ARGS:1
	CASE "首"
		SIF STRCOUNTS(LOCALS:LCOUNT, "首", "ペンダント", "ネックレス", "タイ", "チョーカー", "マフラー", "ストール")
			RETURNF LOCALS:LCOUNT
	CASE "眼鏡"
		SIF STRCOUNTS(LOCALS:LCOUNT, "頭上")
			CONTINUE
		SIF STRCOUNTS(LOCALS:LCOUNT, "眼鏡", "モノクル", "グラス", "目隠し")
			RETURNF LOCALS:LCOUNT
	CASE "帽子"
		SIF STRCOUNTS(LOCALS:LCOUNT, "ハット", "帽", "ヘッドドレス", "ボンネット", "キャップ", "ベール")
			RETURNF LOCALS:LCOUNT
	CASE "つけ耳"
		SIF STRCOUNT(LOCALS:LCOUNT, "耳")
			RETURNF LOCALS:LCOUNT
	CASE "髪飾り"
		SIF STRCOUNTS(LOCALS:LCOUNT, "カチューシャ", "リボン", "サークレット", "ティアラ", "かんざし", "髪")
			RETURNF LOCALS:LCOUNT
	CASE "外套"
		SIF STRCOUNTS(LOCALS:LCOUNT, "外套", "マント", "コート", "ジャケット", "ジャンバー", "パーカー", "どてら", "ケープ", "ジャージ")
			RETURNF LOCALS:LCOUNT
	CASE "サスペンダー"
		SIF STRCOUNTS(LOCALS:LCOUNT, "サスペンダー")
			RETURNF LOCALS:LCOUNT
	CASEELSE
		SIF STRCOUNTS(LOCALS:LCOUNT, ARGS:1)
			RETURNF LOCALS:LCOUNT
	ENDSELECT
NEXT
RETURNF ""

;-------------------------------------------------
;ARG番のアクセサリの名称。着せ替えなどで表を作る際に使う
;-------------------------------------------------
@NAME_OTHER(ARG)
#FUNCTIONS
VARSET LOCALS

SELECTCASE ARG
CASE 0
	LOCALS = ネクタイ
CASE 1
	LOCALS = リボンタイ
CASE 2
	LOCALS = フリルタイ
CASE 3
	LOCALS = 蝶ネクタイ

CASE 5
	LOCALS = マフラー
CASE 6
	LOCALS = ストール

CASE 10
	LOCALS = チョーカー
CASE 11
	LOCALS = 首輪
CASE 12
	LOCALS = イルカのペンダント

CASE 20
	LOCALS = 眼鏡
CASE 21
	LOCALS = ブロー眼鏡
CASE 22
	LOCALS = フルリム眼鏡
CASE 23
	LOCALS = フィンチ眼鏡
CASE 24
	LOCALS = ロイド眼鏡
CASE 25
	LOCALS = モノクル
CASE 26
	LOCALS = サングラス
CASE 27
	LOCALS = 目隠し

CASE 30
	LOCALS = シルクハット
CASE 31
	LOCALS = 魔女の帽子
CASE 32
	LOCALS = ベレー帽
CASE 33
	LOCALS = ヘッドドレス
CASE 34
	LOCALS = メイドキャップ
CASE 35
	LOCALS = ピクチャーハット
CASE 36
	LOCALS = 麦わら帽子
CASE 37
	LOCALS = ニット帽
CASE 38
	LOCALS = ナースキャップ
CASE 39
	LOCALS = ハーフボンネット
CASE 40
	LOCALS = キャスケット帽
CASE 41
	LOCALS = シスターベール

CASE 50
	LOCALS = ネコ耳
CASE 51
	LOCALS = うさ耳
CASE 52
	LOCALS = キツネ耳
CASE 53
	LOCALS = イヌ耳

CASE 60
	LOCALS = カチューシャ
CASE 61
	LOCALS = リボン
CASE 62
	LOCALS = サークレット
CASE 63
	LOCALS = ティアラ
CASE 64
	LOCALS = かんざし
CASE 65
	LOCALS = 頭上サングラス

CASE 70
	LOCALS = ケープ
CASE 71
	LOCALS = マント
CASE 72
	LOCALS = 分厚いコート
CASE 73
	LOCALS = ステッチコート
CASE 74
	LOCALS = トレンチコート
CASE 75
	LOCALS = ダッフルコート
CASE 76
	LOCALS = ポンチョコート
CASE 77
	LOCALS = ダウンジャケット
CASE 78
	LOCALS = 肩出しジャケット
CASE 79
	LOCALS = ジャージ

CASE 80
	LOCALS = キャットガーター
CASE 81
	LOCALS = サスペンダー
CASE 82
	LOCALS = フリルサスペンダー
ENDSELECT

RETURNF LOCALS

;-------------------------------------------------
;ARGの身につけている小物が条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_OTHER(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:アクセサリ == 0
	RETURNF 0

SELECTCASE ARGS
CASE "首", "眼鏡", "帽子", "つけ耳", "髪飾り", "外套"
	SIF OTHER(CSTR:ARG:アクセサリ, ARGS) != ""
		RETURNF 1
CASE "髪飾りリボン"
	SIF STRCOUNTS(@"\/%CSTR:ARG:アクセサリ%", "/リボン/", "/両側リボン/", "/片側リボン/", "/片側リボン２/", "/後ろリボン/", "/頭上リボン/")
		RETURNF 1
CASEELSE
	SIF STRCOUNT(CSTR:ARG:アクセサリ, ARGS)
		RETURNF 1
ENDSELECT
RETURNF 0

