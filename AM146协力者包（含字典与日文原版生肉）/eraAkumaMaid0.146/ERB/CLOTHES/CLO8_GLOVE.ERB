;-------------------------------------------------
;キャラARGに手袋ARGSを着せる。ARGS == ""なら脱がす
;-------------------------------------------------
@SET_GLOVE, ARGS, ARG
SIF ARG == 0 && TARGET > 0
	ARG = TARGET

;汚れ＆ずれたりしたフラグ等を初期化して新品にする
STAIN:ARG:手袋 = 0

CALL PALAM_GLOVE, ARGS, ARG

EQUIP:ARG:手袋 = STRLENS(CSTR:ARG:手袋)
TFLAG:着替えた = 1

;-------------------------------------------------
;ARGの手袋の名前。ARGSに"略"と入れると略称になる
;-------------------------------------------------
@GLOVE(ARG, ARGS)
#FUNCTIONS

VARSET LOCALS
SELECTCASE ARGS
CASE "略"
	IF CHECK_GLOVE("手露出", ARG)
		LOCALS = 手
	ELSEIF CHECK_GLOVE("指露出", ARG)
		LOCALS = 指
	ELSEIF CHECK_GLOVE("鎧", ARG)
		LOCALS = 篭手
	ELSEIF CHECK_GLOVE("肉球", ARG)
		LOCALS = 肉球
	ELSE
		LOCALS = 手袋
	ENDIF
CASEELSE
	;LOCALSに名称が入る
	SPLIT CSTR:ARG:手袋, "/", LOCALS
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;ARG番の手袋の名称。着せ替えなどで表を作る際に使う
;-------------------------------------------------
@NAME_GLOVE(ARG)
#FUNCTIONS

VARSET LOCALS

SELECTCASE ARG
CASE 0
	LOCALS = シルクグローブ
CASE 1
	LOCALS = ドレスグローブ
CASE 2
	LOCALS = レザースリーブ
CASE 3
	LOCALS = ドレススリーブ
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;キャラARGに手袋ARGSを着せた場合のCSTR処理
;-------------------------------------------------
@PALAM_GLOVE, ARGS, ARG
;追加で記述された属性を一時的に保管する
#DIMS STR_ADD

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

STR_ADD = %EXTSTR(ARGS, "/")%
ARGS = %EXTSTR(ARGS, "", "/")%

SELECTCASE ARGS
CASE ""
	CSTR:ARG:手袋 = 
CASE "シルクグローブ"
	CSTR:ARG:手袋 = シルクグローブ/
CASE "ドレスグローブ"
	CSTR:ARG:手袋 = ドレスグローブ/ロング/レース/
CASE "レザースリーブ"
	CSTR:ARG:手袋 = レザースリーブ/肘まで/手露出/
CASE "ドレススリーブ"
	CSTR:ARG:手袋 = ドレススリーブ/ロング/レース/手露出/
CASE "アームバインダー"
	CSTR:ARG:手袋 = アームバインダー/ロング/革/指の使用禁止
CASEELSE
	CSTR:ARG:手袋 = %ARGS%/
ENDSELECT

CSTR:ARG:手袋 = %MERGE_STR(CSTR:ARG:手袋, STR_ADD)%

;-------------------------------------------------
;ARGの手袋が条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_GLOVE(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:手袋 == 0
	RETURNF 0
SIF STRCOUNT(CSTR:ARG:手袋, ARGS)
	RETURNF 1
RETURNF 0

