;-------------------------------------------------
;キャラARGにARGSを着せる。ARGS == ""なら脱がす
;-------------------------------------------------
@SET_OUTER, ARGS, ARG
SIF ARG == 0 && TARGET > 0
	ARG = TARGET
;汚れ＆ずれたりしたフラグ等を初期化して新品にする
STAIN:ARG:アウター = 0

CALL PALAM_OUTER, ARGS, ARG

;コモンフラグを設定する
CSTR:ARG:アウター = %SET_CLO_COMMON(CSTR:ARG:アウター, ARG)%
EQUIP:ARG:アウター = STRLENS(CSTR:ARG:アウター)

;パンストを履いたら靴下は脱ぐ
SIF CHECK_OUTER("パンスト", ARG)
	CALL SET_SOCKS, "", ARG
TFLAG:着替えた = 1

;-------------------------------------------------
;ARGのアウターの名前。ARGSに"略"と入れると略称になる
;-------------------------------------------------
@OUTER(ARG, ARGS)
#FUNCTIONS
#DIM LCOUNT
#DIMS COLOR_ABOUT

VARSET LOCALS
SELECTCASE ARGS
CASE "CIP種類"
	IF CHECK_OUTER("ブルマ", ARG)
		LOCALS = ブルマ%CLOCOLOR_MAIDFACE("ブルマ", CHECK_CLOCOLOR(CSTR:ARG:アウター), ARG)%
	ELSEIF CHECK_OUTER("スパッツ", ARG)
		LOCALS = スパッツ%CLOCOLOR_MAIDFACE("スパッツ", CHECK_CLOCOLOR(CSTR:ARG:アウター), ARG)%
	ELSE
		RETURNF ""
	ENDIF
CASE "略"
	IF CHECK_OUTER("ブルマ", ARG)
		LOCALS = ブルマ
	ELSEIF CHECK_OUTER("ホットパンツ", ARG)
		LOCALS = ホットパンツ
	ELSEIF CHECK_OUTER("スパッツ", ARG)
		LOCALS = スパッツ
	ELSEIF CHECK_OUTER("パジャマ", ARG)
		LOCALS = パジャマズボン
	ELSEIF CHECK_OUTER("ハーフパンツ", ARG)
		LOCALS = ハーフパンツ
	ELSEIF CHECK_OUTER("ショートパンツ", ARG)
		LOCALS = ショートパンツ
	ELSEIF CHECK_OUTER("ジャージ", ARG)
		LOCALS = ジャージ
	;ELSEIF CHECK_OUTER("ローライズジーンズ", ARG)
	;	LOCALS = ローライズジーンズ
	ELSEIF CHECK_OUTER("ジーンズ", ARG)
		LOCALS = ジーンズ
	ELSEIF CHECK_OUTER("キュロットスカート", ARG)
		LOCALS = キュロット
	ELSEIF CHECK_OUTER("網パンスト", ARG)
		LOCALS = 網パンスト
	ELSEIF CHECK_OUTER("オープンパンスト", ARG)
		LOCALS = オープンパンスト
	ELSEIF CHECK_OUTER("パンスト", ARG)
		LOCALS = パンスト
	ELSEIF CHECK_OUTER("レギンス", ARG)
		LOCALS = レギンス
	ELSE
		LOCALS = ズボン
	ENDIF
CASEELSE
	;LOCALS:0に名称、LOCALS:1～に色が入る場合がある
	SPLIT CSTR:ARG:アウター, "/", LOCALS

	COLOR_ABOUT = 
	FOR LCOUNT, 1, 10
		SELECTCASE LOCALS:LCOUNT
		CASE ""
			BREAK
		CASE "縞々", "水玉模様", "豹柄"
			COLOR_ABOUT = %LOCALS:LCOUNT%
			BREAK
		CASE "赤", "青", "黒", "白", "ベージュ", "ブラウン", "ピンク"
			COLOR_ABOUT = %LOCALS:LCOUNT%
			BREAK
		CASE "紫", "真紅", "薄桃色", "緑色", "黄色", "紺色", "薄緑", "橙色", "薄青色", "純白"
			COLOR_ABOUT = %LOCALS:LCOUNT%
			BREAK
		ENDSELECT
	NEXT

	IF ARGS == "CIPカラー：種類"
		SELECTCASE COLOR_ABOUT
		CASE "赤", "ピンク", "紫", "真紅", "薄桃色"
			RETURNF "赤"
		CASE "白", "ベージュ", "純白"
			RETURNF "白"
		CASEELSE
			RETURNF "黒"
		ENDSELECT
	ELSE
		SIF COLOR_ABOUT != ""
			LOCALS += @"（%COLOR_ABOUT%）"
	ENDIF
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;ARG番のアウターの名称
;-------------------------------------------------
@NAME_OUTER(ARG)
#FUNCTIONS
VARSET LOCALS

SELECTCASE ARG
CASE 0
	LOCALS = ブルマ
;CASE 1
;	LOCALS = ハイレグブルマ
CASE 2
	LOCALS = スパッツ
CASE 3
	LOCALS = ホットパンツ
CASE 4
	LOCALS = ショートパンツ
CASE 5
	LOCALS = ハーフパンツ
CASE 6
	LOCALS = ズボン
CASE 7
	LOCALS = ジャージズボン
CASE 8
	LOCALS = ジーンズ
CASE 9
	LOCALS = ローライズジーンズ
CASE 10
	LOCALS = キュロットスカート
CASE 11
	LOCALS = レギンス
CASE 12
	LOCALS = パンスト
CASE 13
	LOCALS = 網パンスト
CASE 14
	LOCALS = オープンパンスト
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;キャラARGにアウターARGSを着せた場合のCSTR処理
;-------------------------------------------------
@PALAM_OUTER, ARGS, ARG
;追加で記述された属性を一時的に保管する
#DIMS STR_ADD

SIF ARG == 0 && TARGET > 0
	ARG = TARGET

STR_ADD = %EXTSTR(ARGS, "/")%
ARGS = %EXTSTR(ARGS, "", "/")%

SELECTCASE ARGS
CASE ""
	CSTR:ARG:アウター = 
CASE "ブルマ"
	CSTR:ARG:アウター = ブルマ/%CLOCOLOR_MAIDFACE("ブルマ", "", ARG)%/
;CASE "ハイレグブルマ"
;	CSTR:ARG:アウター = ハイレグブルマ/%CLOCOLOR_MAIDFACE("ブルマ", "", ARG)%/Ｔバック/
CASE "スパッツ"
	CSTR:ARG:アウター = スパッツ/ずれ不可/
CASE "ホットパンツ"
	CSTR:ARG:アウター = ホットパンツ/ずれ不可/コキ不可/隙間チラ可/ズボン/社会の窓/
CASE "ホットパンツ"
	CSTR:ARG:アウター = ホットパンツ/ずれ不可/コキ不可/隙間チラ可/ズボン/社会の窓/
CASE "ショートパンツ"
	CSTR:ARG:アウター = ショートパンツ/ずれ不可/コキ不可/隙間チラ可/ズボン/社会の窓/
CASE "ハーフパンツ"
	CSTR:ARG:アウター = ハーフパンツ/ずれ不可/コキ不可/隙間チラ可/ズボン/男物可/
CASE "ジャージズボン"
	CSTR:ARG:アウター = ジャージズボン/ずれ不可/コキ不可/ズボン/男物可/
CASE "ジーンズ"
	CSTR:ARG:アウター = ジーンズ/ずれ不可/コキ不可/ズボン/男物可/社会の窓/
CASE "ローライズジーンズ"
	CSTR:ARG:アウター = ローライズジーンズ/ずれ不可/コキ不可/ズボン/社会の窓/
CASE "レギンス"
	CSTR:ARG:アウター = レギンス/ずれ不可/コキ不可/ズボン/

CASE "パンスト"
	CSTR:ARG:アウター = パンスト/ずれ不可/コキ不可/シースルー/
CASE "黒いパンスト"
	CSTR:ARG:アウター = パンスト/黒/ずれ不可/コキ不可/シースルー/
CASE "網パンスト"
	CSTR:ARG:アウター = 網パンスト/ずれ不可/コキ不可/レース/
CASE "オープンパンスト"
	CSTR:ARG:アウター = オープンパンスト/ずれ不可/コキ不可/Ｖ露出/Ａ露出/

CASE "ズボン"
	CSTR:ARG:アウター = ズボン/ずれ不可/コキ不可/ズボン/男物可/社会の窓/
CASE "スラックス"
	CSTR:ARG:アウター = スラックス/ずれ不可/コキ不可/ズボン/男物可/社会の窓/
CASE "キュロットスカート"
	CSTR:ARG:アウター = キュロットスカート/ずれ不可/コキ不可/隙間チラ可/ズボン/
CASEELSE
	CSTR:ARG:アウター = %ARGS%/
ENDSELECT

CSTR:ARG:アウター = %MERGE_STR(CSTR:ARG:アウター, STR_ADD)%


;-------------------------------------------------
;ARGのアウターが条件ARGSを満たすか確認する
;-------------------------------------------------
@CHECK_OUTER(ARGS, ARG)
#FUNCTION
SIF ARG == 0 && TARGET
	ARG = TARGET
SIF EQUIP:ARG:アウター == 0
	RETURNF 0

SELECTCASE ARGS
;主に埴輪ルックの判定で使う
CASE "丈長ズボン"
	;ズボンじゃないとダメ
	SIF STRCOUNT(CSTR:ARG:アウター, "ズボン") == 0
		RETURNF 0
	;丈の短いもの
	SIF STRCOUNTS(CSTR:ARG:アウター, "ホットパンツ", "ショート")
		RETURNF 0
	RETURNF 1
CASE "股間の隙間"
	;太ももの付け根までを布地が締め付けている服
	SIF STRCOUNTS(CSTR:ARG:アウター, "スパッツ", "ホットパンツ")
		RETURNF 1
;主に"アソコのチラ見せ撮影"の判定で使う
CASE "めくり可能"
	;めくれなさそうなもの
	SIF STRCOUNTS(CSTR:ARG:アウター, "パンスト", "ずれ不可")
		RETURNF 0
	RETURNF 1
;ずれ不可でなく、ずらしていない
CASE "ずらし可", "ずらせる"
	SIF STRCOUNT(CSTR:ARG:アウター, "Ｖ露出")
		RETURNF 0
	IF STRCOUNT(CSTR:ARG:アウター, "ファスナー")
		SIF STRCOUNT(CSTR:ARG:アウター, "ファスナー開")
			RETURNF 0
		RETURNF 1
	ENDIF
	SIF STRCOUNTS(CSTR:ARG:アウター, "ずらした", "ずれ不可")
		RETURNF 0
	RETURNF 1
;パンストで、破いていない
CASE "破ける"
	SIF STRCOUNT(CSTR:ARG:アウター, "破いた")
		RETURNF 0
	SIF STRCOUNT(CSTR:ARG:アウター, "パンスト")
		RETURNF 1
;ファスナーがあり、あけていない
CASE "ファスナー閉"
	SIF STRCOUNT(CSTR:ARG:アウター, "ファスナー開")
		RETURNF 0
	SIF STRCOUNT(CSTR:ARG:アウター, "ファスナー")
		RETURNF 1
CASE "ＶＡ露出"
	SIF STRCOUNT(CSTR:ARG:アウター, "Ｖ露出") && STRCOUNT(CSTR:ARG:アウター, "Ａ露出")
		RETURNF 1
CASE "クロッチ"
	SIF STRCOUNTS(CSTR:ARG:アウター, "ファスナー開", "ずらした", "破いた", "Ｖ露出", "スリット")
		RETURNF 0
	RETURNF 1
CASE "クロッチありパンスト"
	SIF STRCOUNTS(CSTR:ARG:アウター, "ファスナー開", "ずらした", "破いた", "Ｖ露出", "スリット", "オープン")
		RETURNF 0
	SIF STRCOUNT(CSTR:ARG:アウター, "パンスト")
		RETURNF 1
CASE "Ａクロッチ"
	SIF STRCOUNTS(CSTR:ARG:アウター, "ファスナー開", "ずらした", "破いた", "Ａ露出", "スリット")
		RETURNF 0
	RETURNF 1
CASE "食い込み"
	SIF STRCOUNTS(CSTR:ARG:アウター, "ずらした", "ファスナー開", "Ｖ露出", "スリット", "破いた")
		RETURNF 0
	SIF STRCOUNTS(CSTR:ARG:アウター, "ハイレグ")
		RETURNF 1
CASE "ローライズ"
	SIF STRCOUNTS(CSTR:ARG:アウター, "ローライズ", "ローレグ")
		RETURNF 1
CASE "蒸れ"
	SIF STRCOUNTS(CSTR:ARG:アウター, "ずらした", "ファスナー開", "Ｖ露出", "スリット", "破いた")
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:アウター, "蒸れ")
		RETURNF 1
CASE "染み"
	SIF STRCOUNTS(CSTR:ARG:アウター, "ずらした", "ファスナー開", "Ｖ露出", "スリット", "破いた", "通気性無し")
		RETURNF 0
	SIF CHECK_STAIN(STAIN:ARG:アウター, "濡れ")
		RETURNF 1
CASEELSE
	SIF STRCOUNT(CSTR:ARG:アウター, ARGS)
		RETURNF 1
ENDSELECT
RETURNF 0
