@KISEKAE
#DIM LCOUNT
#DIM MEMO_REDRAW
;最初にハダカなら1
#DIM IS_HADAKA
;淫紋が見えるようになったかをチェックする
#DIM IS_VISIBLE_INMON
;着せ替え前の衣装の記録
#DIMS MEMO_CLO, 50

MEMO_REDRAW = CURRENTREDRAW()
REDRAW 0
;着ていた服を記憶
FOR LCOUNT, 40, 50
	MEMO_CLO:LCOUNT = %CSTR:LCOUNT%
NEXT
IS_HADAKA = CHECK_CLO("ハダカ")

$START_KISEKAE

;淫紋確認の地の文を出すかのチェックに使う
IS_VISIBLE_INMON = CHECK_CLO("淫紋露出")

DRAWLINE

PRINTFORML %CALLNAME:TARGET%に何を着替えてもらいますか？
CALL PRINT_MAIDFACE, TARGET
PRINTFORM 現在:
CALL PRINT_DRESSL, TARGET, "拡大表示"
PRINTL 
PRINTFORML  [ 0] コーディネート
PRINTFORML  [ 1] 服、スカートなど
IF COND("脚の使用")
	PRINTFORML  [ 2] ボディスーツ、ワンピース水着、ブルマ、ズボン、パンストなど
ELSE
	CALL PRINT_STR, "灰色_ [×] ボディスーツ、ワンピース水着、ブルマ、ズボン、パンストなど_L_"
ENDIF
PRINTFORML  [ 3] 下着、セパレート水着等
IF COND("脚の使用")
	PRINTFORML  [ 4] 靴下、グローブ、靴など
ELSE
	PRINTFORML  [ 4] グローブなど
ENDIF
PRINTFORML  [ 5] アクセサリ
IF CSTR:今年の水着 != ""
	PRINTFORML  [ 9] 今年の水着（%SWIM(CSTR:今年の水着)%）に着替えてもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] 今年の水着に着替えてもらう"
ENDIF
PRINTL 
PRINTFORML  [10] 現在の衣装をエッチのデフォルト衣装にしてもらう
IF CSTR:エッチ衣装 != ""
	PRINTFORM  お願い中：
	CALL PRINT_MEMO_DRESS, "エッチ衣装"
	PRINTL 
ENDIF
IF CSTR:エッチ衣装 != ""
	PRINTFORML  [11] エッチのデフォルト衣装を無くしてもらう
ELSE
	CALL PRINT_STRL, "灰色_ [×] エッチのデフォルト衣装を無くしてもらう"
ENDIF
PRINTL 
IF EQUIP:服
	PRINTFORML  [90] %SHIRT(TARGET)%を脱いでもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] 服を脱いでもらう"
ENDIF
SIF CHECK_CLO("ハダカ") == 0
	PRINTFORML  [91] 靴下、靴、グローブ、アクセサリを残して脱いでもらう
SIF CHECK_CLO("全裸") == 0
	PRINTFORML  [92] 全部脱いでもらう
PRINTFORML  [99] 着せ替えに関するコンフィグを開く
PRINTFORML 
PRINTFORML [100] これでＯＫ

$INPUT_LOOP
INPUT

SELECTCASE RESULT
CASE 100
	SIF CFLAG:電源ボックス && CHECK_CLO("電源ボックス保持可能") == 0
		CFLAG:電源ボックス = 0
	REDRAW MEMO_REDRAW
	;服を着替えたなら、それの感想
	IF EQUIP:服 && CSTR:服 != MEMO_CLO:42
		TFLAG:地の文分岐 = 1
	;ボディスーツを着替えたなら、それの感想
	ELSEIF EQUIP:ボディスーツ && CSTR:ボディスーツ != MEMO_CLO:44
		TFLAG:地の文分岐 = 1
	;水着に着替えたなら、その感想
	ELSEIF CHECK_CLO("水着姿") && CSTR:パンツ != MEMO_CLO:40
		TFLAG:地の文分岐 = 1
	;ハダカになったなら、それの感想
	ELSEIF CHECK_CLO("ハダカ") && IS_HADAKA == 0
		TFLAG:地の文分岐 = 2
	;パンツを着替えたor脱いだなら、それの感想
	ELSEIF CSTR:パンツ != MEMO_CLO:40
		TFLAG:地の文分岐 = 3
	;アウターを着替えたなら、それの感想
	ELSEIF CSTR:アウター != MEMO_CLO:45
		TFLAG:地の文分岐 = 4
	;ブラだけ替えたor外した
	ELSEIF CSTR:ブラ != MEMO_CLO:41
		TFLAG:地の文分岐 = 5
	;手袋を替えたor外した
	ELSEIF CSTR:手袋 != MEMO_CLO:48
		TFLAG:地の文分岐 = 6
	;靴下を替えたor外した
	ELSEIF CSTR:靴下 != MEMO_CLO:46
		TFLAG:地の文分岐 = 7
	;靴だけ替えたor外した
	ELSEIF CSTR:靴 != MEMO_CLO:47
		TFLAG:地の文分岐 = 8
	;その他小物だけ替えたor外した
	ELSEIF CSTR:アクセサリ != MEMO_CLO:49
		TFLAG:地の文分岐 = 9
	ELSE
		TFLAG:コマンドキャンセル = 1
		;何も変えなかったかチェック
		FOR LCOUNT, 40, 50
			IF CSTR:LCOUNT != MEMO_CLO:LCOUNT
				TFLAG:コマンドキャンセル = 0
				BREAK
			ENDIF
		NEXT
	ENDIF
	RETURN 0
CASE 0
	CALL SELECT_COORDINATE
CASE 1
	CALL SELECT_SHIRT
CASE 2
	SIF COND("脚の使用")
		CALL SELECT_BODYSUIT_OUTER
CASE 3
	CALL SELECT_PANTU_BRA
CASE 4
	CALL SELECT_SOCKS_GLOVE_SHOES
CASE 5
	CALL SELECT_OTHER
CASE 9
	IF CSTR:今年の水着 != ""
		CALL SET_COORDINATE, "今年の水着"
	ENDIF
CASE 10
	CALL MEMO_DRESS, "エッチ衣装"
CASE 11
	CSTR:エッチ衣装 = 
CASE 90
	SIF EQUIP:服
		CALL EVENT_DATUI, "衣服"
CASE 91
	IF CHECK_CLO("ハダカ") == 0
		CALL EVENT_DATUI, "ハダカ"
		CALL SETFLAG, "ハダカにする", TARGET
	ENDIF
CASE 92
	IF CHECK_CLO("全裸") == 0
		CALL SETFLAG, "服以外全脱ぎ", TARGET
		CALL EVENT_DATUI, "ハダカ"
		CALL SETFLAG, "ハダカにする", TARGET
	ENDIF
CASE 99
	CALL SETCONFIG, "着せ替え設定"
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

;淫紋イベント
SIF IS_VISIBLE_INMON == 0 && MARK:淫紋 >= 2 && CFLAG:淫紋の様子 == 0 && CHECK_CLO("淫紋露出")
	CALL TEXT, "淫紋の様子：脱衣後"

GOTO START_KISEKAE

;-------------------------------------------------
;服装の一時記憶や呼出処理
;-------------------------------------------------
@MEMO_DRESS, ARGS, ARG
#DIM LCOUNT
SIF ARG <= 0
	ARG = TARGET

CSTR:ARG:ARGS = 
FOR LCOUNT, 40, 50
	CSTR:ARG:ARGS = %CSTR:ARG:ARGS%{EQUIP:ARG:LCOUNT},%CSTR:ARG:LCOUNT%,
NEXT

@PRINT_MEMO_DRESS, ARGS, ARG
#DIM LCOUNT
#DIM MEMO_STAIN, 10
#DIM MEMO_EQUIP, 10
#DIMS MEMO_CSTR, 10

SIF ARG <= 0
	ARG = TARGET

FOR LCOUNT, 0, 10
	MEMO_STAIN:LCOUNT = STAIN:ARG:(LCOUNT + 10)
	MEMO_EQUIP:LCOUNT = EQUIP:ARG:(LCOUNT + 40)
	MEMO_CSTR:LCOUNT = %CSTR:ARG:(LCOUNT + 40)%
NEXT

CALL SET_MEMO_DRESS, ARGS, ARG
CALL PRINT_DRESS, ARG

FOR LCOUNT, 0, 10
	STAIN:ARG:(LCOUNT + 10) = MEMO_STAIN:LCOUNT
	EQUIP:ARG:(LCOUNT + 40) = MEMO_EQUIP:LCOUNT
	CSTR:ARG:(LCOUNT + 40) = %MEMO_CSTR:LCOUNT%
NEXT


@SET_MEMO_DRESS, ARGS, ARG
#DIM LCOUNT
SIF ARG <= 0
	ARG = TARGET

SELECTCASE ARGS
CASE "着替え希望衣装"
	CFLAG:ARG:コスプレ中 = 1
	;既に脱いだ服がある場合には記録を上書きしない
	SIF CSTR:ARG:脱いだ服一式 == ""
		CALL MEMO_DRESS, "脱いだ服一式", ARG
ENDSELECT

CALL SETFLAG, "衣装全脱ぎ", ARG

VARSET LOCALS
SPLIT CSTR:ARG:ARGS, ",", LOCALS

FOR LCOUNT, 0, 10
	EQUIP:ARG:(LCOUNT + 40) = TOINT(LOCALS:(LCOUNT*2) )
	CSTR:ARG:(LCOUNT + 40) = %LOCALS:(LCOUNT*2 + 1)%
NEXT

SELECTCASE ARGS
CASE "脱いだ服一式", "着替え希望衣装"
	CSTR:ARG:ARGS = 
ENDSELECT


;-------------------------------------------------
;コーディネート選択
;ARGSに条件を入れる事が出来る
;-------------------------------------------------
@SELECT_COORDINATE, ARGS
#DIM MEMO_FLAG

MEMO_FLAG = FLAG:着せ替え画面
CALL SELECT_COORDINATE_MAIN, ARGS
FLAG:着せ替え画面 = MEMO_FLAG


@SELECT_COORDINATE_MAIN, ARGS
#DIM LCOUNT
#DIM CHOICE
#DIM MEMO_LINECOUNT
;着せ替え前の衣装の記録
#DIMS MEMO_CLO, 50
#DIM MEMO_EQUIP, 50
#DIMS SHOW_FILTER
;対応した番号の服に着替えられるかどうか
#DIM CAN_CHANGE, 200

VARSET CAN_CHANGE

;着ていた服を記憶
FOR LCOUNT, 40, 50
	MEMO_CLO:LCOUNT = %CSTR:LCOUNT%
	MEMO_EQUIP:LCOUNT = EQUIP:LCOUNT
NEXT

MEMO_LINECOUNT = LINECOUNT

PRINTL 
SELECTCASE ARGS
CASE "着替え希望"
	PRINTFORML どれにしますか？
	FLAG:着せ替え画面 = 1p1
CASE "コスプレ"
	PRINTFORML どれに着替えてもらいますか？
	CALL PRINT_MAIDFACE, TARGET
	PRINTFORM 現在:
	CALL PRINT_DRESSL, TARGET, "拡大表示"
	FLAG:着せ替え画面 = 1p1
CASEELSE
	PRINTFORML どのコーディネートに着替えてもらいますか？
	PRINTFORML 下の表示ジャンルをクリックする事で表示を変更出来ます
	CALL PRINT_MAIDFACE, TARGET
	PRINTFORM 現在:
	CALL PRINT_DRESSL, TARGET, "拡大表示"
	SIF	FLAG:着せ替え画面 == 0
		FLAG:着せ替え画面 = 1p1
ENDSELECT
PRINTL 
FOR LCOUNT, 0, 100
	SELECTCASE NAME_COORDINATE(LCOUNT)
	CASE ""
		CONTINUE
	CASEELSE
		SELECTCASE LCOUNT
		CASE IS < 20
			SIF CONFIG("コーディネート：コスプレ") == 0 && CONFIG("コーディネート：一覧") == 0
				CONTINUE
			SIF LCOUNT == 0 && ARGS == ""
				PRINTFORML - コスプレ -
		CASE IS < 40
			SIF CONFIG("コーディネート：制服") == 0 && CONFIG("コーディネート：一覧") == 0
				CONTINUE
			SIF LCOUNT == 20
				PRINTFORML - 制服 -
		CASE IS < 60
			SIF CONFIG("コーディネート：体操服") == 0 && CONFIG("コーディネート：一覧") == 0
				CONTINUE
			SIF LCOUNT == 40
				PRINTFORML - 体操服 -
		CASE IS < 80
			SIF CONFIG("コーディネート：水着") == 0 && CONFIG("コーディネート：一覧") == 0
				CONTINUE
			SIF LCOUNT == 60
				PRINTFORML - 水着 -
		ENDSELECT

		CAN_CHANGE:LCOUNT = 1

		;衣装リセット
		CALL SETFLAG, "衣装全脱ぎ", TARGET
		CALL SET_COORDINATE, NAME_COORDINATE(LCOUNT)
		PRINTFORM [{LCOUNT, 3}] %TEXT_LJ(NAME_COORDINATE(LCOUNT), 20)%
		CALL PRINT_DRESSL, TARGET, "衣装表示"
	ENDSELECT
NEXT

PRINTL 
SELECTCASE ARGS
CASE "コスプレ", "着替え希望"

CASEELSE
	PRINTFORM 表示ジャンル：
	VARSET SHOW_FILTER
	;SIF CONFIG("コーディネート：一覧")
	;	SHOW_FILTER += "黄色/"
	;SHOW_FILTER += "一覧_1000/"

	;SIF CONFIG("コーディネート：キャラ衣装")
	;	SHOW_FILTER += "黄色/"
	;SHOW_FILTER += "キャラ衣装_1001/"

	SIF CONFIG("コーディネート：コスプレ")
		SHOW_FILTER += "黄色/"
	SHOW_FILTER += "コスプレ_1002/"

	SIF CONFIG("コーディネート：制服")
		SHOW_FILTER += "黄色/"
	SHOW_FILTER += "制服_1003/"

	SIF CONFIG("コーディネート：体操服")
		SHOW_FILTER += "黄色/"
	SHOW_FILTER += "体操服_1004/"

	SIF CONFIG("コーディネート：水着")
		SHOW_FILTER += "黄色/"
	SHOW_FILTER += "水着_1005/"

	CALL PRINT_LIST, SHOW_FILTER
	PRINTL 
ENDSELECT

PRINTFORML [999] 戻る

;着ていた服を戻す
FOR LCOUNT, 40, 50
	CSTR:LCOUNT = %MEMO_CLO:LCOUNT%
	EQUIP:LCOUNT = MEMO_EQUIP:LCOUNT
NEXT

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 999
	FLAG:着せ替え画面 = 0
	RETURN 0
ELSEIF CHOICE >= 1002 && CHOICE <= 1005
	SELECTCASE ARGS
	CASE "コスプレ", "着替え希望"
	CASEELSE
		IF CHOICE == 1000
			FLAG:着せ替え画面 = 0
		ELSEIF CHOICE >= 1001 && CHOICE <= 1005
			FLAG:着せ替え画面 = 0
			SETBIT FLAG:着せ替え画面, CHOICE - 1001
		ENDIF
	ENDSELECT
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ELSEIF CHOICE >= 0 && CHOICE < 100 && NAME_COORDINATE(CHOICE) != "" && CAN_CHANGE:CHOICE
	CALL SET_COORDINATE, NAME_COORDINATE(CHOICE)

	SELECTCASE CHOICE
	CASE IS >= 100
		;PRINTFORM %CSTR:着替え希望履歴%　

		CSTR:着替え希望履歴 = ＠%NAME_COORDINATE(CHOICE)%\/%CSTR:着替え希望履歴%
		VARSET LOCALS
		SPLIT CSTR:着替え希望履歴, "/", LOCALS
		CSTR:着替え希望履歴 = 
		;新しい方から１００個まで記録
		FOR LCOUNT, 0, 100
			SIF LOCALS:LCOUNT == ""
				BREAK
			CSTR:着替え希望履歴 += @"%LOCALS:LCOUNT%\/"
		NEXT

		;PRINTFORML →　%CSTR:着替え希望履歴%
	ENDSELECT

	SELECTCASE ARGS
	CASE "着替え希望"
		CALL MEMO_DRESS, "着替え希望衣装"

		;服を戻す
		FOR LCOUNT, 40, 50
			CSTR:LCOUNT = %MEMO_CLO:LCOUNT%
			EQUIP:LCOUNT = MEMO_EQUIP:LCOUNT
		NEXT
		RETURN 1
	CASEELSE
		IF CHECK_SHIRT("Ｙシャツ")
			IF TALENT:小柄
				PRINTFORMW %CALLNAME:TARGET%にはブカブカの、%CALLNAME:MASTER%のＹシャツに着替えてもらいました
				PRINTFORMW あまりに袖が余っているので、くるくると折って手を出させてあげます
			ELSE
				PRINTFORMW %CALLNAME:TARGET%には丈が少し長めな、%CALLNAME:MASTER%のＹシャツに着替えてもらいました
				IF TALENT:恥薄い || RAND:6 == 0
					PRINTFORM 裾の合わせ目からチラチラ見える
					IF EQUIP:パンツ && COND("脚の使用")
						PRINT パンツ
					ELSE
						IF COND("陰毛") >= 5
							PRINT 茂み
						ELSEIF COND("陰毛") == 4
							PRINT 剃り跡
						ELSE
							IF COND("陰毛") && PENIS(TARGET) == 0 && RAND:2 == 0
								PRINT 無毛のアソコ
							ELSE
								PRINT 股間
							ENDIF
						ENDIF
					ENDIF
					PRINTFORMW が%CALLNAME:MASTER%の目を奪います…
				ELSEIF TALENT:恥じらい || RAND:5 == 0
					IF RAND:2 == 0
						PRINTFORMW 長めの裾はスカートとしては物足りないようで、恥ずかしそうに脚をもじもじしています…
					ELSE
						PRINTFORMW 長めの裾はスカートとしては物足りないようで、ぎゅっと裾を掴んで下に引き伸ばしています…
					ENDIF
				ENDIF
			ENDIF
		ELSEIF CHECK_SHIRT("女教師")
			PRINTFORMW タイトスカートの女教師の格好になってもらいました
		ELSEIF CHECK_SHIRT("和服")
			PRINTFORMW 和服姿になってもらいました
		ELSEIF CHECK_SHIRT("メイド服")
			PRINTFORMW ぴしっと糊の効いた、清潔なメイド服を着てもらいました
		ELSEIF CHECK_SHIRT("体操服")
			PRINTFORMW %OUTER(TARGET, "略")%から伸びる脚が健康的な体操服に着替えてもらいました
		ELSEIF CHECK_SHIRT("ナース服")
			PRINTFORMW 白衣の天使になってもらいました
		ELSEIF CHECK_SHIRT("チャイナドレス")
			PRINTFORMW \@ COND("脚の使用") ? スリットから伸びる脚が # \@艶かしいチャイナドレスに着替えてもらいました
		ELSEIF CHECK_SHIRT("セーラー服")
			PRINTFORMW チラリと見えるおへそが魅力的なセーラー服に着替えてもらいました
		ELSEIF CHECK_SHIRT("巫女")
			PRINTFORMW 穢れ無き清純さ溢れる紅白の巫女服に着替えてもらいました
		ELSEIF CHECK_SHIRT("ベビードール")
			PRINTFORMW ほんのり透ける可愛らしいベビードールに着替えてもらいました
		ELSEIF CHECK_SHIRT("ウェディングドレス")
			PRINTFORMW 純白のウェディングドレスに着替えてもらいました
			IF TALENT:小柄
				PRINTFORMW ちょっと背伸びした感じも、可愛らしさに一役買っています
			ELSEIF TALENT:生意気
				PRINTFORM %CALLNAME:TARGET%は恥ずかしさ
				SIF TALENT:恋慕
					PRINT と嬉しさ
				PRINTFORMW を紛らわすためか、悪態をつきはじめました…
			ELSEIF (TALENT:素直 || TALENT:清楚) && TALENT:恋慕
				PRINTFORMW %CALLNAME:TARGET%は滲み出る嬉しさを隠せていません…
			ENDIF
		ELSEIF CHECK_SHIRT("ゴスロリドレス")
			PRINTFORMW \@ AGE(TARGET) <= 2 ? 妖しくも可愛らしい # 可愛らしさとのギャップに妖しい魅力を感じる \@ゴシック＆ロリータ服に着替えてもらいました
		ELSEIF CHECK_SHIRT("チアユニフォーム")
			PRINTFORMW 健康的な魅力に、見ていると元気になれるチアユニフォームに着替えてもらいました
		ELSEIF CHECK_SHIRT("パジャマ")
			PRINTFORMW 可愛らしいパジャマに着替えてもらいました
		ELSEIF CHECK_SHIRT("ウェイトレス")
			PRINTFORMW 喫茶店でおなじみな制服姿になってもらいました
		ELSEIF CHECK_CLO("ビキニ姿")
			IF TALENT:巨乳 && RAND:2 == 0
				PRINT 大きな胸がこぼれそうな
			ELSE
				PRINT 大胆な
			ENDIF
			PRINTFORMW ビキニ姿になってもらいました
		ELSEIF CHECK_BODYS("スク水")
			PRINTFORMW 紺色のスク水を着てもらいました
		ELSEIF CHECK_BODYS("競泳水着")
			PRINTFORMW 肢体のラインを引き立てる競泳水着を着てもらいました
		ELSEIF CHECK_BODYS("レオタード") && CHECK_BODYS("Ｖ露出")
			PRINTFORMW 股間と胸の周りだけが露出しているレオタード姿になってもらいました
		ELSEIF CHECK_BODYS("ボンデージ")
			PRINTFORMW \@ PENIS(TARGET) == 0 ? 乳房が丸出しの上に # \@股間にファスナーの付いたボンデージ姿になってもらいました
		ELSEIF CHECK_BODYS("バニー")
			PRINTFORMW 大人の魅力満点のバニーガール姿になってもらいました
		ELSEIF CHECK_BODYS("ワンピース水着")
			PRINTFORMW ちょっと清楚な雰囲気が漂う白いワンピース水着になってもらいました
		ELSE
			PRINTFORMW %NAME_COORDINATE(CHOICE)%に着替えてもらいました
		ENDIF
	ENDSELECT
ELSE
	GOTO INPUT_LOOP
ENDIF

SIF EQUIP:服 == 0 && COND("脚の使用")
	CALL SHOW_PUBICHAIR


;-------------------------------------------------
;服に関する関数
;-------------------------------------------------
@SELECT_SHIRT
#DIM LCOUNT
#DIM CHOICE
#DIM NEXT_LINE
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT

PRINTL 
CALL PRINT_MAIDFACE, TARGET
CALL PRINT_STRL, @"どれに着替えてもらいますか？身に着けている物は_黄色_色が変わり_、選択する事で外せます。"
PRINTFORM 現在:
CALL PRINT_DRESSL, TARGET, "拡大表示"
PRINTL 

NEXT_LINE = 0
FOR LCOUNT, 0, 80
	SELECTCASE NAME_SHIRT(LCOUNT)
	CASE ""
		CONTINUE
	CASEELSE
		SIF SHIRT(TARGET) == NAME_SHIRT(LCOUNT)
			SETCOLOR DEF_COLOR("黄色")
		PRINTFORM  [{LCOUNT, 2}] %TEXT_LJ(NAME_SHIRT(LCOUNT), 22)%
		RESETCOLOR

		NEXT_LINE += 1
		SIF NEXT_LINE%3 == 0
			PRINTL 
	ENDSELECT
NEXT
SIF NEXT_LINE%3
	PRINTL 
PRINTL 

IF EQUIP:服
	PRINTFORML  [90] %SHIRT(TARGET)%を脱いでもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] 服を脱いでもらう"
ENDIF
IF EQUIP:スカート && !CHECK_SKIRT("ワンピース")
	PRINTFORML  [91] スカートだけ脱いでもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] スカートだけ脱いでもらう"
ENDIF

PRINTFORML [100] 戻る

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	RETURN 0
ELSEIF CHOICE == 90 && EQUIP:服 + EQUIP:スカート
	CALL EVENT_DATUI, "衣服"
ELSEIF CHOICE == 91 && EQUIP:スカート && !CHECK_SKIRT("ワンピース")
	CALL SET_SKIRT, ""
ELSEIF NAME_SHIRT(CHOICE) != ""
	IF SHIRT(TARGET) == NAME_SHIRT(CHOICE)
		CALL SET_SHIRT, ""
		CALL SET_SKIRT, ""
	ELSE
		CALL SET_BODYS, ""
		CALL SET_SKIRT, ""
		CALL SET_SHIRT, NAME_SHIRT(CHOICE)
		SELECTCASE SHIRT(TARGET)
		CASE "女教師"
			PRINTFORMW タイトスカートの女教師の格好になってもらいました
		CASE "和服"
			PRINTFORMW 和服姿になってもらいました
		CASE "メイド服"
			PRINTFORML どのようなメイド服にしましょう？
			PRINTFORML  [ 0] 清楚で露出の少ないロングスカートタイプ
			PRINTFORML  [ 1] やはりミニスカートに限る
			PRINTFORML  [ 2] これって本当にメイド？なフレンチメイドスタイル

			CALL INPUT_SELECT, 3

			SELECTCASE RESULT
			CASE 0
				PRINTFORMW ぴしっと糊の効いた、清潔なメイド服を着てもらいました
			CASE 1
				PRINTFORMW 半ばまで露出した太ももが目に眩しい、ミニスカ型のメイド服を着てもらいました

				CALL SET_SHIRT, "ミニスカメイド"
			CASE 2
				PRINTFORMW ノースリーブに超ミニスカートを合わせた、扇情的なメイド服を着てもらいました

				CALL SET_SHIRT, "フレンチメイド"
			ENDSELECT
		CASE "体操服"
			PRINTFORMW \@ COND("脚の使用") ? ブルマから伸びる脚が # \@健康的な体操服に着替えてもらいました
		CASE "ナース服"
			PRINTFORMW 白衣の天使になってもらいました
		CASE "チャイナドレス"
			PRINTFORMW \@ COND("脚の使用") ? スリットから伸びる脚が # \@艶かしいチャイナドレスに着替えてもらいました
		CASE "セーラー服"
			PRINTFORMW チラリと見えるおへそが魅力的なセーラー服に着替えてもらいました
		CASE "巫女服"
			PRINTFORML …どのような巫女服にしましょう？
			PRINTFORML  [ 0] 伝統の巫女服
			PRINTFORML  [ 1] やはりミニスカートに限る
			PRINTFORML  [ 2] これって本当に巫女？な%TEXTS("腋")%見せスタイル

			CALL INPUT_SELECT, 3

			SELECTCASE RESULT
			CASE 0
				PRINTFORMW 穢れ無き清純さ溢れる紅白の巫女服に着替えてもらいました
			CASE 1
				PRINTFORMW 半ばまで露出した太ももが目に眩しい、ミニスカ型の巫女服を着てもらいました

				CALL SET_SHIRT, "ミニスカ巫女"
			CASE 2
				PRINTFORMW ノースリーブに超ミニスカートを合わせた、扇情的な巫女服を着てもらいました

				CALL SET_SHIRT, "腋巫女"
			ENDSELECT
		CASE "ベビードール"
			PRINTFORMW ほんのり透ける可愛らしいベビードールに着替えてもらいました
		CASE "ウェディングドレス"
			PRINTFORMW 純白のウェディングドレスに着替えてもらいました
		CASE "ゴスロリドレス"
			PRINTFORMW \@ AGE(TARGET) <= 2 ? 妖しくも可愛らしい # 可愛らしさとのギャップに妖しい魅力を感じる \@ゴシック＆ロリータ服に着替えてもらいました
		CASE "チアユニフォーム"
			PRINTFORMW 健康的な魅力に、見ていると元気になれるチアユニフォームに着替えてもらいました
		CASE "パジャマ"
			PRINTFORMW 可愛らしい子供用パジャマに着替えてもらいました。
		CASE "怪獣パジャマ"
			PRINTFORML 怪獣の格好のように見える、子供らしくて可愛らしいパジャマを着てもらいました。
			PRINTFORMW ちなみに、上下が繋がっている構造なのでトイレ用にファスナーも付いています。
		CASEELSE
			IF CHECK_SHIRT("Ｙシャツ")
				IF TALENT:小柄
					PRINTFORMW %CALLNAME:TARGET%にはブカブカの、%CALLNAME:MASTER%のＹシャツに着替えてもらいました
					PRINTFORMW あまりに袖が余っているので、くるくると折って手を出させてあげます
				ELSE
					PRINTFORMW %CALLNAME:TARGET%には丈が少し長めな、%CALLNAME:MASTER%のＹシャツに着替えてもらいました
					IF TALENT:恥薄い || RAND:6 == 0
						IF CHECK_CLO("ブルマ露出") || CHECK_CLO("スパッツ露出")
							PRINTFORMW 裾の合わせ目からチラチラ見える%OUTER(TARGET, "略")%が%CALLNAME:MASTER%の目を奪います…
						ELSEIF CHECK_CLO("パンツ露出")
							PRINTFORMW 裾の合わせ目からチラチラ見える%PANTIES(TARGET, "略")%が%CALLNAME:MASTER%の目を奪います…
						;アソコ丸見えorパンストのみ
						ELSEIF CHECK_CLO("Ｖ露出")
							PRINTFORM 裾の合わせ目からチラチラ見える
							IF COND("陰毛") >= 5
								PRINT 茂み
							ELSEIF COND("陰毛") == 4
								PRINT 剃り跡
							ELSE
								IF COND("陰毛") && PENIS(TARGET) == 0 && RAND:2 == 0
									PRINT 無毛のアソコ
								ELSE
									PRINT 股間
								ENDIF
							ENDIF
							PRINTFORMW が%CALLNAME:MASTER%の目を奪います…
						ENDIF
					ELSEIF TALENT:恥じらい || RAND:5 == 0
						IF RAND:2 == 0
							PRINTFORMW 長めの裾はスカートとしては物足りないようで、恥ずかしそうに脚をもじもじしています…
						ELSE
							PRINTFORMW 長めの裾はスカートとしては物足りないようで、ぎゅっと裾を掴んで下に引き伸ばしています…
						ENDIF
					ENDIF
				ENDIF
			ELSE
				PRINTFORMW %SHIRT(TARGET)%を着てもらいました
			ENDIF
		ENDSELECT
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;パンツとブラに関する関数
;-------------------------------------------------
@SELECT_PANTU_BRA
#DIM LCOUNT
#DIM CHOICE
#DIM NEXT_LINE
#DIM MEMO_LINECOUNT
#DIMS MEMO_PANTU
#DIMS MEMO_BRA
#DIMS COLOR_TABLE
#DIMS COLORS, 20

MEMO_LINECOUNT = LINECOUNT

MEMO_PANTU = %CSTR:パンツ%
MEMO_BRA = %CSTR:ブラ%


PRINTL 
CALL PRINT_MAIDFACE, TARGET
CALL PRINT_STRL, @"どれに着替えてもらいますか？身に着けている物は_黄色_色が変わり_、選択する事で外せます。"
PRINTFORM 現在:
CALL PRINT_DRESSL, TARGET, "拡大表示"
PRINTL 

PRINTFORML ・パンツ
NEXT_LINE = 0
FOR LCOUNT, 0, 40
	SELECTCASE NAME_PANTIES(LCOUNT)
	CASE ""
		CONTINUE
	CASEELSE
		SIF PANTIES(TARGET) == NAME_PANTIES(LCOUNT)
			SETCOLOR DEF_COLOR("黄色")
		PRINTFORM  [{LCOUNT, 2}] %TEXT_LJ(NAME_PANTIES(LCOUNT), 22)%
		RESETCOLOR

		NEXT_LINE += 1
		SIF NEXT_LINE%3 == 0
			PRINTL 
	ENDSELECT
NEXT
SIF NEXT_LINE%3
	PRINTL 

PRINTL 
PRINTFORML ・ブラ
NEXT_LINE = 0
FOR LCOUNT, 0, 40
	SELECTCASE NAME_BRA(LCOUNT)
	CASE ""
		CONTINUE
	CASEELSE
		SIF BRA(TARGET) == NAME_BRA(LCOUNT)
			SETCOLOR DEF_COLOR("黄色")
		PRINTFORM  [{LCOUNT+40, 2}] %TEXT_LJ(NAME_BRA(LCOUNT), 22)%
		RESETCOLOR

		NEXT_LINE += 1
		SIF NEXT_LINE%3 == 0
			PRINTL 
	ENDSELECT
NEXT
SIF NEXT_LINE%3
	PRINTL 
PRINTL 

PRINTFORM ・下着の色指定
IF SAVESTR:下着の指定色 != ""
	PRINTFORM  … 現在は
	SETCOLOR DEF_COLOR("黄色")
	PRINTFORM %SAVESTR:下着の指定色%
	RESETCOLOR
ENDIF
PRINTL 
PRINTFORM  [80]無し　

COLOR_TABLE = 黒/紫/真紅/赤/薄桃色/緑色/青/黄色/紺色/薄緑/橙色/薄青色/白/純白
VARSET COLORS
SPLIT COLOR_TABLE, "/", COLORS

NEXT_LINE = 1
FOR LCOUNT, 0, 20
	SELECTCASE COLORS:LCOUNT
	CASE ""
		PRINTL 
		BREAK
	CASE SAVESTR:下着の指定色
		SETCOLOR DEF_COLOR("黄色")
	ENDSELECT
	PRINTFORM  [{81 + LCOUNT}]%TEXT_LJ(COLORS:LCOUNT, 6)%
	RESETCOLOR

	NEXT_LINE += 1
	SIF NEXT_LINE%5 == 0
		PRINTL 
NEXT
SIF NEXT_LINE%5
	PRINTL 

PRINTFORML  [96] \@ CONFIG("ブラを合わせる") ? ○ # － \@ パンツを選択した際にブラも自動で選ぶ

IF EQUIP:パンツ
	PRINTFORML  [97] %PANTIES(TARGET)%を脱いでもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] パンツを脱いでもらう"
ENDIF
IF EQUIP:ブラ
	PRINTFORML  [98] %BRA(TARGET)%を脱いでもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] ブラを脱いでもらう"
ENDIF
IF EQUIP:パンツ && EQUIP:ブラ
	PRINTFORML  [99] 上下とも脱いでもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] 上下とも脱いでもらう"
ENDIF
PRINTFORML [100] 戻る

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	RETURN 0
ELSEIF CHOICE == 80
	SAVESTR:下着の指定色 = 
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ELSEIF CHOICE >= 81 && CHOICE <= 94
	SAVESTR:下着の指定色 = %COLORS:(CHOICE - 81)%
	CALL EQ_CLOCOLOR, TARGET, SAVESTR:下着の指定色
ELSEIF CHOICE == 96
	INVERTBIT FLAG:着せ替え, 3
ELSEIF CHOICE == 97 && EQUIP:パンツ
	CALL SET_PANTIES, ""
ELSEIF CHOICE == 98 && EQUIP:ブラ
	CALL SET_BRA, ""
ELSEIF CHOICE == 99 && EQUIP:パンツ && EQUIP:ブラ
	CALL SET_PANTIES, ""
	CALL SET_BRA, ""
ELSEIF CHOICE >= 0 && CHOICE <= 39 && NAME_PANTIES(CHOICE) != ""
	IF PANTIES(TARGET) == NAME_PANTIES(CHOICE)
		CALL SET_PANTIES, ""
	ELSE
		CALL SET_PANTIES, NAME_PANTIES(CHOICE)
		SIF CONFIG("ブラを合わせる")
			CALL COORDINATE_BRA, TARGET

		IF CHECK_PANTIES("Ｃストリング")
			PRINTFORML …どうせなら、更にエロチックなものを選びますか？
			PRINTFORML [ 0] シースルー！
			PRINTFORML [ 1] 穴開き！
			PRINTFORML [ 2] いや、別に普通ので良い
			CALL INPUT_SELECT, 3
			PRINTFORMW %DETAIL_PANTIES(TARGET)%を付けてもらいました。
			IF RESULT == 0
				CSTR:パンツ += "シースルー/"
				PRINTFORMW 生地がスケスケなので、模様越しに%CALLNAME:TARGET%の大事な部分が見えてとても扇情的です。
			ELSEIF RESULT == 1
				CSTR:パンツ += "Ｖ露出/"
				PRINTFORMW 穴が開いているおかげで、下着というよりも性器がここに有ると強調するための物の様です。
			ENDIF
		ELSEIF CHECK_PANTIES("前貼り")
			PRINTFORMW %CALLNAME:TARGET%のアソコに前貼りを貼り付けました。
		ELSE
			PRINTFORMW %DETAIL_PANTIES(TARGET)%を穿いてもらいました。
		ENDIF
	ENDIF
ELSEIF CHOICE >= 40 && CHOICE <= 79 && NAME_BRA(CHOICE - 40) != ""
	IF BRA(TARGET) == NAME_BRA(CHOICE - 40)
		CALL SET_BRA, ""
	ELSE
		CALL SET_BRA, NAME_BRA(CHOICE - 40)
		IF CHECK_BRA("タンクトップ")
			PRINTFORMW 活動的なタンクトップを着てもらいました。
		ELSEIF CHECK_BRA("さらし")
			PRINTFORMW 胸にさらしを巻いてもらいました。
		ELSEIF CHECK_BRA("キャミソール")
			PRINTFORMW 可愛らしいキャミソールを着てもらいました。
		ELSE
			PRINTFORMW %DETAIL_BRA(TARGET)%を着けてもらいました。
		ENDIF
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

;拘束などされていたり、パンツはいてないならここまで
IF TEQUIP:アイマスク || TEQUIP:拘束 || TEQUIP:口枷 || TEQUIP:室内露出プレイ || TEQUIP:野外プレイ
;スカート かつ 拘束などされてない かつ 陽気or恋慕or小悪魔 かつ 反発0LV
ELSEIF EQUIP:スカート && EQUIP:パンツ && MEMO_PANTU != CSTR:パンツ && TALENT:陽気 + TALENT:恋慕 + TALENT:小悪魔 && CHECK_PANTIES("男物") == 0 && RAND:3 == 0
	PRINTFORM %CALLNAME:TARGET%は
	;淫乱or欲情1000以上
	IF (TALENT:淫乱 && PALAM:欲情 >= 1000) && RAND:4
		PRINT 妖艶に微笑みながら
	ELSEIF RAND:3 == 0
		SIF RAND:2 == 0
			PRINT 楽しそうに
		PRINT 微笑みながら
	ELSEIF TALENT:高慢 + TALENT:淫乱 + ABL:サドっ気 >= 3 && RAND:2 == 0
		PRINT 不敵な笑みを浮かべながら
	;小悪魔
	ELSEIF TALENT:小悪魔 && RAND:2 == 0
		PRINT ニヤニヤしながら
	ELSEIF TALENT:恥じらい
		PRINT 頬を染めながら
	ENDIF
	IF TALENT:好色 + TALENT:淫乱 && RAND:2 == 0
		PRINT 大胆に
	ELSEIF RAND:3
		PRINT ゆっくりと
	ENDIF
	PRINTFORM %SKIRT(TARGET, "略")%をたくし上げて、
	IF CHECK_CLO("水着姿")
		PRINT 水着
	ELSE
		SIF PALAM:Ｖ潤 >= 3000 && PENIS(TARGET) == 0 && RAND:2 == 0
			PRINT しっとりと汗ばんだ
		PRINT パンツ
	ENDIF
	PRINTW のフィット具合を見せ付けてきた。
ENDIF

;ぱんつを履き替えた
IF EQUIP:パンツ && MEMO_PANTU != CSTR:パンツ && COND("脚の使用") && PENIS(TARGET) == 0 && RANDIF(1, EQUIP:ブラ && MEMO_BRA != CSTR:ブラ)
	CALL SHOW_PUBICHAIR
;ブラを着けかえた
ELSEIF EQUIP:ブラ && MEMO_BRA != CSTR:ブラ
	CALL SHOW_NYUURIN
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;ブラ着用後の詳細描写。主に形状と材質で言及
@SHOW_NYUURIN
IF CHECK_BRA("オープンブラ") || CHECK_BRA("シェルフブラ") || CHECK_BRA("ハーフカップ") || CHECK_BRA("シースルー")
	IF CHECK_BRA("シースルー")
		PRINTFORM %TEXTR("生地越しに透けて見える/うっすらと透ける/儚い生地越しに透ける")%
	ELSE
		PRINTFORM %TEXTR("丸見えの/力強く主張する/恥ずかしげも無く晒されている")%
	ENDIF
	IF CALCF("Ｂ発情")
		IF COND("乳首肥大") && RANDIF(1, TALENT:ニプルピアス)
			IF COND("乳首肥大") >= 2
				PRINTFORM 肥大しきった
			ELSE
				PRINTFORM 大きな
			ENDIF
		ELSEIF TALENT:ニプルピアス
			PRINTFORM ピアスで飾られた
		ENDIF
	ELSE
		PRINTFORM 陥没
	ENDIF
	PRINTFORM 乳首と
	SELECTCASE COND("巨乳輪")
	CASE IS >= 2
		PRINTFORM 大きすぎる
	CASE 1
		PRINTFORM 大きな
	ENDSELECT
	PRINTFORMW 乳輪がなんとも%TEXTR("淫靡です/興奮を誘う/お洒落です")%…
ELSEIF CHECK_BRA("マイクロ")
	IF CALCF("Ｂ発情") && COND("乳首肥大") && RANDIF(1, COND("巨乳輪") )
		PRINTFORMW 密着度の高い生地のため、下で主張している乳首の大きさを隠すことができないどころか逆に強調されてしまっている…
	ELSEIF COND("巨乳輪")
		PRINTFORM 乳首はかろうじて隠せているものの、
		SELECTCASE COND("巨乳輪")
		CASE IS >= 2
			PRINTFORM 恥ずかしいほどに大きな
		CASE 1
			PRINTFORM 平均よりもかなり大きめの
		ENDSELECT
		PRINTFORMW 乳輪はその大半を晒してしまっている…
	ENDIF
ENDIF

;途中
@SHOW_PUBICHAIR
SIF TALENT:オトコ
	RETURN 0
;アソコ穴開きでの分岐はとりあえず無し
;生え放題
SELECTCASE COND("陰毛")
CASE 7
	IF CHECK_CLO("食い込み")
		IF RAND:3 == 0
			;マイクロ
			IF CHECK_CLO("パンツ露出") && RAND:2
				IF CHECK_PANTIES("マイクロ")
					PRINTW 股間の小さな生地の全方位からは、盛大に陰毛がはみ出している…
				ELSE
					PRINTW 小さな布地からは、大分陰毛がはみ出ている…
				ENDIF
			ELSEIF RAND:2 == 0
				PRINTW 股間に食い込む細い布地の脇からは、盛大に陰毛がはみ出している…
			ELSE
				PRINTW 激しく切れ上がった股の脇からは、盛大に陰毛がはみ出している…
			ENDIF
		ELSEIF RAND:3 == 0
			PRINTW はみ出している陰毛がなんとも興奮を誘う…
		ELSEIF RAND:2 == 0
			PRINTW 処理されていない陰毛がはみ出ているのがなんとも淫靡だ…
		ELSE
			PRINTW 処理されていないので、陰毛が大分はみ出ている…
		ENDIF
	ELSEIF CHECK_CLO("パンツ露出") == 0
	ELSEIF CHECK_PANTIES("シースルー")
		PRINTW うっすらと透けて見える剛毛がなんとも興奮を誘う…
	ELSEIF RAND:2 == 0
		PRINTFORMW %PANTIES(TARGET, "略")%の上のほうからは、処理されていない剛毛が見えてしまっている…
	ELSE
		PRINTFORMW 下の毛を手入れしていないおかげで、%PANTIES(TARGET, "略")%の端々から陰毛がハミ出て見えてしまっている…
	ENDIF
;ナチュラル
CASE 6
	IF CHECK_CLO("パンツ露出") == 0
	ELSEIF CHECK_CLO("食い込み")
		IF RAND:3 == 0
			IF CHECK_CLO("パンツ露出") && RAND:2
				IF CHECK_PANTIES("マイクロ")
					PRINTW 股間の小さな生地からは、大分陰毛がはみ出している…
				ELSE
					PRINTW 小さな布地からは、だらしなく陰毛がはみ出ている…
				ENDIF
			ELSEIF RAND:2 == 0
				PRINTW 股間に食い込む布地の脇からは、僅かに陰毛がはみ出している…
			ELSE
				PRINTW 激しく切れ上がった股の脇からは、僅かに陰毛がはみ出している…
			ENDIF
		ELSEIF RAND:3 == 0
			PRINTW だらしなくはみ出た陰毛がなんとも興奮を誘う…
		ELSEIF RAND:2 == 0
			PRINTW 処理されていない陰毛がはみ出ているのがなんとも淫靡だ…
		ELSE
			PRINTW 処理されていないので、陰毛が見えてしまっている…
		ENDIF
	ELSEIF CHECK_CLO("パンツ露出") == 0
	ELSEIF CHECK_PANTIES("シースルー")
		PRINTW うっすらと透けて見える柔らかそうな毛がなんとも興奮を誘う…
	ELSEIF CHECK_PANTIES("ローライズ")
		PRINTW パンツの上のほうからは、柔らかそうな毛が見えてしまっている…
	ENDIF
;お手入れ済
CASE 5
	IF CHECK_CLO("パンツ露出") == 0
	ELSEIF CHECK_CLO("食い込み") 
		IF CHECK_PANTIES("ローレグ")
			PRINTW パンツの上のほうからは、陰毛どころか、縦スジが見えてしまっている…
		ELSE
			PRINTW 股間の小さな生地からは、微かに陰毛がはみ出している…
		ENDIF
	ELSEIF CHECK_PANTIES("シースルー")
		PRINTW うっすらと透けて見える柔らかそうな毛がなんとも興奮を誘う…
	ELSEIF CHECK_PANTIES("ローライズ")
		PRINTW パンツの上のほうからは、お手入れされた陰毛が見えてしまっている…
	ENDIF
;生えかけ
CASE 4
	IF CHECK_CLO("パンツ露出") == 0
	ELSEIF CHECK_CLO("食い込み")
		IF CHECK_PANTIES("ローレグ")
			PRINTW パンツの上のほうからは、縦スジが見えてしまっている…
		ELSE
			PRINTW 股間の小さな生地からは、微かに陰毛がはみ出している…
		ENDIF
	ELSEIF CHECK_PANTIES("シースルー")
		PRINTW うっすらと透けて見える柔らかそうな毛がなんとも興奮を誘う…
	ELSEIF CHECK_PANTIES("ローライズ")
		PRINTW パンツの上のほうからは、柔らかそうな丘が見えてしまっている…
	ENDIF
;無毛
CASE 1 TO 3
	IF CHECK_CLO("パンツ露出") == 0
	ELSEIF CHECK_PANTIES("マイクロ")
		PRINTW 陰毛が全く生えてないので、少しずれただけで陰唇が見えてしまいそうだ…
	ELSEIF CHECK_PANTIES("シースルー")
		PRINTW うっすら透ける股間の縦スジが、なんとも魅力的だ…
	ELSEIF CHECK_PANTIES("ローライズ")
		PRINTW パンツの上のほうでは、柔らかそうな丘が見えてしまっている…
	ENDIF
ENDSELECT

;-------------------------------------------------
;ボディスーツとアウターに関する関数。脚が無い人は選べない
;-------------------------------------------------
@SELECT_BODYSUIT_OUTER
#DIM LCOUNT
#DIM CHOICE
#DIM NEXT_LINE
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT

PRINTL 
CALL PRINT_MAIDFACE, TARGET
CALL PRINT_STRL, @"どれに着替えてもらいますか？身に着けている物は_黄色_色が変わり_、選択する事で外せます。"
PRINTFORM 現在:
CALL PRINT_DRESSL, TARGET, "拡大表示"
PRINTL 

PRINTFORML ・ボディスーツ、ワンピース水着
NEXT_LINE = 0
FOR LCOUNT, 0, 20
	SELECTCASE NAME_BODYS(LCOUNT)
	CASE ""
		CONTINUE
	CASEELSE
		SIF BODYS(TARGET) == NAME_BODYS(LCOUNT)
			SETCOLOR DEF_COLOR("黄色")
		PRINTFORM  [{LCOUNT, 2}] %TEXT_LJ(NAME_BODYS(LCOUNT), 20)%
		RESETCOLOR

		NEXT_LINE += 1
		SIF NEXT_LINE%3 == 0
			PRINTL 
	ENDSELECT
NEXT
SIF NEXT_LINE%3
	PRINTL 
PRINTL 

PRINTFORML ・ブルマ、ズボン、パンストなど
NEXT_LINE = 0
FOR LCOUNT, 0, 20
	SELECTCASE NAME_OUTER(LCOUNT)
	CASE ""
		CONTINUE
	CASEELSE
		SIF OUTER(TARGET) == NAME_OUTER(LCOUNT)
			SETCOLOR DEF_COLOR("黄色")
		PRINTFORM  [{LCOUNT+20, 2}] %TEXT_LJ(NAME_OUTER(LCOUNT), 20)%
		RESETCOLOR

		NEXT_LINE += 1
		SIF NEXT_LINE%3 == 0
			PRINTL 
	ENDSELECT
NEXT
SIF NEXT_LINE%3
	PRINTL 
PRINTL 

IF EQUIP:ボディスーツ
	PRINTFORML  [90] %BODYS(TARGET)%を脱いでもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] 靴下を脱いでもらう"
ENDIF
IF EQUIP:アウター
	PRINTFORML  [91] %OUTER(TARGET)%を脱いでもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] 靴を脱いでもらう"
ENDIF

PRINTFORML [100] 戻る

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	RETURN 0
ELSEIF CHOICE == 90 && EQUIP:ボディスーツ
	CALL SET_BODYS, ""
ELSEIF CHOICE == 91 && EQUIP:アウター
	CALL SET_OUTER, ""
ELSEIF CHOICE >= 0 && CHOICE <= 19 && NAME_BODYS(CHOICE) != "" && COND("脚の使用")
	IF BODYS(TARGET) == NAME_BODYS(CHOICE)
		CALL SET_BODYS, ""
	ELSE
		CALL SET_SHIRT, ""
		CALL SET_BODYS, NAME_BODYS(CHOICE)
		SELECTCASE NAME_BODYS(CHOICE)
		CASE "レオタード"
			PRINTFORMW 体のラインを際立たせる、薄手のレオタードに着替えてもらいました
		CASE "セクシーレオタード"
			PRINTFORMW 伸縮性のある生地が際どいカットで股間に食い込むハイレグのレオタードに着替えてもらいました
		CASE "エナメルレオタード"
			PRINTFORMW エナメルの質感がなんとも淫靡な、股間にファスナーの隠されたＴバックのレオタードに着替えてもらいました
		CASE "バニースーツ"
			PRINTFORML 実は股間にファスナーなんて用意されてたりは……
			PRINTFORML [ 0] します
			PRINTFORML [ 1] まさか！
			CALL INPUT_SELECT, 2
			SIF RESULT == 0
				CSTR:ボディスーツ += "ファスナー/"
			IF CHECK_BODYS("黒")
				PRINTFORM 可愛らしいタイツ生地の
			ELSEIF CHECK_BODYS("白")
				PRINTFORM 清楚な白に対して艶めかしいギャップを感じさせる
			ELSE
				PRINTFORM 真っ赤な
			ENDIF
			PRINTFORMW バニースーツに着替えてもらいました
			SIF CHECK_BODYS("ファスナー")
				PRINTFORMW しかも、股間には何の用途に使うのかファスナーがついています！
		CASE "ハイレグバニー"
			PRINTFORMW ちょっとエッチなハイレグのバニースーツに着替えてもらいました
		CASE "ボンデージ"
			PRINTFORMW \@ PENIS(TARGET) == 0 ? 乳房が丸出しの上に # \@股間にファスナーの付いたボンデージに着替えてもらいました
		CASE "スク水"
			PRINTFORMW 旧型のスク水を着てもらいました
		CASE "ワンピース水着"
			PRINTFORMW 清楚な白いワンピースタイプの水着を着てもらいました
		CASE "ハイレグ水着"
			PRINTFORMW 股間の食い込みが凄い、ハイレグ＆Ｔバック仕様のワンピース水着を着てもらいました
		CASE "競泳水着"
			PRINTFORMW 程よいハイレグカットで泳ぎやすそうな競泳水着を着てもらいました
		CASEELSE
			PRINTFORMW %BODYS(TARGET)%を着てもらいました
		ENDSELECT
		IF CHECK_BODYS("ハイレグ") && RAND:2
			PRINTFORMW 惜しげもなく晒されている鼠径部が、なんとも素晴らしい眺めです
		ELSE
			CALL SHOW_PUBICHAIR
		ENDIF
	ENDIF
ELSEIF CHOICE >= 20 && CHOICE <= 39 && NAME_OUTER(CHOICE - 20) != "" && COND("脚の使用")
	IF OUTER(TARGET) == NAME_OUTER(CHOICE - 20)
		CALL SET_OUTER, ""
	ELSE
		CALL SET_OUTER, NAME_OUTER(CHOICE - 20)
		SELECTCASE NAME_OUTER(CHOICE - 20)
		CASE "ブルマ"
			PRINTFORMW 脚線美を際立たせるブルマをはいてもらいました
		;CASE "ハイレグブルマ"
		;	PRINTFORMW 食い込んだ股間とお尻が可愛いハイレグブルマをはいてもらいました
		CASE "スパッツ"
			PRINTFORMW 太ももから腰にかけてを絶妙に締め付けるスパッツをはいてもらいました
			IF CHECK_CLO("アウター直穿き")
				IF RAND:2 == 0
					PRINTFORMW 柔らかそうな股間のふくらみがなんとも魅力的です…
				ELSE
					PRINTFORMW 食い込みぎみの股間がなんとも魅力的です…
				ENDIF
			ELSEIF EQUIP:パンツ
				PRINTFORMW 薄手なので、%PANTIES(TARGET, "略")%のラインが浮き出ています…
			ENDIF
		CASE "ホットパンツ"
			PRINTFORMW ピチピチのホットパンツをはいてもらいました
		CASE "レギンス"
			PRINTFORMW 露出した足首がお洒落なレギンスをはいてもらいました

		CASE "パンスト"
			PRINTFORMW 下半身のラインを整える薄手のパンストをはいてもらいました
		CASE "網パンスト"
			PRINTFORMW 網目がなんともセクシーなパンストをはいてもらいました
		CASE "オープンパンスト"
			PRINTFORMW 股間部分だけが大胆に開かれた、セクシーなパンストをはいてもらいました
		CASEELSE
			PRINTFORMW %OUTER(TARGET)%をはいてもらいました
		ENDSELECT
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART


;-------------------------------------------------
;靴下、靴、手袋に関する関数
;-------------------------------------------------
@SELECT_SOCKS_GLOVE_SHOES
#DIM LCOUNT
#DIM CHOICE
#DIM NEXT_LINE
#DIM MEMO_LINECOUNT

MEMO_LINECOUNT = LINECOUNT

PRINTL 
CALL PRINT_MAIDFACE, TARGET
CALL PRINT_STRL, @"どれに着替えてもらいますか？身に着けている物は_黄色_色が変わり_、選択する事で外せます。"
PRINTFORM 現在:
CALL PRINT_DRESSL, TARGET, "拡大表示"
PRINTL 

IF COND("脚の使用")
	PRINTFORML ・靴下
	NEXT_LINE = 0
	FOR LCOUNT, 0, 20
		SELECTCASE NAME_SOCKS(LCOUNT)
		CASE ""
			CONTINUE
		CASEELSE
			SIF SOCKS(TARGET) == NAME_SOCKS(LCOUNT)
				SETCOLOR DEF_COLOR("黄色")
			PRINTFORM  [{LCOUNT, 2}] %TEXT_LJ(NAME_SOCKS(LCOUNT), 20)%
			RESETCOLOR

			NEXT_LINE += 1
			SIF NEXT_LINE%3 == 0
				PRINTL 
		ENDSELECT
	NEXT
	SIF NEXT_LINE%3
		PRINTL 
	PRINTL 

	PRINTFORML ・靴
	NEXT_LINE = 0
	FOR LCOUNT, 0, 20
		SELECTCASE NAME_SHOES(LCOUNT)
		CASE ""
			CONTINUE
		CASEELSE
			SIF SHOES(TARGET) == NAME_SHOES(LCOUNT)
				SETCOLOR DEF_COLOR("黄色")
			PRINTFORM  [{LCOUNT+20, 2}] %TEXT_LJ(NAME_SHOES(LCOUNT), 20)%
			RESETCOLOR

			NEXT_LINE += 1
			SIF NEXT_LINE%3 == 0
				PRINTL 
		ENDSELECT
	NEXT
	SIF NEXT_LINE%3
		PRINTL 
	PRINTL 
ENDIF

PRINTFORML ・手袋
NEXT_LINE = 0
FOR LCOUNT, 0, 20
	SELECTCASE NAME_GLOVE(LCOUNT)
	CASE ""
		CONTINUE
	CASEELSE
		SIF GLOVE(TARGET) == NAME_GLOVE(LCOUNT)
			SETCOLOR DEF_COLOR("黄色")
		PRINTFORM  [{LCOUNT+40, 2}] %TEXT_LJ(NAME_GLOVE(LCOUNT), 20)%
		RESETCOLOR

		NEXT_LINE += 1
		SIF NEXT_LINE%3 == 0
			PRINTL 
	ENDSELECT
NEXT
SIF NEXT_LINE%3
	PRINTL 
PRINTL 

IF COND("脚の使用")
	IF EQUIP:靴下
		PRINTFORML  [90] %SOCKS(TARGET)%を脱いでもらう
	ELSE
		CALL PRINT_STRL, "暗灰色_ [×] 靴下を脱いでもらう"
	ENDIF
	IF EQUIP:靴
		PRINTFORML  [91] %SHOES(TARGET)%を脱いでもらう
	ELSE
		CALL PRINT_STRL, "暗灰色_ [×] 靴を脱いでもらう"
	ENDIF
ENDIF

IF EQUIP:手袋
	PRINTFORML  [92] %GLOVE(TARGET)%を脱いでもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] 手袋を脱いでもらう"
ENDIF
PRINTFORML [100] 戻る

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	RETURN 0
ELSEIF CHOICE == 90 && EQUIP:靴下
	CALL SET_SOCKS, ""
ELSEIF CHOICE == 91 && EQUIP:靴
	CALL SET_SHOES, ""
ELSEIF CHOICE == 92 && EQUIP:手袋
	CALL SET_GLOVE, ""
ELSEIF CHOICE >= 0 && CHOICE <= 19 && NAME_SOCKS(CHOICE) != "" && COND("脚の使用")
	IF SOCKS(TARGET) == NAME_SOCKS(CHOICE)
		CALL SET_SOCKS, ""
	ELSE
		CALL SET_SOCKS, NAME_SOCKS(CHOICE)
		SELECTCASE NAME_SOCKS(CHOICE)
		CASE "ソックス"
			PRINTFORMW 靴下をはいてもらいました
		CASE "ハイソックス"
			PRINTFORMW ハイソックスをはいてもらいました
		CASE "ニーソックス"
			PRINTFORMW 絶対領域が素晴らしいニーソをはいてもらいました
		CASE "サイハイソックス"
			PRINTFORMW 太もも中央まで覆うサイハイソックスをはいてもらいました
		CASE "ストッキング"
			PRINTFORMW 途中まで覆われた太ももがなんとも魅力的なストッキングをはいてもらいました
		CASE "網ストッキング"
			PRINTFORMW 網目がなんともセクシーなストッキングをはいてもらいました
		CASE "ガーターストッキング"
			PRINTFORMW 華やかさと淫靡さが融合したガーターベルトで吊られたストッキングをはいてもらいました
		CASE "ガーターニーソ"
			PRINTFORMW 可愛らしいガーターベルトで吊られたニーソックスをはいてもらいました
		CASE "足袋"
			PRINTFORMW 穢れなき真っ白な足袋をはいてもらいました
		CASE "フリルニーソ"
			PRINTFORMW フリルがあしらわれたニーソックスをはいてもらいました
		CASEELSE
			PRINTFORMW %SOCKS(TARGET)%をはいてもらいました
		ENDSELECT
	ENDIF
ELSEIF CHOICE >= 20 && CHOICE <= 39 && NAME_SHOES(CHOICE - 20) != "" && COND("脚の使用")
	IF SHOES(TARGET) == NAME_SHOES(CHOICE - 20)
		CALL SET_SHOES, ""
	ELSE
		CALL SET_SHOES, NAME_SHOES(CHOICE - 20)
		PRINTFORMW %SHOES(TARGET)%を履いてもらいました
	ENDIF
ELSEIF CHOICE >= 40 && CHOICE <= 59 && NAME_GLOVE(CHOICE - 40) != ""
	IF GLOVE(TARGET) == NAME_GLOVE(CHOICE - 40)
		CALL SET_GLOVE, ""
	ELSE
		CALL SET_GLOVE, NAME_GLOVE(CHOICE - 40)
		SELECTCASE NAME_GLOVE(CHOICE - 40)
		CASE "シルクグローブ"
			PRINTFORMW 肌触りの素晴らしい絹で出来た手袋をはめてもらいました
		CASE "ドレスグローブ"
			PRINTFORMW 二の腕の半ばまで覆う、センスの良い長手袋をはめてもらいました
		CASE "レザースリーブ"
			PRINTFORMW 所々に鋲の打たれた、革製の長手袋をはめてもらいました
		CASE "ドレススリーブ"
			PRINTFORMW 手首から二の腕まで覆う、装飾の素晴らしいドレススリーブをつけてもらいました
		CASEELSE
			PRINTFORMW %GLOVE(TARGET)%をつけてもらいました
		ENDSELECT
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

;-------------------------------------------------
;メガネなどその他小物に関する関数
;-------------------------------------------------
@SELECT_OTHER
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE
#DIM NEXT_LINE

MEMO_LINECOUNT = LINECOUNT

PRINTL 
CALL PRINT_MAIDFACE, TARGET
CALL PRINT_STRL, @"身に着けている物は_黄色_色が変わり_、選択する事で外せます。\@ CONFIG("顔グラＯＮ") ? ものによっては顔グラに変化が起きます。 # \@"
PRINTFORM 現在:

VARSET LOCALS
IF EQUIP:アクセサリ
	SPLIT CSTR:アクセサリ, "/", LOCALS
	FOR LCOUNT, 0, 10
		SIF LOCALS:LCOUNT != ""
			PRINTFORM [%LOCALS:LCOUNT%]
	NEXT
	PRINTL 
ELSE
	PRINTFORML 無し
ENDIF
PRINTL 

NEXT_LINE = 0
FOR LCOUNT, 0, 90
	SELECTCASE NAME_OTHER(LCOUNT)
	CASE ""
		CONTINUE
	CASEELSE
		SELECTCASE LCOUNT
		CASE 20, 30, 50, 60, 70
			SIF NEXT_LINE%3
				PRINTL 
			NEXT_LINE = 0
		ENDSELECT
		SELECTCASE LCOUNT
		CASE 0
			PRINTFORML ・首周りのアクセサリ
		CASE 20
			PRINTFORML ・眼鏡
		CASE 30
			PRINTFORML ・帽子
		CASE 50
			PRINTFORML ・つけ耳
		CASE 60
			PRINTFORML ・髪飾り
		CASE 70
			PRINTFORML ・外套、その他
		ENDSELECT

		SIF STRCOUNT(@"\/%CSTR:アクセサリ%", @"\/%NAME_OTHER(LCOUNT)%\/")
			SETCOLOR DEF_COLOR("黄色")
		PRINTFORM  [{LCOUNT, 2}] %TEXT_LJ(NAME_OTHER(LCOUNT), 20)%
		RESETCOLOR

		NEXT_LINE += 1
		SIF NEXT_LINE%3 == 0
			PRINTL 
	ENDSELECT
NEXT
PRINTL 
IF EQUIP:アクセサリ
	PRINTFORML  [99] すべて外してもらう
ELSE
	CALL PRINT_STRL, "暗灰色_ [×] すべて外してもらう"
ENDIF
PRINTFORML [100] 戻る

$INPUT_LOOP
INPUT

CHOICE = RESULT

IF CHOICE == 100
	RETURN 0
ELSEIF CHOICE == 99 && EQUIP:アクセサリ
	CALL SET_OTHER, ""
ELSEIF NAME_OTHER(CHOICE) != ""
	;すでに身に着けているものと完全一致したものを選んだら外す
	CALL INVERTSET_OTHER, NAME_OTHER(CHOICE)
ELSE
	GOTO INPUT_LOOP
ENDIF

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

