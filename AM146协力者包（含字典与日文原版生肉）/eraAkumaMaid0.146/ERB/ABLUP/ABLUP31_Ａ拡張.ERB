;-------------------------------------------------
;ABL:Ａ拡張 のLVUP処理
;-------------------------------------------------
@ABLUP31
#DIM LCOUNT
#DIM NUM_CHOICES
#DIM SET_CHOICE
#DIM NUM_JUEL, 10

SIF CONFIG("ＶＡ拡張の手動ＬＶＵＰ") == 0
	RETURN 0

;必要な珠などの値を取得
CALL IS_ABLUP31
;何もできない場合
IF RESULT == 0 && ABL:Ａ拡張 >= 99
	PRINTFORMW 既にMAXです。
	RETURN 0
ENDIF

DRAWLINE

PRINTFORML %CALLNAME:TARGET%の現在のＡ拡張は{ABL:Ａ拡張}LVでＡ名器度は{ABL_A(TARGET, "絶対値")}です
PRINTFORML Ａ拡張LVを上昇させますか？（注意：相対的には名器度は減る事となります）
PRINTFORML 
;ＬＶＵＰ可能
IF CALCF("Ａ拡張の上限", TARGET) > ABL:Ａ拡張
	ABL:Ａ拡張 += 1
	PRINTFORML  [ 0] Ａ拡張LVを{ABL:Ａ拡張}に上げる（レベルを上げた後のＡ名器度は{ABL_A(TARGET, "絶対値")}です）
	ABL:Ａ拡張 -= 1
ELSE
	SETCOLOR DEF_COLOR("灰色")
	PRINTFORML  [×] Ａ拡張LVを{ABL:Ａ拡張 + 1}に上げる ……Ａ拡張経験や素質が不足しているようです
	RESETCOLOR
ENDIF

PRINTFORML [100] - やめる

CALL INPUT_SELECT, 1, 100

SELECTCASE RESULT
CASE 100
	RETURN 0
CASEELSE
	IF CALCF("Ａ拡張の上限", TARGET) > ABL:Ａ拡張
		ABL:Ａ拡張 += 1
		CALL GET_EXABL2, TARGET
		PRINTFORMW %CALLNAME:TARGET%のＡ拡張LVが{ABL:Ａ拡張}になり、名器度は{ABL_A(TARGET, "絶対値")}となりました。
	ENDIF
ENDSELECT

;まだレベルが上げられたりする場合には最初に戻る
CALL IS_ABLUP31
SIF RESULT
	RESTART



;ABL:Ａ拡張のLVUPに必要な条件を算出し、LVUP出来るなら1を返す
@IS_ABLUP31, ARGS
SIF CONFIG("ＶＡ拡張の手動ＬＶＵＰ") == 0
	RETURN 0
;最大LV未満か？
SIF ABL:Ａ拡張 >= 99
	RETURN 0
;条件のどれかをクリアしてる？
SIF CALCF("Ａ拡張の上限", TARGET) > ABL:Ａ拡張
	RETURN 1

RETURN 0
