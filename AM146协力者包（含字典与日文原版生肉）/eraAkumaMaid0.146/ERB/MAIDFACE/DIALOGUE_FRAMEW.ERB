;-------------------------------------------------
;STR毎にWAITするDIALOGUE_FRAME
;STRが空だった場合はその行は止まらないので間を作りたいなら空白文字でも入れといてください
;ARGはTARGETとかARGSに初期の顔グラ変化
;STR内で文章の後に"#F"で区切って顔グラを変化させることができる
;例）STR:1 = こんにちは#Fにっこり
;-------------------------------------------------
@DIALOGUE_FRAMEW, ARG, ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC MEMO_LINECOUNT
#DIMS DYNAMIC MEMO_EMOTION
#DIMS DYNAMIC MEMO_STR, 20

MEMO_LINECOUNT = LINECOUNT

;STRの上書き
FOR LCOUNT, 1, 10
	SIF ARGS:LCOUNT != ""
		STR:LCOUNT = %ARGS:LCOUNT%
NEXT

;表情のコピー
SIF ARGS:0 != ""
	MEMO_EMOTION = %ARGS:0%

;STRの内容をコピー
FOR LCOUNT, 0, 20
	MEMO_STR:LCOUNT = %STR:LCOUNT%
NEXT

;メモを取ったSTRは空にする
VARSET STR, "", 0, 20

FOR LCOUNT, 1, 20

	SELECTCASE LCOUNT
	CASE 19
		SIF MEMO_STR:LCOUNT == ""
			CONTINUE
	;２行連続空白ならテキスト表示終了
	CASEELSE
		SIF MEMO_STR:LCOUNT == "" && MEMO_STR:(LCOUNT + 1) == ""
			BREAK
	ENDSELECT

	;顔グラ変化用の処理
	IF STRCOUNT(MEMO_STR:LCOUNT, "#F")
		VARSET LOCALS
		SPLIT MEMO_STR:LCOUNT, "#F", LOCALS
		MEMO_STR:LCOUNT = %LOCALS:0%
		MEMO_EMOTION = %LOCALS:1%
	ENDIF

	;MEMOからSTRの内容をもどしていく
	FOR LCOUNT2, 0, LCOUNT + 1
		STR:LCOUNT2 = %MEMO_STR:LCOUNT2%
	NEXT

	;以前表示させたものは消す
	SIF MEMO_LINECOUNT != LINECOUNT
		CLEARLINE LINECOUNT - MEMO_LINECOUNT

	;表示させる
	CALL DIALOGUE_FRAME, ARG, MEMO_EMOTION
	SIF MEMO_STR:LCOUNT != ""
		WAIT
NEXT

;-------------------------------------------------
;上の@DIALOGUE_FRAMEWの名前表示追加版
;-------------------------------------------------
@DIALOGUE_FRAME_NAMEW, ARG, ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7

STR:0 = - _%DISPLAY_NAME(ARG)%_ -
CALL DIALOGUE_FRAMEW, ARG, ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7

;-------------------------------------------------
;STR毎にWAITするITEM版
;STR内で文章の後に"#G"で区切って表示画像を変更できる
;例）STR:1 = こんにちは#G22まぐろう1
;-------------------------------------------------
@DIALOGUE_FRAME_ITEMW, ARGS, ARGS:1, ARGS:2, ARGS:3, ARGS:4, ARGS:5, ARGS:6, ARGS:7, ARGS:8, ARGS:9
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC MEMO_LINECOUNT
#DIMS DYNAMIC MEMO_IMG
#DIMS DYNAMIC MEMO_STR, 20

MEMO_LINECOUNT = LINECOUNT

;STRの上書き
FOR LCOUNT, 1, 10
	SIF ARGS:LCOUNT != ""
		STR:LCOUNT = %ARGS:LCOUNT%
NEXT

;表示画像のコピー
SIF ARGS:0 != ""
	MEMO_IMG = %ARGS:0%

;STRの内容をコピー
FOR LCOUNT, 0, 20
	MEMO_STR:LCOUNT = %STR:LCOUNT%
NEXT

;メモを取ったSTRは空にする
VARSET STR, "", 0, 20

FOR LCOUNT, 1, 20

	SELECTCASE LCOUNT
	CASE 19
		SIF MEMO_STR:LCOUNT == ""
			CONTINUE
	;２行連続空白ならテキスト表示終了
	CASEELSE
		SIF MEMO_STR:LCOUNT == "" && MEMO_STR:(LCOUNT + 1) == ""
			BREAK
	ENDSELECT

	;表示画像変更用の処理
	IF STRCOUNT(MEMO_STR:LCOUNT, "#G")
		VARSET LOCALS
		SPLIT MEMO_STR:LCOUNT, "#G", LOCALS
		MEMO_STR:LCOUNT = %LOCALS:0%
		MEMO_IMG = %LOCALS:1%
	ENDIF

	;MEMOからSTRの内容をもどしていく
	FOR LCOUNT2, 0, LCOUNT + 1
		STR:LCOUNT2 = %MEMO_STR:LCOUNT2%
	NEXT

	;以前表示させたものは消す
	SIF MEMO_LINECOUNT != LINECOUNT
		CLEARLINE LINECOUNT - MEMO_LINECOUNT

	;表示させる
	CALL DIALOGUE_FRAME_ITEM, MEMO_IMG
	SIF MEMO_STR:LCOUNT != ""
		WAIT
NEXT
