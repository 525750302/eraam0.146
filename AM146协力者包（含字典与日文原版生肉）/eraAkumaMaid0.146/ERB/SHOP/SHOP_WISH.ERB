;-------------------------------------------------
;悪魔と取引する
;-------------------------------------------------
@EVENT_WISH
PRINTL 
CALL KOJO_WISH, "取引開始"
IF COND("魂の取引可能")
	PRINTFORMW  魂一つを対価として、[%CSTR:肩書%]%NAME:TARGET%と取引をします。
ELSEIF COND("天使の取引可能")
	PRINTFORMW  魂一つを差し出して、 [%CSTR:肩書%]%NAME:TARGET%に願いを叶えて貰います。
ELSE
	PRINTFORMW  [%CSTR:肩書%]%NAME:TARGET%にお願いをします。
	PRINTFORMW ……要求次第で対価を払わなければならないかもしれません。
ENDIF
SIF COND("調教中")
	CALL PRINT_STRW, "黄色_※エッチの最中なので大部分が選択不能となっています※"
CALL EVENT_WISH_MAIN

CFLAG:寝起き = 0
SIF CFLAG:汗 >= 3
	CFLAG:汗 = 2
SIF CFLAG:MASTER:汗 >= 3
	CFLAG:MASTER:汗 = 2


@EVENT_WISH_MAIN
#DIM MEMO_LINECOUNT
#DIM MEMO_LINECOUNT2
#DIM CHOICE
#DIM ADDCOND_RESULT
#DIM LCOUNT
;例外的に魂不要になった場合にONになる
#DIM NO_SOUL, 101
;望みの名前。選択肢が存在するかの判定にも使う
#DIMS WISHNAME, 101
#DIM NO_WISH
#DIM IS_CANCEL
;肉体変化の望みで使用
#DIMS MEMO_BODY
;悪魔用の注釈
#DIMS TEXT_AKUMA
;具現用の注釈
#DIMS TEXT_GUGEN_PLAYER
#DIMS TEXT_GUGEN_TARGET
;呼び名の記録
#DIMS MEMO_CNAME
#DIMS MEMO_CNAME_KANA
#DIMS MEMO_MASTERCALLNAME
#DIMS MEMO_NICKNAME
;起きうるイベントの判定に使う
#DIMS EVENT_TABLE

MEMO_LINECOUNT = LINECOUNT

;取引に関わるTFLAG初期化
TFLAG:取引承諾 = 0
TFLAG:取引価格 = 0
TFLAG:値切った = 0
TFLAG:値上げ交渉 = 0
TFLAG:取引拒否 = 0

PRINTL 
DRAWLINE
IF COND("魂の取引可能")
	PRINTFORML  何を望みますか？(現在の%CALLNAME:MASTER%の魂：%TEXTS("0.01", BASE:MASTER:魂)%)
ELSEIF COND("天使の取引可能")
	PRINTFORML  何をお願いしますか？(現在の%CALLNAME:MASTER%の魂：%TEXTS("0.01", BASE:MASTER:魂)%)
ELSE
	PRINTFORML  何をお願いしますか？（所持金：%MONEY_C(MONEY)%）
ENDIF
PRINTL 

TEXT_AKUMA = 
SIF COND("魂の取引可能") || COND("天使の取引可能")
	TEXT_AKUMA = \(魂不要\)
TEXT_GUGEN_PLAYER = 
TEXT_GUGEN_TARGET = 
SIF PENIS(PLAYER) == 0 && TALENT:PLAYER:具現
	TEXT_GUGEN_PLAYER = [具現]で自分に生やすペニスの大きさが
SIF PENIS(TARGET) == 0
	TEXT_GUGEN_TARGET = [具現]で生やすペニスの大きさが
VARSET WISHNAME
VARSET NO_SOUL

STR:選択肢 = 

IF COND("調教中") == 0
	IF TALENT:相思相愛 == 0
		STR:選択肢 = %STR:選択肢% [ 0] %TEXT_LJ("恋人になって！", 16)% … 愛の告白をする%TEXT_AKUMA%/
		WISHNAME:0 = 愛の告白
	ENDIF

	IF COND("パンツ没収実行可能") == 0
		STR:選択肢 = %STR:選択肢%灰色/
	ELSE
		WISHNAME:1 = パンツ
	ENDIF
	;今は死に分岐
	IF COND("合意：パンツ没収")
		STR:選択肢 = %STR:選択肢% [ 1] %TEXT_LJ("パンツ", 16)% … 今穿いているパンツを没収する/
	ELSEIF COND("魂の取引可能") || COND("天使の取引可能")
		IF COND("合意：パンツ取引")
			STR:選択肢 = %STR:選択肢% [ 1] %TEXT_LJ("パンツ", 16)% … 今穿いているパンツをもらう(魂支払い済)/
		ELSE
			STR:選択肢 = %STR:選択肢% [ 1] %TEXT_LJ("パンツ", 16)% … 今穿いているパンツを魂と引き換えに譲ってもらう/
		ENDIF
	ELSE
		STR:選択肢 = %STR:選択肢% [ 1] %TEXT_LJ("パンツ", 16)% … 今穿いているパンツを交渉して譲ってもらう/
	ENDIF
	IF COND("同棲中")
		IF COND("部室出現可能")
			WISHNAME:2 = 同棲頻度の相談
			IF COND("同棲：ずっと")
				IF CHARA_NUM("同棲：ずっと") >= 2
					STR:選択肢 = %STR:選択肢% [ 2] %TEXT_LJ("同棲頻度の相談", 16)% … 同棲度合い(現在は常時同棲)を変更します%TEXT_AKUMA%/
				ELSE
					STR:選択肢 = %STR:選択肢%灰色/ [ 2] %TEXT_LJ("同棲頻度の相談", 16)% … 同棲度合い(現在は常時同棲)を変更します%TEXT_AKUMA%/
				ENDIF
			ELSEIF COND("同棲：一週間")
				STR:選択肢 = %STR:選択肢% [ 2] %TEXT_LJ("同棲頻度の相談", 16)% … 同棲度合い(残り同棲期間{COND("残り同棲日数")}日)を変更します%TEXT_AKUMA%/
			ELSEIF COND("同棲：週末のみ")
				STR:選択肢 = %STR:選択肢% [ 2] %TEXT_LJ("同棲頻度の相談", 16)% … 同棲度合い(現在は週末のみ)を変更します%TEXT_AKUMA%/
			ENDIF
		ENDIF
	ELSE
		WISHNAME:2 = 同棲希望
		STR:選択肢 = %STR:選択肢% [ 2] %TEXT_LJ("同棲して欲しい", 16)% … 期間を決めて、一緒の家で生活して貰います%TEXT_AKUMA%/
	ENDIF

	IF CHARA_NUM("お弁当の約束")
		STR:選択肢 = %STR:選択肢%灰色/
	ELSE
		WISHNAME:3 = お弁当作って
	ENDIF
	STR:選択肢 = %STR:選択肢% [ 3] %TEXT_LJ("お弁当作って", 16)% … お昼に食べるお弁当を作ってもらいます%TEXT_AKUMA%/

	IF TALENT:オトコ == 0 && COND("種族：人形") == 0
		WISHNAME:4 = ピル
		IF CFLAG:ピル == 0
			STR:選択肢 = %STR:選択肢% [ 4] %TEXT_LJ("ピルを渡す", 16)% … 経口薬での避妊をしてもらいます%TEXT_AKUMA%/
		ELSE
			STR:選択肢 = %STR:選択肢% [ 4] %TEXT_LJ("ピルの服用中断", 16)% … 避妊薬の使用を止めてもらいます%TEXT_AKUMA%/
		ENDIF
	ENDIF

	IF MARK:淫紋 < 4 && MARK:淫紋時計 == 0 && TALENT:MASTER:禁断の知識
		WISHNAME:5 = 淫紋
		IF MARK:淫紋 >= 2
			STR:選択肢 = %STR:選択肢% [ 5] %TEXT_LJ("淫紋彫らせて", 16)% … 刻まれた紋章の画数を増やし強化します%TEXT_AKUMA%/
		ELSE
			STR:選択肢 = %STR:選択肢% [ 5] %TEXT_LJ("淫紋彫らせて", 16)% … 下腹部に情欲をかきたてる紋章を刻みます%TEXT_AKUMA%/
		ENDIF
	ENDIF

	;性処理できるかチェック
	CALL CHECK_COM_ABLE, TARGET

	IF COND("性処理指名中")
		WISHNAME:6 = 性処理の一時中断
		STR:選択肢 = %STR:選択肢% [ 6] %TEXT_LJ("性処理の一時中断", 16)% … 性欲処理目的で呼び出すのを止めます%TEXT_AKUMA%/
	ELSE
		WISHNAME:6 = 性処理を頼む
		SIF COND("性処理コマンド実行可能") == 0
			STR:選択肢 = %STR:選択肢%灰色/
		STR:選択肢 = %STR:選択肢% [ 6] %TEXT_LJ("性処理を頼む", 16)% … 学園で休み時間に呼び出して、性欲を処理して貰います%TEXT_AKUMA%/
	ENDIF

	IF COND("尻尾隠蔽中")
		WISHNAME:7 = 尻尾を見せて
		STR:選択肢 = %STR:選択肢% [ 7] %TEXT_LJ("尻尾を見せて", 16)% … 普段は隠している尻尾や耳を見せて貰います%TEXT_AKUMA%/
	ENDIF

	IF CHECK_CLO("ハダカ")
		STR:選択肢 = %STR:選択肢%灰色/
	ELSE
		WISHNAME:8 = 裸になって欲しい
	ENDIF
	STR:選択肢 = %STR:選択肢% [ 8] %TEXT_LJ("裸になって欲しい", 16)% … 目の前で服を脱いで貰います%TEXT_AKUMA%/

	;***着床判定追加パッチで追加
	;うっかり受精させちゃったり、心配な場合に緊急避妊薬を飲んで貰います。
	;着床判定がONの場合だけ有効です。OFFの場合は何の効果もないので
	;避妊不要だったり、もうアフターピルが効果中だったりする場合は無効
	IF COND("避妊不要") || COND("アフターピル")
		STR:選択肢 = %STR:選択肢%灰色/
	ELSE
		WISHNAME:9 = アフターピル
	ENDIF
	STR:選択肢 = %STR:選択肢% [ 9] %TEXT_LJ("アフターピル", 16)% … 緊急避妊薬を飲んで貰います/
ENDIF

IF COND("種族：悪魔祓い")
	IF COND("調教中") == 0
		STR:選択肢 = %STR:選択肢% [10] %TEXT_LJ(MONEY_C(50000), 16)% … %MONEY_C(50000)%を出してもらいます/
		WISHNAME:10 = お金が欲しい
	ENDIF

	SELECTCASE TALENT:MASTER:力持ち
	CASE IS < 9
		STR:選択肢 = %STR:選択肢% [13] %TEXT_LJ("力持ち", 16)% … [%TALENT_NAME(GETNUM(TALENT, "力持ち"), TALENT:MASTER:力持ち + 1)%]を得る。押し倒されても安心。ムキムキにはならない/
		WISHNAME:13 = 力持ち
	ENDSELECT
ELSEIF COND("悪魔の取引可能") || COND("天使の取引可能") || COND("魂の取引可能")
	IF COND("調教中") == 0
		STR:選択肢 = %STR:選択肢% [10] %TEXT_LJ(MONEY_C(50000), 16)% … %MONEY_C(50000)%を出してもらいます/
		WISHNAME:10 = お金が欲しい
	ENDIF

	SELECTCASE TALENT:MASTER:甘い言葉
	CASE IS < 9
		STR:選択肢 = %STR:選択肢% [11] %TEXT_LJ("甘い言葉", 16)% … [%TALENT_NAME(GETNUM(TALENT, "甘い言葉"), TALENT:MASTER:甘い言葉 + 1)%]を得る。人を誑かす話術です/
		WISHNAME:11 = 甘い言葉
	ENDSELECT

	SELECTCASE TALENT:MASTER:謎の魅力
	CASE IS < 9
		STR:選択肢 = %STR:選択肢% [12] %TEXT_LJ("謎の魅力", 16)% … [%TALENT_NAME(GETNUM(TALENT, "謎の魅力"), TALENT:MASTER:謎の魅力 + 1)%]を得る。相手から好かれやすくなります/
		WISHNAME:12 = 謎の魅力
	ENDSELECT

	SELECTCASE TALENT:MASTER:力持ち
	CASE IS < 9
		STR:選択肢 = %STR:選択肢% [13] %TEXT_LJ("力持ち", 16)% … [%TALENT_NAME(GETNUM(TALENT, "力持ち"), TALENT:MASTER:力持ち + 1)%]を得る。押し倒されても安心。ムキムキにはならない/
		WISHNAME:13 = 力持ち
	ENDSELECT

	SELECTCASE TALENT:MASTER:性豪
	CASE IS < 9
		STR:選択肢 = %STR:選択肢% [14] %TEXT_LJ("性豪", 16)% … [%TALENT_NAME(GETNUM(TALENT, "性豪"), TALENT:MASTER:性豪 + 1)%]を得る。精力上限がＵＰ！/
		WISHNAME:14 = 性豪
	ENDSELECT

	WISHNAME:15 = 禁断の知識
	SELECTCASE TALENT:MASTER:禁断の知識
	CASE 0
		STR:選択肢 = %STR:選択肢% [15] %TEXT_LJ("禁断の知識", 16)% … [淫紋の刻印]を得る。淫紋を彫る事が出来るようになる/
	CASE 1
		STR:選択肢 = %STR:選択肢% [15] %TEXT_LJ("禁断の知識", 16)% … [具現]を得る。ペニスを出し入れ出来るようになる/
	CASE 2
		STR:選択肢 = %STR:選択肢% [15] %TEXT_LJ("禁断の知識", 16)% … [認識阻害]を得る。人物の特定を不可能とさせる/

	;CASE 
	;	STR:選択肢 = %STR:選択肢% [15] %TEXT_LJ("禁断の知識", 16)% … [%TALENT_NAME(GETNUM(TALENT, "禁断の知識"), TALENT:MASTER:禁断の知識 + 1)%]を得る。人の認識を阻害できるようになる/
	CASEELSE
		WISHNAME:15 = 
		STR:選択肢 = %STR:選択肢%灰色/
		STR:選択肢 = %STR:選択肢% [15] %TEXT_LJ("禁断の知識", 16)% … 現在は封印中…/
	ENDSELECT

	IF TALENT:MASTER:豊穣 == 0
		STR:選択肢 = %STR:選択肢% [16] %TEXT_LJ("豊穣", 16)% … [豊穣]を得る。おっぱいに射精or揉む事で少しづつ大きくなります/
		WISHNAME:16 = 豊穣
	ENDIF

	IF PENIS(PLAYER) || TALENT:PLAYER:具現
		SELECTCASE TALENT:MASTER:射精量強化
		CASE IS < 9
			STR:選択肢 = %STR:選択肢% [17] %TEXT_LJ("射精量強化", 16)% … [%TALENT_NAME(GETNUM(TALENT, "射精量強化"), TALENT:MASTER:射精量強化 + 1)%]を得る。一度の射精量が大幅に増える/
			WISHNAME:17 = 射精量強化
		ENDSELECT
	ENDIF

	;SELECTCASE TALENT:MASTER:錬金術
	;CASE IS < 9
	;	STR:選択肢 = %STR:選択肢% [13] %TEXT_LJ("錬金術", 16)% … [%TALENT_NAME(GETNUM(TALENT, "錬金術"), TALENT:MASTER:錬金術 + 1)%]を得る。習得LV次第で色々と出来るようになる/
	;	WISHNAME:13 = 錬金術
	;ENDSELECT

	IF COND("悪魔の取引可能") || COND("天使の取引可能")
		IF COND("調教中") == 0
			IF TALENT:PLAYER:長身
				STR:選択肢 = %STR:選択肢%灰色/
			ELSE
				WISHNAME:20 = 長身化
			ENDIF
			STR:選択肢 = %STR:選択肢% [20] %TEXT_LJ("背を大きく", 16)% … [長身]になる/

			IF TALENT:PLAYER:小柄
				STR:選択肢 = %STR:選択肢%灰色/
			ELSE
				WISHNAME:21 = 小柄化
			ENDIF
			STR:選択肢 = %STR:選択肢% [21] %TEXT_LJ("背を小さく", 16)% … [小柄]になる/
		ENDIF

		IF PENIS(PLAYER) || TALENT:PLAYER:具現
			WISHNAME:22 = 巨根化
			IF COND("調教中") && INSERT("挿入中", PLAYER)
				STR:選択肢 = %STR:選択肢%灰色/
				WISHNAME:22 = 
			ENDIF
			SELECTCASE CONDS("潜在的含むペニスサイズ", PLAYER)
			CASE "規格外"
				STR:選択肢 = %STR:選択肢%灰色/ [22] %TEXT_LJ("ペニスを大きく", 16)% … これ以上大きくは出来ない/
				WISHNAME:22 = 
			CASE "腕並み"
				STR:選択肢 = %STR:選択肢% [22] %TEXT_LJ("ペニスを大きく", 16)% … %TEXT_GUGEN_PLAYER%[規格外]になる/
			CASE "馬並み"
				STR:選択肢 = %STR:選択肢% [22] %TEXT_LJ("ペニスを大きく", 16)% … %TEXT_GUGEN_PLAYER%[腕並]になる/
			CASE "巨根"
				STR:選択肢 = %STR:選択肢% [22] %TEXT_LJ("ペニスを大きく", 16)% … %TEXT_GUGEN_PLAYER%[馬並]になる/
			CASE "普通"
				STR:選択肢 = %STR:選択肢% [22] %TEXT_LJ("ペニスを大きく", 16)% … %TEXT_GUGEN_PLAYER%[巨根]になる/
			CASE "短小"
				STR:選択肢 = %STR:選択肢% [22] %TEXT_LJ("ペニスを大きく", 16)% … %TEXT_GUGEN_PLAYER%[普通]になる/
			ENDSELECT

			WISHNAME:23 = 短小化
			IF COND("調教中") && INSERT("挿入中", PLAYER)
				STR:選択肢 = %STR:選択肢%灰色/
				WISHNAME:23 = 
			ENDIF
			SELECTCASE CONDS("潜在的含むペニスサイズ", PLAYER)
			CASE "規格外"
				STR:選択肢 = %STR:選択肢% [23] %TEXT_LJ("ペニスを小さく", 16)% … %TEXT_GUGEN_PLAYER%[腕並]になる/
			CASE "腕並み"
				STR:選択肢 = %STR:選択肢% [23] %TEXT_LJ("ペニスを小さく", 16)% … %TEXT_GUGEN_PLAYER%[馬並]になる/
			CASE "馬並み"
				STR:選択肢 = %STR:選択肢% [23] %TEXT_LJ("ペニスを小さく", 16)% … %TEXT_GUGEN_PLAYER%[巨根]になる/
			CASE "巨根"
				STR:選択肢 = %STR:選択肢% [23] %TEXT_LJ("ペニスを小さく", 16)% … %TEXT_GUGEN_PLAYER%[普通]になる/
			CASE "普通"
				STR:選択肢 = %STR:選択肢% [23] %TEXT_LJ("ペニスを小さく", 16)% … %TEXT_GUGEN_PLAYER%[短小]になる/
			CASE "短小"
				STR:選択肢 = %STR:選択肢%灰色/ [23] %TEXT_LJ("ペニスを小さく", 16)% … これ以上小さくは出来ない/
				WISHNAME:23 = 
			ENDSELECT
		ENDIF

		IF TALENT:PLAYER:オトコ == 0 && COND("調教中") == 0
			WISHNAME:24 = 巨乳化
			WISHNAME:25 = 貧乳化
			SELECTCASE CONDS("おっぱい", PLAYER)
			CASE "超乳"
				STR:選択肢 = %STR:選択肢%灰色/ [24] %TEXT_LJ("胸を大きく", 16)% … これ以上大きくは出来ない/
				WISHNAME:24 = 
				STR:選択肢 = %STR:選択肢% [25] %TEXT_LJ("胸を小さく", 16)% … [爆乳]になる/
			CASE "爆乳"
				STR:選択肢 = %STR:選択肢% [24] %TEXT_LJ("胸を大きく", 16)% … [超乳]になる/
				STR:選択肢 = %STR:選択肢% [25] %TEXT_LJ("胸を小さく", 16)% … [巨乳]になる/
			CASE "巨乳"
				STR:選択肢 = %STR:選択肢% [24] %TEXT_LJ("胸を大きく", 16)% … [爆乳]になる/
				STR:選択肢 = %STR:選択肢% [25] %TEXT_LJ("胸を小さく", 16)% … [並乳]になる/
			CASE "並乳"
				STR:選択肢 = %STR:選択肢% [24] %TEXT_LJ("胸を大きく", 16)% … [巨乳]になる/
				STR:選択肢 = %STR:選択肢% [25] %TEXT_LJ("胸を小さく", 16)% … [貧乳]になる/
			CASE "貧乳"
				STR:選択肢 = %STR:選択肢% [24] %TEXT_LJ("胸を大きく", 16)% … [並乳]になる/
				STR:選択肢 = %STR:選択肢%灰色/ [25] %TEXT_LJ("胸を小さく", 16)% … これ以上小さくは出来ない/
				WISHNAME:25 = 
			ENDSELECT

			WISHNAME:26 = 巨尻化
			WISHNAME:27 = 小尻化
			SELECTCASE CONDS("お尻", PLAYER)
			CASE "超尻"
				STR:選択肢 = %STR:選択肢%灰色/ [26] %TEXT_LJ("お尻を大きく", 16)% … これ以上大きくは出来ない/
				WISHNAME:26 = 
				STR:選択肢 = %STR:選択肢% [27] %TEXT_LJ("お尻を小さく", 16)% … [爆尻]になる/
			CASE "爆尻"
				STR:選択肢 = %STR:選択肢% [26] %TEXT_LJ("お尻を大きく", 16)% … [超尻]になる/
				STR:選択肢 = %STR:選択肢% [27] %TEXT_LJ("お尻を小さく", 16)% … [巨尻]になる/
			CASE "巨尻"
				STR:選択肢 = %STR:選択肢% [26] %TEXT_LJ("お尻を大きく", 16)% … [爆尻]になる/
				STR:選択肢 = %STR:選択肢% [27] %TEXT_LJ("お尻を小さく", 16)% … [並尻]になる/
			CASE "並尻"
				STR:選択肢 = %STR:選択肢% [26] %TEXT_LJ("お尻を大きく", 16)% … [巨尻]になる/
				STR:選択肢 = %STR:選択肢% [27] %TEXT_LJ("お尻を小さく", 16)% … [小尻]になる/
			CASE "小尻"
				STR:選択肢 = %STR:選択肢% [26] %TEXT_LJ("お尻を大きく", 16)% … [並尻]になる/
				STR:選択肢 = %STR:選択肢%灰色/ [27] %TEXT_LJ("お尻を小さく", 16)% … これ以上小さくは出来ない/
				WISHNAME:27 = 
			ENDSELECT
		ENDIF

		IF PENIS(PLAYER)
			IF TALENT:PLAYER:早漏
				WISHNAME:28 = 早漏治療
				STR:選択肢 = %STR:選択肢% [28] %TEXT_LJ("早漏治療", 16)% … [早漏]を失う/
			ELSEIF TALENT:PLAYER:遅漏
				WISHNAME:28 = 遅漏治療
				STR:選択肢 = %STR:選択肢% [28] %TEXT_LJ("遅漏治療", 16)% … [遅漏]を失う/
			ENDIF
		ENDIF

		IF COND("調教中") == 0
			IF TALENT:PLAYER:オンナ
				STR:選択肢 = %STR:選択肢%灰色/
			ELSE
				WISHNAME:30 = 女性化
			ENDIF
			IF TALENT:PLAYER:具現 && TALENT:PLAYER:ふたなり
				STR:選択肢 = %STR:選択肢% [30] %TEXT_LJ("女性化", 16)% … [具現]の力で[オンナ]に\@ TRUE_SEX(PLAYER) == "オンナ" ? 戻る # なる \@%TEXT_AKUMA%/
				NO_SOUL:30 = 1
			ELSE
				STR:選択肢 = %STR:選択肢% [30] %TEXT_LJ("女性化", 16)% … [オンナ]に\@ TRUE_SEX(PLAYER) == "オンナ" ? 戻る # なる \@/
			ENDIF

			IF TALENT:PLAYER:オトコ
				STR:選択肢 = %STR:選択肢%灰色/
			ELSE
				WISHNAME:31 = 男性化
			ENDIF
			STR:選択肢 = %STR:選択肢% [31] %TEXT_LJ("男性化", 16)% … [オトコ]に\@ TRUE_SEX(PLAYER) == "オトコ" ? 戻る # なる \@/

			IF TALENT:PLAYER:ふたなり
				STR:選択肢 = %STR:選択肢%灰色/
			ELSE
				WISHNAME:32 = ふたなり化
			ENDIF
			IF TALENT:PLAYER:具現 && TALENT:PLAYER:オンナ
				STR:選択肢 = %STR:選択肢% [32] %TEXT_LJ("ふたなり化", 16)% … [具現]の力で[ふたなり]に\@ TRUE_SEX(PLAYER) == "ふたなり" ? 戻る # なる \@%TEXT_AKUMA%/
				NO_SOUL:32 = 1
			ELSE
				STR:選択肢 = %STR:選択肢% [32] %TEXT_LJ("ふたなり化", 16)% … [ふたなり]に\@ TRUE_SEX(PLAYER) == "ふたなり" ? 戻る # なる \@/
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF TALENT:避妊の護符
	STR:選択肢 = %STR:選択肢% [39] %TEXT_LJ("避妊護符の解除", 16)% … [避妊の護符]を解除して、妊娠できるようにしてもらう/
	WISHNAME:39 = 避妊解除
ELSEIF COND("初期避妊の護符")
	STR:選択肢 = %STR:選択肢% [39] %TEXT_LJ("避妊護符の装着", 16)% … [避妊の護符]を再び着けて、避妊をしてもらう/
	WISHNAME:39 = 避妊の護符
ENDIF

IF COND("調教中") == 0
	IF PLACE("髪型変更可能")
		;髪型を弄るには髪の長さがボブ以上必要
		SELECTCASE CONDS("髪の長さ")
		CASE "ボブ", "セミロング", "ロング", "超ロング"
			WISHNAME:40 = 髪を弄らせて
			STR:選択肢 = %STR:選択肢% [40] %TEXT_LJ("髪を弄らせて", 16)% … 髪型を長さを変えない範囲で弄らせてもらいます%TEXT_AKUMA%/
		CASEELSE
			STR:選択肢 = %STR:選択肢%灰色/
			STR:選択肢 = %STR:選択肢% [40] %TEXT_LJ("髪を弄らせて", 16)% … 髪型を長さを変えない範囲で弄らせてもらいます(ある程度の長さが必要)/
		ENDSELECT
	ELSE
		STR:選択肢 = %STR:選択肢%灰色/
		STR:選択肢 = %STR:選択肢% [40] %TEXT_LJ("髪を弄らせて", 16)% … 髪型を長さを変えない範囲で弄らせてもらいます(部室や自宅でのみ可能)/
	ENDIF

	;普段パンスト履かない人に頼むのはなんか違う気がする？
	WISHNAME:41 = パンスト着替えて
	STR:選択肢 = %STR:選択肢% [41] %TEXT_LJ("パンスト着替えて", 16)% … パンストを%CALLNAME:MASTER%好みのものに履き替えてもらいます%TEXT_AKUMA%/

	IF TALENT:具現
		IF TALENT:オンナ
			STR:選択肢 = %STR:選択肢%灰色/
		ELSE
			WISHNAME:50 = 女性化して
		ENDIF
		STR:選択肢 = %STR:選択肢% [50] %TEXT_LJ("女性化して", 16)% … [オンナ]に\@ TRUE_SEX(TARGET) == "オンナ" ? 戻って # なって \@もらう%TEXT_AKUMA%/

		IF TALENT:ふたなり
			STR:選択肢 = %STR:選択肢%灰色/
		ELSE
			WISHNAME:52 = ふたなり化して
		ENDIF
		STR:選択肢 = %STR:選択肢% [52] %TEXT_LJ("ふたなり化して", 16)% … [ふたなり]に\@ TRUE_SEX(TARGET) == "ふたなり" ? 戻って # なって \@もらう%TEXT_AKUMA%/
	ENDIF

	IF PENIS(TARGET) || (STRCOUNT(CSTR:特別経験, "ふたなり化") && TALENT:PLAYER:具現 + TALENT:具現)
		IF BASE:MASTER:魂 > 100 && (COND("悪魔の取引可能") + COND("天使の取引可能") || (ASSI && COND("悪魔の取引可能", ASSI) + COND("天使の取引可能", ASSI) ) )
			WISHNAME:53 = 巨根化して
			SELECTCASE CONDS("潜在的含むペニスサイズ", TARGET)
			CASE "規格外"
				STR:選択肢 = %STR:選択肢%灰色/ [53] %TEXT_LJ("巨根化して", 16)% … これ以上大きくは出来ない/
				WISHNAME:53 = 
			CASE "腕並み"
				STR:選択肢 = %STR:選択肢% [53] %TEXT_LJ("巨根化して", 16)% … %TEXT_GUGEN_TARGET%[規格外]になってもらう/
			CASE "馬並み"
				STR:選択肢 = %STR:選択肢% [53] %TEXT_LJ("巨根化して", 16)% … %TEXT_GUGEN_TARGET%[腕並]になってもらう/
			CASE "巨根"
				STR:選択肢 = %STR:選択肢% [53] %TEXT_LJ("巨根化して", 16)% … %TEXT_GUGEN_TARGET%[馬並]になってもらう/
			CASE "普通"
				STR:選択肢 = %STR:選択肢% [53] %TEXT_LJ("巨根化して", 16)% … %TEXT_GUGEN_TARGET%[巨根]になってもらう/
			CASE "短小"
				STR:選択肢 = %STR:選択肢% [53] %TEXT_LJ("巨根化して", 16)% … %TEXT_GUGEN_TARGET%[普通]になってもらう/
			ENDSELECT

			WISHNAME:54 = 短小化して
			SELECTCASE CONDS("潜在的含むペニスサイズ", TARGET)
			CASE "規格外"
				STR:選択肢 = %STR:選択肢% [54] %TEXT_LJ("短小化して", 16)% … %TEXT_GUGEN_TARGET%[腕並]になってもらう/
			CASE "腕並み"
				STR:選択肢 = %STR:選択肢% [54] %TEXT_LJ("短小化して", 16)% … %TEXT_GUGEN_TARGET%[馬並]になってもらう/
			CASE "馬並み"
				STR:選択肢 = %STR:選択肢% [54] %TEXT_LJ("短小化して", 16)% … %TEXT_GUGEN_TARGET%[巨根]になってもらう/
			CASE "巨根"
				STR:選択肢 = %STR:選択肢% [54] %TEXT_LJ("短小化して", 16)% … %TEXT_GUGEN_TARGET%[普通]になってもらう/
			CASE "普通"
				STR:選択肢 = %STR:選択肢% [54] %TEXT_LJ("短小化して", 16)% … %TEXT_GUGEN_TARGET%[短小]になってもらう/
			CASE "短小"
				STR:選択肢 = %STR:選択肢%灰色/ [54] %TEXT_LJ("短小化して", 16)% … これ以上小さくは出来ない/
				WISHNAME:54 = 
			ENDSELECT
		ELSE
			STR:選択肢 = %STR:選択肢%灰色/ [53] %TEXT_LJ("巨根化して", 16)% … %TEXTS("対象の名称")%か%TEXTS("助手の名称")%が魂の取引可能＆%CALLNAME:MASTER%の魂が1.01必要です/
			STR:選択肢 = %STR:選択肢%灰色/ [54] %TEXT_LJ("短小化して", 16)% … %TEXTS("対象の名称")%か%TEXTS("助手の名称")%が魂の取引可能＆%CALLNAME:MASTER%の魂が1.01必要です/
		ENDIF
	ENDIF
ENDIF

;口上側で選択肢を自由に作ることができる
TRYCALLFORM WISH_K{NO:TARGET}_ADDSELECT

IF COND("調教中") == 0
	WISHNAME:98 = あだ名変更
	IF NICKNAME:TARGET != ""
		STR:選択肢 = %STR:選択肢% [98] %TEXT_LJ("あだ名変更", 16)% … %CALLNAME:TARGET%のあだ名を%NICKNAME:TARGET%から変更する%TEXT_AKUMA%/
	ELSE
		STR:選択肢 = %STR:選択肢% [98] %TEXT_LJ("あだ名変更", 16)% … %CALLNAME:TARGET%にあだ名をつける%TEXT_AKUMA%/
	ENDIF

	WISHNAME:99 = 呼び名変更
	IF CEVENTS("二人称主人") != ""
		STR:選択肢 = %STR:選択肢% [99] %TEXT_LJ("呼び名変更", 16)% … %CALLNAME:MASTER%と話す際の二人称を%CEVENTS("二人称主人")%から変えてもらう%TEXT_AKUMA%/
	ELSE
		STR:選択肢 = %STR:選択肢% [99] %TEXT_LJ("呼び名変更", 16)% … %CALLNAME:MASTER%と話す際の二人称を変えてもらう%TEXT_AKUMA%/
	ENDIF
ENDIF

;現在のアドバイザーがフェリシエルの場合は周回のお願いが出来るかも
;IF COND("周回可能") && PLAYER == MASTER && COND("フェリシエル")
;	WISHNAME:100 = 次の周に進む
;	STR:選択肢 = %STR:選択肢%[100] %TEXT_LJ("次の周に進む", 16)% … 強くてニューゲームをします
;ENDIF

VARSET LOCALS
SPLIT STR:選択肢, "/", LOCALS

;これがONだとWISHNAMEを消す
IS_CANCEL = 0
;取引画面の表示
FOR LCOUNT, 0, 200
	SELECTCASE LOCALS:LCOUNT
	CASE ""
		BREAK
	;灰色の場合は選択不能
	CASE "灰色"
		SETCOLOR DEF_COLOR(LOCALS:LCOUNT)
		IS_CANCEL = 1
		CONTINUE
	CASE "水色", "ハートピンク", "青", "緑", "黄色"
		SETCOLOR DEF_COLOR(LOCALS:LCOUNT)
		CONTINUE
	ENDSELECT

	PRINTFORML %LOCALS:LCOUNT%

	RESETCOLOR

	NO_WISH = TOINT_BL(EXTSTR(LOCALS:LCOUNT, "[", "]") )
	IF IS_CANCEL
		WISHNAME:NO_WISH = 
	ELSEIF WISHNAME:NO_WISH == ""
		WISHNAME:NO_WISH = %EXTSTR_BL(LOCALS:LCOUNT, "] ", " …")%
	ENDIF
	IS_CANCEL = 0
NEXT

PRINTFORML 
PRINTFORML [999] \@ COND("魂の取引可能") ? 取引を終える # 戻る \@

CALL INPUT_SELECT, 101, 999
CHOICE = RESULT

;選択肢がない場合
IF CHOICE <= 100 && WISHNAME:CHOICE == ""
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	RESTART
ENDIF

SELECTCASE CHOICE
CASE 999
	PLAYER = MASTER
	;何か契約したりしたなら行動の記録
	SIF STR:日常イベント名 != ""
		CALL SET_PREV_DAILYCOM
	RETURN 0
;強くてニューゲーム
CASE 100
	CALL EMOTION_PLUS, "リセット", TARGET
	PRINTFORMW 「ご主人様。この取引を行うと、少々の特典と引き換えに多くのものを失う事になると思います」
	PRINTFORMW 「具体的にはですね……」
	PRINTL 
	CALL PRINT_STRL, "黄色_※次の周に移行すると以下が起きます※"
	CALL PRINT_STRL, "黄色_1.日付が１年目の１日目に戻る"
	CALL PRINT_STRL, "黄色_2.周回数と陥落人数に応じた実行値のボーナスを得る(永続＆周回ごとに純増)"
	CALL PRINT_STRL, "黄色_3.あなたの素質＆能力の変化を行える。各LVを減らすのも可能"
	CALL PRINT_STRW, "黄色_4.好感度が2500以上の部員を３人まで引き継げる"
	PRINTL 
	PRINTFORMW 「……本当に実行しても大丈夫でしょうか？」
	CALL PRINT_SELECT, "大丈夫。問題ない。/やっぱやめる"
	IF RESULT == 0
		CALL GAME_RENEWAL
		RETURN 0
	ENDIF
CASE 0
	IF TALENT:相思相愛 == 0
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%に思いをぶつけた！
		PRINTL 

		STR:実行条件 = 
		RESULT = 0
		TRYCALLFORM ADDCOND_K{NO:TARGET}("相思相愛")
		ADDCOND_RESULT = RESULT

		;ダメだった場合
		IF STR:実行条件 != ""
			CALL KOJO_WISH, WISHNAME:CHOICE
			CALL PRINT_STRW, STR:実行条件
		ELSEIF TALENT:恋慕 || ADDCOND_RESULT
			TALENT:相思相愛 = 1
			TALENT:MASTER:相思相愛 = 1

			CALL KOJO_WISH, WISHNAME:CHOICE

			CALL RECORD_SP_EXP, TARGET

			STR:日常イベント名 = %WISHNAME:CHOICE%
			;その他合意チェック
			CALL SETFLAG, "合意", TARGET
		ELSE
			CALL KOJO_WISH, WISHNAME:CHOICE
			PRINTFORMW ……%CALLNAME:TARGET%の心には響かなかったようだ。
			PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
			CALL PRINT_STRW, "イエロー_(※[相思相愛]になるには、相手が[恋慕]の必要があります)"
		ENDIF
	ENDIF
CASE 1
	;イベントとして呼び出せるように関数化
	CALL EVENT_WISH_PANTU

CASE 2
	IF COND("同棲可能") == 0
		CALL KOJO_WISH, "同棲拒否"
		IF COND("同棲中")
			PRINTFORMW ……%CALLNAME:TARGET%に同棲状態の変更を断られてしまった。
			PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
			CALL PRINT_STR, "イエロー_(※現状から変更してもらうには、相手が[恋慕]or[屈服刻印LV3]or[信頼LV5]の必要があります)_W_"
		ELSE
			PRINTFORMW ……%CALLNAME:TARGET%に同棲を断られてしまった。
			PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
			CALL PRINT_STR, "イエロー_(※同棲をしてもらうには、相手が[恋慕]or[屈服刻印LV3]or[信頼LV5]の必要があります)_W_"
		ENDIF
	ELSEIF COND("同棲：ずっと") && CHARA_NUM("同棲：ずっと") <= 1
		PRINTFORMW %CALLNAME:TARGET%との同棲関係を見直そう……と思ったのですが無理がある様です。
		CALL PRINT_STR, "イエロー_(※最低一人は常に同棲してくれる相手が必要です。他の同棲相手を探してから提案しましょう)_W_"
	ELSE
		CALL KOJO_WISH, WISHNAME:CHOICE

		SELECTCASE WISHNAME:CHOICE
		CASE "同棲希望"
			PRINTFORML %CALLNAME:TARGET%に同棲を了承してもらえました……期間はどうしますか？
		CASE "同棲頻度の相談"
			PRINTFORML %CALLNAME:TARGET%との同棲関係を見直します……どうしますか？
		ENDSELECT

		EVENT_TABLE = やっぱやめる\/
		SIF COND("同棲中")
			EVENT_TABLE += "同棲解消/"
		SIF COND("同棲：ずっと") == 0
			EVENT_TABLE += "ずっと！/"
		SIF COND("同棲：一週間") == 0
			EVENT_TABLE += "とりあえず１週間/"
		SIF COND("同棲：週末のみ") == 0
			EVENT_TABLE += "毎週土曜(夜)から月曜(朝)まで/"

		VARSET LOCALS
		SPLIT EVENT_TABLE, "/", LOCALS
		CALL PRINT_SELECT, EVENT_TABLE

		SELECTCASE LOCALS:RESULT
		CASE "やっぱやめる"

		CASE "同棲解消"
			PRINTFORMW %CALLNAME:TARGET%との同棲関係を一時的に解消しました。
			CALL SETFLAG, "同棲解消", TARGET
		CASE "ずっと！"
			PRINTFORMW どちらかが切り出すまで、ずっと同棲する事にしました。
			CALL SETFLAG, "同棲：ずっと", TARGET
		CASE "とりあえず１週間"
			PRINTFORMW 今日から１週間の間、%CALLNAME:TARGET%がお泊りしてくれる事になりました
			CALL SETFLAG, "同棲：一週間", TARGET
		CASE "毎週土曜(夜)から月曜(朝)まで"
			PRINTFORMW 週末に限り、%CALLNAME:TARGET%が遊びに来てくれるようになりました
			CALL SETFLAG, "同棲：週末のみ", TARGET
		ENDSELECT

		SELECTCASE WISHNAME:CHOICE
		CASE "同棲希望"
			;同棲中＆個室が必要な関係なら用意する
			IF FLAG:個室 == 0 && COND("同棲中") && COND("要個室")
				IF FIND_COND("フェリシエル")
					PRINTL 
					PRINTFORMW %CALLNAME:TARGET%の部屋については[%CSTR:FIND_COND("フェリシエル"):肩書%]%NAME:FIND_COND("フェリシエル")%が何とかしてくれるようです
					PRINTFORMW 今後%CALLNAME:MASTER%と暮らす人が増えるたびに、片付けたり掃除したり色々して用意してくれる事でしょう
				ENDIF
				FLAG:個室 = 1
			ENDIF
		ENDSELECT
	ENDIF
CASE 3
	;すでに誰かに依頼中…もういいなんて言うのは無礼過ぎですよ！
	IF CHARA_NUM("お弁当の約束")
	;作ってくれない……
	ELSEIF COND("お弁当可能") == 0
		CALL KOJO_WISH, "お弁当拒否"
		PRINTFORMW ……%CALLNAME:TARGET%はお弁当を作ってくれなかった。

		IF CEVENT("合意禁止：お弁当")
			PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
			CALL PRINT_STRW, @"イエロー_(※%CALLNAME:TARGET%は、何らかの理由があってお弁当を作りたくないようです)"
		ELSEIF COND("部室出現可能") == 0
			CALL PRINT_STRW, "イエロー_(※お弁当は、部室に現れる相手が[恋慕]or[屈服刻印LV1]or[奉仕精神LV3]なら作ってくれるようになります)"
		ELSE
			PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
			CALL PRINT_STRW, "イエロー_(※お弁当は、相手が[恋慕]or[屈服刻印LV1]or[奉仕精神LV3]なら作ってくれるようになります)"
		ENDIF
	;作ってくれる！
	ELSE
		CALL KOJO_WISH, WISHNAME:CHOICE
		PRINTFORMW %CALLNAME:TARGET%にお弁当を用意してもらえることとなりました。
		CALL SETFLAG, "お弁当", TARGET
		PRINTFORMW 期間は話し合いの末、切り良く\@ (CFLAG:お弁当 > 7) ? 来週 # 次 \@の土曜日までです。
		CALL SETFLAG, "合意：お弁当", TARGET
	ENDIF

CASE 4
	;ピル服用
	IF CFLAG:ピル == 0
		;ピル嫌いの人に服用してもらうのは条件有り
		IF COND("ピル嫌い")
			IF TALENT:恋慕
				CFLAG:ピル = 1
				CALL KOJO_WISH, "ピル"
				PRINTFORMW %CALLNAME:TARGET%にピルでの避妊をお願いしました。
			ELSE
				CALL KOJO_WISH, "ピル服用拒否"
				PRINTFORMW ……%CALLNAME:TARGET%はピルの服用を拒否した。
				PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
				CALL PRINT_STR, "イエロー_(※ピルでの避妊をしてもらうには、相手が[恋慕]である必要があります)_W_"
			ENDIF
		ELSE
			CFLAG:ピル = 1
			CALL KOJO_WISH, "ピル"
			PRINTFORMW %CALLNAME:TARGET%にピルでの避妊をお願いしました。
		ENDIF
	ELSE
		;ピル派の人にやめてもらうのは条件有り
		IF COND("ピル派")
			IF TALENT:恋慕
				CFLAG:ピル = 0
				CALL KOJO_WISH, "ピル"
				PRINTFORMW %CALLNAME:TARGET%にピルでの避妊を中断してもらいました。
				IF CFLAG:絆創膏装着 && COND("合意：危険日生セックス") == 0 && CONFIG("胎内精子で妊娠") && COND("避妊不要") == 0 && BASE:胎児 == 0 && BASE:受精 == 0
					CALL PRINT_STR, "イエロー_(※絆創膏をつけたまま避妊を中断したため、胎内精子量がゼロになります)_W_"
					BASE:胎内精液量 += BASE:胎内精子量
					BASE:胎内精子量 = 0
					BASE:精液の優勢 = 0
				ENDIF
			ELSE
				CALL KOJO_WISH, "ピル中断拒否"
				PRINTFORMW ……%CALLNAME:TARGET%はピルの服用を中止する事を拒否した。
				PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
				CALL PRINT_STR, "イエロー_(※ピルでの避妊をやめてもらうには、相手が[恋慕]である必要があります)_W_"
			ENDIF
		ELSE
			CFLAG:ピル = 0
			CALL KOJO_WISH, "ピル"
			PRINTFORMW %CALLNAME:TARGET%にピルでの避妊を中断してもらいました。
			IF CFLAG:絆創膏装着 && COND("合意：危険日生セックス") == 0 && CONFIG("胎内精子で妊娠") && COND("避妊不要") == 0 && BASE:胎児 == 0 && BASE:受精 == 0
				CALL PRINT_STR, "イエロー_(※絆創膏をつけたまま避妊を中断したため、胎内精子量がゼロになります)_W_"
				BASE:胎内精液量 += BASE:胎内精子量
				BASE:胎内精子量 = 0
				BASE:精液の優勢 = 0
			ENDIF
		ENDIF
	ENDIF
CASE 5
	STR:実行条件 = 
	RESULT = 0
	TRYCALLFORM ADDCOND_K{NO:TARGET}("淫紋")
	ADDCOND_RESULT = RESULT

	;淫紋刻印OK
	IF STR:実行条件 == "" && (COND("淫紋刻印可能") == 2 || ADDCOND_RESULT)
		IF MARK:淫紋 == 0
			PRINTFORMW %CALLNAME:TARGET%はシールは勿論のこと、刻印だろうと受け入れてくれそうです。
			PRINTFORML ……どうしますか？

			CALL PRINT_SELECT, "お試し用のシールを貼る（１週間で消滅します）/実際に刻印を入れる　　（注意！一生消えません！）/やっぱやめておく"

			SELECTCASE RESULT
			CASE 0
				WISHNAME:CHOICE = 淫紋シール
			CASE 1
				WISHNAME:CHOICE = 淫紋
			CASEELSE
				WISHNAME:CHOICE = 
			ENDSELECT
		ENDIF
	;シールならどうか？
	ELSE
		STR:実行条件 = 
		RESULT = 0
		TRYCALLFORM ADDCOND_K{NO:TARGET}("淫紋シール")
		ADDCOND_RESULT = RESULT

		;淫紋シールならOK
		IF STR:実行条件 == "" && (COND("淫紋刻印可能") || ADDCOND_RESULT)
			;刻印の実行条件が特殊な場合にはそれを表示
			STR:実行条件 = 
			TRYCALLFORM ADDCOND_K{NO:TARGET}("淫紋")

			PRINTFORMW %CALLNAME:TARGET%は淫紋シールなら付けてくれそうです。
			IF STR:実行条件 != ""
				CALL PRINT_STRW, STR:実行条件
			ELSEIF TALENT:彼氏持ち
				CALL PRINT_STRW, @"イエロー_(※%TALENT_NAME(GETNUM(TALENT, "彼氏持ち"), TALENT:彼氏持ち)%に刻印を受け入れてもらうには、[欲望LV5]かつ[屈服刻印LV3]かつ[マゾっ気LV3]の必要があります)"
			ELSE
				CALL PRINT_STRW, "イエロー_(※刻印を受け入れてもらうには、[淫乱]or[屈服刻印LV3]or[マゾっ気LV3]の必要があります)"
			ENDIF
			PRINTFORML どうしますか？

			CALL PRINT_SELECT, "シールでＯＫ/やっぱやめておく"

			SELECTCASE RESULT
			CASE 0
				WISHNAME:CHOICE = 淫紋シール
			CASEELSE
				WISHNAME:CHOICE = 
			ENDSELECT
		ELSE
			WISHNAME:CHOICE = 淫紋拒否
		ENDIF
	ENDIF

	SELECTCASE WISHNAME:CHOICE
	CASE "淫紋拒否"
		;刻印の実行条件が特殊な場合にはそれを表示
		STR:実行条件 = 
		IF MARK:淫紋 >= 2
			TRYCALLFORM ADDCOND_K{NO:TARGET}("淫紋")
		ELSE
			TRYCALLFORM ADDCOND_K{NO:TARGET}("淫紋シール")
		ENDIF

		CALL KOJO_WISH, WISHNAME:CHOICE
		PRINTFORMW ……%CALLNAME:TARGET%に淫紋の刻印を断られてしまった。
		PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
		IF STR:実行条件 != ""
			CALL PRINT_STRW, STR:実行条件
		ELSEIF TALENT:彼氏持ち
			IF MARK:淫紋 >= 2
				CALL PRINT_STRW, @"イエロー_(※%TALENT_NAME(GETNUM(TALENT, "彼氏持ち"), TALENT:彼氏持ち)%に刻印を受け入れてもらうには、[欲望LV5]かつ[屈服刻印LV3]かつ[マゾっ気LV3]の必要があります)"
			ELSE
				CALL PRINT_STRW, @"イエロー_(※%TALENT_NAME(GETNUM(TALENT, "彼氏持ち"), TALENT:彼氏持ち)%に淫紋を受け入れてもらうには、[欲望LV3]かつ[屈服刻印LV1]の必要があります)"
			ENDIF
		ELSE
			IF MARK:淫紋 >= 2
				CALL PRINT_STRW, "イエロー_(※刻印を受け入れてもらうには、[淫乱]or[屈服刻印LV3]or[マゾっ気LV3]の必要があります)"
			ELSE
				CALL PRINT_STRW, "イエロー_(※淫紋を受け入れてもらうには、[欲望LV3]or[屈服刻印LV1]の必要があります)"
			ENDIF
		ENDIF
	CASE "淫紋シール"
		MARK:淫紋時計 = 7
		MARK:淫紋 = 1
		;貼る前のセリフ
		CALL KOJO_WISH, "淫紋シール直前"

		;どのタイプかの決定
		PRINTFORMW どの淫紋を%CALLNAME:TARGET%に刻印しますか？形状を選んでください
		PRINTFORMW 淫紋シールは段階だと1LVに相当しますが、2LV以上を選ぶ事で彫った際の形の確認が出来ます
		CALL SIMULATE_INMON, TARGET

		SELECTCASE CONDS("淫紋の種類")
		CASE "基本"
			PRINTFORMW %NAME:TARGET%の下腹部に、可愛らしいハート型のシールを貼りました。
		CASEELSE
			CALL PRINT_STRW, @"%CALLNAME:TARGET%に_黄色_%CONDS("淫紋の種類")%_タイプのシールを貼りました。"
		ENDSELECT
		PRINTL 
		CALL KOJO_WISH, WISHNAME:CHOICE
		PRINTFORMW %CALLNAME:TARGET%に貼られたシールは今から一週間有効となります。
	CASE "淫紋"
		MARK:淫紋 = MAX(MARK:淫紋 + 1, 2)
		SIF MARK:淫紋 <= 3
			MARK:淫紋時計 = 7

		;彫る前のセリフ
		CALL KOJO_WISH, "淫紋直前"

		SELECTCASE MARK:淫紋
		CASE 2
			;どのタイプを彫るかの決定
			PRINTFORMW どの淫紋を%CALLNAME:TARGET%に刻印しますか？形状を選んでください
			PRINTFORMW 段階は彫った時点では2LVですが、それ以外を選ぶことで画数を増やした場合の形の確認が出来ます
			PRINTFORMW (淫紋シールの場合が1LVに相当します)
			CALL SIMULATE_INMON, TARGET

			SELECTCASE CONDS("淫紋の種類")
			CASE "基本"
				IF TALENT:オトコ
					PRINTFORMW %NAME:TARGET%の下腹部に、ハートマークベースの文様を彫り込みました。
				ELSE
					PRINTFORMW %NAME:TARGET%の下腹部に、子宮を象ったハートマークベースの文様を彫り込みました。
				ENDIF
			CASEELSE
				CALL PRINT_STRW, @"%CALLNAME:TARGET%に_黄色_%CONDS("淫紋の種類")%_タイプの淫紋を彫り込みました。"
			ENDSELECT
		CASE 3
			CALL PRINT_CUTIN, "淫紋", TARGET
			IF TALENT:オトコ
				PRINTFORMW %NAME:TARGET%に刻まれた淫紋に装飾を加え、効果を高めました。
			ELSE
				PRINTFORMW %NAME:TARGET%の下腹部…まさに子宮のある場所に彫り込まれた刻印に、卵管と卵巣を示す模様を描き加えました。
			ENDIF
		CASE 4
			CALL PRINT_CUTIN, "淫紋", TARGET
			IF TALENT:オトコ
				PRINTFORMW %NAME:TARGET%の下腹部の刻印に紋様を彫り足して淫紋を完成させました。
			ELSE
				PRINTFORMW %NAME:TARGET%の子宮を透かしたかの様な淫靡な紋章に、子宮頸部を具象した紋様を彫り足して淫紋を完成させました。
			ENDIF
		ENDSELECT

		PRINTL 
		CALL KOJO_WISH, WISHNAME:CHOICE

		CALL PRINT_STRW, @"ハートピンク_＜%NAME:TARGET%は淫紋LV{MARK:淫紋}を得た＞"

		;素質の変化
		IF TALENT:濡れにくい
			TALENT:濡れにくい = 0
			PRINTFORMW ＜%NAME:TARGET%は[%TALENT_NAME(GETNUM(TALENT, "濡れにくい"), TALENT:濡れにくい)%]を失った＞
		ENDIF
		IF TALENT:濡れやすい == 0 && MARK:淫紋 >= 2
			TALENT:濡れやすい = 1
			PRINTFORMW ＜%NAME:TARGET%は[%TALENT_NAME(GETNUM(TALENT, "濡れやすい"), TALENT:濡れやすい)%]を得た＞
		ENDIF
		IF TALENT:子宮性感 == 0 && MARK:淫紋 >= 2
			TALENT:子宮性感 = 1
			SIF TALENT:オトコ == 0
				PRINTFORMW ＜%NAME:TARGET%は[子宮性感]を得た＞
		ENDIF

		MAXBASE:体力 += 200
		BASE:体力 += 200
		PRINTFORMW ＜%NAME:TARGET%の体力が200上昇した＞
		MAXBASE:精力 += 5
		BASE:精力 += 5
		PRINTFORMW ＜%NAME:TARGET%の精力が５上昇した＞ 

		SELECTCASE MARK:淫紋
		CASE 2

		CASE 3
			TALENT:吸精体質 = SET_EXTALENT("吸精体質")
			PRINTFORMW ＜%NAME:TARGET%の吸精体質がLV{TALENT:吸精体質}へと上昇した＞
		CASE 4
			SIF TALENT:オトコ == 0
				PRINTFORMW ＜%NAME:TARGET%の子宮口の柔軟性が上昇した＞
		ENDSELECT

		IF TALENT:射精量強化 < 2 && MARK:淫紋 == 4
			TALENT:射精量強化 = 2
			SIF PENIS(TARGET)
				PRINTFORMW ＜%NAME:TARGET%は[%TALENT_NAME(GETNUM(TALENT, "射精量強化"), TALENT:射精量強化)%]を得た＞
		ELSEIF TALENT:射精量強化 == 0 && MARK:淫紋 <= 3
			TALENT:射精量強化 = 1
			SIF PENIS(TARGET)
				PRINTFORMW ＜%NAME:TARGET%は[%TALENT_NAME(GETNUM(TALENT, "射精量強化"), TALENT:射精量強化)%]を得た＞
		ENDIF

		IF MARK:淫紋 <= 3
			PRINTL 
			PRINTFORMW 今回%NAME:TARGET%に刻んだ淫紋が馴染むのに一週間ほどかかります。
			PRINTFORMW ……さらに画数を増やしたいならば、少々の時を要すということです。
		ENDIF
	ENDSELECT
CASE 6
	;念のためもう一回チェック
	CALL CHECK_COM_ABLE, TARGET
	;すでに頼んでいる場合には中断
	IF COND("性処理指名中")
		CALL KOJO_WISH, WISHNAME:CHOICE
		PRINTFORMW %CALLNAME:TARGET%を性処理目的で呼び出すのを止める事にしました。
		;チョーカーを外す
		CALL RESET_OTHER, "黒いチョーカー"
		CLEARBIT CFLAG:性処理, 62
	ELSEIF COND("性処理可能") == 0
		CALL KOJO_WISH, "性処理拒否"
		PRINTFORMW ……%CALLNAME:TARGET%に断られてしまった。
		PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
		IF COND("性処理コマンド実行可能") == 0
			CALL PRINT_STRW, @"イエロー_(※して貰うには、ある程度の性経験に加えて[欲望LV5]or[奉仕精神LV5]or[屈服刻印LV2]が必要です)"
		ELSE
			CALL PRINT_STRW, "イエロー_(※性処理をして貰うには、[欲望LV5]or[奉仕精神LV5]or[屈服刻印LV2]が必要です)"
		ENDIF
	;してくれる！
	ELSE
		CALL KOJO_WISH, WISHNAME:CHOICE
		PRINTFORMW %CALLNAME:TARGET%を休み時間に呼び出して、性処理してもらえるようになりました。

		;性処理係の証をプレゼント
		SIF COND("合意：性処理") == 0
			CALL PRINT_STRW, @"%CALLNAME:MASTER%の性処理係になった証として、%CALLNAME:TARGET%に_黄色_[黒いチョーカー]_を贈りました。"

		CALL SETFLAG, "合意：性処理", TARGET
		CALL SETFLAG, "性処理係の証"

		;条件など
		PRINTFORMW プレイ内容についても、話し合っておくことにします。
		CALL SETFLAG, "性処理でのプレイ内容", TARGET
		SETBIT CFLAG:性処理, 62
	ENDIF

CASE 7
	;見せてくれる
	IF COND("尻尾露呈可能") || COND("合意：尻尾を見せる")
		CALL KOJO_WISH, WISHNAME:CHOICE
		CALL SETFLAG, "尻尾を見せる", TARGET
		CALL SETFLAG, "合意：尻尾を見せる", TARGET
	ELSE
		CALL KOJO_WISH, "尻尾を見せて拒否"
		PRINTFORMW ……%CALLNAME:TARGET%に断られてしまった。
		PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
		CALL PRINT_STR, "イエロー_(※尻尾を見せて貰うには、[信頼LV5]or[屈服刻印LV1]or[恋慕]が必要です)_W_"
	ENDIF
;裸になって欲しい
CASE 8
	;イベントとして呼び出せるように関数化
	CALL EVENT_WISH_HADAKA

;***着床判定追加パッチで追加
;アフターピル
CASE 9
	;取引に関わるTFLAG初期化
	TFLAG:値切った = 0
	TFLAG:値上げ交渉 = 0
	TFLAG:取引拒否 = 0

	;基本価格は$1,500
	TFLAG:取引価格 = 1500

	MEMO_LINECOUNT = LINECOUNT
	CALL KOJO_WISH, "アフターピル取引"
	;もしも口上側で拒否したなら…
	IF TFLAG:取引拒否
		TFLAG:取引承諾 = 0
	ELSE
		TFLAG:取引承諾 = 1
	ENDIF

	IF TFLAG:取引承諾
		;アフターピルは高価ですし、あなたへのペナルティも兼ねてただでは飲んでもらえません
		PRINTFORMW %CALLNAME:TARGET%はアフターピルの使用を受け入れてくれました。
		PRINTFORMW ……が、%CALLNAME:MASTER%にも%MONEY_C(TFLAG:取引価格)%払うようにと求めてきました。

		PRINTFORML どうしますか？（所持金：%MONEY_C(MONEY)%）
		EVENT_TABLE = %CALLNAME:TARGET%の要求通りに%MONEY_C(TFLAG:取引価格)%払う\/払わない\/拝み倒す

		VARSET LOCALS
		SPLIT EVENT_TABLE, "/", LOCALS
		CALL PRINT_SELECT, EVENT_TABLE

		SELECTCASE LOCALS:RESULT
		CASE @"%CALLNAME:TARGET%の要求通りに%MONEY_C(TFLAG:取引価格)%払う"
			;お金を払う場合
			IF MONEY >= TFLAG:取引価格
			;お金がない
			ELSE
				PRINTFORMW ……代金を払おうとしたが持ち合わせがない！！
				;危ないときは仕方ないので許してくれます。相手も妊娠したいわけではないので
				IF COND("受精確認") || COND("胎内の精液量")
					IF TALENT:恋慕
						TFLAG:取引承諾 = 0
					ELSE
						TFLAG:取引価格 = MONEY
						TFLAG:値切った = 1
					ENDIF
				ELSE
					TFLAG:取引承諾 = 0
				ENDIF
			ENDIF
		CASE "払わない"
			TFLAG:取引承諾 = 0
		CASE "拝み倒す"
			PRINTFORMW %CALLNAME:PLAYER%は、代金は払えないがとにかく頼むと必死で%CALLNAME:TARGET%を拝み倒した。
			TFLAG:取引価格 = 0
			;恋慕状態なら仕方なく許してくれます
			SIF TALENT:恋慕 == 0
				TFLAG:取引承諾 = 0
		ENDSELECT
	ENDIF

	PRINTL 
	MEMO_LINECOUNT = LINECOUNT
	IF TFLAG:取引承諾
		CALL KOJO_WISH, "アフターピル"
		;台詞が無い場合には展開に応じた地の文を出す
		IF LINECOUNT == MEMO_LINECOUNT
			;代金をまけてくれた場合
			IF TFLAG:値切った
				PRINTFORMW %CALLNAME:TARGET%は呆れたように、妊娠したくないからと\@ MONEY ? 所持金全額で # \@許してくれました。
			;代金を払わなくても許してくれた場合
			ELSEIF TFLAG:取引価格 == 0
				PRINTFORMW %CALLNAME:TARGET%は仕方なく、今回だけと言って許してくれました。
			ELSE

			ENDIF
			PRINTL 
		ENDIF

		IF TFLAG:取引価格 > 0
			PRINTFORML 所持金‐%MONEY_C(TFLAG:取引価格)%
			MONEY -= TFLAG:取引価格
		ENDIF
		;アフターピルの強度は有効日数
		TALENT:アフターピル = 7
		PRINTFORMW %CALLNAME:TARGET%にアフターピルを飲んでもらいました。
	ELSE
		CALL KOJO_WISH, "アフターピル拒否"
		;台詞が無い場合には展開に応じた地の文を出す
		IF LINECOUNT == MEMO_LINECOUNT
			IF (COND("受精確認") || COND("胎内の精液量") ) && TALENT:恋慕
				PRINTFORMW %CALLNAME:TARGET%は視線を逸らし、それじゃあ避妊できなくても仕方ないと言いました。
				PRINTFORMW 心なしか頬が赤いような……
			;拝み倒して失敗した場合
			ELSEIF TFLAG:取引価格 == 0
				PRINTFORMW ……%CALLNAME:TARGET%は許してくれそうにありません。
			ENDIF
			PRINTL 
		ENDIF
		;相手も妊娠したくはないですが、あなたが無責任な対応をしたので飲んでくれません
		PRINTFORMW %CALLNAME:TARGET%にアフターピルを飲んでもらえませんでした。
	ENDIF

CASE 39

	STR:実行条件 = 
	RESULT = 0
	TRYCALLFORM ADDCOND_K{NO:TARGET}("避妊の護符の解除")
	ADDCOND_RESULT = RESULT

	IF TALENT:避妊の護符 == 0 && COND("初期避妊の護符")
		CALL KOJO_WISH, "避妊の護符"
		TALENT:避妊の護符 = 1
		PRINTFORMW ＜%NAME:TARGET%は[避妊の護符]を身に着けた＞
	;特殊条件で解除を拒否した場合
	ELSEIF STR:実行条件 != ""
		CALL KOJO_WISH, "避妊解除拒否"
		CALL PRINT_STRW, STR:実行条件
	ELSEIF TALENT:恋慕 || ADDCOND_RESULT
		CALL KOJO_WISH, "避妊解除可"
		TALENT:避妊の護符 = 0
		PRINTFORMW ＜%NAME:TARGET%は[避妊の護符]を失った＞
		PRINTFORMW %CALLNAME:TARGET%は自らの意思で避妊の護符を解除しました。
	ELSE
		CALL KOJO_WISH, "避妊解除拒否"
		PRINTFORMW ……%CALLNAME:TARGET%は避妊結界の解除を拒否した。
		PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
		CALL PRINT_STR, "イエロー_(※避妊を解除してもらうには、相手が[恋慕]である必要があります)_W_"
	ENDIF

CASE 40
	;髪を弄らせてくれる
	IF COND("髪型変更のお願い可能") || COND("合意：髪を弄る")
		CALL CHANGE_HAIRSTYLE
	ELSE
		CALL KOJO_WISH, "髪を弄る拒否"
		PRINTFORMW ……%CALLNAME:TARGET%に断られてしまった。
		PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
		SELECTCASE TRUE_SEX(TARGET)
		CASE "ふたなり", "オンナ"
			CALL PRINT_STRW, "イエロー_(※髪を弄らせて貰うには、[信頼LV5]or[屈服刻印LV3]or[恋慕]が必要です)"
		CASE "オトコ"
			CALL PRINT_STRW, "イエロー_(※髪を弄らせて貰うには、[信頼LV3]or[屈服刻印LV1]or[恋慕]が必要です)"
		ENDSELECT
	ENDIF

CASE 41
	;履き替えてくれる
	IF COND("着替え希望了承")
		CALL CHANGE_PANST
	ELSE
		CALL KOJO_WISH, "パンスト着替え拒否"
		PRINTFORMW ……%CALLNAME:TARGET%に断られてしまった。
		PRINTFORMW %CALLNAME:TARGET%が心変わりした後ならあるいは……
		CALL PRINT_STRW, "イエロー_(※パンストを履き替えてもらうには、[信頼LV3]に加えて[露出癖LV3]or[奉仕精神LV5]が必要です)"
	ENDIF
CASE 50 TO 52
	CALL KOJO_WISH, WISHNAME:CHOICE
	CALL SET_WISH, WISHNAME:CHOICE
;パートナーのちんこサイズを変更する。ここに来たなら魂 > 100なので魂が足りない場合の警告は考えなくていい
CASE 53
	;ちんこサイズの変更受け入れ
	IF COND("巨根化して可能")
		CALL KOJO_WISH, WISHNAME:CHOICE
	ELSE
		CALL KOJO_WISH, "巨根化して拒否"
		TFLAG:取引拒否 = 1
	ENDIF
	IF TFLAG:取引拒否 == 0
		;叶えたのは誰？
		IF COND("悪魔の取引可能") + COND("天使の取引可能")
			CALL SET_WISH, WISHNAME:CHOICE
		ELSE
			CALL SET_WISH, WISHNAME:CHOICE, ASSI
		ENDIF
	ELSE
		PRINTFORMW ……%CALLNAME:TARGET%に断られてしまった。
		PRINTFORMW %CALLNAME:TARGET%ともっと仲良くなればあるいは……
	ENDIF
CASE 54
	;ちんこサイズの変更受け入れ
	IF COND("短小化して可能")
		CALL KOJO_WISH, WISHNAME:CHOICE
	ELSE
		CALL KOJO_WISH, "短小化して拒否"
		TFLAG:取引拒否 = 1
	ENDIF
	IF TFLAG:取引拒否 == 0
		;叶えたのは誰？
		IF COND("悪魔の取引可能") + COND("天使の取引可能")
			CALL SET_WISH, WISHNAME:CHOICE
		ELSE
			CALL SET_WISH, WISHNAME:CHOICE, ASSI
		ENDIF
	ELSE

		PRINTFORMW ……%CALLNAME:TARGET%に断られてしまった。
		PRINTFORMW %CALLNAME:TARGET%ともっと仲良くなればあるいは……
	ENDIF
CASE 60 TO 89
	;口上で作った選択肢を選んだならここで地の文と台詞が出る
	TRYCALLFORM KOJO_WISH_K{NO:TARGET}_ADDSELECT, WISHNAME:CHOICE

CASE 98
	MEMO_NICKNAME = %NICKNAME:TARGET%
	CALL KOJO_WISH, WISHNAME:CHOICE

	CALL NICKNAME_CONVERT, TARGET
	PRINTL 
	;変更した
	IF MEMO_NICKNAME != NICKNAME:TARGET
		CALL KOJO_WISH, "あだ名変更完了"
	ELSE
		CALL KOJO_WISH, "あだ名変更キャンセル"
	ENDIF
	IF TFLAG:取引拒否
		NICKNAME:TARGET = 
		SIF MEMO_NICKNAME != ""
			NICKNAME:TARGET = %MEMO_NICKNAME%
	ENDIF
CASE 99
	IF CEVENTS("二人称主人") != ""
		MEMO_CNAME = 二人称主人：%CEVENTS("二人称主人")%
		MEMO_CNAME_KANA = 二人称主人かな：%CEVENTS("二人称主人かな")%
	ENDIF

	MEMO_MASTERCALLNAME = %CNAME("主人")%
	CALL KOJO_WISH, WISHNAME:CHOICE

	CALL NAME_CONVERT_SP, TARGET
	PRINTL 
	;変更した
	IF MEMO_MASTERCALLNAME != CNAME("主人")
		CALL KOJO_WISH, "呼び名変更完了"
	ELSE
		CALL KOJO_WISH, "呼び名変更キャンセル"
	ENDIF
	IF TFLAG:取引拒否
		CALL DEL_CEVENT_GROUP_EX, "二人称主人", TARGET
		IF MEMO_CNAME != ""
			CALL SET_CEVENT, MEMO_CNAME, TARGET
			CALL SET_CEVENT, MEMO_CNAME_KANA, TARGET
		ENDIF
	ENDIF
;以下は魂が必要
CASEELSE
	IF BASE:MASTER:魂 <= 100 && NO_SOUL:CHOICE == 0
		CALL KOJO_WISH, "魂が足りない"
	ELSE
		SELECTCASE CHOICE
		CASE 10
			CALL KOJO_WISH, WISHNAME:CHOICE
			CALL SET_WISH, WISHNAME:CHOICE
		;素質の単純獲得
		CASE 16
			CALL KOJO_WISH, WISHNAME:CHOICE
			CALL SET_WISH, WISHNAME:CHOICE
		CASE 11 TO 19
			CALL KOJO_WISH, WISHNAME:CHOICE
			CALL SET_WISH, WISHNAME:CHOICE
			IF TALENT:PLAYER:(WISHNAME:CHOICE) == 1
				PRINTL 
				PRINTFORMW [%WISHNAME:CHOICE%]は最大で9LVまで取得できます。余裕がある時に伸ばすと良いでしょう。
			ENDIF
		;早漏or遅漏
		CASE 28
			CALL KOJO_WISH, WISHNAME:CHOICE
			CALL SET_WISH, WISHNAME:CHOICE
		;肉体変化＆性別変化
		CASE 20 TO 32
			;元の体型を記憶
			MEMO_BODY = %CONDS("体型", PLAYER)%

			CALL KOJO_WISH, WISHNAME:CHOICE
			CALL SET_WISH, WISHNAME:CHOICE

			;3サイズをチェック
			CALL SET_3SIZE, PLAYER
			;体型自体が変わった？
			IF MEMO_BODY != CONDS("体型", PLAYER)
				PRINTFORM 変化に伴い、%NAME:PLAYER%の体型は
				SETCOLOR DEF_COLOR("黄色")
				PRINTFORM [%CONDS("体型", PLAYER)%]
				RESETCOLOR
				PRINTFORMW となりました。
			ENDIF
		ENDSELECT
	ENDIF
ENDSELECT

;何か契約したりしたなら行動の記録をして終了
IF STR:日常イベント名 != ""
	PLAYER = MASTER
	CALL SET_PREV_DAILYCOM
	;現実時間フラグ取得
	CALL GET_REALTIMEFORFLAG
	RETURN 1
ENDIF

RESTART


;-------------------------------------------------
;パンツ取引
;-------------------------------------------------
@EVENT_WISH_PANTU
#DIMS EVENT_TABLE
#DIM MEMO_LINECOUNT

;取引に関わるTFLAG初期化
TFLAG:取引承諾 = 0
TFLAG:取引価格 = 0
TFLAG:値切った = 0
TFLAG:値上げ交渉 = 0
TFLAG:取引拒否 = 0

;パンツ取引中フラグON
SETBIT FLAG:状況判定, 30

;ノーパンor合意獲得なら魂は不要
IF EQUIP:パンツ == 0
	CALL KOJO_WISH, "ノーパン"
	IF COND("身嗜み：ノーパン") == 0 && COND("パンツ形状確認") == 0
		PRINTFORMW ……%CALLNAME:TARGET%はパンツを穿いていないようだ！
		CALL SETFLAG_DAILY, "パンツ形状確認"
	ENDIF
;魂の取引の場合
ELSEIF COND("魂の取引可能") || COND("天使の取引可能")
	IF COND("合意：パンツ取引")
		IF CHARA_NUM("室内の邪魔者") == 0 && PLACE("野外") == 0
			TFLAG:取引承諾 = 1
		ELSE
			TCVAR:やる気 = 0
			;着せ替えの実行判定
			CALL COM_ORDER_COM175
			TCVAR:やる気 = RESULT
			SIF TCVAR:やる気 > 0
				TFLAG:取引承諾 = 1
		ENDIF
		PRINTL 

		;魂の取引をする場合は基本的にここは空白。だが何かを条件に断っても良い
		CALL KOJO_WISH, "パンツ取引"

		SIF TFLAG:取引拒否
			TFLAG:取引承諾 = 0

		IF TFLAG:取引承諾
			MEMO_LINECOUNT = LINECOUNT
			CALL KOJO_WISH, "パンツ"
			;台詞が無い場合には、パンツを脱いで渡してくる汎用地の文を出す
			SIF LINECOUNT == MEMO_LINECOUNT
				CALL DAILY_MESSAGE, "パンツを脱いで手渡し"
			CALL SET_WISH, "パンツ"
			STR:日常イベント名 = パンツ取引
		ELSE
			CALL KOJO_WISH, "パンツ拒否"
			PRINTFORMW %CALLNAME:TARGET%に断られてしまった。
		ENDIF
	ELSE
		;魂不足
		IF BASE:MASTER:魂 <= 100
			CALL KOJO_WISH, "魂が足りない"
		ELSE
			MEMO_LINECOUNT = LINECOUNT
			CALL KOJO_WISH, "パンツ"
			;台詞が無い場合には、パンツを脱いで渡してくる汎用地の文を出す
			SIF LINECOUNT == MEMO_LINECOUNT
				CALL DAILY_MESSAGE, "パンツを脱いで手渡し"
			CALL SET_WISH, "パンツ"
			STR:日常イベント名 = パンツ取引
		ENDIF
	ENDIF
ELSE
	IF COND("合意：パンツ取引") && CHARA_NUM("室内の邪魔者") == 0 && PLACE("野外") == 0
		TFLAG:取引承諾 = 1
	ELSE
		TCVAR:やる気 = 0
		;着せ替えの実行判定
		CALL COM_ORDER_COM175
		TCVAR:やる気 = RESULT
		SIF TCVAR:やる気 > 0
			TFLAG:取引承諾 = 1
	ENDIF
	PRINTL 

	;基本価格は$500
	TFLAG:取引価格 = 500

	MEMO_LINECOUNT = LINECOUNT
	CALL KOJO_WISH, "パンツ取引"
	SIF TFLAG:取引拒否
		TFLAG:取引承諾 = 0

	IF TFLAG:取引承諾
		IF TFLAG:取引価格
			;台詞が無い場合には、お金要求を示唆
			SIF LINECOUNT == MEMO_LINECOUNT
				PRINTFORMW %CALLNAME:TARGET%は、%PANTIES(TARGET, "略")%を買い替えるお金を貰えるなら受けてくれそうだ。
			PRINTFORML どうしますか？（所持金：%MONEY_C(MONEY)%）
			SELECTCASE TFLAG:取引価格
			;割高時
			CASE IS > 500
				IF MONEY >= 500
					EVENT_TABLE = %CALLNAME:TARGET%の要求通りに%MONEY_C(TFLAG:取引価格)%払う\/……%MONEY_C(500)%なら払ってもいい\/払わない\/
				ELSE
					EVENT_TABLE = %CALLNAME:TARGET%の要求通りに%MONEY_C(TFLAG:取引価格)%払う\/払わない\/
				ENDIF
			CASEELSE
				EVENT_TABLE = 替えのパンツ代など諸々考慮して%MONEY_C(TFLAG:取引価格)%払う\/払わない\/
			ENDSELECT

			VARSET LOCALS
			SPLIT EVENT_TABLE, "/", LOCALS
			CALL PRINT_SELECT, EVENT_TABLE

			SELECTCASE LOCALS:RESULT
			CASE @"%CALLNAME:TARGET%の要求通りに%MONEY_C(TFLAG:取引価格)%払う", @"替えのパンツ代など諸々考慮して%MONEY_C(TFLAG:取引価格)%払う"
				IF MONEY < TFLAG:取引価格
					PRINTFORMW 代金を払おうとしたが持ち合わせがない……！
					TFLAG:取引承諾 = 0
				ENDIF
			CASE @"……%MONEY_C(500)%なら払ってもいい"
				TFLAG:取引価格 = 500
				TFLAG:取引承諾 = 1
				TFLAG:値切った = 1
			CASE "払わない"
				TFLAG:取引承諾 = 0
			ENDSELECT
		ENDIF

		IF TFLAG:取引承諾
			;ここで値切った際にTFLAG:取引承諾 = 0に出来る
			MEMO_LINECOUNT = LINECOUNT
			CALL KOJO_WISH, "パンツ"

			IF TFLAG:取引承諾
				CALL SET_WISH, "パンツ"
				;台詞が無い場合には、パンツを脱いで渡してくる汎用地の文を出す
				SIF LINECOUNT == MEMO_LINECOUNT
					CALL DAILY_MESSAGE, "パンツを脱いで手渡し"
				STR:日常イベント名 = パンツ取引
			ENDIF
		ELSE
			CALL KOJO_WISH, "パンツ拒否"
			PRINTFORMW %CALLNAME:TARGET%に断られてしまった。
		ENDIF
	ELSEIF TFLAG:取引承諾 == 0
		IF TFLAG:取引価格 && TFLAG:値上げ交渉 && MONEY >= TFLAG:取引価格 + TFLAG:値上げ交渉
			CALL KOJO_WISH, "パンツ拒否"
			PRINTFORMW %CALLNAME:TARGET%に断られてしまった。
			PRINTL 
			PRINTFORMW ……もうちょい食い下がってみようか？（所持金：%MONEY_C(MONEY)%）
			EVENT_TABLE = 奮発して%MONEY_C(TFLAG:取引価格 + TFLAG:値上げ交渉)%払うと言い出してみる\/あきらめる\/
			VARSET LOCALS
			SPLIT EVENT_TABLE, "/", LOCALS
			CALL PRINT_SELECT, EVENT_TABLE

			SELECTCASE LOCALS:RESULT
			CASE @"奮発して%MONEY_C(TFLAG:取引価格 + TFLAG:値上げ交渉)%払うと言い出してみる"
				TFLAG:取引承諾 = 1
				TFLAG:取引価格 += TFLAG:値上げ交渉

				MEMO_LINECOUNT = LINECOUNT
				CALL KOJO_WISH, "パンツ"
				;台詞が無い場合には、パンツを脱いで渡してくる汎用地の文を出す
				SIF LINECOUNT == MEMO_LINECOUNT
					CALL DAILY_MESSAGE, "パンツを脱いで手渡し"

				CALL SET_WISH, "パンツ"
				STR:日常イベント名 = パンツ取引
			CASE "あきらめる"

			ENDSELECT
		ELSE
			CALL KOJO_WISH, "パンツ拒否"
			PRINTFORMW %CALLNAME:TARGET%に断られてしまった。
		ENDIF
	ENDIF
ENDIF

;パンツ取引中フラグOFF
CLEARBIT FLAG:状況判定, 30


;-------------------------------------------------
;裸になって欲しい
;-------------------------------------------------
@EVENT_WISH_HADAKA
#DIMS EVENT_TABLE
#DIM MEMO_LINECOUNT
#DIM ACTOR
;淫紋が見えるようになったかをチェックする
#DIM IS_VISIBLE_INMON

;淫紋確認の地の文を出すかのチェックに使う
IS_VISIBLE_INMON = CHECK_CLO("淫紋露出")

;取引に関わるTFLAG初期化
TFLAG:取引承諾 = 0
TFLAG:取引価格 = 0
TFLAG:値切った = 0
TFLAG:値上げ交渉 = 0
TFLAG:取引拒否 = 0

;全裸取引中フラグON
SETBIT FLAG:状況判定, 32

;何度もお願いするのはしつこい
IF CHOSE_DEVENT("裸になった")
	PRINTFORMW 裸になるように何度も頼むのは嫌われそうだ……今はやめておこう。
	RETURN 0
	;全裸取引中フラグOFF
	CLEARBIT FLAG:状況判定, 32
ELSEIF COND("合意：全裸化") && CHARA_NUM("室内の邪魔者") == 0 && PLACE("個室")
	TFLAG:取引承諾 = 1
ELSE
	TCVAR:やる気 = 0
	;着せ替えの実行判定
	CALL COM_ORDER_COM175
	TCVAR:やる気 = RESULT
	SIF TCVAR:やる気 > 0
		TFLAG:取引承諾 = 1
ENDIF
PRINTL 

MEMO_LINECOUNT = LINECOUNT
CALL KOJO_WISH, "全裸取引"
SIF TFLAG:取引拒否
	TFLAG:取引承諾 = 0

IF TFLAG:取引承諾
	;ここで値切った際にTFLAG:取引承諾 = 0に出来る
	MEMO_LINECOUNT = LINECOUNT
	CALL KOJO_WISH, "全裸"
	;台詞が無い場合には汎用地の文を出す
	SIF LINECOUNT == MEMO_LINECOUNT
		CALL EVENT_DATUI, "ハダカ"
ELSE
	IF TFLAG:値上げ交渉 && MONEY >= TFLAG:値上げ交渉
		CALL KOJO_WISH, "全裸拒否"
		PRINTFORMW %CALLNAME:TARGET%に断られてしまった。
		PRINTL 
		PRINTFORMW ……もうちょい食い下がってみようか？（所持金：%MONEY_C(MONEY)%）
		EVENT_TABLE = 奮発して%MONEY_C(TFLAG:値上げ交渉)%払うと言い出してみる\/あきらめる\/
		VARSET LOCALS
		SPLIT EVENT_TABLE, "/", LOCALS
		CALL PRINT_SELECT, EVENT_TABLE

		SELECTCASE LOCALS:RESULT
		CASE @"奮発して%MONEY_C(TFLAG:値上げ交渉)%払うと言い出してみる"
			TFLAG:取引承諾 = 1
			TFLAG:取引価格 += TFLAG:値上げ交渉

			MEMO_LINECOUNT = LINECOUNT
			CALL KOJO_WISH, "全裸"
			;台詞が無い場合には汎用地の文を出す
			SIF LINECOUNT == MEMO_LINECOUNT
				CALL EVENT_DATUI, "ハダカ"
		CASE "あきらめる"

		ENDSELECT
	ELSE
		CALL KOJO_WISH, "全裸拒否"
		PRINTFORMW %CALLNAME:TARGET%に断られてしまった。
	ENDIF
ENDIF

IF TFLAG:取引承諾 && TFLAG:取引拒否 == 0
	MONEY -= TFLAG:取引価格
	SIF TFLAG:取引価格
		PRINTFORMW 所持金‐%MONEY_C(TFLAG:取引価格)%

	CALL SETFLAG, "全裸化", TARGET
	CALL SETFLAG, "合意：全裸化", TARGET

	;淫紋イベント
	SIF IS_VISIBLE_INMON == 0 && MARK:淫紋 >= 2 && CFLAG:淫紋の様子 == 0 && CHECK_CLO("淫紋露出")
		CALL TEXT, "淫紋の様子：脱衣後"

	;誰かひとりの反応を出す
	IF FIND_COND("現在存在", "起床中")
		DRAWLINE
		MEMO_LINECOUNT = LINECOUNT
		ACTOR = FIND_COND("現在存在", "起床中")
		CALL KOJO_GREETING, "全裸化への反応", ACTOR
		IF LINECOUNT == MEMO_LINECOUNT
			IF COND("合意：全裸化", ACTOR)
				IF TALENT:ACTOR:恥薄い
					PRINTFORML 「…………」
				ELSE
					CALL PRINT_STRL, "「……_／／／_」"
				ENDIF
				PRINTFORMW 裸体を衆目に晒す%CALLNAME:TARGET%の姿に、%CALLNAME:ACTOR%は自分が脱いだ時のことを思い出している様だ……
			ELSEIF DEVENT("全裸化目撃", ACTOR) == 0
				PRINTFORML 「……！？」
				PRINTFORMW %CALLNAME:MASTER%に請われるがままに服を脱ぎ捨てて裸体を晒している%CALLNAME:TARGET%に、%CALLNAME:ACTOR%は驚きを隠せない様だ……
				CALL SET_DEVENT, "全裸化目撃", ACTOR
			ELSE
				PRINTFORML 「……」
				PRINTFORMW %CALLNAME:ACTOR%は何とも言えない表情で%CALLNAME:TARGET%に無茶ぶりしている%CALLNAME:MASTER%を見ている……
			ENDIF
		ENDIF
	ENDIF
	STR:日常イベント名 = 裸になった
ENDIF

;全裸取引中フラグOFF
CLEARBIT FLAG:状況判定, 32

;-------------------------------------------------
;髪型を長さを変えない範囲で弄ります
;-------------------------------------------------
@CHANGE_HAIRSTYLE
#DIM MEMO_ASSI
#DIM MEMO_ASSI2
#DIM ASSI_CALLED
#DIMS HAIR_TABLE
#DIMS MEMO_HAIR
#DIMS AFTER_HAIR
#DIMS MEMO_HAIRTYPE
#DIM MEMO_LINECOUNT

MEMO_HAIR = %CONDS("髪型")%\/%CSTR:髪の色%\/%CSTR:髪の長さ%\/%CSTR:前髪%\/%CSTR:後ろ髪%\/%CSTR:アレンジ%\/%CSTR:アホ毛%\/%CSTR:リボン%\/
MEMO_HAIRTYPE = %CONDS("髪型")%

;アドバイザーを記録
MEMO_ASSI = ASSI

;髪を弄るにあたり補助役が一人現れる。アドバイザーが現在いるならその人
IF ASSI && ASSI != TARGET && COND("現在存在", ASSI)
;現在アドバイザーが居ないなら誰かが参加してくる
ELSE
	ASSI = FIND_COND("助手可能", "現在存在")
ENDIF
;指名お手伝いの判定用
MEMO_ASSI2 = ASSI
ASSI_CALLED = 0

CALL KOJO_WISH, "髪を弄る"

;お手伝いの指名があった？
SIF ASSI && ASSI != MEMO_ASSI2
	ASSI_CALLED = ASSI

CALL SETFLAG, "合意：髪を弄る", TARGET

;結局補助役は居るのか？
SELECTCASE ASSI
;補助なし
CASE 0
	PRINTFORMW それでは%CALLNAME:TARGET%の髪型を弄ります。
;指名された人が補助
CASE ASSI_CALLED
	PRINTFORMW それでは、%CALLNAME:ASSI%にも手伝って貰いながら%CALLNAME:TARGET%の髪型を弄ります。
;アドバイザーの人が補助
CASE MEMO_ASSI
	PRINTFORMW 折角なので、%CALLNAME:ASSI%にも手伝って貰いながら%CALLNAME:TARGET%の髪型を弄ります。
;野次馬が参加
CASEELSE
	PRINTFORMW 楽しそう、と寄ってきた%CALLNAME:ASSI%にも手伝って貰いながら%CALLNAME:TARGET%の髪型を弄ります。
ENDSELECT
IF CFLAG:顔グラ
	PRINTFORML 現在の%CALLNAME:TARGET%の髪型は%CONDS("髪色")%%CONDS("髪型")%ですが……どうしますか？
ELSEIF CFLAG:髪型変更のお願い中
	PRINTFORML 現在の%CALLNAME:TARGET%の髪型は%CONDS("髪型")%ですが……どうしますか？
ELSE
	PRINTFORML ……どの髪型になってもらいましょうか？
ENDIF

SETBIT FLAG:状況判定, 26

;顔グラ表示がONになっているキャラ
IF CFLAG:顔グラ && CONFIG("顔グラＯＮ")
	CALL SIMULATE_MAIDFACE, TARGET
ELSE
	SELECTCASE CONDS("アレンジ")
	CASE "ノーアレンジ"
		HAIR_TABLE = ツインテール/サイドテール/ポニーテール/三つ編み/お団子/
	CASE "ツインテール"
		HAIR_TABLE = ノーアレンジ/サイドテール/ポニーテール/三つ編み/お団子/
	CASE "サイドテール"
		HAIR_TABLE = ノーアレンジ/ツインテール/ポニーテール/三つ編み/お団子/
	CASE "ポニーテール"
		HAIR_TABLE = ノーアレンジ/ツインテール/サイドテール/三つ編み/お団子/
	CASE "三つ編み"
		HAIR_TABLE = ノーアレンジ/ツインテール/サイドテール/ポニーテール/お団子/
	CASE "お団子"
		HAIR_TABLE = ノーアレンジ/ツインテール/サイドテール/ポニーテール/三つ編み/
	ENDSELECT
	HAIR_TABLE += "やっぱやめる/"

	VARSET LOCALS
	SPLIT HAIR_TABLE, "/", LOCALS
	CALL PRINT_SELECT, HAIR_TABLE

	SIF LOCALS:RESULT != "やっぱやめる"
		CSTR:アレンジ = %LOCALS:RESULT%
ENDIF

AFTER_HAIR = %CSTR:髪の色%\/%CSTR:髪の長さ%\/%CSTR:前髪%\/%CSTR:後ろ髪%\/%CSTR:アレンジ%\/%CSTR:アホ毛%\/%CSTR:リボン%\/

IF MEMO_HAIR != AFTER_HAIR
	;期間は2週間
	CFLAG:髪型変更のお願い中 = 14
	IF MEMO_HAIRTYPE != CONDS("髪型")
		IF CFLAG:顔グラ
			CALL PRINT_STRW, @"%CALLNAME:TARGET%に_黄色_%CONDS("髪色")%%CONDS("髪型")%_になってもらいました"
		ELSE
			CALL PRINT_STRW, @"%CALLNAME:TARGET%に_黄色_%CONDS("髪型")%_になってもらいました"
		ENDIF
	ELSE
		CALL PRINT_STRW, @"%CALLNAME:TARGET%に髪型を少し変えてもらいました"
	ENDIF
	CALL PRINT_STRW, @"だいたい_黄色_２週間_ぐらいは、このままで居てくれるそうです"
	PRINTL 
	MEMO_LINECOUNT = LINECOUNT
	CALL KOJO_WISH, "髪型変更の感想", "改行無し"
	SIF LINECOUNT == MEMO_LINECOUNT
		CLEARLINE 1
ELSE
	PRINTFORMW やっぱりやめました。
ENDIF

;アドバイザーを戻す
ASSI = MEMO_ASSI

CLEARBIT FLAG:状況判定, 26

;-------------------------------------------------
;パンストを着替えてもらう
;-------------------------------------------------
@CHANGE_PANST
#DIM CHOICE
#DIM MEMO_LINECOUNT
#DIMS MEMO_OUTER
#DIMS COLOR_TABLE
#DIMS COLOR_CHOICE

MEMO_OUTER = %OUTER(TARGET)%

PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:MASTER%の希望を聞いてくれるそうです。
SIF CONDS("パンスト") != ""
	CALL PRINT_STRW, "黄色_今後パンストを履く際にも、ここでのお願いを高確率で聞いてくれるようになります"
IF CHECK_OUTER("パンスト")
	PRINTFORML 	現在%CALLNAME:TARGET%が履いているのは[%OUTER(TARGET)%]ですが……どれをお願いしますか？
ELSEIF EQUIP:アウター
	PRINTFORML 	現在%CALLNAME:TARGET%は%OUTER(TARGET)%を履いているため着替えてはもらえませんが……どれをお願いしますか？
ELSEIF EQUIP:ボディスーツ
	PRINTFORML 	現在%CALLNAME:TARGET%は%BODYS(TARGET)%を着ているため着替えてはもらえませんが……どれをお願いしますか？
ELSE
	PRINTFORML 	現在%CALLNAME:TARGET%はパンストを履いていませんが……どれをお願いしますか？
ENDIF
PRINTFORML [ 0] 脱いで貰う
PRINTFORML [ 1] 通常のパンスト
PRINTFORML [ 2] 網パンスト
PRINTFORML [ 3] オープンパンスト
PRINTFORML [ 4] やっぱりやめる
CALL INPUT_SELECT, 5

CHOICE = RESULT

SELECTCASE CHOICE
CASE 4
	CALL DEL_CEVENT_GROUP, "パンスト"
	RETURN 0
CASE 0
	CALL DEL_CEVENT_GROUP, "パンスト"
	CALL SET_CEVENT, "パンスト：生足"
	CALL SETFLAG, "パンスト着替えのお願い"
	IF OUTER(TARGET) != MEMO_OUTER
		PRINTFORMW %CALLNAME:TARGET%に生足になってもらいました。
	ELSE
		PRINTFORMW 今後%CALLNAME:TARGET%がパンストを履く際には、生足になってくれるようにお願いしました。
	ENDIF
CASEELSE
	PRINTFORML 色はどうしますか？
	COLOR_TABLE = 特にこだわらない/白/黒/ベージュ/ブラウン/ピンク
	VARSET LOCALS
	SPLIT COLOR_TABLE, "/", LOCALS
	CALL PRINT_SELECT, COLOR_TABLE
	SELECTCASE RESULT
	CASE 0
		COLOR_CHOICE = 
	CASEELSE
		COLOR_CHOICE = %LOCALS:RESULT%
	ENDSELECT

	CALL DEL_CEVENT_GROUP, "パンスト"
	SELECTCASE CHOICE
	CASE 1
		CALL SET_CEVENT, @"パンスト：パンスト_%COLOR_CHOICE%"
	CASE 2
		CALL SET_CEVENT, @"パンスト：網パンスト_%COLOR_CHOICE%"
	CASE 3
		CALL SET_CEVENT, @"パンスト：オープンパンスト_%COLOR_CHOICE%"
	ENDSELECT

	CALL SETFLAG, "パンスト着替えのお願い"
	IF OUTER(TARGET) != MEMO_OUTER
		PRINTFORMW %CALLNAME:TARGET%に[%OUTER(TARGET)%]を履いてもらいました。
	ELSE
		PRINTFORMW 今後%CALLNAME:TARGET%がパンストを履く際には、さっき希望したものを履いてくれるようにお願いしました。
	ENDIF
ENDSELECT

PRINTL 
MEMO_LINECOUNT = LINECOUNT
CALL KOJO_WISH, "パンスト着替え", "改行無し"
SIF LINECOUNT == MEMO_LINECOUNT
	CLEARLINE 1

;-------------------------------------------------
;悪魔との取引。
;契約内容に伴ったフラグなどの変更もここで同時に行う
;-------------------------------------------------
@SET_WISH, ARGS, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM PAY_SOUL
#DIM MEMO_TARGET
;日常イベント名を変える際に使う
#DIMS EVENTNAME_WISH

SIF ARG == 0 && TARGET
	ARG = TARGET

;基本は100。異なる場合には以下の選択肢内で値を代入する
IF COND("魂の取引可能", ARG) || COND("天使の取引可能", ARG)
	PAY_SOUL = 100
ELSE
	PAY_SOUL = 0
ENDIF

EVENTNAME_WISH = 契約成立

MEMO_LINECOUNT = LINECOUNT
SELECTCASE ARGS
CASE "パンツ"
	SIF COND("合意：パンツ取引", ARG)
		PAY_SOUL = 0

	PRINTFORM %CALLNAME:MASTER%は
	IF PAY_SOUL
		PRINTFORM 魂と引き換えに
	ELSEIF TFLAG:取引価格
		PRINTFORM %MONEY_C(TFLAG:取引価格)%と引き換えに
	ENDIF
	SELECTCASE PANTIES(ARG, "略")
	CASE "前貼り"
		PRINTFORMW %CALLNAME:ARG%の%PANTIES(ARG)%を手に入れた！
	CASE "Ｃストリング"
		PRINTFORMW %CALLNAME:ARG%が装着していた%PANTIES(ARG)%を手に入れた！
	CASEELSE
		PRINTFORMW %CALLNAME:ARG%が穿いていた%PANTIES(ARG)%を手に入れた！
	ENDSELECT

	MONEY -= TFLAG:取引価格
	SIF TFLAG:取引価格
		PRINTFORML 所持金‐%MONEY_C(TFLAG:取引価格)%

	CALL TEXT, "脱ぎたてパンツの描写", ARG

	IF NUM("パンツ没収数") == 0
		PRINTL 
		SETCOLOR DEF_COLOR("イエロー")
		PRINTFORMW （メイン画面で%CALLNAME:MASTER%の名前の下に並ぶ取得済下着をクリックする事で精力を回復できます）
		RESETCOLOR
	ENDIF

	CALL SETFLAG, "パンツ没収", ARG
	CALL SETFLAG, "合意：パンツ取引", ARG

CASE "お金が欲しい"
	MONEY += 50000
	PRINTFORMW %CALLNAME:MASTER%は魂と引き換えに%MONEY_C(50000)%を手に入れました。

CASE "性豪"
	TALENT:MASTER:ARGS += 1
	CALL SET_COUNTER_BASE
	BASE:MASTER:精力 += 20
	PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[%TALENT_NAME(GETNUM(TALENT, ARGS), TALENT:PLAYER:ARGS)%]を手に入れました。
	PRINTFORMW 精力の上限+20

CASE "禁断の知識"
	TALENT:MASTER:ARGS += 1

	SELECTCASE TALENT:MASTER:ARGS
	;淫紋
	CASE 1
		PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[禁断の知識]のⅠ巻……『淫紋について』を手に入れました。
		PRINTL 
		CALL PRINT_STRW, "淫紋を彫られた相手は_黄色_愛液、体力、精力が増すと共に、性交により得る恭順と欲情と屈服が増大します。"
		CALL PRINT_STRW, "また、_黄色_欲求不満or受精等で紋章の色や形が変化するため、隠し事ができない体になってしまいます。"
		PRINTFORMW ちなみに、刻印せずにお手軽なシールを作成して貼る事も出来ます……効果はやや弱め。
		CALL PRINT_STRW, "この場合は相手に受け入れてもらいやすい代わりに、_黄色_１週間の期間限定_となります。"
	;具現
	CASE 2
		TALENT:MASTER:具現 = 1
		PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[禁断の知識]のⅡ巻……『具現について』を手に入れ、[具現]を習得しました。
		PRINTL 
		CALL PRINT_STRW, "この力を使うことにより、_黄色_オトコ以外にペニスを生やすorしまう_事が可能になります"
		CALL PRINT_STRW, "ただし、変化はごく短時間……具体的には_黄色_エッチシーン一回_で効力が切れて元の状態に戻ります"
	CASE 3
		PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[禁断の知識]のⅢ巻……『認識阻害について』を手に入れました。
		PRINTL 
		CALL PRINT_STRW, @"この力を使う事により、_黄色_対象の詳細な特定が本人と使い手たる%CALLNAME:MASTER%を除いて不可能_になります"
		CALL PRINT_STRW, "その効力は記録映像にまで及び、_黄色_例え本人に暴露されても、よく似た他人としか認識されない_程です"
		CALL PRINT_STRW, "……とはいえ100％ではありません。あまりはしゃぎ過ぎないように注意した方が良いでしょう"
	ENDSELECT
CASE "甘い言葉", "謎の魅力", "力持ち", "錬金術", "射精量強化"
	TALENT:MASTER:ARGS += 1
	PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[%TALENT_NAME(GETNUM(TALENT, ARGS), TALENT:PLAYER:ARGS)%]を手に入れました。
CASE "豊穣"
	TALENT:MASTER:ARGS += 1
	PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[豊穣]を手に入れました。
	PRINTL 
	CALL PRINT_STR, "あなたに胸を揉まれたor精液を胸にかけられた女性は、_黄色_おっぱいが少しだけ大きくなって_しまいます！_W"
	CALL PRINT_STR, "具体的には胸愛撫orおっぱい射精１回で_黄色_0.1cm_程です_L"
	PRINTFORMW 時間帯あたりの成長上限が有るので、日々揉むことが重要となります
	CALL PRINT_STR, "一時的に封印したい場合は、エロシーンで横に並ぶフィルタの[_黄色_豊穣_]をクリックしてください_W"
CASE "長身化"
	IF TALENT:PLAYER:小柄
		CALL CALC, "普通体型化", PLAYER
	ELSE
		CALL CALC, "長身化", PLAYER
	ENDIF
	IF TALENT:PLAYER:長身
		PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[長身]になった。
	ELSEIF TALENT:PLAYER:小柄
		PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[小柄]になった。
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに平均的な身長になった。
	ENDIF
CASE "小柄化"
	IF TALENT:PLAYER:長身
		CALL CALC, "普通体型化", PLAYER
	ELSE
		CALL CALC, "小柄化", PLAYER
	ENDIF
	IF TALENT:PLAYER:長身
		PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[長身]になった。
	ELSEIF TALENT:PLAYER:小柄
		PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[小柄]になった。
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに平均的な身長になった。
	ENDIF
CASE "巨根化"
	TALENT:PLAYER:ペニス += 1
	PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[%TALENT_NAME(GETNUM(TALENT, "ペニス"), TALENT:PLAYER:ペニス, PLAYER)%]になった。
;これはあなたではなくパートナーのちんこをでかくする
CASE "巨根化して"
	TALENT:ペニス += 1
	PRINTFORMW %CALLNAME:PLAYER%の魂と引き換えに、%CALLNAME:TARGET%は[%TALENT_NAME(GETNUM(TALENT, "ペニス"), TALENT:ペニス, TARGET)%]になった。
	SIF ARG == TARGET
		EVENTNAME_WISH = 巨根化契約成立
CASE "短小化"
	TALENT:PLAYER:ペニス -= 1
	PRINTFORMW %CALLNAME:PLAYER%は魂と引き換えに[%TALENT_NAME(GETNUM(TALENT, "ペニス"), TALENT:PLAYER:ペニス, PLAYER)%]になった。
;これはあなたではなくパートナーのちんこを小さくする
CASE "短小化して"
	TALENT:ペニス -= 1
	PRINTFORMW %CALLNAME:PLAYER%の魂と引き換えに、%CALLNAME:TARGET%は[%TALENT_NAME(GETNUM(TALENT, "ペニス"), TALENT:ペニス, TARGET)%]になった。
	SIF ARG == TARGET
		EVENTNAME_WISH = 短小化契約成立

CASE "巨乳化"
	IF TALENT:PLAYER:巨乳 >= 2
		PRINTFORMW %CALLNAME:PLAYER%の胸のサイズは、ありえない大きさの超乳になった。
		CALL CALC, "超乳化", PLAYER
	ELSEIF TALENT:PLAYER:巨乳
		PRINTFORMW %CALLNAME:PLAYER%の胸のサイズは、はちきれんばかりの爆乳になった。
		CALL CALC, "爆乳化", PLAYER
	ELSEIF TALENT:PLAYER:貧乳
		PRINTFORMW %CALLNAME:PLAYER%の胸のサイズは特に希少価値も無い並の大きさになった。
		CALL CALC, "並乳化", PLAYER
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%の胸のサイズは[巨乳]になった。
		CALL CALC, "巨乳化", PLAYER
	ENDIF
CASE "貧乳化"
	IF TALENT:PLAYER:巨乳 >= 3
		PRINTFORMW %CALLNAME:PLAYER%の胸のサイズは[爆乳]になった。
		CALL CALC, "爆乳化", PLAYER
	ELSEIF TALENT:PLAYER:巨乳 == 2
		PRINTFORMW %CALLNAME:PLAYER%の胸のサイズは[巨乳]になった。
		CALL CALC, "巨乳化", PLAYER
	ELSEIF TALENT:PLAYER:巨乳
		PRINTFORMW %CALLNAME:PLAYER%の胸のサイズは巨乳から一転して並の大きさになった。
		CALL CALC, "並乳化", PLAYER
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%の胸のサイズは[貧乳]になった。
		CALL CALC, "貧乳化", PLAYER
	ENDIF
CASE "巨尻化"
	IF TALENT:PLAYER:巨尻 >= 2
		PRINTFORMW %CALLNAME:PLAYER%は[超尻]になった。
		CALL CALC, "超尻化", PLAYER
	ELSEIF TALENT:PLAYER:巨尻
		PRINTFORMW %CALLNAME:PLAYER%は[爆尻]になった。
		CALL CALC, "爆尻化", PLAYER
	ELSEIF TALENT:PLAYER:小尻
		PRINTFORMW %CALLNAME:PLAYER%のお尻のサイズは特に希少価値も無い並の大きさになった。
		CALL CALC, "並尻化", PLAYER
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は[巨尻]になった。
		CALL CALC, "巨尻化", PLAYER
	ENDIF
	CALL SETFLAG, "ヒップ設定", PLAYER
CASE "小尻化"
	IF TALENT:PLAYER:巨尻 >= 3
		PRINTFORMW %CALLNAME:PLAYER%は[爆尻]になった。
		CALL CALC, "爆尻化", PLAYER
	ELSEIF TALENT:PLAYER:巨尻 == 2
		PRINTFORMW %CALLNAME:PLAYER%は[巨尻]になった。
		CALL CALC, "巨尻化", PLAYER
	ELSEIF TALENT:PLAYER:巨尻
		PRINTFORMW %CALLNAME:PLAYER%のお尻のサイズは巨尻から一転して並の大きさになった。
		CALL CALC, "並尻化", PLAYER
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%は[小尻]になった。
		CALL CALC, "小尻化", PLAYER
	ENDIF
	CALL SETFLAG, "ヒップ設定", PLAYER
CASE "早漏治療"
	TALENT:PLAYER:早漏 = 0
	PRINTFORMW %CALLNAME:PLAYER%の[早漏]が治った。
CASE "遅漏治療"
	TALENT:PLAYER:遅漏 = 0
	PRINTFORMW %CALLNAME:PLAYER%の[遅漏]が治った。
CASE "女性化"
	IF TALENT:PLAYER:ふたなり
		;具現持ちなら自力で女性化できる
		SIF TALENT:PLAYER:具現
			PAY_SOUL = 0
		TALENT:PLAYER:オンナ = 1
		TALENT:PLAYER:ふたなり = 0
	ELSE
		CALL CHANGE_SEX, PLAYER
	ENDIF
	PRINTFORMW %CALLNAME:PLAYER%はオンナに\@ TRUE_SEX(PLAYER) == "オンナ" ? 戻った # なった \@。
CASE "女性化して"
	PAY_SOUL = 0
	IF TALENT:ふたなり
		TALENT:オンナ = 1
		TALENT:ふたなり = 0
	ELSE
		CALL CHANGE_SEX, TARGET
	ENDIF
	PRINTFORMW %CALLNAME:TARGET%はオンナに\@ TRUE_SEX(TARGET) == "オンナ" ? 戻った # なった \@。
CASE "男性化"
	CALL CHANGE_SEX, PLAYER
	PRINTFORMW %CALLNAME:PLAYER%はオトコに\@ TRUE_SEX(PLAYER) == "オトコ" ? 戻った # なった \@。
CASE "男性化して"
	CALL CHANGE_SEX, TARGET
	PRINTFORMW %CALLNAME:TARGET%はオトコに\@ TRUE_SEX(TARGET) == "オトコ" ? 戻った # なった \@。
CASE "ふたなり化"
	;具現持ちなら自力でふたなり化できる
	SIF TALENT:PLAYER:具現 && TALENT:PLAYER:オンナ
		PAY_SOUL = 0
	SIF TALENT:PLAYER:オトコ
		CALL CHANGE_SEX, PLAYER
	TALENT:PLAYER:オトコ = 0
	TALENT:PLAYER:オンナ = 0
	TALENT:PLAYER:ふたなり = 2
	PRINTFORMW %CALLNAME:PLAYER%は、ふたなりになった。
CASE "ふたなり化して"
	PAY_SOUL = 0
	SIF TALENT:オトコ
		CALL CHANGE_SEX, TARGET
	TALENT:オトコ = 0
	TALENT:オンナ = 0
	TALENT:ふたなり = 2
	PRINTFORMW %CALLNAME:TARGET%は、ふたなりになった。
ENDSELECT

;テキストが出力されたら１行開け
SIF LINECOUNT > MEMO_LINECOUNT
	CALL BLANKLINE

CALL RECORD_SP_EXP_ALLACTOR

;魂の支払いが無いならここまで
SIF PAY_SOUL == 0
	RETURN 1

;前回コマンドが変わるかも
STR:日常イベント名 = %EVENTNAME_WISH%

;魂のやり取り
CALL EXCHANGE_SOUL, "取引", PAY_SOUL, ARG

IF ARG != TARGET
	SWAP ARG, TARGET
	CALL SET_PREV_DAILYCOM
	SWAP ARG, TARGET
ENDIF

;-------------------------------------------------
;ARGSでの魂のやり取り。あなたがARG:1へとARGだけ魂を渡す
;-------------------------------------------------
@EXCHANGE_SOUL, ARGS, ARG, ARG:1
#DIM MEMO_LINECOUNT
#DIM PAY_SOUL
#DIM ACTOR

SIF PLAYER != MASTER
	RETURN 0

SIF ARG:1 == 0 && TARGET
	ARG:1 = TARGET

PAY_SOUL = 0
ACTOR = ARG:1

SELECTCASE ARGS
CASE "絶頂"
	SIF TALENT:ACTOR:吸精体質
		PAY_SOUL += LIMIT(ARG*ARG/2, 1, TALENT:ACTOR:吸精体質*4) + TALENT:ACTOR:吸精体質

	IF COND("性処理契約", ACTOR)
		PAY_SOUL += 10 * MIN(ARG, 3)
	ELSEIF COND("セフレ契約", ACTOR)
		PAY_SOUL += 5 * MIN(ARG, 4)
	ENDIF
CASE "取引"
	PAY_SOUL = ARG
ENDSELECT

SIF PAY_SOUL <= 0
	RETURN 0

SIF BASE:MASTER:魂 < PAY_SOUL
	PAY_SOUL = BASE:MASTER:魂

BASE:MASTER:魂 -= PAY_SOUL
BASE:ACTOR:魂 += PAY_SOUL
IF TFLAG:システム文スキップ == 0
	PRINTFORML 魂-%TEXTS("0.01", PAY_SOUL)%（%CALLNAME:MASTER%）
	SIF ACTOR != TARGET
		PRINTFORML 魂+%TEXTS("0.01", PAY_SOUL)%（%CALLNAME:ACTOR%）
ENDIF
IF BASE:MASTER:魂 <= 0 && COND("調教中") == 0
	PRINTL 
	PRINTFORMW %CALLNAME:MASTER%はもう限界だ……
	CALL PRINT_STRW, "紅_＜リザレクション！＞"
	PRINTFORMW %CALLNAME:MASTER%は最後の力を振り絞って復活した！
	BASE:MASTER:魂 = 1
ENDIF
IF CFLAG:ACTOR:睡眠 == 0
	;大口の取引をしたなら、信頼が３未満なら+1LV
	IF PAY_SOUL >= 100 && ABL:ACTOR:信頼 < 3
		ABL:ACTOR:信頼 += 1
		SIF TFLAG:システム文スキップ == 0
			CALL PRINT_STRW, @"イエロー_%CALLNAME:ACTOR%の信頼が１上がり、{ABL:ACTOR:信頼}LVになった。"
	ENDIF
ENDIF

