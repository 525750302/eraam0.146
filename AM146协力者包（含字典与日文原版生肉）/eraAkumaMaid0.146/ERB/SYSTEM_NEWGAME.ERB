@EVENTFIRST
#DIM LCOUNT
#DIM MEMO_LINECOUNT
;Verを最新にする
FLAG:VER_eraAkumaMaid = GAMEBASE_VERSION
;フラグに使う現実時間を取得
CALL GET_REALTIMEFORFLAG

ASSI = 0
;これは仮の日付
DAY = 1
;顔グラをデフォに
FLAG:顔グラ = 1

DELCHARA 0
;[0]番はドッペルあなた
;この人の変数を対応した変数の一時保存用に使いまくるので重要な人です
ADDCHARA 99

;[1]番はあなた
ADDCHARA 0
MASTER = 1

MONEY = 5000

;グローバル変数の読み込み
LOADGLOBAL

$SELECT_MODE

REDRAW 0
MEMO_LINECOUNT = LINECOUNT

PRINTL 
;モード選択
PRINTL ★★モードや設定を選択してください★★
PRINTL 
PRINTFORML  [ 0] NORMAL
PRINTFORML  [ 1] BILLIONAIRE (所持金100倍。それ以外に違いはありません) 
PRINTFORML  [10] コンフィグ
SIF GLOBAL > 0 && FLAG:VER_コンフィグ != GLOBAL
	PRINTFORML  [11] コンフィグ設定（記録日:{GLOBAL/10000}年{(GLOBAL%10000)/100}月{GLOBAL%100}日）のロード
PRINTFORML [100] 戻る

$INPUT_LOOP
INPUT

IF RESULT == 100
	REDRAW 1
	BEGIN TITLE
ELSEIF RESULT == 11 && GLOBAL > 0
	CALL LOAD_CONFIGURE
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	GOTO SELECT_MODE
ELSEIF RESULT == 10
	CALL SET_CONFIGURE
	CLEARLINE LINECOUNT - MEMO_LINECOUNT
	GOTO SELECT_MODE
ELSEIF RESULT == 1
	MONEY *= 100
	PRINTW BILLIONAIREモードで始めます
ELSEIF RESULT == 0
ELSE
	GOTO INPUT_LOOP
ENDIF

REDRAW 1
FLAG:オープニング = 1
FLAG:項目OFF = 0
;バージョンチェックはしたものと扱う
FLAG:VER_UPチェック = 1

;キャラメイク中フラグON
SETBIT FLAG:状況判定, 13

CALL START_CHARA_SELECT

PLAYER = MASTER
SAVESTR:現在位置 = %CSVCALLNAME(NO:MASTER)%の部屋

CALL EVENT_OPENING

ASSI:1 = TARGET
ASSI = TARGET

CALL SETFLAG, "同棲：ずっと", TARGET

DAY = 1
TIME = 2

BEGIN SHOP

@EVENT_OPENING
;オープニングのはじまりはじまり
PRINTL 
DRAWLINE
PRINTFORMW 「…………い。……て下さい。起きてください、ご主人様」
PRINTFORMW 寝台で眠る%CALLNAME:MASTER%が揺すられて目を覚ますと、顔を覗き込むメイドと目が合った
PRINTFORMW 清潔なメイド服を身に纏いヘッドドレスにガーターニーソ……これでメイドでない筈がない
PRINTFORMW 僅かに苛立たしさを滲ませている目の前の彼女は……

CALL PRINT_STR, "黄色_※突然ですがメイドのキャラメイクを始めます※_W"

CALL CHARA_MANUAL, 1
DRAWLINE
TARGET = 2

PRINTL 
DRAWLINE
PRINTFORMW 「おはようございます。私が起こしに来るまでグッスリとは随分と暢気なものですねぇ」
PRINTFORMW 「………………もしかして、お忘れですか？」
PRINTL 
SETCOLOR DEF_COLOR("ピンク")
FONTSTYLE 1
PRINTFORMW 「今日は、%CNAME("二人称")%が%CNAME("一人称")%の寝込みを襲う番だったはずですよ」
FONTSTYLE 0
RESETCOLOR
PRINTL 
PRINTFORMW 時を少々巻き戻します
DRAWLINE
PRINTFORMW 目の前のメイド姿の女性は……人の魂を糧とする悪魔です！
PRINTFORMW 事の起こりは、そろそろ長期休暇が終わり新学期が始まるという数日前
PRINTFORMW 甘言を弄する%CALLNAME:TARGET%と%CALLNAME:MASTER%が契約してしまった事から始まります
PRINTFORMW ……普通ならば『残り短い人生だが目の前の女性を自由にできるなら』というお決まりの流れとなる筈でした
PRINTL 
PRINTFORMW 普通ならば。
PRINTL 
CALL PRINT_STR, @"ですが%CALLNAME:MASTER%は、魂の傷が回復するという世にも珍しい異能_黄色_[リザレクション]_持ちだったのです！_W_"
PRINTFORMW その事に気づいた%CALLNAME:TARGET%は%CALLNAME:MASTER%に新たな取引を持ちかけました
PRINTL 
PRINTFORMW 「%CNAME("二人称")%が良ければですが……%CNAME("一人称")%に長期的に魂を差し出すプランに乗り換えませんか？」
PRINTL 
;一番最初の仲間はシステムメッセージ役も担う
CFLAG:初期仲間 = 1
;セフレ契約
TALENT:セフレ = 2
ABL:信頼 += 1
CALL PRINT_STRW, @"%CALLNAME:MASTER%は%CALLNAME:TARGET%と_黄色_[セフレ契約]_を結んだ"
CALL SETFLAG, "合意", TARGET
PRINTL 
PRINTFORMW こうして、冒頭のような関係の出来上がりという訳です
PRINTL 
PRINTFORMW ……え？何をしたら魂を差し出すのかって？
PRINTFORML %CALLNAME:TARGET%曰く
PRINTFORMW 「%CNAME("二人称")%が%CNAME("一人称")%で気持ち良くなられるたび、もしくは取引をなさる際に魂を頂きます」
PRINTFORMW だそうです
;PRINTFORMW あとは分かるな！？
DRAWLINE
PRINTFORMW それでは悪魔のメイドとのお話をお楽しみください
PRINTFORMW ……余裕が有ったらReadMeの"このバリアント独自のルール等"にも目を通して下さい
DRAWLINE


;-------------------------------------------------
;PCを選択式にしてみる
;-------------------------------------------------
@START_CHARA_SELECT
;周回フラグがある場合
IF FLAG:周回数
	PRINTL プレイヤーを選択してください
	PRINTL 
	PRINTL [ 0]この周回での主人を引き続き選択する
	PRINTL [ 1]新規作成

	CALL INPUT_SELECT, 2

	SELECTCASE RESULT
	CASE 0
		PRINTFORMW 引き続き、%CALLNAME:MASTER%をプレイヤーとします
		RETURN 1
	ENDSELECT
ENDIF

CALL CREATE_MASTER
RETURN 1


@CREATE_MASTER
PRINTL 

;IF FLAG:周回数 == 0
;	;CALL INPUT_SELECT, 2
;	RESULT = 0
;ELSE
	CALL RENEWAL_ABL, MASTER
	CALL PRINT_STR, "性別を教えてください（♀とふたなりは自動的に_黄色_[両刀]_になります）_L"
	PRINTL [ 0] ♂
	PRINTL [ 1] 男の娘(女にしか見えない可愛い容姿で、女性的な振る舞いも好む少年)
	PRINTL [ 2] ♀
	PRINTL [ 3] ふたなり
	CALL INPUT_SELECT, 4
;ENDIF

SELECTCASE RESULT
CASE 0
	TALENT:MASTER:長身 = 1
CASE 1
	TALENT:MASTER:男の娘 = 1
CASE 2
	TALENT:MASTER:オトコ = 0
	TALENT:MASTER:オンナ = 1
	TALENT:MASTER:両刀 = 1
CASE 3
	TALENT:MASTER:オトコ = 0
	TALENT:MASTER:ふたなり = 1
	TALENT:MASTER:両刀 = 1
ENDSELECT

TALENT:MASTER:外見年齢 = 3
;初期ちんこは並＆仮性にしておく
TALENT:MASTER:ペニス = 1
TALENT:MASTER:包茎 = 1

;こいつはサービスだぜ
TALENT:MASTER:性豪 = 1
TALENT:MASTER:リザレクション = 100

;MASTERの感度は全て3LV。尿道感覚以外はね
ABL:MASTER:Ｃ感覚 = 3
ABL:MASTER:Ｖ感覚 = 3
ABL:MASTER:Ａ感覚 = 3
ABL:MASTER:Ｂ感覚 = 3
ABL:MASTER:Ｍ感覚 = 3
ABL:MASTER:技巧 = 2

CALL SET_DETAIL, MASTER

CALL SETCONFIG, "弱点"
CALL SETCONFIG, "性癖"

;調教知識の選択
CALL SETFLAG, "調教知識の選択", MASTER

CALL CREATE_CHARA, MASTER
SIF RESULT == 1
	RESTART

;初期パラをここで記録
CALL SETFLAG, "初期パラ記録", MASTER


@SET_DETAIL, ARG
#LOCALSIZE 1000
#DIM LCOUNT
#DIM MEMO_LINECOUNT
#DIM CHOICE

MEMO_LINECOUNT = LINECOUNT
REDRAW 0

;色々設定
EXP:ARG:Ｖ経験 = MAX(EXP:ARG:Ｖ経験, EXP:ARG:Ｖ性交経験, EXP:ARG:Ｖ拡張経験)
IF EXP:ARG:Ｖ経験
	TALENT:ARG:処女 = 0
ELSEIF TALENT:ARG:オトコ == 0
	TALENT:ARG:処女 = 1
ENDIF

EXP:ARG:Ａ経験 = MAX(EXP:ARG:Ａ経験, EXP:ARG:Ａ性交経験, EXP:ARG:Ａ拡張経験)
IF EXP:ARG:Ａ性交経験
	TALENT:ARG:Ａ処女 = 0
ELSE
	TALENT:ARG:Ａ処女 = 1
ENDIF

TALENT:ARG:尿道処女 = 1

IF EXP:ARG:Ｖ挿入経験
	TALENT:ARG:童貞 = 0
ELSEIF TALENT:ARG:オトコ
	TALENT:ARG:童貞 = 1
ENDIF

SIF CONFIG("陰毛") && COND("陰毛", ARG) == 0
	CALL SETFLAG, "陰毛と腋毛", ARG

CFLAG:ARG:身嗜み = 0

SELECTCASE COND("陰毛", ARG)
CASE 3
	SETBIT CFLAG:ARG:身嗜み, 1
CASE 5
	SETBIT CFLAG:ARG:身嗜み, 2
CASE 7
	SETBIT CFLAG:ARG:身嗜み, 3
ENDSELECT

SELECTCASE COND("腋毛", ARG)
CASE 3
	SETBIT CFLAG:ARG:身嗜み, 6
CASE 7
	SETBIT CFLAG:ARG:身嗜み, 7
ENDSELECT

VARSET LOCAL
PRINTL 
PRINTFORML 素質や経験などを決定してください
SIF COND("ガチオトコ", ARG)
	PRINTFORML "体つき"は女性視点なので、男なら[長身]でちょうどいいかも知れません
SIF FLAG:オープニング
	CALL PRINT_STR, "ちなみに_黄色_Ｖ挿入経験_、_黄色_ペニスサイズ_、_黄色_皮被り_、_黄色_性技術_が重要度が高いです_L_"
IF TALENT:ARG:オトコ == 0
	PRINT Ｖ経験　　　　…
	SELECTCASE EXP:ARG:Ｖ経験
	CASE IS >= 50
		CALL PRINT_STR, "ピンク_NOBUTTON_豊富"
	CASE IS >= 20
		CALL PRINT_STR, "ハートピンク_NOBUTTON_多め"
	CASE IS >= 1
		CALL PRINT_STR, "黄色_NOBUTTON_有り"
	CASEELSE
		PRINTPLAIN 処女
	ENDSELECT
	PRINTL 　　[  0]処女　[  1]有り　[  2]多め　[  3]豊富
	LOCAL:0 = 1
	IF EXP:ARG:Ｖ経験
		PRINT Ｖ性交経験　　…
		SELECTCASE EXP:ARG:Ｖ性交経験
		CASE IS >= 50
			CALL PRINT_STR, "ピンク_NOBUTTON_豊富"
		CASE IS >= 20
			CALL PRINT_STR, "ハートピンク_NOBUTTON_多め"
		CASE IS >= 1
			CALL PRINT_STR, "黄色_NOBUTTON_有り"
		CASEELSE
			PRINTPLAIN 無し
		ENDSELECT
		PRINTL 　　[  5]無し　[  6]有り　[  7]多め　[  8]豊富
		LOCAL:5 = 1
		PRINT Ｖ拡張経験　　…
		SELECTCASE EXP:ARG:Ｖ拡張経験
		CASE IS >= 20
			CALL PRINT_STR, "ピンク_NOBUTTON_豊富"
		CASE IS >= 5
			CALL PRINT_STR, "ハートピンク_NOBUTTON_多め"
		CASE IS >= 1
			CALL PRINT_STR, "黄色_NOBUTTON_有り"
		CASEELSE
			PRINTPLAIN 無し
		ENDSELECT
		PRINTL 　　[ 10]無し　[ 11]有り　[ 12]多め　[ 13]豊富
		LOCAL:10 = 1
	ENDIF
ENDIF
PRINT Ａ経験　　　　…
SELECTCASE EXP:ARG:Ａ経験
CASE IS >= 50
	CALL PRINT_STR, "ピンク_NOBUTTON_豊富"
CASE IS >= 20
	CALL PRINT_STR, "ハートピンク_NOBUTTON_多め"
CASE IS >= 1
	CALL PRINT_STR, "黄色_NOBUTTON_有り"
CASEELSE
	PRINTPLAIN 無し
ENDSELECT

PRINTL 　　[ 15]無し　[ 16]有り　[ 17]多め　[ 18]豊富
LOCAL:15 = 1

PRINT Ａ性交経験　　…
SELECTCASE EXP:ARG:Ａ性交経験
CASE IS >= 50
	CALL PRINT_STR, "ピンク_NOBUTTON_豊富"
CASE IS >= 20
	CALL PRINT_STR, "ハートピンク_NOBUTTON_多め"
CASE IS >= 1
	CALL PRINT_STR, "黄色_NOBUTTON_有り"
CASEELSE
	PRINTPLAIN 無し
ENDSELECT
PRINTL 　　[ 20]無し　[ 21]有り　[ 22]多め　[ 23]豊富
LOCAL:20 = 1
PRINT Ａ拡張経験　　…
SELECTCASE EXP:ARG:Ａ拡張経験
CASE IS >= 20
	CALL PRINT_STR, "ピンク_NOBUTTON_豊富"
CASE IS >= 5
	CALL PRINT_STR, "ハートピンク_NOBUTTON_多め"
CASE IS >= 1
	CALL PRINT_STR, "黄色_NOBUTTON_有り"
CASEELSE
	PRINTPLAIN 無し
ENDSELECT
PRINTL 　　[ 25]無し　[ 26]有り　[ 27]多め　[ 28]豊富
LOCAL:25 = 1

IF PENIS(ARG)
	CALL PRINT_STR, "黄色_Ｖ挿入経験_　　…_"
	SELECTCASE EXP:ARG:Ｖ挿入経験
	CASE IS >= 50
		CALL PRINT_STR, "ピンク_NOBUTTON_豊富"
	CASE IS >= 20
		CALL PRINT_STR, "ハートピンク_NOBUTTON_多め"
	CASE IS >= 1
		CALL PRINT_STR, "黄色_NOBUTTON_有り"
	CASEELSE
		PRINTPLAIN 童貞
	ENDSELECT
	PRINTL 　　[ 30]童貞　[ 31]有り　[ 32]多め　[ 33]豊富
	LOCAL:30 = 1
	PRINT Ａ挿入経験　　…
	SELECTCASE EXP:ARG:Ａ挿入経験
	CASE IS >= 50
		CALL PRINT_STR, "ピンク_NOBUTTON_豊富"
	CASE IS >= 20
		CALL PRINT_STR, "ハートピンク_NOBUTTON_多め"
	CASE IS >= 1
		CALL PRINT_STR, "黄色_NOBUTTON_有り"
	CASEELSE
		PRINTPLAIN 無し
	ENDSELECT
	PRINTL 　　[ 35]無し　[ 36]有り　[ 37]多め　[ 38]豊富
	LOCAL:35 = 1
ENDIF

PRINT 体つき　　　　…
IF TALENT:ARG:長身
	CALL PRINT_STR, "黄色_NOBUTTON_長身"
ELSEIF TALENT:ARG:小柄
	CALL PRINT_STR, "黄色_NOBUTTON_小柄"
ELSE
	PRINTPLAIN 普通
ENDIF
PRINTFORML 　　[100]小柄　[101]普通　\@ TALENT:ARG:男の娘 == 0 ? [102]長身 # \@
LOCAL:100 = 1
IF TALENT:ARG:オトコ == 0
	PRINT 胸の大きさ　　…
	IF TALENT:ARG:巨乳 >= 2
		CALL PRINT_STR, "ピンク_NOBUTTON_爆乳"
	ELSEIF TALENT:ARG:巨乳
		CALL PRINT_STR, "ハートピンク_NOBUTTON_巨乳"
	ELSEIF TALENT:ARG:貧乳
		CALL PRINT_STR, "青_NOBUTTON_貧乳"
	ELSE
		PRINTPLAIN 普通
	ENDIF
	PRINTL 　　[105]貧乳　[106]普通　[107]巨乳　[108]爆乳
	LOCAL:105 = 1
ENDIF
IF PENIS(ARG)
	CALL PRINT_STR, "黄色_ペニスサイズ_　…_"
	SELECTCASE TALENT:ARG:ペニス
	CASE IS >= 3
		CALL PRINT_STR, "ピンク_NOBUTTON_馬並"
	CASE 2
		CALL PRINT_STR, "ハートピンク_NOBUTTON_巨根"
	CASE 1
		PRINTPLAIN 普通
	CASE 0
		CALL PRINT_STR, "青_NOBUTTON_短小"
	ENDSELECT
	PRINTFORML 　　[110]短小　[111]普通　[112]巨根　[113]馬並
	LOCAL:110 = 1
	CALL PRINT_STR, "黄色_皮被り_　　　　…_"
	IF COND("真性包茎", ARG)
		CALL PRINT_STR, "青_NOBUTTON_真性"
	ELSEIF COND("仮性包茎", ARG)
		PRINTPLAIN 仮性
	ELSE
		CALL PRINT_STR, "黄色_NOBUTTON_無し"
	ENDIF
	PRINTFORML 　　[115]真性　[116]仮性　[117]無し
	LOCAL:115 = 1
ENDIF
IF CONFIG("陰毛")
	PRINT 陰毛　　　　　…
	SELECTCASE COND("陰毛", ARG)
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_無毛"
	CASE 2, 3
		CALL PRINT_STR, "黄色_NOBUTTON_剃毛"
	CASE 4, 6
		PRINTPLAIN 有り
	CASE 5
		CALL PRINT_STR, "黄色_NOBUTTON_剃整"
	CASE 7
		CALL PRINT_STR, "黄色_NOBUTTON_密林"
	ENDSELECT
	PRINTL 　　[120]無毛　[121]剃毛　[122]有り　[123]剃整　[124]密林
	LOCAL:120 = 1
ENDIF
IF CONFIG("腋毛")
	PRINTFORM %TEXTS("腋")%毛　　　　　…
	SELECTCASE COND("腋毛", ARG)
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_無毛"
	CASE 2
		CALL PRINT_STR, "黄色_NOBUTTON_脱毛"
	CASE 3
		CALL PRINT_STR, "黄色_NOBUTTON_剃毛"
	CASE 4
		PRINTPLAIN 有り
	CASE 7
		CALL PRINT_STR, "黄色_NOBUTTON_密林"
	ENDSELECT
	PRINTL 　　[125]無毛　[126]剃毛　[127]有り　[128]密林
	LOCAL:125 = 1
ENDIF
PRINT 肌の色　　　　…
SELECTCASE TALENT:ARG:肌の色
CASE 0
	CALL PRINT_STR, "NOBUTTON_普通"
CASE 1
	CALL PRINT_STR, "黄色_NOBUTTON_褐色"
CASE 2
	CALL PRINT_STR, "黄色_NOBUTTON_色白"
CASE 3
	CALL PRINT_STR, "黄色_NOBUTTON_青肌"
ENDSELECT
;PRINTPLAIN 　　
PRINTL 　　[130]普通　[131]褐色　[132]色白
LOCAL:130 = 1

PRINT 外見年齢　　　…
SELECTCASE AGE(ARG)
CASE 1
	CALL PRINT_STR, "黄色_NOBUTTON_子供"
CASE 2
	IF TALENT:ARG:オトコ
		CALL PRINT_STR, "黄色_NOBUTTON_少年"
	ELSE
		CALL PRINT_STR, "黄色_NOBUTTON_ＪＫ"
	ENDIF
CASE 3
	IF TALENT:ARG:オトコ
		CALL PRINT_STR, "黄色_NOBUTTON_青年"
	ELSE
		CALL PRINT_STR, "黄色_NOBUTTON_ＪＤ"
	ENDIF
CASE 4
	CALL PRINT_STR, "黄色_NOBUTTON_大人"
ENDSELECT
IF TALENT:ARG:男の娘
	PRINTFORML 　　[135]子供　[136]少年
ELSE
	PRINTFORML 　　[135]子供　[136]\@ TALENT:ARG:オトコ ? 少年 # ＪＫ \@　[137]\@ TALENT:ARG:オトコ ? 青年 # ＪＤ \@　[138]大人
ENDIF
LOCAL:135 = 1

IF TALENT:ARG:オトコ == 0 && CONFIG("乳首肥大")
	PRINT 乳首肥大　　　…
	SELECTCASE COND("乳首肥大", ARG)
	CASE IS >= 3
		CALL PRINT_STR, "ピンク_NOBUTTON_LV３"
	CASE 2
		CALL PRINT_STR, "ハートピンク_NOBUTTON_LV２"
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_LV１"
	CASEELSE
		PRINTPLAIN 無し
	ENDSELECT
	PRINTL 　　[140]無し　[141]LV１　[142]LV２　[143]LV３
	LOCAL:140 = 1
ENDIF
IF TALENT:ARG:オトコ == 0 && CONFIG("陥没乳首")
	PRINT 陥没乳首　　　…
	SELECTCASE COND("陥没乳首", ARG)
	CASE 2
		CALL PRINT_STR, "ハートピンク_NOBUTTON_真性"
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_仮性"
	CASEELSE
		PRINTPLAIN 無し
	ENDSELECT
	PRINTL 　　[145]無し　[146]仮性　[147]真性
	LOCAL:145 = 1
ENDIF
IF TALENT:ARG:オトコ == 0 && CONFIG("巨乳輪")
	PRINT 巨乳輪　　　　…
	SELECTCASE COND("巨乳輪", ARG)
	CASE IS >= 3
		CALL PRINT_STR, "ピンク_NOBUTTON_LV３"
	CASE 2
		CALL PRINT_STR, "ハートピンク_NOBUTTON_LV２"
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_LV１"
	CASEELSE
		PRINTPLAIN 無し
	ENDSELECT
	PRINTL 　　[150]無し　[151]LV１　[152]LV２　[153]LV３
	LOCAL:150 = 1
ENDIF
IF TALENT:ARG:オトコ == 0 && CONFIG("陰核肥大")
	PRINT 陰核肥大　　　…
	SELECTCASE COND("陰核肥大", ARG)
	CASE IS >= 3
		CALL PRINT_STR, "ピンク_NOBUTTON_LV３"
	CASE 2
		CALL PRINT_STR, "ハートピンク_NOBUTTON_LV２"
	CASE 1
		CALL PRINT_STR, "黄色_NOBUTTON_LV１"
	CASEELSE
		PRINTPLAIN 無し
	ENDSELECT
	PRINTL 　　[155]無し　[156]LV１　[157]LV２　[158]LV３
	LOCAL:155 = 1
ENDIF
IF TALENT:ARG:オトコ == 0 && CONFIG("陰核包茎")
	PRINT 陰核包茎　　　…
	IF COND("陰核包茎", ARG)
		CALL PRINT_STR, "黄色_NOBUTTON_包茎"
	ELSE
		PRINTPLAIN 無し
	ENDIF
	PRINTL 　　[160]無し　[161]包茎
	LOCAL:160 = 1
ENDIF

PRINT サドっ気　　　…
SELECTCASE ABL:ARG:サドっ気
CASE 0
	PRINTPLAIN 無し
CASE IS <= 1
	CALL PRINT_STR, "黄色_NOBUTTON_少し"
CASE IS <= 3
	CALL PRINT_STR, "黄色_NOBUTTON_有り"
CASEELSE
	CALL PRINT_STR, "ピンク_NOBUTTON_強い"
ENDSELECT
PRINTL 　　[225]無し　[226]少し　[227]有り　[228]強い
LOCAL:225 = 1

PRINT マゾっ気　　　…
SELECTCASE ABL:ARG:マゾっ気
CASE 0
	PRINTPLAIN 無し
CASE IS <= 1
	CALL PRINT_STR, "黄色_NOBUTTON_少し"
CASE IS <= 3
	CALL PRINT_STR, "黄色_NOBUTTON_有り"
CASEELSE
	CALL PRINT_STR, "ピンク_NOBUTTON_強い"
ENDSELECT
PRINTL 　　[230]無し　[231]少し　[232]有り　[233]強い
LOCAL:230 = 1

IF FLAG:周回数
	IF TALENT:ARG:オトコ
		PRINT 性交相手の好み…
		IF TALENT:ARG:両刀
			CALL PRINT_STR, "ハートピンク_NOBUTTON_両方"
		ELSEIF TALENT:ARG:女嫌い
			CALL PRINT_STR, "黄色_NOBUTTON_男性"
		ELSE
			PRINTPLAIN 女性
		ENDIF
		PRINTL 　　[235]女性　[236]男性　[237]両方
		LOCAL:235 = 1
	ELSE
		PRINT 性交相手の好み…
		IF TALENT:ARG:両刀
			CALL PRINT_STR, "ハートピンク_NOBUTTON_両方"
		ELSEIF TALENT:ARG:男嫌い
			CALL PRINT_STR, "黄色_NOBUTTON_女性"
		ELSE
			PRINTPLAIN 男性
		ENDIF
		PRINTL 　　[240]男性　[241]女性　[242]両方
		LOCAL:240 = 1
	ENDIF
ENDIF

CALL PRINT_STR, "黄色_性技術_　　　　…_"
SELECTCASE ABL:ARG:技巧
CASE IS >= 5
	CALL PRINT_STR, "ピンク_NOBUTTON_巧み"
CASE IS >= 3
	CALL PRINT_STR, "ハートピンク_NOBUTTON_上手"
CASE IS >= 1
	PRINTPLAIN 有り
CASEELSE
	CALL PRINT_STR, "青_NOBUTTON_無し"
ENDSELECT
;技巧で巧みを選択できるのは２週目以降
PRINTFORML 　　[245]無し　[246]有り　[247]上手　\@ FLAG:周回数 ? [248]巧み # \@
LOCAL:245 = 1

PRINTFORML [900] キャラメイクに関わるコンフィグを開く
PRINTFORML [999] 決定

$INPUT_LOOP
INPUT

CHOICE = RESULT

SELECTCASE CHOICE
CASE 900, 999
CASEELSE
	IF LOCAL:(CHOICE/5*5) == 0
		PRINTL 無効な値です
		GOTO INPUT_LOOP
	ENDIF
ENDSELECT

SELECTCASE CHOICE
CASE 999
	REDRAW 1
	RETURN 1
CASE 900
	CALL SET_CONFIGURE, "キャラメイク関連"
CASE 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
CASE 1
	EXP:ARG:Ｖ経験 = 1 + RAND:20
	EXP:ARG:Ｖ性交経験 = MIN(EXP:ARG:Ｖ性交経験, EXP:ARG:Ｖ経験)
	EXP:ARG:Ｖ拡張経験 = MIN(EXP:ARG:Ｖ拡張経験, EXP:ARG:Ｖ経験)
CASE 2
	EXP:ARG:Ｖ経験 = 20 + RAND:30
	EXP:ARG:Ｖ性交経験 = MIN(EXP:ARG:Ｖ性交経験, EXP:ARG:Ｖ経験)
	EXP:ARG:Ｖ拡張経験 = MIN(EXP:ARG:Ｖ拡張経験, EXP:ARG:Ｖ経験)
CASE 3
	EXP:ARG:Ｖ経験 = 50 + RAND:50
	EXP:ARG:Ｖ性交経験 = MIN(EXP:ARG:Ｖ性交経験, EXP:ARG:Ｖ経験)
	EXP:ARG:Ｖ拡張経験 = MIN(EXP:ARG:Ｖ拡張経験, EXP:ARG:Ｖ経験)
CASE 5
	EXP:ARG:Ｖ性交経験 = 0
CASE 6
	EXP:ARG:Ｖ性交経験 = 1 + RAND:20
CASE 7
	EXP:ARG:Ｖ性交経験 = 20 + RAND:30
CASE 8
	EXP:ARG:Ｖ性交経験 = 50 + RAND:50
CASE 10
	EXP:ARG:Ｖ拡張経験 = 0
CASE 11
	EXP:ARG:Ｖ拡張経験 = 1 + RAND:5
CASE 12
	EXP:ARG:Ｖ拡張経験 = 5 + RAND:15
CASE 13
	EXP:ARG:Ｖ拡張経験 = 20 + RAND:20

CASE 15
	EXP:ARG:Ａ経験 = 0
	EXP:ARG:Ａ性交経験 = 0
	EXP:ARG:Ａ拡張経験 = 0
CASE 16
	EXP:ARG:Ａ経験 = 1 + RAND:20
	EXP:ARG:Ａ性交経験 = MIN(EXP:ARG:Ａ性交経験, EXP:ARG:Ａ経験)
	EXP:ARG:Ａ拡張経験 = MIN(EXP:ARG:Ａ拡張経験, EXP:ARG:Ａ経験)
CASE 17
	EXP:ARG:Ａ経験 = 20 + RAND:30
	EXP:ARG:Ａ性交経験 = MIN(EXP:ARG:Ａ性交経験, EXP:ARG:Ａ経験)
	EXP:ARG:Ａ拡張経験 = MIN(EXP:ARG:Ａ拡張経験, EXP:ARG:Ａ経験)
CASE 18
	EXP:ARG:Ａ経験 = 50 + RAND:50
	EXP:ARG:Ａ性交経験 = MIN(EXP:ARG:Ａ性交経験, EXP:ARG:Ａ経験)
	EXP:ARG:Ａ拡張経験 = MIN(EXP:ARG:Ａ拡張経験, EXP:ARG:Ａ経験)
CASE 20
	EXP:ARG:Ａ性交経験 = 0
CASE 21
	EXP:ARG:Ａ性交経験 = 1 + RAND:20
CASE 22
	EXP:ARG:Ａ性交経験 = 20 + RAND:30
CASE 23
	EXP:ARG:Ａ性交経験 = 50 + RAND:50
CASE 25
	EXP:ARG:Ａ拡張経験 = 0
CASE 26
	EXP:ARG:Ａ拡張経験 = 1 + RAND:5
CASE 27
	EXP:ARG:Ａ拡張経験 = 5 + RAND:15
CASE 28
	EXP:ARG:Ａ拡張経験 = 20 + RAND:20
CASE 30
	EXP:ARG:Ｖ挿入経験 = 0
CASE 31
	EXP:ARG:Ｖ挿入経験 = 1 + RAND:20
CASE 32
	EXP:ARG:Ｖ挿入経験 = 20 + RAND:30
CASE 33
	EXP:ARG:Ｖ挿入経験 = 50 + RAND:50
CASE 35
	EXP:ARG:Ａ挿入経験 = 0
CASE 36
	EXP:ARG:Ａ挿入経験 = 1 + RAND:20
CASE 37
	EXP:ARG:Ａ挿入経験 = 20 + RAND:30
CASE 38
	EXP:ARG:Ａ挿入経験 = 50 + RAND:50

CASE 100
	TALENT:ARG:小柄 = 1
	TALENT:ARG:長身 = 0
CASE 101
	TALENT:ARG:小柄 = 0
	TALENT:ARG:長身 = 0
CASE 102
	IF TALENT:ARG:男の娘 == 0
		TALENT:ARG:小柄 = 0
		TALENT:ARG:長身 = 1
	ENDIF
CASE 105
	TALENT:ARG:貧乳 = 1
	TALENT:ARG:巨乳 = 0
CASE 106
	TALENT:ARG:貧乳 = 0
	TALENT:ARG:巨乳 = 0
CASE 107
	TALENT:ARG:貧乳 = 0
	TALENT:ARG:巨乳 = 1
CASE 108
	TALENT:ARG:貧乳 = 0
	TALENT:ARG:巨乳 = 2
CASE 110
	TALENT:ARG:ペニス = 0
CASE 111
	TALENT:ARG:ペニス = 1
CASE 112
	TALENT:ARG:ペニス = 2
CASE 113
	TALENT:ARG:ペニス = 3
CASE 115
	TALENT:ARG:包茎 = 2
CASE 116
	TALENT:ARG:包茎 = 1
CASE 117
	TALENT:ARG:包茎 = 0
CASE 120
	BASE:ARG:陰毛 = 100
CASE 121
	BASE:ARG:陰毛 = 300
CASE 122
	BASE:ARG:陰毛 = 600
CASE 123
	BASE:ARG:陰毛 = 500
CASE 124
	BASE:ARG:陰毛 = 700
CASE 125
	BASE:ARG:腋毛 = 100
CASE 126
	BASE:ARG:腋毛 = 300
CASE 127
	BASE:ARG:腋毛 = 400
CASE 128
	BASE:ARG:腋毛 = 700
CASE 130 TO 132
	TALENT:ARG:肌の色 = CHOICE - 130
;男の娘はこっちだけ
CASE 135 TO 136
	TALENT:ARG:外見年齢 = CHOICE - 134
CASE 137 TO 138
	IF TALENT:ARG:男の娘
		GOTO INPUT_LOOP
	ELSE
		TALENT:ARG:外見年齢 = CHOICE - 134
	ENDIF
CASE 140 TO 143
	TALENT:ARG:乳首肥大 = CHOICE - 140
CASE 145 TO 147
	TALENT:ARG:陥没乳首 = CHOICE - 145
CASE 150 TO 153
	TALENT:ARG:巨乳輪 = CHOICE - 150

CASE 155 TO 158
	TALENT:ARG:陰核肥大 = CHOICE - 155
CASE 160 TO 161
	TALENT:ARG:陰核包茎 = CHOICE - 160

CASE 225
	ABL:ARG:サドっ気 = 0
CASE 226
	ABL:ARG:サドっ気 = 1
CASE 227
	ABL:ARG:サドっ気 = 3
CASE 228
	ABL:ARG:サドっ気 = 5

CASE 230
	ABL:ARG:マゾっ気 = 0
CASE 231
	ABL:ARG:マゾっ気 = 1
CASE 232
	ABL:ARG:マゾっ気 = 3
CASE 233
	ABL:ARG:マゾっ気 = 5

CASE 235
	TALENT:ARG:女嫌い = 0
	TALENT:ARG:両刀 = 0
CASE 236
	TALENT:ARG:女嫌い = 1
	TALENT:ARG:両刀 = 0
CASE 237
	TALENT:ARG:女嫌い = 0
	TALENT:ARG:両刀 = 1

CASE 240
	TALENT:ARG:男嫌い = 0
	TALENT:ARG:両刀 = 0
CASE 241
	TALENT:ARG:男嫌い = 1
	TALENT:ARG:両刀 = 0
CASE 242
	TALENT:ARG:男嫌い = 0
	TALENT:ARG:両刀 = 1

CASE 245
	ABL:ARG:技巧 = 0
CASE 246
	ABL:ARG:技巧 = 1 + RAND:2
CASE 247
	ABL:ARG:技巧 = 3 + RAND:2
CASE 248
	ABL:ARG:技巧 = 5
CASEELSE
	GOTO INPUT_LOOP
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@CREATE_CHARA, ARG
#DIM LCOUNT
#DIM MEMO_LINECOUNT

REDRAW 0

MEMO_LINECOUNT = LINECOUNT
PRINTL 

;MASTERの複製作成
ADDCOPYCHARA ARG

;男にも濡れやすさの変化をつける試み
IF RAND:3 == 0
	IF RAND:2
		TALENT:ARG:濡れやすい = 1
		TALENT:ARG:濡れにくい = 0
	ELSE
		TALENT:ARG:濡れやすい = 0
		TALENT:ARG:濡れにくい = 1
	ENDIF
ENDIF
;IF RAND:3 == 0
;	IF RAND:2
;		TALENT:ARG:賢い = 1
;	ELSE
;		TALENT:ARG:愚か = 1
;	ENDIF
;ENDIF
IF RAND:3 == 0 && PENIS(ARG)
	IF RAND:2
		TALENT:ARG:早漏 = 1
		TALENT:ARG:遅漏 = 0
	ELSE
		TALENT:ARG:早漏 = 0
		TALENT:ARG:遅漏 = 1
	ENDIF
ENDIF

FOR LCOUNT, 0, 4
	SELECTCASE RAND:10 + ABL:ARG:技巧
	CASE 0 TO 2
		LOCAL = 0
	CASE 3 TO 5
		LOCAL = 1
	CASE 6 TO 8
		LOCAL = 2
	CASEELSE
		LOCAL = 3
	ENDSELECT

	SELECTCASE LCOUNT
	CASE 0
		TALENT:ARG:天性の舌使い = LOCAL
	CASE 1
		TALENT:ARG:天性の指使い = LOCAL
	CASE 2
		TALENT:ARG:天性の腰使い = LOCAL
	CASE 3
		SIF TALENT:ARG:オトコ == 0
			TALENT:ARG:天性の魔乳 = LOCAL
	ENDSELECT
NEXT

FOR LCOUNT, 0, 100
	SIF EXP:ARG:LCOUNT
		EXP:ARG:LCOUNT = MAX(MULTIPLY(EXP:ARG:LCOUNT, 50 + (RAND:6 + RAND:6)*10), 1)
NEXT

IF COND("キャラメイク中")
	SIF STR:髪記録 != ""
		CALL SETFLAG, "髪記録からの復元", ARG
	SIF STR:表情記録 != ""
		CALL SETFLAG, "表情記録からの復元", ARG
ENDIF
SIF STR:髪記録 != "" || STR:表情記録 != ""
	CFLAG:ARG:顔グラ = 1

CALL SETFLAG, "識別番号", ARG
CALL RESET_EXP_GAIN, ARG
CALL GET_EXABL, ARG
CALL GET_EXTALENT, ARG
CALL SET_3SIZE, ARG
CALL SETFLAG, "乳首の詳細設定", ARG
CALL SETFLAG, "陰核の詳細設定", ARG
CALL SETFLAG, "属性", ARG
CALL CHECK_FAV_POSITION, ARG
CALL SET_COUNTER_BASE
CALL VARSET_MAIDFACE, ARG

CALL NEW_PRINT_STATE, ARG, "キャラメイク"
PRINTL 
PRINTFORML これでよろしいですか？（[ 3]は設定後リロードします）
IF CONFIG("顔グラＯＮ")
	PRINTFORML [ 0]はい　[ 1]設定をしなおす　[ 2]リロード　[ 3]名前変更　[ 4]コンフィグ　[ 5]顔グラを\@ CFLAG:ARG:顔グラ ? 変更 # 設定 \@する
	CALL INPUT_SELECT, 6
ELSE
	PRINTFORML [ 0]はい　[ 1]設定をしなおす　[ 2]リロード　[ 3]名前変更　[ 4]コンフィグ
	CALL INPUT_SELECT, 5
ENDIF

SELECTCASE RESULT
CASE 0, 1
	REDRAW 1
	DELCHARA CHARANUM-1
	RETURN RESULT
CASE 2
	COPYCHARA CHARANUM-1, ARG
	DELCHARA CHARANUM-1
CASE 3
	COPYCHARA CHARANUM-1, ARG
	DELCHARA CHARANUM-1

	CALL NAME_CONVERT, MASTER
CASE 4
	CALL SET_CONFIGURE, "キャラメイク関連"
	COPYCHARA CHARANUM-1, ARG
	DELCHARA CHARANUM-1
CASE 5
	CALL SIMULATE_MAIDFACE, ARG
	IF CFLAG:ARG:顔グラ
		CALL SETFLAG, "髪の記録", ARG
		CALL SETFLAG, "表情の記録", ARG
	ELSE
		STR:髪記録 = 
		STR:表情記録 = 
	ENDIF
	COPYCHARA CHARANUM-1, ARG
	DELCHARA CHARANUM-1
ENDSELECT

CLEARLINE LINECOUNT - MEMO_LINECOUNT
RESTART

@RENEWAL_ABL, ARG
VARSET TALENT:ARG:0, 0
VARSET CFLAG:ARG:0, 0
VARSET ABL:ARG:0, 0
VARSET EXP:ARG:0, 0
VARSET MARK:ARG:0, 0
VARSET JUEL:ARG:0, 0
SIF FLAG:周回数
	VARSET CSTR:ARG:0, ""

ABL:ARG:技巧 = 0
TALENT:ARG:オトコ = 1
;妊娠イベントなど、ベースフラグのリセット
VARSET BASE:ARG:0, 0, 20, 60
VARSET TCVAR:ARG:0


@GAME_RENEWAL
#DIM LCOUNT
#DIM MEMO_MASTER
