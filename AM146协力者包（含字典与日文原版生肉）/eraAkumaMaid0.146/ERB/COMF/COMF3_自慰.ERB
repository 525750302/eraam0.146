@COM3
;自慰
;バイブ、アナルバイブ、尿道バイブ、ビデオ撮影の時に内容変化
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 3

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
;６９自慰はフェラ
IF CHECK_COM("６９自慰")
	TEQUIP:シックスナイン = 91
	;TEQUIP:PLAYER:シックスナイン = 0
ENDIF
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
IF CHECK_COM("フェラ自慰")
	SELECTCOM = 91
	CALL SAMEN_CHECK, 10
	SELECTCOM = 3
ENDIF

RETURN 1

@FLAG_COM3, ARG
#DIMS PLAY_ONANIE
PLAY_ONANIE = %CHECK_ONANIE(SELECTCOM)%

SIF CHECK_COM("フェラ自慰", 3)
	CALL FLAG_COM91, 10
CALL SETFLAG, "自慰フラグ", TARGET
IF STRCOUNT(PLAY_ONANIE, "Ｃ")
	SIF TALENT:オトコ == 0
		CALL EQ_STAIN, TARGET, "手", TARGET, "Ｖ"
	SIF PENIS(TARGET)
		CALL EQ_STAIN, TARGET, "手", TARGET, "Ｐ"
ENDIF
IF STRCOUNT(PLAY_ONANIE, "Ｖ")
	SIF TEQUIP:バイブ
		CALL FLAG_COM31, 30
	CALL EQ_STAIN, TARGET, "手", TARGET, "Ｖ"
ENDIF
IF STRCOUNT(PLAY_ONANIE, "Ａ")
	SIF TEQUIP:アナルバイブ
		CALL SOURCE_COM33, 30
	CALL EQ_STAIN, TARGET, "手", TARGET, "Ａ"
ENDIF
SIF STRCOUNT(PLAY_ONANIE, "Ｂ")
	CALL EQ_STAIN, TARGET, "手", TARGET, "Ｂ"
IF STRCOUNT(PLAY_ONANIE, "尿")
	SIF TEQUIP:尿道バイブ
		CALL FLAG_COM152, 30
	STAIN:手 = SET_STAIN(STAIN:TARGET:手, "尿")
ENDIF

@SOURCE_COM3, ARG
#DIMS PLAY_ONANIE
PLAY_ONANIE = %CHECK_ONANIE(SELECTCOM)%

SIF CHECK_COM("フェラ自慰", 3)
	CALL SOURCE_COM91, 10
IF STRCOUNT(PLAY_ONANIE, "Ｃ")
	;オナホ自慰
	SIF STRCOUNT(PLAY_ONANIE, "オナホ")
		CALL SOURCE_COM37, 30
ENDIF
IF STRCOUNT(PLAY_ONANIE, "Ｖ")
	SIF TEQUIP:バイブ
		CALL SOURCE_COM31, 30
ENDIF
IF STRCOUNT(PLAY_ONANIE, "Ａ")
	SIF TEQUIP:アナルバイブ
		CALL SOURCE_COM33, 30
ENDIF
IF STRCOUNT(PLAY_ONANIE, "尿")
	SIF TEQUIP:尿道バイブ
		CALL SOURCE_COM152, 30
ENDIF

CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 3

;ABL:技巧をみる
SOURCE:0:性行動 = 100 + ABL:技巧*100

;ABL:露出癖をみる
SELECTCASE ABL:露出癖
CASE 0 TO 5
	SOURCE:0:露出 = 2000 + ABL:露出癖*200
	SOURCE:0:屈従 = 500 + ABL:露出癖*300
	SOURCE:0:逸脱 = 800 - ABL:露出癖*100
CASE 6 TO 10
	SOURCE:0:露出 = 3000 + (ABL:露出癖 - 5)*100
	SOURCE:0:屈従 = 2000 + (ABL:露出癖 - 5)*200
	SOURCE:0:逸脱 = 300 - (ABL:露出癖 - 5)*50
CASEELSE
	SOURCE:0:露出 = 3500 + (ABL:露出癖 - 10)*50
	SOURCE:0:屈従 = 3000 + (ABL:露出癖 - 10)*100
	SOURCE:0:逸脱 = 0
ENDSELECT

;公開のときは露出のSOURCE増加
SIF TEQUIP:ビデオカメラ
	TIMES SOURCE:0:露出, 1.50

SIF STRCOUNT(PLAY_ONANIE, "Ｃ")
	SOURCE:0:Ｃ快楽 = CALCF("Ｃ刺激", 1)
SIF STRCOUNT(PLAY_ONANIE, "Ｂ")
	SOURCE:0:Ｂ快楽 = CALCF("Ｂ刺激", 1)
SIF STRCOUNT(PLAY_ONANIE, "Ｖ")
	SOURCE:0:Ｖ快楽 = CALCF("Ｖ刺激", 1)
IF STRCOUNT(PLAY_ONANIE, "Ａ")
	SOURCE:0:Ａ快楽 = CALCF("Ａ刺激", 1)
	;アナルプレイなら腸液分泌
	SOURCE:0:Ａ液体追加 = 100 * ABL:自慰中毒
ENDIF
IF STRCOUNT(PLAY_ONANIE, "尿")
	SOURCE:0:尿道快楽 = CALCF("尿道刺激", 1)
	CALL SETFLAG, "尿道性感フラグ", TARGET
ENDIF

;ここから下は快ＣＶＡＢに対する補正が大量に来るので、それをLOCALに代入して短く表す
LOCAL = (ABL:自慰中毒 + ABL:露出癖)*20 - 100

;ABL:自慰中毒をみる
SOURCE:0:充足 = ABL:自慰中毒*300 + ABL:露出癖*200

;TARGETの指使いをみる
IF ABL:指使い
	SIF SOURCE:0:Ｃ快楽
		SOURCE:0:Ｃ快楽 = SOURCE_EXABL(SOURCE:0:Ｃ快楽, "指使い", TARGET)
	SIF SOURCE:0:Ｖ快楽
		SOURCE:0:Ｖ快楽 = SOURCE_EXABL(SOURCE:0:Ｖ快楽, "指使い", TARGET)
	SIF SOURCE:0:Ａ快楽
		SOURCE:0:Ａ快楽 = SOURCE_EXABL(SOURCE:0:Ａ快楽, "指使い", TARGET)
	SIF SOURCE:0:Ｂ快楽
		SOURCE:0:Ｂ快楽 = SOURCE_EXABL(SOURCE:0:Ｂ快楽, "指使い", TARGET)

	LOCAL += ABL:指使い * 10
ENDIF

;自慰経験
SELECTCASE EXP:自慰経験
CASE IS < 5
	LOCAL -= 20
CASE IS < 20
	LOCAL -= 10
CASE IS < 50
CASEELSE
	LOCAL += EXP:自慰経験/5
ENDSELECT

;最低でも10％
LOCAL = LIMIT(LOCAL, -90, 400)

SOURCE:0:Ｃ快楽 = SOURCE:0:Ｃ快楽*(100 + LOCAL)/100
SOURCE:0:Ｖ快楽 = SOURCE:0:Ｖ快楽*(100 + LOCAL)/100
SOURCE:0:Ａ快楽 = SOURCE:0:Ａ快楽*(100 + LOCAL)/100
SOURCE:0:Ｂ快楽 = SOURCE:0:Ｂ快楽*(100 + LOCAL)/100
SOURCE:0:尿道快楽 = SOURCE:0:尿道快楽*(100 + LOCAL)/100

;自慰中毒が無いなら、自慰で得た充足のSOURCEは０にする
SIF ABL:自慰中毒 == 0
	SOURCE:0:充足 = 0

IF TEQUIP:ソロプレイヤー
	TIMES SOURCE:0:屈従, 1.50
	TIMES SOURCE:0:逸脱, 0.50
	TIMES SOURCE:0:露出, 1.50
	TIMES SOURCE:0:充足, 1.50
	TIMES SOURCE:0:Ｃ快楽, 1.50
	TIMES SOURCE:0:Ｖ快楽, 1.50
	TIMES SOURCE:0:Ａ快楽, 1.50
	TIMES SOURCE:0:Ｂ快楽, 1.50
	TIMES SOURCE:0:尿道快楽, 1.50
ENDIF
SIF CHECK_COM("６９自慰", 3)
	TIMES SOURCE:0:露出, 2.00

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM3
#DIMS PLAY_ONANIE

PLAY_ONANIE = %CHECK_ONANIE(SELECTCOM)%

;連続実行
IF PREVCOM == SELECTCOM
	SIF TEQUIP:室内露出プレイ
		PRINTFORM 皆に見られてる中、
	IF TEQUIP:バイブ && TEQUIP:アナルバイブ && TEQUIP:尿道バイブ && COND("絶頂") >= 2
		PRINTFORML %CALLNAME:TARGET%は涙と涎をこぼしながら体を痙攣させな、声にならない声をあげている
		PRINTFORMW どうやら痙攣によりバイブを無意識にきつく締め付けてしまい、出し入れの度に絶頂しているようだ…
	ELSEIF TEQUIP:尿道バイブ && TEQUIP:アナルバイブ && COND("絶頂") >= 2
		PRINTFORML %CALLNAME:TARGET%は絶頂と共に弛緩した股間の穴から尿や腸液を溢れさせ、何とも言えない臭いを放ちながらビクビクと体を震わせている
		PRINTFORMW どうやら異常ともいえる排泄器官での快楽が病みつきになってしまった様子だ…
	ELSEIF TEQUIP:バイブ && TEQUIP:尿道バイブ && COND("絶頂") >= 2
		PRINTFORML %CALLNAME:TARGET%は両方のバイブでＧスポットを挟み込み、抜き差ししながら激しい快楽に体を痙攣させ鳴き叫んでいる
		PRINTFORMW どうやら強烈すぎる快感にブレーキは壊れ、手を止めることが出来ない様子だ…
	ELSEIF TEQUIP:バイブ && TEQUIP:アナルバイブ && COND("絶頂") >= 2
		PRINTFORML %CALLNAME:TARGET%はよだれをふきこぼし、ビクビクと身体を痙攣させながら鳴き叫んでいる
		PRINTFORMW どうやら絶頂から降りられず、イキっぱなしになっているようだ…
	ELSEIF TEQUIP:バイブ && COND("絶頂") >= 2
		PRINTFORML %CALLNAME:TARGET%は愛液が飛び散るのもお構いなしに、バイブを激しく出し入れしている
		PRINTFORMW どうやらもうほかの事を気にする余裕も無いようだ…
	ELSEIF TEQUIP:アナルバイブ && COND("アナルパール") == 0 && COND("絶頂") >= 2
		PRINTFORML %CALLNAME:TARGET%はガクガク身体を震わせながら、アヌスにバイブを突きこんでいる
		PRINTFORMW どうやら襞をえぐられるたびに何度も絶頂してしまっているようだ…
	ELSEIF TEQUIP:尿道バイブ && COND("絶頂") >= 2
		PRINTFORM %CALLNAME:TARGET%は
		SIF EXP_GAIN("膀胱精液経験")
			PRINT 精液混じりの
		PRINTFORML 尿を掻き出しながら、尿道内壁を削るかのようにバイブを激しく出し入れしている
		PRINTFORMW どうやら異常だと解っていながらも手を止めることは出来ないようだ…
	ELSEIF ABL:自慰中毒 >= 3 || COND("絶頂") || CHECK_COM("６９自慰") || CHECK_COM("フェラ自慰")
		IF TALENT:オトコ || (TALENT:ふたなり && RAND:2 == 0)
			PRINTFORML %CALLNAME:TARGET%は乱暴とも言える手つきで硬く勃起したペニスを扱き続けている
		ELSE
			PRINTFORML %CALLNAME:TARGET%は嬌声をあげながら一心不乱に自らの気持ちの良い場所をまさぐり続けている
		ENDIF
		PRINTFORMW どうやら止めたくても手が勝手に動いて止まらないようだ…
	ELSE
		PRINTFORML %CALLNAME:TARGET%は声を押し殺しながら、気持ちのいい場所をまさぐり続けている
		PRINTFORMW ……人に見られながらだと、いまいち没頭はできないようだ
	ENDIF
ELSE
	;手加減は終了
	IF TEQUIP:手加減
		PRINTW （手加減を止めます）
		PRINTL 
		TEQUIP:手加減 = 0
	ENDIF
	;いつも出すとくどい
	;SIF TEQUIP:室内露出プレイ
	;	PRINTFORM 皆に見られてる中、
	PRINTFORM %CALLNAME:TARGET%は
	IF INSERT("インサート")
		IF INSERT("Ｖインサート")
			LOCALS = 膣
		ELSEIF INSERT("Ａインサート")
			LOCALS = アナル
		ELSEIF INSERT("尿道インサート")
			LOCALS = 尿道
		ENDIF
		IF INSERT("騎乗位")
			PRINTFORM %CALLNAME:PLAYER%に跨って%LOCALS%に%NAMES("ペニス", PLAYER)%を咥え込んだまま、
		ELSEIF INSERT("背面座位")
			PRINTFORM %LOCALS%で%NAMES("ペニス", PLAYER)%を締め付けたまま%CALLNAME:PLAYER%にもたれ掛かって、
		ELSEIF INSERT("屈曲位")
			PRINTFORM %LOCALS%に%NAMES("ペニス", PLAYER)%を捻じ込まれた窮屈な体勢のまま、
		ELSE
			PRINTFORM %LOCALS%に挿入した体勢で腰の動きを止めている%CALLNAME:PLAYER%に従って、
		ENDIF
	ELSEIF CHECK_COM("６９自慰")
		PRINTFORM 目の前のペニス
		IF COND("絶頂") && RAND:3 == 0
			PRINTFORM を激しく舐めしゃぶりながら、
		ELSEIF ABL:奉仕精神 >= 3 && RAND:3 == 0
			PRINTFORM に奉仕をしながら、
		ELSE
			PRINTFORM %TEXTR("を舐めしゃぶり/にキスをし/をぺろぺろ舐め")%ながら、
		ENDIF
		PRINTFORM %CALLNAME:PLAYER%の顔を跨いだ体勢のままで
	ELSEIF CHECK_COM("フェラ自慰")
		PRINTFORM %CALLNAME:PLAYER%のペニス%TEXTR("にキスをし/をぺろぺろ舐め")%ながら、
	ELSE
		SELECTCASE TCVAR:やる気
		;やる気満々
		CASE IS >= 40
			IF TEQUIP:ビデオカメラ && RANDIF(3, ABL:露出癖 >= 3, TEQUIP:媚薬)
				PRINTFORM レンズに目線を送りながら、
			ELSEIF TEQUIP:媚薬 && RANDIF(2, ABL:露出癖 >= 3)
				PRINTFORM 媚薬で身体を高ぶらせながら、
				SIF ABL:自慰中毒 >= 3
					PRINTFORM 貪るように
			ELSEIF ABL:露出癖 >= 3 && RAND:2
				PRINTFORM %CALLNAME:PLAYER%に見せつけながら、
			ELSEIF RAND:2
				PRINTFORM 嫌がるそぶりも見せず、
			ELSE
				PRINTFORM 頬を薄っすらと朱に染め、
			ENDIF
		;抵抗なし
		CASE IS >= 20
			PRINTFORM %CALLNAME:PLAYER%に従って、
		;仕方なく
		CASEELSE
			IF TALENT:高慢 && RANDIF(1, TALENT:反抗的)
				PRINTFORM プライドの高さを見せながら睨んできたが、再度の言葉に渋々
			ELSEIF TALENT:反抗的
				PRINTFORM 目に反抗的な色を滲ませながら睨んできたが、再度の言葉に渋々
			ENDIF
		ENDSELECT
	ENDIF

	;オナホ自慰
	
	IF STRCOUNT(PLAY_ONANIE, "オナホ")
		PRINTFORM オナホールでペニスを扱いて
	ELSEIF STRCOUNT(PLAY_ONANIE, "両穴バイブ")
		IF COND("アナルパール")
			PRINTFORM 咥え込んだ%NAMES("バイブ", TARGET)%とアナルパールを自ら出し入れ
		ELSEIF SIZE("バイブ", TARGET) >= 2 && SIZE("Ａバイブ", TARGET) >= 2
			PRINTFORM 咥え込んだ２本の大きなバイブを自ら抜き挿し
		ELSE
			PRINTFORM 咥え込んだ２本のバイブを自ら抜き挿し
		ENDIF
		IF NOWEX:絶頂数合計 && RAND:2
			PRINTFORM して物凄い%TEXTS("匂い")%を漂わせつつ
		ELSE
			PRINTFORM して
		ENDIF
	ELSEIF STRCOUNT(PLAY_ONANIE, "Ｖバイブ")
		PRINTFORM ヴァギナに入れられた%NAMES("バイブ", TARGET)%を自ら抜き挿しして
	ELSEIF STRCOUNT(PLAY_ONANIE, "Ａバイブ")
		IF COND("アナルパール")
			PRINTFORM アナルから伸びるパールを自ら出し入れして
		ELSE
			PRINTFORM お尻に突っ込まれている%NAMES("Ａバイブ", TARGET)%を自ら抜き挿しして
		ENDIF
	ELSEIF STRCOUNT(PLAY_ONANIE, "尿道バイブ")
		PRINTFORM 尿道に入れられた%NAMES("尿道バイブ", TARGET)%を自ら抜き挿しして
	ELSE
		;2020/02/1　別人加筆
		SELECTCASE FAV_ONANIE(SELECTCOM, "一番")
		CASE "Ｃ"
			IF PENIS(TARGET)
				IF PALAM:欲情 >= 3000
					PRINTFORM %TEXTS("ペニス：視覚", TARGET)%
				ENDIF
				PRINTFORM ペニスを扱いて
			ELSE
				IF TALENT:クリピアス
					IF COND("陰核肥大")
						PRINTFORM %TEXTR("肥大した/大きな")%
					ENDIF
					PRINTFORM %TEXTR("陰核を貫く/陰核を飾る")%%TEXTS("クリピアス")%を
				ELSE
					IF TEQUIP:絆創膏 || CFLAG:絆創膏装着
						PRINTFORM 絆創膏の上から
					ENDIF
					PRINTFORM \@ COND("陰核肥大") ? 巨クリ # クリトリス \@を
				ENDIF
				PRINTFORM 自らの指で%TEXTR("擦って/弄って/捏ね回して")%
			ENDIF
		CASE "Ｖ"
			IF TALENT:ラビアピアス && (AGE(TARGET) < 3 || RAND:2)
				PRINTFORM %TEXTS("ラビアピアス")%%TEXTR("で飾られた/が輝く")%
				SELECTCASE AGE(TARGET)
					CASE IS >= 4
						PRINTFORM %TEXTR("肉厚の/成熟したオトナの/黒ずんだ")%
					CASEELSE
						PRINTFORM %TEXTR("大人びた/鮮やかな色合いの")%
				ENDSELECT
			ENDIF
			PRINTFORM %TEXTR("アソコ/秘所")%
			PRINTFORM %TEXTR("を自らの指で弄って/を自らの指で掻き回して/に自らの指を出し入れして")%
		CASE "Ａ"
			PRINTFORM %TEXTR("アナル/お尻の穴")%
			IF RAND:3 == 0 || !CHECK_CLO("Ａ露出")
				PRINTFORM を自らの指で弄って
			ELSE
				PRINTFORM %TEXTR("を自らの指で掻き回して/に自らの指を出し入れして")%
			ENDIF
		CASE "Ｂ"
			IF TALENT:ラビアピアス
				IF RAND:2 && CALCF("Ｂ発情")
					IF COND("陥没乳首")
						PRINTFORM %TEXTR("陥没した乳首の先端/顔を出した乳頭/乳輪中央に僅かに顔をだした乳首")%
					ELSE
						IF COND("乳首肥大")
							PRINTFORM %TEXTR("大きな/肥大化した")% 
						ENDIF
						PRINTFORM 乳首
					ENDIF
				ELSE
					PRINTFORM 胸の先端
				ENDIF
				IF CEVENTS("ニプルピアス") != "" && RAND:3
					PRINTFORM に付けられた%CEVENTS("ニプルピアス")%のピアス
				ELSE
					PRINTFORM を%TEXTR("飾る/彩る")%%TEXTR("ピアス/金属の輝き")%
				ENDIF
				PRINTFORM を自らの指で%TEXTR("弾いて/弄んで/軽く引っ張って")%
			ELSE
				IF RAND:2 && CALCF("Ｂ発情")
					IF COND("陥没乳首")
						PRINTFORM %TEXTR("陥没した乳首の先端/顔を出した乳頭/乳輪中央に僅かに顔をだした乳首")%
					ELSE
						PRINTFORM 乳首
					ENDIF
				ELSE
					PRINTFORM 胸
				ENDIF
				PRINTFORM を自らの指で%TEXTR("擦って/弄って/捏ね回して")%
			ENDIF
		CASE "尿"
			;指入れ可能
			IF CALCF("尿道径") > SIZE("指の直径", PLAYER)
				PRINTFORM %TEXTR("尿道/おしっこの穴")%
				PRINTFORM %TEXTR("を自らの指で弄って/を自らの指で掻き回して/に自らの指を出し入れして")%
			ELSE
				PRINTFORM %TEXTR("尿道口/尿道周辺の粘膜")%を自らの指で%TEXTR("擦って/弄って/捏ね回して")%
			ENDIF
		ENDSELECT
	ENDIF
	IF CHECK_COM("６９自慰")
		PRINTW 自慰行為に耽っている…
	ELSE
		PRINTW 快感を味わっている…
	ENDIF
ENDIF

;-------------------------------------------------
;どの種類のオナニーをしているかを決定する
;ARGにはどのSELECTCOMの時かを代入する。省略した場合にはSELECTCOMが代入されるが非推奨
;内容が複雑かつ#FUNCTIONを多数用いるため独立
;-------------------------------------------------
@CHECK_ONANIE(ARG)
#FUNCTIONS
;自慰に使える手の本数
#DIM USE_HAND
;自慰ＯＫな部位
#DIMS FAVPLAY

VARSET LOCALS

;コマンド選択時の表示ではARG == 3となっている
USE_HAND = CALCF("自由な手", ARG)
SIF USE_HAND == 0
	RETURNF LOCALS

;ＣＶＢＡ尿のうち、自慰で使える文字が代入される
FAVPLAY = %FAV_ONANIE(ARG)%

SIF ARG == 0
	ARG = SELECTCOM

IF INSERT("インサート") && CHECK_COM("インサート自慰")
	LOCALS = %FAV_ONANIE(ARG, "一番")%
ELSEIF USE_HAND >= 2
	IF CHECK_COM("オナホ自慰")
		LOCALS += "Ｃ"
	ELSEIF TEQUIP:バイブ && TEQUIP:アナルバイブ && COND("アナルパール") == 0 && TEQUIP:尿道バイブ
		;ＶＡ尿道ともに行けるなら三穴バイブオナニー
		IF STRCOUNT(FAVPLAY, "Ｖ") && STRCOUNT(FAVPLAY, "Ａ") && STRCOUNT(FAVPLAY, "尿")
			LOCALS += "ＶＡ尿"
		ELSEIF STRCOUNT(FAVPLAY, "Ｖ") && (ABL:Ｖ感覚 >= ABL:Ａ感覚 || ABL:Ａ感覚 < 3) && (ABL:Ｖ感覚 >= ABL:尿道感覚 || ABL:尿道感覚 < 3)
			LOCALS += "Ｖ"
		ELSEIF STRCOUNT(FAVPLAY, "Ａ") && (ABL:Ａ感覚 >= ABL:Ｖ感覚 || ABL:Ｖ感覚 < 3) && (ABL:Ａ感覚 >= ABL:尿道感覚 || ABL:尿道感覚 < 3)
			LOCALS += "Ａ"
		ELSE
			LOCALS += "尿"
		ENDIF
	ELSEIF TEQUIP:バイブ && TEQUIP:アナルバイブ && COND("アナルパール") == 0
		;ＶＡともに行けるなら両穴バイブオナニー
		IF STRCOUNT(FAVPLAY, "Ｖ") && STRCOUNT(FAVPLAY, "Ａ")
			LOCALS += "ＶＡ"
		ELSEIF STRCOUNT(FAVPLAY, "Ｖ") && (ABL:Ｖ感覚 >= ABL:Ａ感覚 || ABL:Ａ感覚 < 3)
			LOCALS += "Ｖ"
		ELSE
			LOCALS += "Ａ"
		ENDIF
	ELSEIF TEQUIP:尿道バイブ && TEQUIP:アナルバイブ && COND("アナルパール") == 0
		;Ａ尿道ともに行けるなら両穴バイブオナニー
		IF STRCOUNT(FAVPLAY, "尿") && STRCOUNT(FAVPLAY, "Ａ")
			LOCALS += "Ａ尿"
		ELSEIF STRCOUNT(FAVPLAY, "尿") && (ABL:尿道感覚 >= ABL:Ａ感覚 || ABL:Ａ感覚 < 3)
			LOCALS += "尿"
		ELSE
			LOCALS += "Ａ"
		ENDIF
	ELSEIF TEQUIP:バイブ && TEQUIP:尿道バイブ
		;Ｖ尿道ともに行けるなら両穴バイブオナニー
		IF STRCOUNT(FAVPLAY, "Ｖ") && STRCOUNT(FAVPLAY, "尿")
			LOCALS += "Ｖ尿"
		ELSEIF STRCOUNT(FAVPLAY, "Ｖ") && (ABL:Ｖ感覚 >= ABL:尿道感覚 || ABL:尿道感覚 < 3)
			LOCALS += "Ｖ"
		ELSE
			LOCALS += "尿"
		ENDIF
	ELSEIF ARG == 3
		IF TEQUIP:バイブ && STRCOUNT(FAVPLAY, "Ｖ")
			LOCALS += "Ｖ"
		ELSEIF TEQUIP:アナルバイブ && COND("アナルパール") == 0 && STRCOUNT(FAVPLAY, "Ａ")
			LOCALS += "Ａ"
		ELSEIF TEQUIP:尿道バイブ && STRCOUNT(FAVPLAY, "尿")
			LOCALS += "尿"
		ELSE
			LOCALS = %FAV_ONANIE(ARG, "一番")%
		ENDIF
	ENDIF
ENDIF
IF CHECK_COM("オナホ自慰") && STRCOUNT(FAVPLAY, "Ｃ")
	LOCALS += "オナホ"
ELSEIF TEQUIP:バイブ && TEQUIP:アナルバイブ && COND("アナルパール") == 0 && STRCOUNT(FAVPLAY, "Ｖ") && STRCOUNT(FAVPLAY, "Ａ")
	LOCALS += "ＶバイブＡバイブ両穴バイブ"
ELSEIF TEQUIP:バイブ && STRCOUNT(FAVPLAY, "Ｖ")
	LOCALS += "Ｖバイブ"
ELSEIF TEQUIP:アナルバイブ && COND("アナルパール") == 0 && STRCOUNT(FAVPLAY, "Ａ")
	LOCALS += "Ａバイブ"
ELSEIF TEQUIP:尿道バイブ && STRCOUNT(FAVPLAY, "尿")
	LOCALS += "尿道バイブ"
ENDIF

RETURNF LOCALS



@FAV_ONANIE(ARG, ARGS)
#FUNCTIONS
#DIM LCOUNT
;自慰に使える手の本数
#DIM USE_HAND
;自慰ＯＫな部位
#DIMS FAVPLAY
;オナニーする部位
#DIMS IS_ONANIES, 5
;好きなオナニー場所
#DIMS FAV_ONANIES, 5

;好きな場所決定(優先度はＣ＞Ｖ＞Ｂ＞Ａ＞尿道)
;ARGは実行中のSEELCTCOMに相当する値が代入されている（コマンド選択時ではARG == 3）
;ただし、インサートされている時に弄れないであろう場所等は選べない
LOCAL:0 = ABL:Ｃ感覚 + TALENT:Ｃ性向*3 + TALENT:淫核*5 + PENIS(TARGET)*3
LOCAL:1 = ABL:Ｖ感覚 + TALENT:Ｖ性向*3 + TALENT:淫壷*5
LOCAL:2 = ABL:Ｂ感覚 + TALENT:Ｂ性向*3 + TALENT:淫乳*5
LOCAL:3 = ABL:Ａ感覚 + TALENT:Ａ性向*3 + TALENT:尻穴狂い*5
LOCAL:4 = ABL:尿道感覚 + TALENT:尿道性感*3 + TALENT:尿道狂い*5

VARSET IS_ONANIES
VARSET FAV_ONANIES

IS_ONANIES:0 = Ｃ

SIF TALENT:オトコ == 0 && TALENT:処女 == 0
	IS_ONANIES:1 = Ｖ
SIF USE_V(TARGET, "バイブ") == 0
	IS_ONANIES:1 = 

IS_ONANIES:2 = Ｂ
SIF ARG == 92 || ARG == 104 || CHECK_COM("パイズリフェラ", ARG) || CHECK_COM("ぱふぱふ手コキ", ARG) || CHECK_COM("６９自慰", ARG)
	IS_ONANIES:2 = 
SIF CHECK_SHIRT("鎧") && CHECK_CLO("谷間露出") + CHECK_CLO("乳首露出") + CHECK_CLO("はだけ可") == 0
	IS_ONANIES:2 = 

SIF TALENT:Ａ性向 || TALENT:尻穴狂い
	IS_ONANIES:3 = Ａ
SIF USE_A(TARGET, "アナルバイブ") == 0
	IS_ONANIES:3 = 

SIF TALENT:尿道性感 || TALENT:尿道狂い
	IS_ONANIES:4 = 尿
SIF TEQUIP:絆創膏 || CFLAG:絆創膏装着
	IS_ONANIES:4 = 

;Ｖと尿道
SELECTCASE ARG
CASE 60 TO 69, 70, 73, 83 TO 88
	IS_ONANIES:1 = 
	SIF PENIS(TARGET) == 0
		IS_ONANIES:4 = 
CASE IS >= 0
	IF INSERT("Ｖインサート") || INSERT("正常位") || INSERT("対面座位")
		IS_ONANIES:1 = 
		SIF PENIS(TARGET) == 0
			IS_ONANIES:4 = 
	ENDIF
ENDSELECT

;Ａ
SELECTCASE ARG
CASE 60 TO 62, 64, 70 TO 79
	IS_ONANIES:3 = 
CASEELSE
	SIF INSERT("Ａインサート") || INSERT("正常位") || INSERT("後背位") || INSERT("屈曲位") || INSERT("背面座位")
		IS_ONANIES:3 = 
ENDSELECT

FOR LCOUNT, 0, 5
	;PRINTFORML LOCAL:{LCOUNT} = {LOCAL:LCOUNT}
	SIF LOCAL:LCOUNT == 0 || IS_ONANIES:LCOUNT == ""
		CONTINUE
	;好きな場所決定(優先度はＣ＞Ｖ＞Ｂ＞Ａ＞尿道)
	IF FAV_ONANIES:0 == "" && LOCAL:LCOUNT >= MAX(LOCAL:0, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4)
		FAV_ONANIES:0 = %IS_ONANIES:LCOUNT%
	ELSEIF FAV_ONANIES:1 == "" && LOCAL:LCOUNT >= MAX(LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4)
		FAV_ONANIES:1 = %IS_ONANIES:LCOUNT%
	ELSEIF FAV_ONANIES:2 == "" && LOCAL:LCOUNT >= MAX(LOCAL:2, LOCAL:3, LOCAL:4)
		FAV_ONANIES:2 = %IS_ONANIES:LCOUNT%
	ELSEIF FAV_ONANIES:3 == "" && LOCAL:LCOUNT >= MAX(LOCAL:3, LOCAL:4)
		FAV_ONANIES:3 = %IS_ONANIES:LCOUNT%
	ELSEIF FAV_ONANIES:4 == ""
		FAV_ONANIES:4 = %IS_ONANIES:LCOUNT%
	ENDIF
NEXT

;コマンド選択時の表示ではARG == 3となっている
USE_HAND = CALCF("自由な手", ARG)

FAVPLAY = 
FOR LCOUNT, 0, USE_HAND
	FAVPLAY += @"%FAV_ONANIES:LCOUNT%"
NEXT
SIF FAVPLAY == ""
	FAVPLAY = Ｃ

SELECTCASE ARGS
CASE "一番"
	RETURNF SUBSTRINGU(FAVPLAY, 0, 1)
CASEELSE
	RETURNF FAVPLAY
ENDSELECT


@EQUIP_COM3
;自慰中毒0LV
SIF ABL:自慰中毒 == 0
	RETURN 0

SELECTCASE SELECTCOM
;セックス系、奉仕系、何もしない 選択時に行う
CASE 60 TO 89, 139
CASEELSE
	RETURN 0
ENDSELECT

;調教中でないor自慰を禁止されてるorTARGETが挿入してる側だとしない
SIF COND("調教中") == 0 || COND("身嗜み：自慰禁止") || INSERT("挿入中")
	RETURN 0
;苦痛あり、性欲満足、Ｗ押し倒し中、手が余ってない
SIF TEQUIP:Ｗ押し倒し || CUP:苦痛 > PALAM_LV(ABL:マゾっ気) || BASE:性欲 >= MAXBASE:性欲 || CALCF("自由な手") <= 0
	RETURN 0

;欲望、信頼、奉仕精神も判定に使用
;前のターンに自慰しているorこのターンに何もしないなら高確率で自慰しちゃう
IF PREVCOM == 3 && ABL:自慰中毒 >= RAND:4
ELSEIF SELECTCOM == 139 && ABL:自慰中毒 >= RAND:5
ELSEIF ABL:自慰中毒 <= RAND:8 || ABL:自慰中毒*3 + ABL:欲望 + RAND:5 < ABL:信頼 + ABL:奉仕精神*2
	RETURN 0
ENDIF

CALL SETFLAG, "ながらオナニーフラグ", TARGET

;SOURCEの増加
CALL SOURCE_COM3,30
CALL FLAG_COM3, 30

@EQUIP_COM3_2
;自慰中毒0LV
SIF ABL:自慰中毒 == 0
	RETURN 0
SIF COND("ながらオナニーフラグ") == 0
	RETURN 0

SELECTCASE SELECTCOM
CASE 60 TO 79, 83 TO 88
	PRINTL ＜自慰中毒＞
	IF SELECTCOM == 65 || SELECTCOM == 75 || SELECTCOM == 88 || TEQUIP:押し倒し
		PRINTFORM %CALLNAME:TARGET%は腰を振りながら指先を
	ELSE
		PRINTFORM %CALLNAME:TARGET%は犯されながら指先を
	ENDIF
	LOCALS = 伸ばし
;何もしない
CASE 139
	PRINTL ＜自慰中毒＞
	PRINTFORM %CALLNAME:TARGET%の指先は\@ ABL:自慰中毒 >= 3 ? 無意識に # \@
	LOCALS = 伸び
CASEELSE
	PRINTFORM その指先は
	LOCALS = 伸び
ENDSELECT

SELECTCASE FAV_ONANIE(SELECTCOM, "一番")
CASE "Ｃ"
	IF PENIS(TARGET)
		IF CHECK_COM("オナホ自慰")
			PRINTFORMW 股間に%LOCALS%、%TEXTR("ガシガシと激しくオナホールを上下させている/オナホールで思う存分ペニスを扱いている")%…
		ELSE
			PRINTFORMW ペニスに%LOCALS%、%TEXTR("敏感な亀頭を撫で回している/ガシガシと激しく扱いている")%…
		ENDIF
		CALL EQ_STAIN, TARGET, "手", TARGET, "Ｐ"
	ELSE
		PRINTFORM 股間に%LOCALS%、
		IF RAND:2 == 0 || (SELECTCOM >= 60 && SELECTCOM <= 79)
			IF CALCF("Ｃ発情") >= 3
				PRINTFORM 肥大した
			ELSE
				SIF CALCF("Ｃ発情") <= 1
					PRINTFORM 包皮越しに
				PRINTFORM 敏感な
			ENDIF
			PRINTFORM 突起を
			IF TALENT:クリピアス && RAND:2 == 0
				PRINTW 貫くピアスを弄っている…
			ELSE
				PRINTW こねくり回している…
			ENDIF
		ELSE
			IF TALENT:クリピアス && RAND:2 == 0
				IF RAND:2
					PRINTFORMW ピアスを捻りあげてクリトリスを虐めている…
				ELSE
					PRINTFORMW 淫核に付けられたピアスを弄んでいる…
				ENDIF
			ELSEIF CALCF("Ｃ発情") < 3 && RAND:2
				PRINTW 徐々に尖っていくクリトリスを弄くり回している…
			ELSE
				PRINTW くちゅくちゅと淫靡な音をさせている…
			ENDIF
		ENDIF
		CALL EQ_STAIN, TARGET, "手", TARGET, "Ｖ"
	ENDIF
CASE "Ｖ"
	PRINTFORM 股間に%LOCALS%、
	IF TEQUIP:バイブ
		IF SIZE("バイブ", TARGET) >= 2
			PRINTFORM 膣を押し広げている
		ELSE
			PRINTFORM 咥え込んでいる
		ENDIF
		SIF SIZE("バイブ", TARGET) == 2
			PRINTFORM 大きな
		SIF SIZE("バイブ", TARGET) >= 3
			PRINTFORM 野太い
		PRINTW バイブを出し入れさせて楽しんでいる…
	ELSEIF INSERT("正常位") + INSERT("屈曲位") && ABL:露出癖 >= 3 && RAND:2
		PRINTFORMW 時折秘部を押し開いたりしながら自慰を楽しんでいる…
	ELSE
		PRINTW くちゅくちゅと淫靡な音をさせている…
		CALL EQ_STAIN, TARGET, "手", TARGET, "Ｖ"
	ENDIF
	EXP:Ｖ経験 += 1
	PRINTFORML Ｖ経験＋1
CASE "Ａ"
	PRINTFORM 股間に%LOCALS%、
	IF TEQUIP:アナルバイブ
		PRINTW アナルから顔を出しているおもちゃを弄って楽しんでいる…
	ELSE
		PRINTW 柔らかいお尻の穴を弄っているようだ…
		CALL EQ_STAIN, TARGET, "手", TARGET, "Ａ"
	ENDIF
	EXP:Ａ経験 += 1
	PRINTFORML Ａ経験＋1
CASE "Ｂ"
	IF TALENT:巨乳
		IF RAND:2
			PRINTFORM 豊満な
		ELSE
			PRINTFORM 柔らかな
		ENDIF
	ELSEIF TALENT:貧乳
		PRINTFORM 小さな
	ENDIF
	PRINTFORM 胸に%LOCALS%、
	IF RAND:2 == 0 && TALENT:巨乳
		SIF ABL:Ｂ感覚 >= 4 && RAND:2
			PRINTFORM 指を沈ませながら
		PRINTW 乳房を揉みしだいている…
	ELSE
		LOCAL = 0
		IF TALENT:ニプルピアス && RAND:2 == 0
			PRINTFORM %TEXTS("ニプルピアス")%で飾られた
			LOCAL = 1
		ELSEIF CALCF("Ｂ発情") >= 3
			PRINTFORM 快感でふくれた
		ELSEIF CALCF("Ｂ発情") >= 2
			PRINTFORM ツンと上向いた
		ELSEIF CALCF("Ｂ発情")
			PRINTFORM 勃起しかけの
		ELSE
			PRINTFORM ピンクの
		ENDIF
		PRINTFORM 突起
		IF TALENT:ニプルピアス && RAND:3 == 0 && LOCAL == 0
			PRINTW を貫くピアスを弄っている…
		ELSE
			IF RAND:2
				PRINTW をしごいている…
			ELSE
				PRINTW をこねくり回している…
			ENDIF
		ENDIF
	ENDIF
	CALL EQ_STAIN, TARGET, "手", TARGET, "Ｂ"
CASE "尿"
	PRINTFORM 股間に%LOCALS%、
	IF TEQUIP:尿道バイブ
		PRINTFORM %TEXTR("尿道/おしっこの穴")%
		IF SIZE("尿道バイブ", TARGET) >= 2 && RAND:2
			PRINTFORM を押し広げている
		ELSE
			PRINTFORM で咥え込んでいる
		ENDIF
		SELECTCASE SIZE("尿道バイブ", TARGET)
		CASE 2
			PRINTFORM 大きな
		CASE IS >= 3
			PRINTFORM 野太い
		ENDSELECT
		PRINTFORM バイブを出し入れさせて楽しんでいる…
	ELSE
		;指入れ可能
		IF CALCF("尿道径") > SIZE("指の直径", PLAYER)
			PRINTFORM 緩んだ%TEXTR("尿道/おしっこの穴")%
			PRINTFORM %TEXTR("を掻き回して/に出し入れして")%いる…
		ELSE
			PRINTFORM 大好きな%TEXTR("尿道口/尿道周辺の粘膜")%を%TEXTR("擦って/弄って/捏ね回して")%楽しんでいる…
		ENDIF
	ENDIF
	PRINTFORMW 
	STAIN:手 = SET_STAIN(STAIN:TARGET:手, "尿")
	EXP:尿道経験 += 1
	PRINTFORML 尿道経験＋1
ENDSELECT

EXP:自慰経験 += 1
PRINTFORML 自慰経験＋1


@COM_ORDER_COM3, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:露出癖
IF ABL:露出癖
	LOCAL += MIN(ABL:露出癖, 10) * 2
	LOCALS = %LOCALS% + 露出癖LV{ABL:露出癖}({MIN(ABL:露出癖, 10) * 2})
ENDIF
;ABL:自慰中毒
IF ABL:自慰中毒
	LOCAL += MIN(ABL:自慰中毒, 5) * 3
	LOCALS = %LOCALS% + 自慰中毒LV{ABL:自慰中毒}({MIN(ABL:自慰中毒, 5) * 3})
ENDIF

;強気
IF TALENT:強気
	LOCAL -= 5
	LOCALS = %LOCALS% - 強気(5)
ENDIF
;冷静
IF TALENT:冷静
	LOCAL -= 5
	LOCALS = %LOCALS% - 冷静(5)
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 15
	LOCALS = %LOCALS% - 恥じらい(15)
ENDIF
;恥薄い
IF TALENT:恥薄い
	LOCAL += 2
	LOCALS = %LOCALS% + 恥薄い(2)
ENDIF
;快感に素直
IF TALENT:快感に素直
	LOCAL += 5
	LOCALS = %LOCALS% + 快感に素直(5)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 5
	LOCALS = %LOCALS% - 快感の否定(5)
ENDIF
;６９中
IF TEQUIP:シックスナイン
	LOCAL -= 10
	LOCALS = %LOCALS% - シックスナイン中(10)
ENDIF
IF COND("陶酔")
	LOCAL += 10
	LOCALS = %LOCALS% + 陶酔(10)
ENDIF
IF TEQUIP:媚薬
	LOCAL += 6
	LOCALS = %LOCALS% + 媚薬(6)
ENDIF
;バイブ
IF TEQUIP:バイブ
	LOCAL -= 5
	LOCALS = %LOCALS% - バイブ(5)
ENDIF
;Ａバイブ
IF TEQUIP:アナルバイブ && COND("アナルパール") == 0
	LOCAL -= 10
	LOCALS = %LOCALS% - アナルバイブ(10)
ENDIF
;ビデオ撮影
IF TEQUIP:ビデオカメラ
	LOCAL -= 20
	LOCALS = %LOCALS% - 撮影中(20)
ENDIF
IF TEQUIP:野外プレイ
	LOCAL -= 10
	LOCALS = %LOCALS% - 野外プレイ(10)
ENDIF

;合計を表示(33以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 33

RETURN RESULT
