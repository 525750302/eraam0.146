@COM170
;ビデオカメラ
;恥じらいや清楚、貞操観念持ちだと凄い反発。
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

IF TEQUIP:ビデオカメラ
	STR:前回コマンド = ビデオカメラ(撮影開始)
ELSE
	STR:前回コマンド = ビデオカメラ(撮影終了)
ENDIF

RETURN 1

@TRAIN_MESSAGE_COM170
;ビデオ撮影がされていれば終了させる
IF TEQUIP:ビデオカメラ
	;短すぎる
	IF TEQUIP:ビデオカメラ <= 3
		;認識阻害持ち
		IF TALENT:MASTER:禁断の知識 >= 3
			PRINTFORMW ここで止めると短すぎて売り物にはなりませんが、撮るのをやめて良いですか？
		ELSE
			PRINTFORMW ここで止めると短すぎて残す価値がなさそうですが、撮るのをやめて良いですか？
		ENDIF
		CALL PRINT_SELECT, "はい/いいえ"
		IF RESULT
			TFLAG:コマンドキャンセル = 1
			RETURN 0
		ENDIF
		STR:ビデオ内容 = 
		STR:ビデオジャンル = 
	ENDIF

	TEQUIP:ビデオカメラ = 0
	PRINTW ★★★ビデオ撮影を終了します★★★
	;助手が居ない
	IF ASSI <= 0 || ASSIPLAY

	ELSEIF ABL:ASSI:自慰中毒 >= RAND:6 && RAND:2
		IF TALENT:ASSI:冷静 + TALENT:ASSI:感情乏しい && RAND:2
			PRINTFORMW 撮影を終えた%CALLNAME:ASSI%は冷静を装っているが、熱を持った自らの股間を触りたい様子で内腿に手を這わせている…
		ELSE
			PRINTFORMW たっぷりと見せつけられた%CALLNAME:ASSI%は我慢できなかったのか、片手を股間に伸ばしている…
		ENDIF
	ELSE
		IF TALENT:ASSI:濡れやすい && ABL:ASSI:欲望 >= RAND:6 && RAND:2
			PRINTFORMW %CALLNAME:ASSI%は%TEXTS("助手の名称")%としてしっかり仕事をこなしたが、その脚には淫液が一筋伝っている…
		ELSE
			PRINTFORMW %CALLNAME:ASSI%は良い映像が撮れたと嬉しそうにしている…
		ENDIF
	ENDIF
ELSE
	IF STR:ビデオ内容 != ""
		PRINTFORMW 既に今回収録した分がありますが、こちらを放棄して新たに撮り直して良いですか？
		CALL PRINT_SELECT, "はい/いいえ"
		IF RESULT
			TFLAG:コマンドキャンセル = 1
			RETURN 0
		ENDIF
	ENDIF

	TEQUIP:ビデオカメラ = 1
	STR:ビデオ内容 = 
	STR:ビデオジャンル = 
	PRINTFORML ★★★ビデオ撮影を開始します★★★

	IF EXP:撮影経験 == 0
		IF TALENT:好奇心
			PRINT 初めて見たものに興味津々の
		ELSE
			PRINT 不思議なものを見るかのように撮影機材を眺める
		ENDIF
	ELSEIF ABL:露出癖 >= 3 || TALENT:恥薄い
		IF TALENT:相思相愛 && TALENT:淫乱 && RAND:2
			PRINT 自分たちの愛を他人に見せつけてやろうと盛り上がっている
		;ELSEIF COND("人気：ＡＶ") && RAND:2
		;	IF TALENT:強気 || TALENT:高慢
		;		PRINT 自分の人気の有り様を受けて満更でもない様子で準備をしている
		;	ELSE
		;		PRINT 作品作りにも慣れてレンズ越しのお客からの受けを思案している
		;	ENDIF
		ELSE
			PRINT ビデオカメラが視界に入った途端に目を輝かせる
		ENDIF
	ELSEIF TEQUIP:野外プレイ
		PRINT 周囲を不安そうに伺っている
	ELSEIF TALENT:恥じらい
		PRINT 顔を真っ赤にしてこちらの様子を窺っている
	ELSE
		PRINT どんなプレイをしようかと考えを巡らせている
	ENDIF
	PRINTFORM %CALLNAME:TARGET%を尻目に、
	IF ASSI <= 0
		PRINTFORMW %CALLNAME:PLAYER%はビデオカメラを手ごろな高さの台に置いて再開した。
		PRINTFORMW （%TEXTS("助手の名称")%がいないため少々質が落ちますが仕方ありません…）
	ELSEIF ASSIPLAY
		PRINTFORMW %CALLNAME:PLAYER%は撮影を%CALLNAME:MASTER%に任せてエッチを再開した。
	ELSE
		LOCALS = 
		IF TEQUIP:野外プレイ || TEQUIP:室内露出プレイ
			IF CHARA_NUM("室内露出観客数")
				LOCALS = ギャラリーたちと一緒にくすくすと笑いながら、
			ELSEIF ABL:ASSI:サドっ気 >= 3
				LOCALS = ギャラリーがいないことを惜しがりつつ、
			ELSE
				LOCALS = 周囲に人がいないことを確認しつつ、
			ENDIF
		ELSEIF TEQUIP:室内露出プレイ
			LOCALS = 部員たちの反応も一緒に記録することを意識しながら、
		ELSEIF (COND("母親") == ASSI || COND("父親") == ASSI || COND("母親", ASSI) == TARGET || COND("父親", ASSI) == TARGET) && RAND:3 == 0
			IF COND("母親") == ASSI || COND("父親") == ASSI
				LOCALS = 我が子の性的な成長記録をしっかり残そうと胸の内で思いながら、
			ELSE
				LOCALS = 自分の親の情事を記録することに興奮しつつ、
			ENDIF
		ELSEIF (TALENT:ASSI:淫乱 || TALENT:ASSI:相思相愛) && RAND:4 == 0
			IF TALENT:ASSI:淫乱 && RANDIF(1, TALENT:ASSI:相思相愛)
				IF TALENT:ASSI:高慢
					LOCALS = 過激なシーンを見せるようにと指示を出して、
				ELSE
					LOCALS = これから見せられる情事に期待をしながら、
				ENDIF
			ELSE
				IF TALENT:ASSI:淫乱
					LOCALS = 「今は%CALLNAME:TARGET%の番」と自分に言い聞かせ、
				ELSE
					LOCALS = 寂しげな表情を浮かべていたが、しぶしぶ
				ENDIF
			ENDIF
		ELSEIF ABL:ASSI:自慰中毒 >= 3 && RAND:3 == 0
			LOCALS = 片手を\@ TALENT:ASSI:オトコ ? 股間 # 秘所 \@に伸ばしそうになるのを我慢しながら、

		ELSEIF ABL:ASSI:サドっ気 >= 3 && RANDIF(1, TALENT:ASSI:素直, TALENT:ASSI:好奇心, TALENT:ASSI:鼓舞, TALENT:ASSI:粗野)
			LOCALS = 直接相手出来ないのを悔しがりつつも、しっかり仕事をこなそうと
		ELSEIF TALENT:ASSI:粗野 && RANDIF(1, TALENT:ASSI:素直, TALENT:ASSI:好奇心, TALENT:ASSI:鼓舞)
			LOCALS = 自分に任せるようにと胸を張り、
		ELSEIF TALENT:ASSI:鼓舞 && RANDIF(1, TALENT:ASSI:素直, TALENT:ASSI:好奇心)
			LOCALS = %CALLNAME:TARGET%をやんややんやと応援しながら、
		ELSEIF TALENT:ASSI:好奇心 && RANDIF(1, TALENT:ASSI:素直)
			IF RELATION:ASSI:TARGET >= 150
				LOCALS = %CALLNAME:TARGET%の痴態を逃さず記録しようと、はりきって
			ELSE
				LOCALS = %CALLNAME:MASTER%達の痴態を逃さず記録しようと、はりきって
			ENDIF
		ELSEIF TALENT:ASSI:素直
			LOCALS = %CALLNAME:MASTER%に言われた通りに
		ELSEIF ABL:ASSI:自慰中毒 >= 3 && RAND:3 == 0
			LOCALS = 片手を\@ TALENT:ASSI:オトコ ? 股間 # 秘所 \@に伸ばしそうになるのを我慢しながら、
		ENDIF

		IF LOCALS == ""
			PRINTFORMW %CALLNAME:PLAYER%はビデオカメラを%CALLNAME:ASSI%に手渡して撮影を始めさせた…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%はビデオカメラを%CALLNAME:ASSI%に手渡した。
			PRINTFORMW %CALLNAME:ASSI%は%LOCALS%撮影を開始した…
		ENDIF
		CALL KOJO_GUEST, "助手：撮影開始", ASSI
	ENDIF
ENDIF

@EQUIP_COM170
SIF TEQUIP:ビデオカメラ == 0
	RETURN 0
SIF SELECTCOM == 170
	RETURN 0

CALL SOURCE_COM170, 10

RETURN 1

@FLAG_COM170, ARG

@SOURCE_COM170, ARG
SIF TEQUIP:ビデオカメラ == 0
	RETURN 0

CALL VARSET_COMF

SOURCE:0:露出 = 3000
SOURCE:0:逸脱 = 5000

;ハダカ
IF CHECK_CLO("ハダカ")
	;種族衣装がハダカでない
	IF CLO_ORI(TARGET, "服", "ボディスーツ", "アウター", "パンツ")
		SOURCE:0:露出 += 3000
		SOURCE:0:逸脱 += 1000
	ENDIF
;パンツのみ
ELSEIF CHECK_CLO("下着姿")
	;種族衣装が服orボディスーツorアウターを含む
	IF CLO_ORI(TARGET, "服", "ボディスーツ", "アウター")
		SOURCE:0:露出 += 2000
		SOURCE:0:逸脱 += 500
	ENDIF
;ノーパン
ELSEIF CHECK_CLO("ノーパン")
	;種族衣装に下着あり
	IF CLO_ORI(TARGET, "パンツ")
		SOURCE:0:露出 += 1000
		SOURCE:0:逸脱 += 300
	ENDIF
ENDIF

;拘束の状態により露出を追加
IF GETBIT(TEQUIP:拘束, 5)
	SOURCE:0:露出 += 1000
ELSEIF GETBIT(TEQUIP:拘束, 4)
	SOURCE:0:露出 += 500
ELSEIF GETBIT(TEQUIP:拘束, 3)
	SOURCE:0:露出 += 200
ENDIF

;露出はTRACHECKで補正がかかるので、逸脱に恥じらいなどで補正
SIF TALENT:恥じらい
	TIMES SOURCE:0:逸脱, 2.00
SIF TALENT:清楚
	TIMES SOURCE:0:逸脱, 1.50
SIF TALENT:目立ちたがり
	TIMES SOURCE:0:逸脱, 0.50
SIF TALENT:内気
	TIMES SOURCE:0:逸脱, 1.70
SIF TALENT:賢い
	TIMES SOURCE:0:露出, 1.50
SIF TALENT:愚か
	TIMES SOURCE:0:露出, 0.70

SOURCE:0:恐れ += SOURCE:0:逸脱 / 10

CALL CALC, "調教ソース", ARG


@EQUIP_COM170_2
SIF TEQUIP:ビデオカメラ == 0
	RETURN 0
SIF SELECTCOM == 170
	RETURN 0

PRINTFORML ＜ビデオ撮影中{TEQUIP:ビデオカメラ}/10＞
EXP:露出経験 += 1
EXP:撮影経験 += 1
PRINTFORML 露出経験＋１
PRINTFORML 撮影経験＋１

RETURN 1

;ターン終了時の色々なチェック
@EQUIP_COM170_EVENTCOMEND
;ここでは主にジャンルのチェックをする
;撮影開始時
IF TEQUIP:ビデオカメラ == 1
	IF CHECK_SHIRT("女教師")
		STR:ビデオジャンル += "女教師/"
	ELSEIF CHECK_SHIRT("メイド服")
		STR:ビデオジャンル += "メイド/"
	ELSEIF CHECK_OUTER("ブルマ")
		STR:ビデオジャンル += "ブルマ/"
	ELSEIF CHECK_SHIRT("体操服")
		STR:ビデオジャンル += "体操服/"
	ELSEIF CHECK_SHIRT("看護婦")
		STR:ビデオジャンル += "ナース/"
	ELSEIF CHECK_SHIRT("アメスク")
		STR:ビデオジャンル += "アメスク/"
	ELSEIF CHECK_SHIRT("セーラー服")
		STR:ビデオジャンル += "セーラー服/"
	ELSEIF CHECK_SHIRT("学生服")
		STR:ビデオジャンル += "学生服/"
	ELSEIF CHECK_SHIRT("巫女")
		STR:ビデオジャンル += "巫女/"
	ELSEIF CHECK_CLO("水着姿")
		IF CHECK_CLO("マイクロビキニ姿")
			STR:ビデオジャンル += "マイクロビキニ/"
		ELSEIF CHECK_CLO("ビキニ姿")
			STR:ビデオジャンル += "ビキニ/"
		ELSEIF CHECK_BODYS("競泳水着")
			STR:ビデオジャンル += "競泳水着/"
		ELSEIF CHECK_BODYS("スク水")
			STR:ビデオジャンル += "スク水/"
		ELSE
			STR:ビデオジャンル += "水着/"
		ENDIF
	ELSEIF CHECK_BODYS("ボンデージ")
		STR:ビデオジャンル += "ボンデージ/"
	ELSEIF CHECK_BODYS("バニー")
		STR:ビデオジャンル += "バニー/"
	ELSEIF CHECK_SHIRT("ゴスロリドレス")
		STR:ビデオジャンル += "ゴスロリ/"
	ELSEIF CHECK_SHIRT("チアユニフォーム")
		STR:ビデオジャンル += "チアガール/"
	ELSEIF CHECK_SHIRT("チャイナドレス")
		STR:ビデオジャンル += "チャイナドレス/"
	ENDIF

	SIF TALENT:オトコ && TALENT:PLAYER:オトコ
		STR:ビデオジャンル += "ＢＬ/"
	SIF TALENT:オンナ && TALENT:PLAYER:オンナ
		STR:ビデオジャンル += "レズ/"
	SIF TALENT:ふたなり || TALENT:PLAYER:ふたなり
		STR:ビデオジャンル += "ふたなり/"
	SIF TEQUIP:野外プレイ
		STR:ビデオジャンル += "野外/"

	IF TALENT:巨乳 >= 3
		STR:ビデオジャンル += "超乳/"
	ELSEIF TALENT:巨乳 >= 2
		STR:ビデオジャンル += "爆乳/"
	ELSEIF TALENT:巨乳
		STR:ビデオジャンル += "巨乳/"
	ELSEIF TALENT:貧乳
		STR:ビデオジャンル += "貧乳/"
	ENDIF

	SIF TALENT:巨尻
		STR:ビデオジャンル += "巨尻/"

	SELECTCASE COND("陰毛")
	CASE 1 TO 3
		STR:ビデオジャンル += "パイパン/"
	CASE IS >= 7
		STR:ビデオジャンル += "剛毛/"
	ENDSELECT

	SIF TALENT:魅力
		STR:ビデオジャンル += "美少女/"
ENDIF
SIF SELECTCOM == 170
	RETURN 0

;このターンのプレイを記録
SIF SELECTCOM >= 0
	STR:ビデオ内容 += @"%TRAINNAME:SELECTCOM%\/"

SIF NOWEX:処女喪失
	STR:ビデオジャンル += "処女喪失/"
SIF NOWEX:噴乳 || COND("母乳飲みフラグ", PLAYER)
	STR:ビデオジャンル += "母乳/"
SIF NOWEX:絶頂お漏らし
	STR:ビデオジャンル += "放尿/"
SIF NOWEX:排泄
	STR:ビデオジャンル += "スカ/"
;SIF CHECK_ECST(TARGET) >= 4
;	STR:ビデオジャンル += "多重絶頂/"
SIF COND("お掃除フェラフラグ")
	STR:ビデオジャンル += "お掃除/"
SIF COND("Ａお掃除フェラフラグ")
	STR:ビデオジャンル += "ＡＴＭ/"
SIF TEQUIP:アイマスク + TEQUIP:拘束 + TEQUIP:口枷
	STR:ビデオジャンル += "拘束/"
SIF COND("童貞喪失フラグ")
	STR:ビデオジャンル += "童貞喪失/"
SIF COND("童貞喪失フラグ", PLAYER)
	STR:ビデオジャンル += "童貞狩り/"

;コマンド依存
SELECTCASE SELECTCOM
;使用前と使用後の比較
CASE TNUM("自慰")
	STR:ビデオジャンル += "自慰/"
CASE TNUM("尿道指挿入れ"), TNUM("尿道カテーテル"), TNUM("尿道ブジー"), TNUM("尿道バイブ")
	STR:ビデオジャンル += "尿道/"
CASE TNUM("くぱぁさせる"), TNUM("Ａくぱぁさせる")
	STR:ビデオジャンル += "局部アップ/"
CASE TNUM("バイブ")
	SIF SIZE("バイブ") >= 3
		STR:ビデオジャンル += "拡張/"
CASE TNUM("アナルバイブ")
	STR:ビデオジャンル += "アナル/"
	SIF SIZE("Ａバイブ") >= 3
		STR:ビデオジャンル += "拡張/"
CASE TNUM("正常位") TO TNUM("子宮挿入")
	STR:ビデオジャンル += "セックス/"
CASE TNUM("アナル正常位") TO TNUM("アナル騎乗位")
	STR:ビデオジャンル += "アナルセックス/"
CASE TNUM("尿道正常位") TO TNUM("尿道騎乗位")
	STR:ビデオジャンル += "尿道セックス/"
CASE TNUM("イラマチオ")
	STR:ビデオジャンル += "イラマチオ/"
CASE TNUM("フィストファック")
	STR:ビデオジャンル += "拡張/フィストファック/"
CASE TNUM("アナルフィスト") TO TNUM("両穴フィスト")
	STR:ビデオジャンル += "拡張/フィストファック/アナル/"
ENDSELECT

SIF COND("飲尿フラグ", PLAYER)
	STR:ビデオジャンル += "聖水/"
SIF COND("飲尿フラグ", TARGET)
	STR:ビデオジャンル += "飲尿/"

IF TEQUIP:ビデオカメラ >= 10
	TEQUIP:ビデオカメラ = 0
	PRINTL 
	PRINTL これだけ撮れば十分だろう。
	PRINTW ＜ビデオ撮影を終了します＞
	RETURN 1
ELSEIF BASE:体力 < 100
	TEQUIP:ビデオカメラ = 0
	PRINTFORML 
	PRINTFORML %CALLNAME:TARGET%からは気だるげな雰囲気が漂ってきている…
	PRINTFORMW これ以上の撮影はよした方が良さそうだ。
	PRINTW ＜ビデオ撮影を終了します＞
	RETURN 1
ENDIF

TEQUIP:ビデオカメラ += 1

RETURN 1
