@COM142
;アナルフィスト
CALL PRINT_TRAIN_NAME(SELECTCOM)

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

RETURN 1

@FLAG_COM142, ARG
CALL FLAG_INSERTION, "Ａフィスト", ARG, 100
;アナルを露出させる
SIF CHECK_CLO("Ａ露出") == 0
	CALL SETFLAG, "ボトムを露出：テキスト無し", TARGET

@SOURCE_COM142, ARG
CALL SOURCE_INSERTION, "Ａフィスト", ARG, 100
CALL SOURCE_SMPLAY, "フィスト", ARG

@TRAIN_MESSAGE_COM142
#DIM FLAG_MESSAGE
#DIM FLAG_MESSAGE2
#DIM SIZE_FIST

IF CHECK_COM("結腸責め")
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の
	IF PREVPLAY("結腸責め")
		PRINTFORM %TEXTR("お腹が侵入に合わせて変形するのを楽しみ/お腹の盛り上がりで何処まで入っているかを確かめ/粘膜を傷つけないように細心の注意を払い")%ながら、
		PRINTFORMW %TEXTR("更に奥深くまで咥え込める体になれるようにと/力強くゆっくりと/執拗に")%腕を出し入れし続けた…
	ELSE
		PRINTFORM 直腸の突き当りの脇……Ｓ状結腸の入り口を指先で探りあてると、慎重に手の平を潜り込ませて行った…
	ENDIF
	RETURN 1
ELSEIF SELECTCOM == PREVCOM || SELECTCOM == PREVCOM:2
	PRINTFORM %CALLNAME:PLAYER%は
	IF COND("前Ｔ調教者") == PLAYER
		PRINTFORMW %CALLNAME:TARGET%の腸内を執拗に\@ CHECK_COM("二本フィスト") ? 両腕 # 拳 \@で攪拌し続けた…
	ELSE
		IF PLAYER == MASTER
			PRINTFORM %CALLNAME:ASSI%
		ELSE
			PRINTFORM %CALLNAME:MASTER%
		ENDIF
		PRINTFORMW に続いて、%CALLNAME:TARGET%のアナルを拳でズボズボと掻き回した…
	ENDIF
	RETURN 1
ENDIF
IF ABL:PLAYER:サドっ気 >= 3 && ABL:Ａ拡張 >= SIZE("二本フィスト") - 1
	PRINTFORML %CALLNAME:TARGET%の\@ ABL:Ａ名器度 <= 20 ? ユルい # 拡張された \@アナルなら、両腕同時でもいけそうです。
	PRINTFORML どうしますか？
	PRINTFORML [ 0] 両腕を捻じ込む
	PRINTFORML [ 1] 片手で楽しむ
	CALL INPUT_SELECT, 2
	SIF RESULT == 0
		CALL SETFLAG, "二本フィスト", PLAYER
ENDIF
;アナルの拡張具合の余裕 FLAG_MESSAGE == 3 でぴったり　2以下はかなり厳しい
IF CHECK_COM("二本フィスト")
	SIZE_FIST = SIZE("二本フィスト", PLAYER)
ELSE
	SIZE_FIST = SIZE("片手フィスト", PLAYER)
ENDIF

FLAG_MESSAGE = 3 + ABL:Ａ拡張 - SIZE_FIST
FLAG_MESSAGE2 = RAND:3

;前回フィスト
IF PREVCOM == 141 && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:TARGET%の膣を犯していた腕を引き抜いて、今度は
	IF FLAG_MESSAGE >= 4 && RAND:2 == 0
		PRINT 柔らかな
	ELSEIF PALAM:欲情 >= 10000 && CHECK("Ａフィスト")
		PRINT ヒクついている
	ENDIF
	PRINT アナルに
ELSE
	;前回Ａ愛撫
	IF PREVCOM == 2 && COND("前Ｔ調教者") == PLAYER
		PRINTFORM %CALLNAME:TARGET%のアヌスから指を引き抜くと、
	ELSE
		;シックスナイン
		IF TEQUIP:シックスナイン
			PRINTFORM %TEXTS("６９")%
		ELSEIF TEQUIP:拘束
			PRINT 大きく股を開いた格好で縛られている
		ELSEIF (ABL:マゾっ気 >= 3 || (FLAG_MESSAGE >= 2 && ABL:Ａ感覚 >= 4) ) && FLAG_MESSAGE2
			IF TALENT:好色 + TALENT:淫乱 && FLAG_MESSAGE >= 3 && ABL:Ａ感覚 >= 3 && RAND:3 == 0
				PRINT お尻を指で拡げて
			ELSEIF ABL:信頼 + ABL:欲望 >= 6 && RAND:3 == 0
				PRINT 両腕で脚を抱えて
			ELSEIF RAND:2 == 0
				PRINT 股を広げて
			ELSE
				PRINT 腰を突き出して
			ENDIF
			IF TALENT:淫乱 || FLAG_MESSAGE >= 3
				PRINT 誘っている
			ELSEIF RAND:2 == 0
				PRINT 待ち構えている
			ELSE
				PRINT 準備している
			ENDIF
		ENDIF
		PRINTFORM %CALLNAME:TARGET%の
		SIF (ABL:マゾっ気 < 3 || FLAG_MESSAGE2 == 0) && TEQUIP:拘束 + TEQUIP:シックスナイン == 0 && (ABL:信頼 + ABL:欲望 < 6)
			PRINT 脚を大きく開かせると、
	ENDIF
	IF FLAG_MESSAGE >= 4 && RAND:2 == 0
		PRINT 緩められた
	ELSE
		PRINTFORM %TEXTS("アナル")%
	ENDIF
	PRINT アナルに
	;拡張ＬＶがかなりあるなら握り拳ルートも。
	IF FLAG_MESSAGE >= 4 && RAND:4 == 0
		PRINTFORM \@ CHECK_COM("二本フィスト") ? 両手の # \@握り拳
		;拳フラグ
		SETBIT TFLAG:地の文分岐, 0
		FLAG_MESSAGE -= 1
	ELSEIF CHECK_COM("二本フィスト")
		PRINTFORM 合掌させた指先
	ELSEIF FLAG_MESSAGE >= 2 && RAND:2 == 0
		PRINTFORM 揃えた指先
	ELSE
		PRINTFORM すぼめた指先
	ENDIF
	PRINT を押し当て、
ENDIF
;恋慕で分岐
IF TALENT:恋慕 && TEQUIP:シックスナイン == 0 && RAND:3 == 0
	PRINT 顔を見つめながら
ELSEIF ABL:PLAYER:サドっ気 >= 3 && RAND:3 == 0
	PRINT 力任せに
ELSEIF FLAG_MESSAGE >= 4 && RAND:3 == 0
	PRINT 抵抗を感じることなく
ELSEIF FLAG_MESSAGE >= 3 && RAND:3 == 0
	PRINT 一気に
ELSEIF FLAG_MESSAGE >= 2 && RAND:2 == 0
	PRINT 捻りながら
ELSEIF (FLAG_MESSAGE >= 2 && TFLAG:地の文分岐) || FLAG_MESSAGE <= 2
	PRINT 慎重に
ELSE
	PRINT ゆっくりと
ENDIF
;TFLAG:地の文分岐 1bit入り口周辺、2bit直腸、3bit結腸より深くを攻めるイメージ
IF FLAG_MESSAGE >= 4 && CHECK_COM("二本フィスト") == 0 && (RAND:2 || EXP:結腸経験 == 0)
	IF FLAG_MESSAGE >= 6 && RAND:3
		PRINT Ｓ状結腸を越えた更に奥深くへと腕を呑み込ませ、肩まで
	ELSEIF FLAG_MESSAGE >= 5 && RAND:3
		PRINT 結腸の奥深くへ、肘まで
	ELSEIF FLAG_MESSAGE >= 4 && RAND:3
		PRINT Ｓ状結腸の中腹めがけて腕の半ばまで
	ELSE
		PRINT Ｓ状結腸の入り口まで
	ENDIF
	;結腸責めをしているならフラグをセット
	CALL SETFLAG, "結腸責め", TARGET
	SETBIT TFLAG:地の文分岐, 3
	FLAG_MESSAGE -= 1
ELSEIF FLAG_MESSAGE >= 4 || (FLAG_MESSAGE >= 3 && RAND:2)
	IF FLAG_MESSAGE == 3 && RAND:2
		PRINT 直腸の半ばまで
	ELSE
		PRINT 直腸の突き当たりまで
	ENDIF
	SETBIT TFLAG:地の文分岐, 2
ELSE
	IF FLAG_MESSAGE <= 2
		PRINT 手の甲まで
	ELSE
		PRINT 手首まで
	ENDIF
	SETBIT TFLAG:地の文分岐, 1
ENDIF
IF RAND:3 == 0
	PRINT 埋め込
ELSEIF RAND:2 == 0
	PRINT 捻じ込
ELSE
	PRINT 押し込
ENDIF
IF TFLAG:地の文分岐 == 0 && FLAG_MESSAGE >= 3 && RAND:2
	PRINTFORMW むと、中で\@ CHECK_COM("二本フィスト") ? 両手を握り合わせた。 # 握りこぶしを作った。 \@
	SETBIT TFLAG:地の文分岐, 0
	FLAG_MESSAGE -= 1
ELSE
	PRINTFORMW んだ。
ENDIF

IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 4) && GETBIT(TFLAG:地の文分岐, 3)
	IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 4) && RAND:3 == 0
		PRINT 容赦なく
	ELSEIF RAND:2
		PRINT 勢い良く
	ELSE
		PRINT ズボズボと
	ENDIF
	PRINT 体内に叩き込まれる
ELSEIF ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2
	PRINT ズボズボと
	SIF RAND:2 == 0
		PRINT 激しく
	PRINT 出し入れされる
ELSE
	IF ABL:Ａ感覚 >= 3 && GETBIT(TFLAG:地の文分岐, 1)
		PRINT 入り口周辺の敏感な粘膜も刺激しながら
	ELSEIF FLAG_MESSAGE >= 3  && RAND:2 == 0
		PRINT 中を探るように
	ELSE
		PRINT 中を拡げるように
	ENDIF
	PRINT 動かされる
ENDIF
IF GETBIT(TFLAG:地の文分岐, 0)
	PRINTFORM \@ CHECK_COM("二本フィスト") ? 束ねられた # \@握り拳
ELSEIF CHECK_COM("二本フィスト")
	PRINTFORM 両腕
ELSEIF FLAG_MESSAGE <= 1
	PRINTFORM 手
ELSE
	SIF RAND:2 && TALENT:PLAYER:オトコ && COND("男の娘", PLAYER) == 0
		PRINTFORM \@ RAND:2 ? 男の # 太い \@
	PRINTFORM 腕
ENDIF
;アナル描写
IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1)
	PRINT に
;受け入れられる
ELSEIF FLAG_MESSAGE >= 4
	PRINT を
;まだまだ厳しい
ELSE
	PRINT に
ENDIF
;前回フィストならＡとＶの大きさ比較
IF PREVCOM == 141 && ABL:Ａ拡張 > ABL:Ｖ拡張 && CHOSECOM(SELECTCOM) == 0 && FLAG_MESSAGE >= 4
	IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1)
		PRINT 、膣口
	ELSE
		IF RAND:2
			PRINT 、前の穴
		ELSE
			PRINT 、膣
		ENDIF
	ENDIF
	IF ABL:Ａ拡張 > ABL:Ｖ拡張 + 1
		PRINTFORM とは比べものにならない程に拡張された
	ELSE
		PRINTFORM よりも鍛えられた
	ENDIF
ENDIF
IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1)
	PRINT アヌスは
ELSE
	PRINT アナルは
ENDIF
IF FLAG_MESSAGE >= 4
	IF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1)
		IF RAND:2 == 0
			PRINT ぽっかりと
		ELSE
			PRINT 驚くほど
		ENDIF
		PRINT 拡げられ、
	ELSE
		IF GETBIT(TFLAG:地の文分岐, 3) && RAND:3 == 0
			PRINT 深々と
		ELSEIF RAND:2 == 0
			PRINT 柔らかく
		ELSE
			PRINT 楽々
		ENDIF
		PRINT 受け止め、
	ENDIF
ELSEIF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 2) && GETBIT(TFLAG:地の文分岐, 1) && RAND:2 == 0
	PRINT 捲りあげられ、
ELSEIF (ABL:PLAYER:サドっ気 >= 3 || FLAG_MESSAGE >= 3) && GETBIT(TFLAG:地の文分岐, 3) && RAND:2 == 0
	PRINT パンパンに拡げられ、
ELSEIF FLAG_MESSAGE >= 2 || ABL:Ａ感覚 >= 5 || PALAM:Ａ潤 >= PALAMLV:4 || RAND:2 == 0
	PRINT 裂けそうなほどに押し広げられ、
ELSE
	PRINT 無残に傷つけられ、
ENDIF
;初めて？
IF FIRSTCOMS("フィスト") == 0
	PRINT 初めて味わう
ELSE
	PRINT その
ENDIF
;乱数分岐
IF CHECK("Ａフィスト") && FLAG_MESSAGE >= 4
	PRINT たまらない
ELSEIF RAND:2
	PRINT 圧倒的な
ELSE
	PRINT 暴力的な
ENDIF
;Ｖ快感で分岐
IF CHECK("Ａフィスト") && ABL:Ａ感覚 >= SIZE_FIST && RAND:3 == 0
	PRINT 快感に
ELSEIF GETBIT(TFLAG:地の文分岐, 3) && RAND:2
	PRINT 深さに
ELSEIF FLAG_MESSAGE <= 2 && (RAND:2 || CUP:苦痛 >= 500)
	PRINT 拡張感に
ELSE
	PRINT 挿入感に
ENDIF
PRINTFORM %CALLNAME:TARGET%は
;倒錯的かマゾっ気が3以上
IF (ABL:マゾっ気 >= SIZE_FIST || TALENT:倒錯的) && SIZE_FIST >= 3 && RAND:2
	IF FLAG_MESSAGE >= 3 || CHECK("Ａフィスト")
		IF ABL:Ａ感覚 >= 3 && RAND:2
			PRINT 不浄の場所を性器にされてしまったと興奮し、
		ELSE
			PRINT 排泄器官をユルユルにされたことに興奮しながら、
		ENDIF
	ELSE
		PRINT 自分の排泄する場所を壊されるかもしれないと興奮し、
	ENDIF
ELSEIF CHECK("Ａフィスト") && ABL:Ａ感覚 >= SIZE_FIST && RAND:2
	PRINT 悦楽に蕩けた艶やかな声をあげて
ELSEIF (ABL:マゾっ気 < 3 || FLAG_MESSAGE <= 2) && CHECK("Ａフィスト") == 0 && RAND:2
	PRINT 目尻に涙を浮かばせて
;プライド高いか恥じらい
ELSEIF (TALENT:高慢 || TALENT:恥じらい) && RAND:3
	PRINT 顔を真っ赤に染めて
ELSE
	PRINT 獣のような声をあげて
ENDIF
PRINTW 激しく身をくねらせた…
