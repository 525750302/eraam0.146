@COM6
;キス
CALL PRINT_TRAIN_NAME(SELECTCOM)

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
CALL SHOW_COM_ORDER_COM, 6

;実行できない
SIF TCVAR:やる気 == 0
	RETURN 0
;-------------------------------------------------
;実行決定
;-------------------------------------------------
CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = %STR:今回コマンド%

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK

RETURN 1

@FLAG_COM6, ARG
;対象の口⇔調教者の口の汚れが移動
CALL EQ_STAIN, TARGET, "Ｍ", PLAYER, "Ｍ"


@SOURCE_COM6, ARG
CALL VARSET_COMF

CALL SOURCE_DOWNBASE_HP, 6

;汚れデータを元にする
SOURCE:0:不潔 = COM_ORDER_STAIN("Ｍ", "Ｍ")*100 + 100

SOURCE:0:屈従 = 100
SOURCE:0:情愛 = 300
SOURCE:0:性行動 = 300
SOURCE:0:達成感 = 300

;ABL:Ｍ感覚をみる
SOURCE:0:Ｍ快楽 = CALCF("Ｍ刺激", 1)
;調教者の舌使いをみる
SOURCE:0:Ｍ快楽 = SOURCE_EXABL(SOURCE:0:Ｍ快楽, "舌使い", PLAYER)

;テンダーキッス
IF CHECK_COM("テンダーキス")
	TIMES SOURCE:0:Ｍ快楽, 0.10
	SOURCE:0:情愛 += 700
ENDIF

;キスをどのぐらい続けたかでボーナス
SOURCE:0:情愛 += 200 * TEQUIP:キス
SOURCE:0:性行動 += 200 * TEQUIP:キス
SOURCE:0:達成感 += 100 * TEQUIP:キス
SOURCE:0:屈従 += 100 * TEQUIP:キス

SIF TALENT:Ｍ性向
	CALL CALC, "好みのプレイ", SOURCE:0:Ｍ快楽

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM6
;テンダーキッス
IF CHECK_COM("テンダーキス")
	IF TEQUIP:押し倒し
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%の頬に手を添えて、優しく
	ELSE
		SIF PLAYER != MASTER
			PRINTFORM %CALLNAME:PLAYER%は
		;相思相愛
		IF TALENT:相思相愛 &&  PLAYER == MASTER && RAND:3 == 0
			PRINTFORM %CALLNAME:TARGET%に愛を囁きながら優しく
		ELSEIF TALENT:小柄 && RAND:2 == 0
			PRINTFORM %CALLNAME:TARGET%の頭を撫でながら、そっと
		ELSE
			PRINTFORM %CALLNAME:TARGET%を抱き寄せて、優しく
		ENDIF
	ENDIF
	IF RAND:2 == 0
		PRINTFORMW キスをした…
	ELSE
		PRINTFORMW 唇を重ねた…
	ENDIF
;押し倒し中
ELSEIF TEQUIP:押し倒し
	PRINTFORM %CALLNAME:TARGET%は
	;助手が前回と同じ行動(違う行動の時は、ここでなく、EQUIP_COM32で記述)
	SIF TEQUIP:Ｗ押し倒し && SELECTCOM:1 == PREVCOM:1 && SELECTCOM:1 >= 0
		PRINTFORM %TEXTS("Ｗ押し倒し中")%
	IF TEQUIP:キス
		IF TEQUIP:Ｗ押し倒し == 0
			PRINTFORMW 空気を求めて喘ぐ%CALLNAME:PLAYER%の唇を逃さず、再びキスで口を塞いでしまう…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%と夢中でキスをしている…
		ENDIF
	ELSEIF INSERT("合体中") && TEQUIP:Ｗ押し倒し == 0
		PRINTFORM %TEXTS("合体中", TARGET)%
		IF (ABL:欲望 >= 4 || TALENT:強気) && RAND:2
			PRINTFORMW %CALLNAME:PLAYER%の唇を奪ってきた…
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%にキスをしてきた…
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:PLAYER%の唇を奪い、
		IF PREVPLAY("飲尿") && CHECK_STAIN(STAIN:Ｍ, "尿") && ABL:サドっ気 >= 3
			PRINTFORMW さっきのお礼と言わんばかりに尿混じりの唾液を流し込んできた…
		ELSEIF (PREVCOM == 91 || PREVCOM == 99 || PREVCOM == 140) && CHECK_STAIN(STAIN:Ｍ, "精液") && ABL:サドっ気 >= 3
			PRINTFORMW 舌を絡めながら口に貯めた精液を口移しで飲ませてきた…
		ELSE
			IF RAND:2 == 0
				PRINTFORMW 舌を絡めて貪ってきた…
			ELSE
				PRINTFORMW 唾液を流し込んできた…
			ENDIF
		ENDIF
	ENDIF
;キス継続中
ELSEIF TEQUIP:キス && COND("前Ｔ調教者") == PLAYER
	PRINTFORM %CALLNAME:PLAYER%は%TEXTS("電話中の")%%CALLNAME:TARGET%
	IF INSERT("正常位")
		PRINTFORM に腰を押しつけながら、啄むような
	ELSEIF INSERT("対面座位")
		PRINTFORM と抱きあったまま、腰を揺すりながら
	ELSE
		IF TALENT:相思相愛 && PLAYER == MASTER && RAND:2
			PRINTFORM と愛を確かめ合うように
		ELSE
			PRINTFORM を抱き寄せ、
			IF RAND:3 == 0
				PRINT お互いの口内を舌先で舐め回しながら
			ELSEIF RAND:2
				PRINT 舌を絡めて唾液を交換し合うような
			ELSE
				PRINT 口の周りをベタベタにしながら
			ENDIF
		ENDIF
	ENDIF
	PRINTFORMW キスを続けた…
;インサート中
ELSEIF INSERT("合体中")
	PRINTFORM %CALLNAME:PLAYER%は%TEXTS("合体中", PLAYER)%
	IF EX:Ｖ中出し == 1 && PREVPLAY("生中出しした", PLAYER) && PREVPLAY("Ｖ挿入中", PLAYER)
		PRINTFORM %TEXTR("膣内射精の余韻に浸っている/生中出しを受け入れてくれた/ゆっくりと呼吸して息を整えている")%%CALLNAME:TARGET%
	ELSE
		PRINTFORM %CALLNAME:TARGET%%TEXTS("に電話させたまま、")%
	ENDIF
	IF INSERT("挿入中") && CONDS("体位", PLAYER) != "騎乗位"
		PRINTFORMW にキスをねだった…
	ELSE
		PRINTFORMW の%TEXTR("唇を塞ぎ唾液を流し込んだ/口腔を隅々まで舐め回した/唇を奪った")%…
	ENDIF
ELSE
	PRINTFORM %CALLNAME:PLAYER%は
	IF PREVPLAY("飲尿")
		PRINTFORM 飲尿プレイ直後でオシッコ臭い息を吐きながら呼吸を整えていた
	ELSEIF COND("前回フェラ")
		PRINTFORM ペニスをしゃぶらせるのを中断させると、
	ELSEIF COND("前Ｔ調教者") == PLAYER && (PREVCOM == 98 || PREVCOM == 99)
		PRINTFORM アソコへの奉仕を中断させると、
	ELSEIF COND("前Ｔ調教者") == PLAYER && PREVCOM == 96
		PRINTFORM お尻の穴への奉仕をしていた
	ELSEIF TEQUIP:電話中
		PRINTFORM %TEXTS("電話中の")%
	ENDIF
	PRINTFORM %CALLNAME:TARGET%
	IF PREVCOM == 285
		PRINTFORM を抱きしめながら
	ELSEIF RAND:3 == 0 && (COND("前回フェラ") || (COND("前Ｔ調教者") == PLAYER && (PREVCOM == 96 || PREVCOM == 98 || PREVCOM == 99)) )
		PRINTFORM の舌使いを褒め、優しく
	ELSE
		PRINTFORM %TEXTR("を抱き寄せて/の頭を撫でながら/の髪を触りながら/の顔に手を添えて")%
	ENDIF
	PRINTFORMW キスをした。
ENDIF

IF INSERT("インサート") && (ABL:信頼 >= 5 || ABL:欲望 >= 5) && ABL:Ｖ感覚 >= 5 && PREVCOM >= 66 && PREVCOM <= 68
	PRINTFORM %CALLNAME:TARGET%は敏感な場所を%TEXTR("襲う快感に鼻息を荒く/攻められる度に甘い呻き声を漏ら")%しながら、
	IF TEQUIP:押し倒し || RAND:3 == 0
		PRINTFORMW 情熱的に%CALLNAME:PLAYER%を求めている…
	ELSEIF RAND:2
		PRINTFORMW 舌を絡めてくる…
	ELSE
		PRINTFORMW キスに応じている…
	ENDIF
ELSEIF TEQUIP:押し倒し == 0 && ABL:信頼 >= 3 && (ABL:信頼 >= 5 || ABL:Ｍ感覚 + ABL:PLAYER:舌使い >= 3)
	PRINTFORM %CALLNAME:TARGET%は
	;テンダーキス
	IF CHECK_COM("テンダーキス")
		IF RAND:3 == 0
			PRINTFORMW %TEXTR("目を閉じて/小さく吐息を漏らしながら")%、%CALLNAME:PLAYER%に身を委ねている…
		ELSEIF RAND:2 == 0
			PRINTFORMW 目を閉じて、頬を赤く染めながら%CALLNAME:PLAYER%に応えている…
		ELSE
			PRINTFORMW %TEXTR("目を閉じて/小さく吐息を漏らしながら")%うっとりとしている…
		ENDIF
	ELSEIF TALENT:小柄 && TALENT:PLAYER:長身 && INSERT("インサート") + INSERT("挿入中") == 0 && RAND:2
		PRINTFORMW 身長差を爪先立ちでぷるぷるしながら補いつつ、%CALLNAME:PLAYER%に応えて積極的に舌を絡めている…
	ELSE
		PRINTFORMW %CALLNAME:PLAYER%に応えるように、積極的に舌を絡めている…
	ENDIF
ENDIF


@EQUIP_COM6
SIF TEQUIP:キス == 0 || COND("追加キスフラグ") == 0
	RETURN 0

CALL SOURCE_COM6, 30
CALL FLAG_COM6, 30

RETURN 1

@EQUIP_COM6_2
SIF TEQUIP:キス == 0 || COND("追加キスフラグ") == 0
	RETURN 0

PRINTFORML ＜隙あらばキス中＞

IF TEQUIP:押し倒し
	LOCAL = MIN(LIMIT(TEQUIP:キス - 1, 0, ABL:舌使い) + TALENT:蕩唇, 4) / 2
ELSE
	LOCAL = MIN(LIMIT(TEQUIP:キス - 1, 0, ABL:PLAYER:舌使い) + TALENT:蕩唇, 4) / 2
ENDIF

EXP:キス経験 += 1 + LOCAL
EXP:PLAYER:キス経験 += 1 + LOCAL
PRINTFORML キス経験＋{1 + LOCAL}
PRINTFORML キス経験＋{1 + LOCAL}（%CALLNAME:PLAYER%）

@COM_ORDER_COM6, ARG
VARSET LOCAL
LOCALS = 

;すべての命令に共通の要素を考慮
;(信頼が高いと命令に従いやすいなど)
CALL COM_ORDER, ARG

LOCAL = RESULT

;ABL:奉仕精神
IF ABL:奉仕精神
	LOCAL += MIN(ABL:奉仕精神, 10) * 2
	LOCALS = %LOCALS% + 奉仕精神LV{ABL:奉仕精神}({MIN(ABL:奉仕精神, 10) * 2})
ENDIF
;貞操無頓着
IF TALENT:貞操無頓着
	LOCAL += 20
	LOCALS = %LOCALS% + 貞操無頓着(20)
ENDIF
;恥じらい
IF TALENT:恥じらい
	LOCAL -= 3
	LOCALS = %LOCALS% - 恥じらい(3)
ENDIF
;献身的
IF TALENT:献身
	LOCAL += 6
	LOCALS = %LOCALS% + 献身(6)
ENDIF
;快感の否定
IF TALENT:快感の否定
	LOCAL -= 1
	LOCALS = %LOCALS% - 快感の否定(1)
ENDIF
;恋慕
IF TALENT:恋慕 && ASSIPLAY == 0
	LOCAL += 5
	LOCALS = %LOCALS% + 恋慕(5)
ENDIF
;Ｍ性向
IF TALENT:Ｍ性向
	LOCAL += 10
	LOCALS = %LOCALS% + Ｍ性向(10)
ENDIF

;汚れあり
IF COM_ORDER_STAIN("Ｍ", "Ｍ")
	LOCAL -= COM_ORDER_STAIN("Ｍ", "Ｍ")
	LOCALS = %LOCALS% - 汚れあり
	;汚臭鈍感
	SIF TALENT:汚臭鈍感 || TALENT:汚臭敏感
		LOCALS = %LOCALS%、\@TALENT:汚臭鈍感 ? 汚臭鈍感 # 汚臭敏感\@
	SIF COND("陶酔")
		LOCALS = %LOCALS%、陶酔で半減
	LOCALS = %LOCALS%({COM_ORDER_STAIN("Ｍ", "Ｍ")})
ENDIF

;合計を表示(15以上で実行)
CALL PRINT_COM_ORDER_COM, LOCALS, ARG, LOCAL, 15

RETURN RESULT
