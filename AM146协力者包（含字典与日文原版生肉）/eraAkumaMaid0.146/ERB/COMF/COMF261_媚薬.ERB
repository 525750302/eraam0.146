@COM261
;媚薬

PRINTL 媚薬

CALL TRAIN_MESSAGE_B

SIF REFUSE_CHECK()
	RETURN 0

STR:前回コマンド = 媚薬

TEQUIP:媚薬 = 0
ITEM:媚薬 -= 1

RETURN 1

@FLAG_COM261, ARG
IF TEQUIP:押し倒し
	;錬金術があれば効果ＵＰ
	SELECTCASE MAX(TALENT:ASSI:錬金術, TALENT:錬金術)
	CASE 2
		TEQUIP:PLAYER:媚薬 = 200
	CASE 1
		SIF TALENT:ASSI:錬金術 + TALENT:錬金術
			TEQUIP:PLAYER:媚薬 = 100
	ENDSELECT

	CALL ADD_STATE, PLAYER, "媚薬"
ELSE
	IF ASSI:1 > 0 && STRCOUNT(CSTR:(ASSI:1):アドバイス, "淫蕩薬師")
		TEQUIP:媚薬 = 200
	ELSEIF TALENT:ASSI:錬金術 + TALENT:MASTER:錬金術
		TEQUIP:媚薬 = 100
	ENDIF
ENDIF
;ここでTEQUIP:媚薬がさらに+100（基本値）され、薬毒耐性はその後に75％軽減される
CALL ADD_STATE, TARGET, "媚薬"

@SOURCE_COM261, ARG
CALL VARSET_COMF

SOURCE:0:逸脱 = 2000
SIF TALENT:好色
	SOURCE:0:逸脱 /= 2
SIF TEQUIP:押し倒し
	SOURCE:0:逸脱 /= 2

CALL CALC, "調教ソース", ARG


@TRAIN_MESSAGE_COM261
IF TEQUIP:押し倒し
	;騎乗位出来るなら騎乗位ルート
	CALL COM_ABLE65
	IF RESULT && TALENT:好色*3 + TALENT:淫乱*3 + ABL:欲望 >= 3 && PENIS(PLAYER) && COND("Ｖ熟達")
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスに媚薬を垂らし、敏感になったそれを咥え込んで腰を振り始めた。
		CALL SET_INSERT, TARGET, PLAYER, 65
	ELSEIF CHECK_COM("キス")
		PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%を押さえつけ、口移しで媚薬を飲ませてきた。
	ELSE
		PRINTFORMW %CALLNAME:TARGET%は用意された薬品の中から媚薬を選び出し、身動きの取れない%CALLNAME:PLAYER%に飲ませてきた。
	ENDIF
ELSE
	PRINTFORM %CALLNAME:TARGET%に
	IF ASSI:1 > 0 && STRCOUNT(CSTR:(ASSI:1):アドバイス, "淫蕩薬師")
		PRINTFORM 特別に調合した、処女だろうと快感に溺れる強力な
	ELSEIF TALENT:ASSI:錬金術 + TALENT:MASTER:錬金術
		PRINTFORM 特別に調合した
	ENDIF
	PRINTFORML 媚薬を飲ませた。
ENDIF
IF TALENT:薬毒耐性
	PRINTFORMW %CALLNAME:TARGET%は薬に耐性を持っているようだが少しは効果があるようで、ほんのりと肌を紅潮させている…
ELSE
	PRINTFORMW %CALLNAME:TARGET%は荒い息をついて肌を紅潮させている…
ENDIF

PRINTL 
CALL PRINT_STR, "媚薬の効果により_イエロー_欲情と全快楽が上がりやすく_なり、_イエロー_苦痛と恐怖と反感が上がりにくく_なります。_W"
CALL PRINT_STR, "※ただし、副作用として_青_恭順と習得がとても上がりにくく_なり、_青_獲得する珠の量も減少します_※_W"

